//===== rAthena Script =======================================
//= Rare Diamond Merchant
//===== By: ==================================================
//= Z3R0
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Exchanges 17 Carat Diamond
//===== Additional Comments: =================================
//= 1.0 First / Optimized Version
//= 1.1 Adjusted for Missing ; on Line #271 (#15425)
//= 1.2 Removed Comments per Request (#15426)
//= 1.3 Changed Emotion # to Const Definition (#15427)
//= 1.4 Removed Double Space on Line #148 (#15428)
//= 1.5 Added This Version History (#15429)
//===== Translated By: =======================================
//= dsc
//============================================================

// Main NPC :: diamond
//============================================================
-	script	RareDiamondMerchant	58,{

	set .@npc$, "[Rare Diamond Merchant]";

	mes .@npc$;
	mes "女士们，先生们！ ！ ！";
	mes "你听过传闻，但从未亲眼见过";
	mes "世界上最稀有的钻石！";
	mes "^FF82FF'17克拉钻石'^000000目前正在低价出售！！";
	mes "如果您对珍贵的钻石感兴趣，请听好！";
	next;
	
	mes .@npc$;
	mes "您可以将钻石作为礼物送给您的爱人。";
	mes "^FF82FF'17克拉钻石'^000000的接收者会珍惜它";
	mes "并把你珍藏在心里。";
	emotion ET_BLABLA;
	next;
	
	mes .@npc$;
	mes "什么？你没有爱人吗？";
	mes "你仍然可以自己坚持下去。";
	mes "很可能是《17克拉钻石》《CLR1》的迷人光环";
	mes "把爱带入你的生活。";
	next;
	
	mes .@npc$;
	mes "被誉为钻石中的迷人钻石...";
	mes "是^FF82FF‘17克拉钻石'^000000！";
	next;

	set .@menu$, "^FF82FF'17 Carat Diamond'^000000?";
	if (countitem(6024))
		set .@menu$, .@menu$ + ":Exchange my 17 Carat Diamond for zeny...";

	switch(select(.@menu$)) {
	case 1:
		mes .@npc$;
		mes "是的！";
		mes "呵呵，你现在看到的^FF82FF'17克拉钻石'^000000";
		mes "是最高品质的 17 克拉钻石之一。";
		mes "将它与您可能拥有的任何豌豆大小的无色岩石进行比较";
		mes "从摩洛哥挖出来的真是难以形容！";
		emotion ET_HUK;
		next;
		
		mes .@npc$;
		mes "有人声称这是同一颗钻石";
		mes "这是卡丘亚在科摩多看到的！";
		next;
		
		if (select("对话结束：多少钱？") == 1) {
			mes .@npc$;
			mes "还算比较便宜吧！";
			mes "如果您有兴趣，请随时与我交谈！";
			close;
		}
		
		mes .@npc$;
		mes "当然！最重要的是价格！";
		mes "正如我之前所说，^FF82FF“17 克拉钻石”^000000 已";
		mes "鉴定为17克拉钻石。";
		mes "世界上这样的人并不多。";
		next;
		
		mes .@npc$;
		mes "价格才不过五亿泽尼！";
		mes "还有1,000,000z的服务费。";
		mes "与钻石的品质相比";
		mes "这是一个很小的代价。";
		next;
		
		if (select("^828282太贵了。^000000：我买它。") == 1) {
			mes .@npc$;
			mes "说这样的钻石";
			mes "^FF82FF‘17克拉钻石’^000000太贵了，";
			mes "你让我无言以对。";
			close;
		}
		
		mes .@npc$;
		mes "噢！从我看到你的那一刻起我就知道你是正义的";
		mes "这颗美妙钻石的所有者。";
		mes "我很高兴我没有看错！";
		mes "我相信这颗钻石非常适合您。";
		mes "我所需要的就是你完成这份合同。";
		next;
		
		while(1) {
			if (select("^B9062F我会签合同。^000000：^828282没关系，我改变主意了。^000000") == 2) {
				mes .@npc$;
				mes "我明白了，这太糟糕了。。";
				close;
			}
			
			mes .@npc$;
			mes "你叫什么名字？";
			next;
			
			mes "[" + strcharinfo(0) + "]";
			mes "我的名字是" + strcharinfo(0) + ".";
			next;

			mes .@npc$;
			mes "好吧，请在这里签名。";
			next;
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "好的，很好。";
				mes "现在剩下的就是付款了。";
				next;
				
				if (Zeny >= 501000000) {
					mes .@npc$;
					mes "。 。 。 。 。 。";
					next;
					
					mes .@npc$;
					mes "计算价格和服务费..";
					mes "5.01 亿女性。";
					mes "金额已确认。";
					emotion ET_BLABLA;
					set Zeny, Zeny - 501000000;
					getitem 6024, 1;
					next;

					mes .@npc$;
					mes "您可以随时将钻石换回 zeny。";
					mes "但会产生1,000,000z的服务费。";
					mes "感谢您的惠顾。";
					emotion ET_THANKS;
					close;
				}
				else {
					mes .@npc$;
					mes "嗯？";
					mes "抱歉，您没有足够的资金。";
					mes "包括服务费，总共需要501,000,000z。";
					mes "请检查您的 zeny 余额并重试。";
					emotion ET_SCRATCH;
					close;
				}
			}
			else {
				mes .@npc$;
				mes "这真的是你的签名吗？";
				mes "会再签字确认吗？";
				next;
			}
		}
	case 2:
		mes .@npc$;
		mes "啊啊。您想将您的^FF82FF'17克拉钻石'^000000换成zeny吗？";
		emotion ET_SURPRISE;
		next;
		if (select("是：否") == 2) {
			mes .@npc$;
			mes "那么，你想要什么？...";
			mes "。 。 。 。 。 ....";
			emotion ET_THINK;
			close;
		}

		mes .@npc$;
		mes "我懂了。收到您的签名后，";
		mes "您必须提供您的^FF82FF“17 克拉钻石”^000000。";
		mes "另外别忘了还有1,000,000z的服务费！";
		next;
		
		mes .@npc$;
		mes "你叫什么名字？";
		next;
		
		mes "[" + strcharinfo(0) + "]";
		mes "我的名字是" + strcharinfo(0) + ".";
		next;
		
		mes .@npc$;
		mes "好吧，请在这里签名。";
		next;
		
		while (1) {
			if (select("签名：请勿签名") == 2) {
				mes .@npc$;
				mes "没有您的签名，交易无法完成。";
				close;
			}
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "您的签名已收到。";
				mes "我现在将拿走您的^FF82FF“17 克拉钻石”^000000..";
				mes "之后您将收到 ^0000FF499,000,000z^000000。";
				next;
				
				mes .@npc$;
				mes "感谢您的惠顾。";
				mes "每当您需要^FF82FF'17克拉钻石'^000000时，";
				mes "带着5亿泽尼随时回来。";
				emotion ET_THANKS;
				delitem 6024, 1;
				set Zeny, Zeny + 499000000;
				close;
			}
			else {
				mes .@npc$;
				mes "这真的是你的签名吗？";
				mes "会再签字确认吗？";
				next;
			}
		}
	}
}

prontera,165,89,6	duplicate(RareDiamondMerchant)	Rare Diamond Merchant#1	58
geffen,106,63,6	duplicate(RareDiamondMerchant)	Rare Diamond Merchant#2	58
morocc,146,100,6	duplicate(RareDiamondMerchant)	Rare Diamond Merchant#3	58
alberta,129,60,6	duplicate(RareDiamondMerchant)	Rare Diamond Merchant#4	58
lighthalzen,163,65,6	duplicate(RareDiamondMerchant)	Rare Diamond Merchant#5	58
rachel,106,142,6	duplicate(RareDiamondMerchant)	Rare Diamond Merchant#6	58
payon,180,130,6	duplicate(RareDiamondMerchant)	Rare Diamond Merchant#7	58
