//===== rAthena Script =======================================
//= Guild Mission - Core Script
//===== Description: =========================================
//= Template for guild mission added in episode 14.3
//===== Changelogs: ==========================================
//= [Official Conversion]
//= 1.0 First version [Secretdataz]
//= 1.1 Fixed potential exploit from original Aegis script. [Secretdataz]
//============================================================

-	script	gld_mission#core	-1,{
function brief;
function mob_brief;
function calcexp;
function calcjobexp;

	if (!checkweight(1201,3)){
		mes "您的物品太多，无法继续任务。";
		mes "请减轻负担并重试。";
		close;
	}

	.@zone$ = strnpcinfo(2);
	if(.@zone$ == "gqaldeg"){
		.@zoneid = 3;
		.@edition = 1;
	} else if(.@zone$ == "gqaru"){
		.@zoneid = 4;
		.@edition = 2;
	} else if(.@zone$ == "gqgefg"){
		.@zoneid = 2;
		.@edition = 1;
	} else if(.@zone$ == "gqpayg"){
		.@zoneid = 1;
		.@edition = 1;
	} else if(.@zone$ == "gqprtg"){
		.@zoneid = 0;
		.@edition = 1;
	} else if(.@zone$ == "gqsch"){
		.@zoneid = 5;
		.@edition = 2;
	} else if(.@zone$ == "gqtealde"){
		.@zoneid = 6;
		.@edition = 3;
	} else if(.@zone$ == "gqteprt"){
		.@zoneid = 7;
		.@edition = 3;
	} else {
		// Custom error message.
		debugmes "Guild mission script called from an unknown agit region [" + .@zone$ + "].";
		end;
	}
	.@npcname$ = .npcname$[.@zoneid];

	mes .@npcname$;
	mes "我们提供您可以在" + .region$[.@zoneid] + ".";
	mes "有简单的日常任务以及您必须在攻城时间内完成的任务。那么，想一想吧~";
	next;
	switch(select("征服" + .region$[.@zoneid], "公会地下城每日任务", "关于任务", "我不需要它。")){
		case 1: // Conquering.
			.@gid = getcharid(2);
			if(.@gid && is_guild_leader() == true){
				.@time_check1 = checkquest(.conquer_delay_questid[.@zoneid], PLAYTIME);
				if(.@time_check1 == 0){
					mes .@npcname$;
					mes "距离上次检查还不到一个小时！";
					mes "等待时间结束后回来。";
					next;
					mes .@npcname$;
					mes "如果你运气好的话，你会得到好的结果。";
					close;
				} else if(.@time_check1 == 2){
					erasequest .conquer_delay_questid[.@zoneid];
					mes .@npcname$;
					mes "我已经确认征服任务的等待时间已经结束了。";
					mes "我会帮你删除不必要的记录。";
					close;
				} else {
					.@siege_check = (.@edition == 1 ? agitcheck() : .@edition == 2 ? agitcheck2() : agitcheck3());
					.@time = atoi(gettimestr("%H%M",4));
					.@after_time = ((.weekday[.@zoneid] == gettime(4)) && ((.@time > .start_time[.@zoneid]) && (.@time < .end_time[.@zoneid])));

					if(.@siege_check && .@after_time){
						mes .@npcname$;
						mes "根据^4d4dff你们公会此时攻克的要塞数量，^000000我会给你奖励。";
						mes "您想立即检查吗？您知道^4d4dff即使在我们讲话时数字也会改变，^000000对吗？";
						next;
						if(select("我稍后回来。","我们现在就检查一下！") == 1){
							mes .@npcname$;
							mes "正确的。生命在于时间。";
							mes "当它最闪亮的时候回来。";
							close;
						} else {
							.@total = 0;
							for(.@i = 0; .@i < 5; ++.@i){
								.@total += (.@gid == getcastledata(.castle$[.@i+.@zoneid*5],CD_GUILD_ID));
							}

							if(!.@total){
								mes .@npcname$;
								mes "不幸的是，你们的公会还没有征服任何堡垒。";
								mes "再试一次！";
								close;
							} else {
								mes .@npcname$;
								if(.@total == 5)
									mes "极好的。";
								else
									mes "目前，你的公会已攻克的要塞总数";
								for(.@i = 0; .@i < 5; ++.@i){
									mes getcastlename(.castle$[.@i+.@zoneid*5]) + (.@gid == getcastledata(.castle$[.@i+.@zoneid*5],CD_GUILD_ID) ? ": ^4d4dff已占用^000000" : "");
								}
								if(.@total == 5)
									mes "你的公会已经征服了" + .region$[.@zoneid] + "！";
								else
									mes "是" + .@total + "！";
								next;
								mes .@npcname$;
								mes "我会给你相应的奖励。我希望你好好利用它。";
								mes "您有一个小时的等待时间，直到下一份报告。记住这一点，然后再试一次。";
								setquest .conquer_delay_questid[.@zoneid];
								getitem 6489,.@total;
								getitem 6615,.@total;
								getitem 12888,.@total;
								close;
							}
						}
					} else {
						mes .@npcname$;
						mes "抱歉，您只能在攻城时间内完成此任务。恐怕我在这里无能为力。";
						close;
					}
				}
			} else {
				mes .@npcname$;
				mes "抱歉，征服任务只适合公会大师。";
				mes "会员应该尝试公会地下城的每日任务。";
				close;
			}
			break;
		case 2: // Daily Quests
			mes .@npcname$;
			mes "这些是你可以在" + .region$[.@zoneid] + "地区的公会地下城完成的任务。";
			mes "如果您愿意，可以取消它们。随意挑战自己。";
			next;
			.@cancel_index = (.@edition == 1 ? 3 : 2);
			for(.@i = 0; .@i < 3; ++.@i){
				if(.daily_hunting_questid[.@zoneid*3 + .@i])
					.@menu$ = .@menu$ + .region$[.@zoneid] + " Mission " + (.@i+1) + (isbegin_quest(.daily_hunting_questid[.@zoneid*3 + .@i]) ? " ^4d4dffCheck the result^000000" : "") + ":";
			}
			.@sel = select(.@menu$) - 1;
			.@index = .@zoneid*3 + .@sel;
			if(.@sel == .@cancel_index){
				mes .@npcname$;
				mes "你不需要着急，因为这些任务发生在公会地下城中。";
				mes "只要您愿意，请随时尝试。";
				close;
			}
			if(BaseLevel < .level_req[.@index]){
				mes .@npcname$;
				mes "嗯，不幸的是，此任务仅限于 ^4d4dff 级别 " + .level_req[.@index] + "^000000 或更高级别的用户。";
				mes "你的水平还不够高。";
				close;
			}
			.@hunting = checkquest(.daily_hunting_questid[.@index], HUNTING);
			.@playtime = checkquest(.daily_delay_questid[.@index], PLAYTIME);
			if(.@hunting > -1){
				if(.@hunting == 2){
					mes .@npcname$;
					mes "哦，你做得很好。";
					mes "虽然不多，但希望对您有所帮助。";
					setquest .daily_delay_questid[.@index];
					erasequest .daily_hunting_questid[.@index];
					getexp calcexp(.@zoneid,.@index),calcjobexp(.@edition);
					getitem 6615,1;
				} else {
					mes .@npcname$;
					mes "唔？看来你还没有完成任务..";
					mes "有什么问题吗？";
					next;
					if(select("不。","放弃任务。") == 1){
						mes .@npcname$;
						mes "我不确定你可以使用公会地下城多久，但祝你好运。";
						close;
					} else {
						mes .@npcname$;
						mes "嗯，没人强迫你。";
						mes "做任何你喜欢做的事。";
						mes "那我就取消任务了";
						erasequest .daily_hunting_questid[.@index];
						close;
					}
				}
			} else {
				if(.@playtime == 0 || .@playtime == 1){
					mes .@npcname$;
					mes "这里设置的规则是每天只能完成一项任务。";
					mes "我希望您在等待时间结束后回来。";
					close;
				} else if(.@playtime == 2){
					erasequest .daily_delay_questid[.@index];
					mes .@npcname$;
					mes "我已经确认这个任务的等待时间已经结束了。";
					mes "我将删除用于确认的记录。";
					mes "现在，尝试一下来完成任务吧！";
					close;
				} else {
					mes .@npcname$;
					mes "这是" + .ordinal$[.@sel] + "中的" + .region$[.@zoneid] + ".";
					brief(.daily_hunting_questid[.@index]);
					next;
					mes .@npcname$;
					if(mob_brief(.daily_hunting_questid[.@index]))
						mes "完成这个任务后我会给予你相应的奖励。";
					else
						mes "我会给你经验值作为回报。";
					next;
					if(select("接受它。","拒绝它。") == 1){
						mes .@npcname$;
						mes "公会地下城的入口受到限制。但这并不是说你找不到方法去那里。";
						mes "我会祈祷你能成功对抗那些怪物。";
						setquest .daily_hunting_questid[.@index];
						close;
					} else {
						mes .@npcname$;
						mes "那就如你所愿吧。";
						close;
					}
				}
			}
			break;
		case 3: // About quests
			mes .@npcname$;
			mes "这一点也不难。";
			mes "我根据你征服的堡垒数量给予奖励^4d4dff。^000000";
			mes "^4d4dff奖励的类型和质量取决于我查看时征服的要塞数量。^000000";
			next;
			mes .@npcname$;
			mes "当然，我只在围攻时间内检查。";
			mes "^4d4dff收到奖励后需要一个小时的等待时间。所以你最好也找出正确的时机。^000000";
			next;
			mes .@npcname$;
			mes "如果你是正在参加攻城战的公会高手，不妨一试。";
			next;
			mes .@npcname$;
			mes "公会地下城的每日任务包括可以在公会地下城中完成的战斗任务。";
			mes "这很简单。";
			close;
		case 4: // I don't need it.
			mes .@npcname$;
			mes "你还真是不一样啊~！";
			close;
	}
	end;

OnInit:
	setarray .castle$[0],
		"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05",
		"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",
		"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",
		"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05",
		"arug_cas01","arug_cas02","arug_cas03","arug_cas04","arug_cas05",
		"schg_cas01","schg_cas02","schg_cas03","schg_cas04","schg_cas05",
		"te_aldecas1","te_aldecas2","te_aldecas3","te_aldecas4","te_aldecas5",
		"te_prtcas01","te_prtcas02","te_prtcas03","te_prtcas04","te_prtcas05";
	setarray .npcname$[0],
		"[Altir]","[Alshine]","[Denev]","[Tarazed]","[Mirah]","[Almark]","[Becrux]","[Acrux]";
	setarray .region$[0],
		"Valkyrie Realm","Greenwood Lake","Britoria","Luina","Valfreyja","Nidhoggur","Kafragarten","Gloria";
	setarray .weekday[0], // Day of the week. 0 = Sunday, 6 = Saturday.
		0,0,0,0,0,0,6,6;
	setarray .start_time[0],
		2200,2200,2200,2200,2200,2200,2200,2200;
	setarray .end_time[0],
		2230,2230,2230,2230,2230,2230,2230,2230;
	setarray .conquer_delay_questid[0], // Quest ID for cooldowns
		7517,7520,7519,7518,7522,7521,7524,7523;
	setarray .level_req[0], // Level requirement for quests. There are 2-3 quests per region
		100,120,120,	//prt
		100,120,120,	//pay
		100,120,120,	//gef
		100,120,120,	//alde
		90,90,0,		//aru
		90,90,0,		//sch
		70,70,0,		//tealde
		70,70,0;		//teprt
	setarray .max_level[0], // Exp increase cap for quests. 0 for unlimited/not exist
		130,140,0,	//prt
		130,140,0,	//pay
		130,140,0,	//gef
		130,140,0,	//alde
		120,120,0,	//aru
		120,120,0,	//sch
		99,99,0,	//tealde
		99,99,0;	//teprt
	setarray .daily_hunting_questid[0], //Quest ID for daily quests.
		7525,7526,7527,	//prt
		7534,7535,7536,	//pay
		7531,7532,7533,	//gef
		7528,7529,7530,	//alde
		7539,7540,0,	//aru
		7537,7538,0,	//sch
		7561,7562,0,	//tealde
		7541,7542,0;	//teprt
	setarray .daily_delay_questid[0],	//Quest ID for mob hunting check.
		7543,7544,7545,	//prt
		7552,7553,7554,	//pay
		7549,7550,7551,	//gef
		7546,7547,7548,	//alde
		7557,7558,0,	//aru
		7555,7556,0,	//sch
		7563,7564,0,	//tealde
		7559,7560,0;	//teprt
	setarray .ordinal$[0],"first","second","third";
	end;

	function brief {
		switch(getarg(0)){
			case 7527:
			case 7530:
			case 7533:
			case 7536:
				mes "就是为了对付出现在深渊回廊中的强大生物。";
				break;
			default:
				mes "就是消灭公会副本中出现的怪物。";
				break;
		}
		return;
	}

	function mob_brief {
		switch(getarg(0)){
			case 7525:
				mes "您应该猎杀 50 只或更多毛毛虫和奶油恐惧怪物。";
				break;
			case 7526:
				mes "追捕全部 3 种深渊狗头人中的 30 只或更多。";
				break;
			case 7527:
				mes "^4d4dff你应该杀死愤怒的'Pyuriel'和战士'Lora'。^000000";
				return 1;
			case 7528:
				mes "^4d4dff分别狩猎 50 只或更多巨型黄蜂和远古蠕虫。^000000";
				break;
			case 7529:
				mes "^4d4dff猎杀 40 只或更多杀手螳螂和 50 只或更多安格拉螳螂。^000000";
				break;
			case 7530:
				mes "她们是^4d4dff“埃尔维拉”和“乔亚”，这是英雄眼泪的奇怪机械生物。^000000";
				return 1;
			case 7531:
				mes "^4d4dff分别狩猎 50 个或更多僵尸大师和幽灵死者。^000000";
				break;
			case 7532:
				mes "^4d4dff猎杀 50 个 GLD_DARK_SHADOW、20 个 GLD_DARK_FRAME 和 20 个 GLD_DARK_PRIEST 怪物。^000000";
				break;
			case 7533:
				mes "他们是在死者山上徘徊的^4d4dff“卡德斯”和“鲁多”。^000000";
				return 1;
			case 7534:
				mes "^4d4dff分别狩猎 50 个或更多 Gullinbursti 和 Leib Olmai。^000000";
				break;
			case 7535:
				mes "^4d4dff分别狩猎 45 个或更多骷髅将军、20 个或更多 Am Mut 和 25 个或更多 Gajomart。^000000";
				break;
			case 7536:
				mes "^4d4dff他们是旧帝国的将军“大贤”和守卫“素宪”。^000000";
				return 1;
			case 7537:
				mes "^4d4dff分别狩猎 50 个或更多地狱启示录和扎古达姆。^000000";
				break;
			case 7538:
				mes "^4d4dff分别寻找 35 个或更多重金属和钴矿物。^000000";
				break;
			case 7539:
				mes "^4d4dff狩猎超过 50 名女妖大师和 30 名眼魔大师。^000000";
				break;
			case 7540:
				mes "^4d4dff狩猎 50 个奥诺斯和 20 个法纳特。^000000";
				break;
			case 7541:
				mes "^4d4dff分别狩猎 30 个或更多骑士、铁匠、刺客。^000000";
				break;
			case 7542:
				mes "^4d4dff分别狩猎30个或更多巫师、牧师、猎人。^000000";
				break;
			case 7561:
				mes "^4d4dff分别狩猎30个或更多，十字军、盗贼、炼金术士。^000000";
				break;
			case 7562:
				mes "^4d4dff分别狩猎 30 个或更多贤者、武僧、吟游诗人。^000000";
				break;
		}
		return 0;
	}
	
	function calcexp {
		.@zoneid = getarg(0);
		.@index = getarg(1);
		.@maxlevel = .max_level[.@index];
		.@isboss = (.@zoneid < 4 && !((.@index+1)%3));
		
		if(!.@isboss && BaseLevel > .@maxlevel){
			return 1000 * (100 + ((.@maxlevel - 100) * 3));
		} else {
			return 1000 * (100 + ((BaseLevel - 100) * 3)) * (1 + .@isboss);
		}
	}
	
	function calcjobexp {
		.@edition = getarg(0);
		
		if(.@edition == 3){
			if(jobcanentermap("te_prtcas01")){
				if (JobLevel > 50)
					return 1000 * (50 + ((JobLevel - 50) * 3));
				else
					return 5000;
			} else {
				debugmes "Unexpected case for Job: " + Class + "(" + jobname(Class) + ") for gld_mission#core::calcjobexp";
				return 0;
			}
		} else if(.@edition == 2 ) {
			if(JobLevel > 50)
				return 1000 * (50 + ((JobLevel - 50) * 3));
			else
				return 3000;
		} else {
			return 5000 * JobLevel;
		}
		return 0;
	}
}
