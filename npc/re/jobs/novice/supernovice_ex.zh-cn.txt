//===== rAthena Script ======================================= 
//= Expanded Super Novice Job Quest
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change quest for Expanded Super Novice class.
//===== Additional Comments: ================================= 
//= 1.0 All dialogue is custom, contains some incorrect behaviors.
//= 1.1 Switched to a translated copy of the official script
//=     by Gennosuke Kouga, but vastly edited its content. [Euphy]
//= 1.1a Replaced effect numerics with constants. [Euphy]
//============================================================ 

function	script	Esseray_Ex	{
	if (BaseLevel > 98 && JobLevel > 98) {
		if (EXSUPNOV_Q > 0 && EXSUPNOV_Q < 6) {
			mes "[论文]";
			mes "新手界的一位传奇人物为了在科摩多做事而消失了。";
			mes "你应该找到他。";
			next;
			mes "[论文]";
			mes "他可以引导你前行";
			mes "正确的路径...";
			close;
		}
		mes "[论文]";
		mes "哦？你是我们新手社团的一员吗？";
		mes "你看起来不错。";
		next;
		mes "[论文]";
		mes "你看上去精力充沛...";
		mes "你看起来足够坚强，能够经受住考验。";
		next;
		mes "[论文]";
		mes "我知道一个让自己变得更强的方法。你愿意尝试一下吗？";
		next;
		if(select("不，我现在很好：我想变得更强！") == 1) {
			mes "[论文]";
			mes "如果你满足于平庸，我也帮不了你。";
			mes "那就过正常人的生活吧。";
			close;
		}
		mes "[论文]";
		mes "好的！";
		mes "新手界的一位传奇人物为了在科摩多做事而消失了。";
		mes "你应该找到他。";
		next;
		setquest 5092;
		set EXSUPNOV_Q,1;
		mes "[论文]";
		mes "他可以引导你前行";
		mes "正确的路径...";
		close;
	}
	else return;
}

beach_dun2,119,92,4	script	Fishing Novice#sp2	567,{
	if (checkquest(5092) >= 0) {
		mes "[钓鱼新手]";
		mes "嘘---------!!";
		mes "请保持安静。";
		mes "你会吓到鱼的。";
		next;
		select("嗯……抱歉……");
		mes "[钓鱼新手]";
		mes "啊啊啊啊！！";
		mes "你在干什么？";
		mes "我告诉过你要安静！";
		mes "他们因为噪音而游走了~";
		next;
		mes "[钓鱼新手]";
		mes "(--嘘--我的--)";
		mes "你……是谁？";
		mes "你为什么要打扰我钓鱼？";
		next;
		select("我收到了来自埃瑟雷的快递...");
		emotion ET_SCRATCH;
		mes "[钓鱼新手]";
		mes "什么？散文？散文？";
		mes "唔！我认识他...";
		mes "埃塞雷……埃塞雷……嗯。";
		next;
		emotion ET_THINK, playerattached();
		emotion ET_THINK;
		mes "..................";
		next;
		emotion ET_SURPRISE;
		mes "[钓鱼新手]";
		mes "啊啊——！！ -----啊啊啊----！！";
		mes "知道了！ - - 知道了！";
		mes "^0000FF毕宿五新手协会的成员。^000000";
		mes "论文，对吗？错误的？";
		next;
		if(select("不，他不是：是的，对") == 2) {
			mes "[钓鱼新手]";
			mes "呼呼~我的感觉还活着……";
			next;
			mes "[钓鱼新手]";
			mes "那你想从我这里得到什么？";
		}
		else {
			mes "[钓鱼新手]";
			mes "什么？！他不是吗？";
			mes "那么埃瑟雷来自哪里？";
			next;
			emotion ET_THINK, playerattached();
			emotion ET_THINK;
			mes "……………………";
			next;
			mes "[钓鱼新手]";
			mes "什么  -  -  -  -  ！！";
			mes "这并不重要，对吧？";
			next;
			mes "[钓鱼新手]";
			mes "然后  -  -  -  -  ！！";
			mes "你为什么来这里？";
		}
		next;
		mes "- 你解释一下你为什么来这里。 -";
		next;
		emotion ET_SCRATCH;
		mes "[钓鱼新手]";
		mes "嗯……这是一个既简单又困难的问题。";
		next;
		mes "- 你听到一个声音在抱怨某事。 -";
		next;
		emotion ET_FRET;
		mes "[钓鱼新手]";
		mes "论文！这样一个%@&$#的儿子破坏了我的假期......";
		next;
		mes "[钓鱼新手]";
		mes "哈哈哈哈~";
		mes "我可怜的生活~";
		mes "啊啊~问题太多了。";
		next;
		emotion ET_QUESTION;
		mes "[钓鱼新手]";
		mes "太～难了。嘿，小孩子！";
		mes "你叫什么名字？";
		next;
		select(strcharinfo(0)+"， 你呢？");
		mes "[诺多]";
		mes "我的名字是诺多尔！诺多尔。";
		next;
		mes "[诺多]";
		mes "………………";
		next;
		mes "[诺多]";
		mes "嘿！从你的表情我就能看出你在想什么~";
		next;
		mes "[诺多]";
		mes "自从我叫诺多以来，我一生都被取笑。";
		mes "秃鹰和气味...";
		mes "别试图愚弄我。";
		next;
		changequest 5092,5093;
		mes "[诺多]";
		mes "不管是什么原因……你都在这里！";
		mes "让我们齐心协力，走得更远。";
		mes "过段时间再来吧~";
		next;
		mes "- 再和他说话。 -";
		close;
	}
	else if (checkquest(5093) >= 0) {
		mes "[诺多]";
		mes "嗯嗯-----!!";
		next;
		mes "- 诺多尔用怀疑的眼神看着你。 -";
		next;
		mes "[诺多]";
		mes "你不知道^0000FF超级新手^000000很优秀吗？";
		next;
		mes "——诺多尔自信地问道。 -";
		next;
		mes "[诺多]";
		mes "嘿！超级新手~";
		mes "你知道我是谁吗？";
		next;
		select("你在说什么？！");
		mes "[诺多]";
		mes "呵呵...";
		mes "你很有勇气，不是吗？";
		next;
		select("... ..... ...");
		mes "- 这家伙是个怪人。 -";
		next;
		mes "[诺多]";
		mes "嘿！超级新手~";
		mes "我会让你变得更强！";
		mes "那么你会相信我告诉你的话吗？";
		next;
		if(select("我不会跟着你：为什么不呢？咳咳~") == 1) {
			mes "[诺多]";
			mes "拒绝？你不想坚强吗？";
			mes "我也愿意什么都不做。";
			mes "如果你想去的话就让我去钓鱼吧。";
			close;
		}
		mes "[诺多]";
		mes "emmmmm~我的语气是不是很刺耳？";
		mes "嗯，我忍不住用这样的词。";
		next;
		mes "[诺多]";
		mes "别出汗。";
		mes "我教你变强是没问题的。";
		next;
		mes "[诺多]";
		mes "太棒了~！";
		mes "你的问题是什么？";
		mes "你不知道如何变得更强。正确的？";
		next;
		select("是的，我无法变得更强。");
		emotion ET_SCRATCH;
		mes "[诺多]";
		mes "嗯嗯~！";
		mes "有一种方法可以做到...";
		mes "虽然很危险，但我可以";
		mes "告诉你怎么做。";
		next;
		mes "[诺多]";
		mes "我只会在一个条件下这样做。你什么都不问，你就按照我告诉你的去做！";
		mes "怎么样？";
		mes "你愿意这样做吗？";
		next;
		if(select("不，我不会！：让我们这样做吧！") == 1) {
			mes "[诺多]";
			mes "坚强，才是活下去的唯一出路！";
			mes "你想永远当弱者吗？";
			next;
			mes "[诺多]";
			mes "我会给你一个提示。";
			mes "你不可能总是对的。";
			mes "有时你需要检查你的自我并倾听别人的意见。";
			emotion ET_FRET;
			close;
		}
		mes "[诺多]";
		mes "好的  -  -  -  - - ！！";
		mes "你的热情会让你变得更坚强。";
		mes "你冷静吗？";
		next;
		mes "[诺多]";
		mes "我要你做的是...";
		mes "狩猎 1,000 只怪物。";
		next;
		mes "[诺多]";
		mes "你知道这是做什么用的吗？";
		next;
		setarray .@Monsters$[0],"Poring","Lunatic","Fabre","Picky";
		set .@i, rand(4);
		changequest 5093,5094+.@i;
		mes "[诺多]";
		mes "你知道^FF0000"+.@Monsters$[.@i]+"^000000怪物是什么，对吧？";
		next;
		mes "[诺多]";
		mes "狩猎 1,000！";
		mes "不要说你做不到！";
		mes "我知道怪物很多，但他们太弱了！";
		next;
		select("你在开玩笑吧？");
		mes "[诺多]";
		mes "呵呵~！";
		mes "记住，没有问题！";
		mes "猎杀 1,000 只之前不要回来。";
		close;
	}
	else if (checkquest(5094) >= 0 || checkquest(5095) >= 0 || checkquest(5096) >= 0 || checkquest(5097) >= 0) {
		mes "["+strcharinfo(0)+"]";
		mes "嘿，诺多尔！";
		next;
		emotion ET_HUK;
		mes "[诺多]";
		mes "我的天啊！";
		mes "好害怕……让我看看？";
		next;
		if (checkquest(5094,HUNTING) != 2 && checkquest(5095,HUNTING) != 2 && checkquest(5096,HUNTING) != 2 && checkquest(5097,HUNTING) != 2) {
			mes "[诺多]";
			mes "嘿！超级新手！";
			mes "我不是说要猎杀1000只吗？";
			mes "在你回来之前不要回来。";
			close;
		}
		mes "[诺多]";
		mes "你已经把 1,000 只都猎完了吗？";
		next;
		mes "[诺多]";
		mes "你比我想象的还要好。";
		mes "不管怎样，我让你去猎杀的怪物是什么？";
		next;
		set .@i, select("波利:疯子:法布尔:挑剔:巴风特")-1;
		if (.@i == 4) {
			mes "[诺多]";
			mes "呵呵？你真的能独自追捕巴风特吗？";
			mes "不要说谎！！我就打你！！";
			close;
		}
		if (checkquest(5094+.@i,HUNTING) != 2) {
			mes "[诺多]";
			mes "不要说谎！";
			mes "我没叫你去狩猎这个！";
			close;
		}
		changequest 5094+.@i,5098;
		emotion ET_SMILE;
		mes "[诺多]";
		mes "咳咳咳咳~";
		next;
		mes "[诺多]";
		mes "你惹恼了我，所以我让你做这个任务......";
		next;
		mes "[诺多]";
		mes "哇！你真的把 1000 只都猎杀了吗？";
		mes "我想我不能质疑你变得更坚强的热情。";
		next;
		mes "[诺多]";
		mes "好的！到目前为止，一切都很好。";
		mes "我会让你知道如何克服你的力量缺陷。";
		close;
	}
	else if (checkquest(5098) >= 0) {
		if (rand(10) != 1) {
			mes "[诺多]";
			mes "哦！你已经来过这里了吗？";
			mes "你不是说你有事要处理吗？";
			mes "你有足够的热情，但是...";
			mes "你需要学会耐心。";
			close;
		}
		emotion ET_KIK;
		mes "[诺多]";
		mes "哈哈哈哈~";
		next;
		mes "[诺多]";
		mes "哦，对不起！对不起！";
		mes "我很久以前收到了我哥哥的一封信，你能读一下我的信以供参考吗？";
		next;
		if(select("是的，我会：不，我不会") == 2) {
			mes "[诺多]";
			mes "你应该多读书！";
			next;
		}
		else {
			mes "["+strcharinfo(0)+"]";
			mes "啧*……好吧……";
			next;
		}
		mes "- 他向你扔了一封捆绑的信。 -";
		next;
		mes "亲爱的兄弟。诺多尔，";
		mes " ";
		mes "嘿兄弟，这是你的兄弟努多尔。你最近怎么样？我写这封信的原因是...";
		mes "我需要钱买食物。你有那么多钱，帮帮我吧。";
		next;
		mes "如果你不...还记得上次你%$&*@*&%$...";
		mes "- 这封信皱巴巴的，所以你无法阅读更多内容。 -";
		mes "我还在黑森林共和国。";
		mes "〜努多尔";
		next;
		mes "- 读了这封信，你有一种不安的感觉。 -";
		next;
		mes "[诺多]";
		mes "呼~呼~";
		mes "嗯，我只有一个兄弟，他总是要钱。";
		mes "这一定是我的名气...T_T";
		next;
		mes "——现在你确实有一种不安的感觉。 -";
		next;
		changequest 5098,5099;
		mes "[诺多]";
		mes "嘿-超级新手"+strcharinfo(0)+"！";
		next;
		mes "[诺多]";
		mes "你可以给我兄弟一些。他在飞艇上。";
		close;
	}
	else if (checkquest(5099) >= 0) {
		mes "[诺多]";
		mes "你已经去过了吗？";
		next;
		select("我应该给多少钱？");
		mes "[诺多]";
		mes "哦，你还没去那儿吗？";
		mes "给他一点吧~";
		next;
		mes "[诺多]";
		mes "10万zeny就可以了~";
		mes "1,000,000 zeny 就可以了~";
		mes "10,000,000 zeny 就可以了~";
		close;
	}
	else if (checkquest(5100) >= 0) {
		if (EXSUPNOV_Q_ == 3) set .@n,300000;
		else if (EXSUPNOV_Q_ == 4) set .@n,1000000;
		else set .@n,100000;
		mes "[诺多]";
		mes "嘿，你去过那里吗？";
		mes "我又收到了我兄弟的消息。";
		next;
		mes "兄弟，有人给我...";
		mes callfunc("F_InsertComma",.@n)+" Zeny。";
		mes "你确实关心我...";
		mes "〜努多尔";
		next;
		select("他看起来好饿……");
		mes "[诺多]";
		mes "顺便一提...";
		mes callfunc("F_InsertComma",.@n)+" 考虑到我的名人身份，Zeny 太小了，你不觉得吗？";
		next;
		select("呃……");
		mes "[诺多]";
		mes "无论如何~";
		mes callfunc("F_InsertComma",.@n)+" Zeny...";
		mes "你不觉得你给他的太少了吗？我的名誉现在就要受到打击了。";
		next;
		select("嘿...");
		mes "[诺多]";
		mes "哇，你已经明白我的意思了吗？";
		next;
		mes "[诺多]";
		mes "我认为你是对的。";
		mes "这个测试是关于你是否有能力打开别人的同情心。";
		next;
		mes "[诺多]";
		mes "我指的是你的消费习惯和你的直觉......";
		mes "哈哈哈哈！";
		next;
		select("嘿，这不是我的...");
		mes "[诺多]";
		mes "唔？啊...";
		mes "我差点忘了告诉";
		mes "你如何超越你的极限，";
		mes "我没有吗？";
		next;
		mes "[诺多]";
		mes "呃哼！我发送给你之​​后不仅仅是为了消磨时间！";
		next;
		emotion ET_HUK;
		mes "[诺多]";
		mes "什么？ ---啊？ ---!!";
		next;
		select("??????");
		emotion ET_SWEAT;
		mes "[诺多]";
		mes "当心！一群魔像正在追我们​​！";
		mes "我们……必须跑……！！！";
		next;
		emotion ET_HUK, playerattached();
		if(select("什么？！：嗯？") == 2) {
			mes "- 你环顾四周，然后 -";
			mes "- 你身后什么也没有。 -";
			mes "- 当你快要怀疑的时候... -";
		}
		else {
			mes "- 你问过诺多尔一次 -";
			mes "- 再次让你感到害怕...... -";
		}
		next;
		if (SkillPoint) {
			mes "[诺多]";
			mes "你还有一些技能点没有用完。";
			mes "在你使用这些点数之前我无法让你变得更强。";
			close;
		}
		specialeffect2 EF_LORD;
		specialeffect2 EF_SIGHTRASHER;
		specialeffect2 EF_STORMGUST;
		erasequest 5100;
		mes "[诺多]";
		mes "哈哈-------------!!";
		mes "-----呃哈哈哈-----！！";
		mes "-  -  -  -  -  -  耶  -  ！！";
		next;
		nude;
		callfunc "F_ClearJobVar"; // -- Clears all job variables for the current player
		jobchange roclass(eaclass()|EAJL_THIRD);
		set EXSUPNOV_Q,5;	
		emotion ET_SMILE;
		mes "[诺多]";
		mes "普哈哈哈哈哈哈！！";
		mes "好的！你害怕了吗？";
		next;
		select("... ... ...");
		emotion ET_THINK, playerattached();
		emotion ET_SORRY;
		mes "[诺多]";
		mes "抱歉吓到你了！";
		mes "我认为，作为新手，我们的职责和使命就是让日子变得快乐。";
		next;
		mes "[诺多]";
		mes "那么，你不觉得有些事情发生了变化吗？";
		mes "感觉身体比以前更轻了吗...？";
		mes "或者更强大？";
		next;
		select("...? ...? ...?");
		emotion ET_QUESTION, playerattached();
		mes "[诺多]";
		mes "哦——我的——（啧啧啧）！！";
		next;
		emotion ET_SCRATCH;
		mes "[诺多]";
		mes "超级笨先生——";
		mes "并不是刻薄，但是";
		mes "你有点迟钝...";
		mes "你现在不觉得不一样了吗？";
		next;
		if(select("你在说什么？：我明白了！") == 1) {
			mes "[诺多]";
			mes "呃——对了，你很迟钝吧？";
			mes "你已经超出了你的极限。";
			next;
			mes "[诺多]";
			mes "你可以比以前更坚强。";
			mes "这不是你的梦想吗？";
			next;
		}
		mes "[诺多]";
		mes "好的~！";
		mes "现在就向世界进军，以新手的身份，享受自由、快乐的生活吧！";
		mes "就是这样！";
		close;
	}
	else if (EXSUPNOV_Q == 5) {
		mes "[诺多]";
		mes "（噘嘴）-------------！！";
		mes "所以你终于回来了。";
		next;
		mes "[诺多]";
		mes "为了庆祝这位新的新手同志，我准备了一些装备送给你们——";
		next;
		mes "[诺多]";
		mes "别忘了确保你有足够的^FF0000库存^000000来从我这里拿走这些物品。";
		mes "别说我也没有警告过你。";
		next;
		if(select("是的，让我检查一下我的库存。:我很好，给我看看。") == 1) {
			mes "[诺多]";
			mes "好吧，好吧！";
			mes "你知道！不要后悔。";
			close;
		}
		mes "[诺多]";
		mes "还有我小时候穿的衣服";
		mes "但他们仍然很好。";
		if (EXSUPNOV_Q_ == 3) getitem 2522,1; // Undershirt
		else if (EXSUPNOV_Q_ == 4) {
			getitem 2628,1; // Novice Armlet [1]
			getitem 2113,1; // Novice Shield [1]
			getitem 2416,1; // Novice Shoes [1]
			getitem 5119,1; // Super Novice Hat [1]
			getitem 2512,1; // Novice Manteau [1]
			getitem 2340,1; // Novice Breastplate [1]
		}
		else getitem 2340,1; // Novice Breastplate [1]
		next;
		select("二手货吧？");
		mes "[诺多]";
		mes "把它们还给我。";
		mes "当我向你提供我的财产时，你怎么会有这样的反应？";
		next;
		mes "[诺多]";
		mes "我...";
		mes "我无法收回我已经放弃的东西。";
		next;
		mes "[诺多]";
		mes "现在我要再次向大家致以问候。";
		next;
		specialeffect2 EF_RECOVERY;
		mes "[诺多]";
		mes "让超级新手"+strcharinfo(0)+"获得超级新手守护者的祝福！";
		set EXSUPNOV_Q,6;
		close;
	}
	else if (EXSUPNOV_Q > 5) {
		mes "[诺多]";
		mes "不要忘记你是一个新手。";
		mes "永远是新手！";
		mes "人生的目标就是拥有美好而正常的时光！";
		close;
	}
	mes "[钓鱼新手]";
	mes "嘘！";
	mes "请保持安静，因为你会";
	mes "把所有的鱼都吓跑了。";
	close;
}

airplane,33,47,5	script	Nudor#sp2	729,{
	if (checkquest(5099) >= 0) {
		mes "[努多尔]";
		mes "啊啊~没钱买吃的。";
		mes "我饿了，但不知道哥哥什么时候来……";
		next;
		select("嘿，你是……努多尔先生吗？");
		mes "[努多尔]";
		mes "诶诶诶？！谁想知道？";
		mes "我知道你？";
		next;
		mes "- 你向努多尔解释你为什么来看他。 -";
		next;
		mes "[努多尔]";
		mes "啊哈！你是来代替我哥给我零花钱的~";
		next;
		mes "[努多尔]";
		mes "哇哇哇~~他给了你多少钱？";
		next;
		mes "- 努多尔目光闪烁 -";
		mes "- 眼睛，伸出双手。 -";
		mes "- 你想捐多少钱？ -";
		next;
		set .@i, select("我没有钱：给100,000 Zeny：给300,000 Zeny：给1,000,000 Zeny：给你所有的钱");
		mes "[努多尔]";
		if (.@i == 1) {
			mes "-哭了-";
			mes "我讨厌诺多——！";
			mes "我也讨厌你——！";
			close2;
			warp "airplane",70,93;
			end;
		}
		else if (.@i < 5) {
			switch(.@i) {
				case 2: set .@n,100000; break;
				case 3: set .@n,300000; break;
				case 4: set .@n,1000000; break;
			}
			mes "可爱~~他给了你"+callfunc("F_InsertComma",.@n)+" Zeny吗？差不多了！";
			next;
			if (Zeny < .@n) {
				mes "[努多尔]";
				mes "什么？但是 "+callfunc("F_InsertComma",.@n)+" Zeny 在哪里？";
				close;
			}
		}
		else {
			mes "哦...哦天哪，你真的把你所有的 Zeny 都给了我吗？真的吗？";
			next;
			if(select("不，不是真的...：全力以赴") == 1) {
				mes "[努多尔]";
				mes "呸！多么令人失望啊！";
				close;
			}
			if (!Zeny) {
				mes "[努多尔]";
				mes "呃？但你没有钱。你真穷啊？";
				close;
			}
			set .@n,Zeny;
		}
		mes "[努多尔]";
		mes "他们会的！"+.@n+"泽尼！这对我很有帮助~Tehee~";
		mes "我简直不敢相信...";
		changequest 5099,5100;
		set Zeny, Zeny - .@n;
		if (.@i < 5) set EXSUPNOV_Q_, .@i;
		else {
			if (.@n > 1000000) set EXSUPNOV_Q_,4;
			else if (.@n > 300000) set EX_SUPNOV_Q_,3;
			else set EXSUPNOV_Q_,2;
		}
		next;
		mes "[努多尔]";
		if (Sex == SEX_MALE) mes "你可知道？你真的好帅啊~";
		else mes "我要告诉你，你真的…真的~美丽。";
		next;
		mes "[努多尔]";
		mes "- 你感觉你正在 -";
		mes "- 被骗了，所以回到诺多。 -";
		close;
	}
	else if (checkquest(5100) >= 0) {
		mes "[努多尔]";
		mes "我简直不敢相信我的兄弟给我寄了钱...我喜欢它~";
		next;
		mes "[努多尔]";
		mes "哎呀！";
		mes strcharinfo(0)+"，嘘——！！";
		next;
		mes "[努多尔]";
		mes "不要告诉诺多这件事~~";
		close;
	}
	mes "[努多尔]";
	mes "啊啊~没钱买吃的。";
	mes "我饿了，但不知道哥哥什么时候来……";
	close;
}
