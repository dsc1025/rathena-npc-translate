//===== rAthena Script =======================================
//= Shadow Blacksmith
//===== Description: =========================================
//= [Official Conversion]
//= Shadow equipments refining NPC.
//===== Changelog: ===========================================
//= 1.0 First version [Aleos]
//= 1.1 Removed re-roll behavior and fetch materials from db
//=     [Secret]
//= 1.2 Added Refine UI [Atemo, Lemongrass]
//= 1.3 Moved to Cash Mall [Lemongrass]
//============================================================
-	script	::ShadowBlacksmith	-1,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[暗影铁匠]";
		mes "现在巴斯塔负责暗影物品的精炼。";
		mes "如果你想精炼影子物品，请拜访他。";
		close;
	}

	.@zeny_cost = 200000; // Zeny cost is 200,000 according to official script [Secret]
	disable_items;
	mes "[暗影铁匠]";
	mes "你想精炼影子物品吗？选择你的毒药！";
	next;
	setarray .@indices[1], EQI_SHADOW_ARMOR, EQI_SHADOW_WEAPON, EQI_SHADOW_SHIELD, EQI_SHADOW_SHOES, EQI_SHADOW_ACC_R, EQI_SHADOW_ACC_L;
	.@indlen = getarraysize(.@indices) - 1;
	for(.@i = 1; .@i <= .@indlen; .@i++)
		.@menu$ = .@menu$ + (getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +"-[Not equipped]") +":";
	.@menu$ = .@menu$ + "Refine info";
	.@choice = select(.@menu$);
	if (.@choice == .@indlen + 1) { // Refine info
		mes "[暗影铁匠]";
		mes "当影子物品精炼后，它会像普通物品一样获得额外的奖励。";
		next;
		mes "[暗影铁匠]";
		mes "武器：精炼成功率每+1，攻击力、魔力+1。";
		mes "等等：每+1精炼成功，HP+10增加。";
		next;
		mes "[暗影铁匠]";
		mes "所有类型的 Oridecon 和 Elunium 都可以用来精炼影子物品。每次尝试也将花费 20,000 zeny。";
		next;
		mes "[暗影铁匠]";
		mes "HD矿石可用于精炼度至少为+7的装备，并且只要你继续跟我说话就可以防止损坏。";
		close;
	}

	.@part = .@indices[.@choice];

	if (!getequipisequiped(.@part)) {
		mes "[暗影铁匠]";
		mes "这里什么都没有！";
		close;
	}
	.@equip_id = getequipid(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	while(1) {
		.@equip_refine = getequiprefinerycnt(.@part);
		mes "[暗影铁匠]";
		mes "我需要 " + callfunc("F_InsertComma", .@zeny_cost) + " zeny 作为每次精炼尝试的费用。";
		mes "选择你的矿石并开始精炼。";
		next;

		.@material[0] = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);
		.@material[1] = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);
		.@material[2] = getequiprefinecost(.@part, REFINE_COST_HD, REFINE_MATERIAL_ID);
		.@is_enriched = false;

		if (countitem(.@material[0]))
			.@mate$[0] = getitemname(.@material[0]);
		else
			.@mate$[0] = "^8C8C8C"+ getitemname(.@material[0]) +"^000000";
		if (countitem(.@material[1]))
			.@mate$[1] = getitemname(.@material[1]);
		else
			.@mate$[1] = "^8C8C8C"+ getitemname(.@material[1]) +"^000000";
		if (.@equip_refine > 6 && countitem(.@material[2]))
			.@mate$[2] = getitemname(.@material[2]);
		else
			.@mate$[2] = "^8C8C8C"+ getitemname(.@material[2]) +"^000000";

		switch( select( .@mate$[0], .@mate$[1], .@mate$[2], "取消" ) ) {
		case 1:// NORMAL
			.@choose = .@material[0];
			break;
		case 2:// ENRICHED
			.@is_enriched = true;
			.@choose = .@material[1];
			break;
		case 3:// HD
			if (.@equip_refine < 7) {
				mes "[暗影铁匠]";
				mes "HD矿石只能用于+7或更高精炼度的物品。";
				close;
			}
			.@hoihoi = 1;
			.@choose = .@material[2];
			break;
		case 4:
			mes "[暗影铁匠]";
			mes "您已取消精炼。";
			close;
		}
		if (!countitem(.@choose)) {
			mes "[暗影铁匠]";
			mes "您没有足够的"+ getitemname(.@choose) +".";
			close;
		}
		if (Zeny < .@zeny_cost) {
			mes "[暗影铁匠]";
			mes "你没有足够的 zeny。";
			close;
		}
		if (.@equip_refine > 9) {
			mes "[暗影铁匠]";
			mes "暗影装备最多可以精炼到10...";
			close;
		}
		if (!getequipisenableref(.@part)) {
			mes "[暗影铁匠]";
			mes "该物品无法精炼。";
			close;
		}
		if (getequippercentrefinery(.@part) < 100) {
			mes "[暗影铁匠]";
			mes "暗影装备的安全精炼度+4。";
			if (!.@hoihoi)
				mes "暗影装备可能会在后续的精炼中被破坏。想开始吗？";
			else
				mes "下次精炼如果失败的话，精炼就会降级。你想精进吗？";
			next;
			if (select("继续","取消") == 2) {
				mes "[暗影铁匠]";
				mes "呵呵，我就知道了！";
				close;
			}
		}
		mes "[暗影铁匠]";
		mes "那么……这里什么也没有！";
		next;
		delitem .@choose,1;
		Zeny -= .@zeny_cost;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
			close;

		if (getequippercentrefinery(.@part, .@is_enriched) > rand(100)) {
			successrefitem .@part;
			mes "[暗影铁匠]";
			mes "成功了！成功了！";
			next;
		} else {
			if (.@hoihoi)
				downrefitem .@part;
			else
				failedrefitem .@part;
			mes "[暗影铁匠]";
			mes "哦奥丁不！";
			close;
		}
	}
}

//moc_paraup,45,185,5	duplicate(ShadowBlacksmith)	Shadow Blacksmith#eden1	4_F_JOB_BLACKSMITH // Commented out until it's added to the map index
//prt_in,61,54,3	duplicate(ShadowBlacksmith)	Shadow Blacksmith#itemmall	4_F_JOB_BLACKSMITH
itemmall,31,76,3	duplicate(ShadowBlacksmith)	Shadow Blacksmith#itemmall	4_F_JOB_BLACKSMITH
