//===== rAthena Script =======================================
//= Episode 17.2 - Sage's Legacy Enchants
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Sage's Legacy related merchants and enchanters
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//= 1.2 Cleanup [Capuche]
//===== Translated By: =======================================
//= dsc
//============================================================

ba_in01,87,370,3	script	Lisa#ep172_merchant	20698,{
	if (checkweight(1201,3) == 0) {
		mes "^ff0000您拥有的物品的类型或重量似乎太多了。请整理您的库存。^000000";
		close;
	}
	disable_items;
	mes "[丽莎]";
	mes "欢迎欢迎~";
	next;
	switch( select( "探索设备", "自动发动机机翼增强", "自动装甲增强", "自动腿部增强", "自动配件（R）增强", "自动配件（L）增强" ) ) {
	case 1:
		mes "[丽莎]";
		mes "如果您携带自动装备和强化模块，我们可以在装备上附魔相应的模块。";
		next;
		mes "[丽莎]";
		mes "这是按照规范模块完成的，因此设备不会被破坏。但是，升级次数可能因模块而异。";
		next;
		mes "[丽莎]";
		mes "以后需要帮助时请再次访问~";
		close;
	case 2:
		.@part = EQI_GARMENT;
		setarray .@equip_required[0],480020,480021;
		break;
	case 3:
		.@part = EQI_ARMOR;
		setarray .@equip_required[0],450127,450128;
		break;
	case 4:
		.@part = EQI_SHOES;
		setarray .@equip_required[0],470022,470023;
		break;
	case 5:
		.@part = EQI_ACC_R;
		setarray .@equip_required[0],490024,490026;
		break;
	case 6:
		.@part = EQI_ACC_L;
		setarray .@equip_required[0],490025,490027;
		break;
	}
	.@equip_id = getequipid(.@part);
	.@equip_name$ = getequipname(.@part);
	.@refine = getequiprefinerycnt(.@part);
	for ( .@i = 0; .@i < 4; ++.@i )
		.@card[.@i] = getequipcardid(.@part,.@i);

	mes "[丽莎]";
	mes "如果您携带自动装备和强化模块，我们可以在装备上附魔相应的模块。";
	next;
	mes "[丽莎]";
	mes strcharinfo(0) + ".";
	mes "请选择您要使用的模块等级。";
	next;
	switch( select( "取消", "普通等级", "稀有等级", "独特等级", "传奇等级", "史诗1级", "史诗2级", "史诗3级" ) ) {
	case 1:
		mes "[丽莎]";
		mes "停止谈话。";
		close;
	case 2:
		setarray .@module_etc[0],1000105,1000106,1000107,1000108,1000109,1000110,1000111,1000112;
		break;
	case 3:
		setarray .@module_etc[0],1000113,1000114,1000115,1000116,1000117,1000118,1000119,1000120,1000121,1000122,1000123,1000124,1000125,1000126,1000127;
		break;
	case 4:
		setarray .@module_etc[0],1000128,1000129,1000130,1000131,1000132,1000133,1000134,1000135,1000136,1000137,1000138,1000139,1000140,1000141,1000142,1000143,1000207,1000208;
		break;
	case 5:
		setarray .@module_etc[0],1000144,1000145,1000146,1000147,1000148,1000149;
		break;
	case 6:
		setarray .@module_etc[0],1000152,1000153,1000154,1000155,1000156,1000157,1000158,1000159,1000160,1000161,1000162,1000163,1000164,1000165,1000166,1000167,1000168;
		break;
	case 7:
		setarray .@module_etc[0],1000169,1000170,1000171,1000172,1000173,1000174,1000175,1000176,1000177,1000178,1000179,1000180,1000181,1000182,1000183,1000184,1000185;
		break;
	case 8:
		setarray .@module_etc[0],1000186,1000187,1000188,1000189,1000190,1000191,1000192,1000193,1000194,1000195,1000196,1000197,1000198,1000199,1000200,1000201,1000202;
		break;
	}
	.@size = getarraysize(.@module_etc);
	.@menu$ = "Cancel:";

	// Build the menu according to all the modules with the given grade in inventory
	for ( .@i = 0; .@i < .@size; ++.@i ) {
		if (countitem(.@module_etc[.@i]) < 1)
			.@menu$ += "^808080(Module Amount 0)^000000" + ":";
		else {
			.@item_name$ = getitemname(.@module_etc[.@i]);
			.@item_name$ = replacestr(.@item_name$, "Automatic Modification Module(", "");
			.@item_name$ = replacestr(.@item_name$, ")", "");
			.@menu$ += .@item_name$ + ":";
		}
	}
	.@s = select(.@menu$) - 2;
	if (.@s == -1) {
		mes "[丽莎]";
		mes "停止谈话。";
		close;
	}
	.@module_id = .@module_etc[.@s];

	if (countitem(.@module_id) < 1) {
		mes "[丽莎]";
		mes "您的库存中必须拥有一个与继续升级功能相匹配的模块。";
		close;
	}
	if (.@equip_required[0] != .@equip_id && .@equip_required[1] != .@equip_id) {
		mes "[丽莎]";
		mes "请确保您已配备了您想要增强的适当的自动化设备。";
		close;
	}
	if (.@card[1] > 0) {
		mes "[丽莎]";
		mes "该自动设备已达到最大容量。";
		mes "请尝试其他自动设备。";
		close;
	}
	if (.@module_id < 1000105 || .@module_id == 1000150 || .@module_id == 1000151 || (.@module_id >= 1000203 && .@module_id != 1000207 && .@module_id != 1000208)) {
		mes "[丽莎]";
		mes "发生未知错误。";
		close;
	}

	// Each part has a list of allowed module and a maximum allowed number of the same module
	switch( .@part ) {
	case EQI_GARMENT:
		setarray .@module_etc_data[0],1000105,3,1000106,3,1000125,2,1000126,2,1000127,2,1000135,1,1000142,1,1000208,1;
		break;
	case EQI_ARMOR:
		setarray .@module_etc_data[0],1000105,3,1000106,3,1000122,2,1000123,2,1000124,2,1000128,1,1000129,1,1000130,1,1000131,1,1000132,1,1000133,1,1000207,1,1000140,1;
		.@size = getarraysize(.@module_etc_data);

		// Epic enchants. Max 2 epic enchants of the same enchant allowed
		for (.@i = 1000152; .@i < 1000203; .@i++) {
			setarray .@module_etc_data[.@size], .@i, 2;
			.@size += 2;
		}
		break;
	case EQI_SHOES:
		setarray .@module_etc_data[0],
			1000105,3,1000106,3,1000119,2,1000120,2,1000121,2,1000134,1,1000141,1,
			1000144,1,1000145,1,1000146,1,1000147,1,1000148,1,1000149,1;	// Legendary enchants (must be unique on EQI_SHOES)
		break;
	case EQI_ACC_L:
	case EQI_ACC_R:
		setarray .@module_etc_data[0],1000107,3,1000108,3,1000109,3,1000110,3,1000111,3,1000112,3,1000113,2,1000114,2,1000115,1,1000116,1,1000117,1,1000118,1,1000136,1,1000137,1,1000138,1,1000139,1,1000143,1;
		break;
	}

	.@data_index = inarray(.@module_etc_data, .@module_id);

	if (.@data_index == -1) {
		mes "[丽莎]";
		mes "您不能将该模块附魔到该自动设备上。";
		close;
	}
	// Max number of the selected module allowed
	.@max_module = .@module_etc_data[.@data_index+1];
	
	// Bind the module etc to the specific enchant
	for ( .@i = 1000105; .@i < 1000203; ++.@i ) {
		if (.@i == 1000150 || .@i == 1000151)
			continue;
		.@auto_module[.@i] = 310082 + .@size_auto_module;
		.@size_auto_module++;
	}
	.@auto_module[1000207] = 310178;
	.@auto_module[1000208] = 310179;

	// Check the count of the corresponding enchant in card slots
	.@enchant_id = .@auto_module[.@module_id];
	if (.@enchant_id == 0) {
		mes "[丽莎]";
		mes "发生未知错误。";
		close;
	}
	if (countinarray(.@card[0], .@enchant_id) >= .@max_module) {
		mes "[丽莎]";
		mes "该自动化设备已经最大限度地增强了该模块的性能。";
		mes "请尝试另一个模块。";
		close;
	}
	mes "[丽莎]";
	mes "您确定要使用 <<<CLR1>>>" + .@equip_name$ + "^000000 来增强您的 ^000000" + getitemname(.@module_id) + "^000000 吗？此过程无法逆转。";
	next;
	if (select( "等等...不要", "我确定。做吧。" ) == 1) {
		mes "[丽莎]";
		mes "停止谈话。";
		close;
	}
	delitem .@module_id,1;

	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]))
		end;

	for ( .@i = 3; .@i > 0; .@i--) {
		if (.@card[.@i] == 0) {
			.@card[.@i] = .@enchant_id;
			break;
		}
	}
	delequip .@part;
	getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	mes "[丽莎]";
	mes "哇啊啊啊~我已经感受到你的自动装备的威力了~！";
	close;
}

//= Merchants
-	marketshop	ep172littlemanager00	-1,1000227:172:999999

ba_in01,87,386,3	script	Child Manager &#65533;&#65533;#ep172_pet	EP17_2_CHILD_ADMIN1,{
	if (checkweight(1201,3) == 0) {
		mes "- 你的物品太多了。";
		mes "请清理您的库存并重试。 -";
		close;
	}
	mes "[儿童经理&#65533;&#65533;]";
	mes "我有一大堆小东西！";
	next;
	switch( select( "小经理", "云棉", "小头饰", "取消" ) ) {
	case 1:
		mes "[儿童经理&#65533;&#65533;]";
		mes "如果你想和小经理一起玩，就需要30张Barmeal门票。";
		next;
		if (select( "交换。", "停止。" ) == 2) {
			mes "[儿童经理&#65533;&#65533;]";
			mes "那么，到了我玩的时间了！";
			close;
		}
		if (countitem(1000103) < 30) {
			mes "[儿童经理&#65533;&#65533;]";
			mes "就像我说的，如果你想和小经理一起玩，你需要30张Barmeal门票！";
			close;
		}
		mes "[儿童经理&#65533;&#65533;]";
		mes "和你的新朋友一起玩得开心！";
		delitem 1000103,30;
		getitem 9123,1;
		close;
	case 2:
		callshop "ep172littlemanager00";
		end;
	case 3:
		callshop "barter_Ep_17_2_C_Admin_Acc";
		end;
	case 4:
		mes "[儿童经理&#65533;&#65533;]";
		mes "那么，到了我玩的时间了！";
		close;
	}
	end;
}

//= Cube Shop
-	marketshop	ep172cubeshop00	-1,100251:10000000:-1,100252:30000000:-1

ba_in01,87,376,3	script	Cube Lane#ep172cube	4_EP17_GUARD_B,{
	mes "[方块巷]";
	mes "我们提供强化方块，可将您的^0000CD+4^000000幻象和自动装备瞬间提升至^FF0000+7^000000！";
	mes "瓦蒙特无疑是世界上最好的男人！";
	close2;
	callshop "ep172cubeshop00";
	end;
}

//= Module Shop
ba_in01,87,373,3	script	Spiera#ep172_soapstone	MD_ASSISTANT,{
	callshop "barter_EP17_2_EP";
	end;
}

//= Equipment Shop
ba_in01,87,383,3	script	Yeoncheong#equipment_exchange	4_EP17_CLEANER_W,{
	if (checkweight(1201,3) == 0) {
		cutin "ep172_omega.bmp",2;
		mes "[延清]";
		mes "袋子已经满了。在再次交谈之前，请检查您所携带物品的重量和数量。";
		close3;
	}
	disable_items;
	cutin "ep172_omega",2;
	mes "[延清]";
	mes "我们接受 Barmeal 门票，用于将您的幻象装备升级为自动装备。";
	mes "你想交换什么？";
	next;
	switch( select( "交换信息", "自动设备交换", "自动配件交换", "退出" ) ) {
	case 1:
		mes "[延清]";
		mes "您可以将^0000CD+9幻象装甲、引擎或腿部以及900张麦芽券^000000兑换成^FF0000自动装甲、引擎或腿部^000000。";
		next;
		mes "[延清]";
		mes "^FF0000自动配件^000000可以兑换^0000CD幻象配件和1500张Barmeal券^000000。";
		next;
		mes "[延清]";
		mes "你想要兑换的装备必须在你的库存里，然后跟我说。";
		mes "^FF0000仅携带一件您想要交换的装备并继续。^000000";
		close3;
	case 2:
		.@shop$ = "barter_auto_equip_1";
		break;
	case 3:
		.@shop$ = "barter_auto_equip_2";
		break;
	case 4:
		mes "[延清]";
		mes "亲爱的客人，祝你有美好的一天~";
		close3;
	}
	mes "[延清]";
	mes "^FF0000仅携带一件您想要交换的装备并继续。^000000";
	mes "如果你的库存中有多件装备，符合条件的话会随机兑换。";
	close2;
	cutin "",255;
	callshop .@shop$;
	end;
}

//= Device Module Exchange
ba_in01,87,380,3	script	Yecheon#ep172module_trader	4_EP17_CLEANER_W,{
	if (checkweight(1201,3) == 0) {
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "袋子已经满了。在再次交谈之前，请检查您所携带物品的重量和数量。";
		close3;
	}
	disable_items;
	cutin "ep172_omega",2;
	mes "[醴泉]";
	mes "我可以给你提供自动模块、物理自动提升装置、魔法自动提升装置。它将帮助您提高自动化设备的能力。";
	next;
	cutin "",255;
	switch( select( "更多信息。", "获取自动模块", "获取物理自动提升装置", "获取魔法自动提升装置", "取消" ) ) {
	case 1:
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "您可以用^0000CD90张Barmeal券^000000获得^FF0000自动模块^000000。我会给你一个随机的自动模块，不包括史诗级模块。";
		next;
		mes "[醴泉]";
		mes "我还提供^FF0000物理自动强化装置和魔法自动强化装置^000000。您可以用它兑换^0000CD45 Barmeal券^000000，或^0000CD1500000 Zeny^000000。";
		next;
		mes "[醴泉]";
		mes "那么，战斗专精的客人，就放心地提升自己的能力吧。";
		close3;
	case 2:
		.@price = 90;
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "我将用 1 个随机自动模块（不包括史诗级模块）来交换 " + .@price + " " + mesitemlink(1000103) + ".";
		next;
		cutin "",255;
		if (select( "兑换Barmeal券。", "不兑换" ) == 2) {
			mes "[醴泉]";
			mes "交易结束。";
			close3;
		}
		cutin "ep172_omega",2;
		if (countitem(1000103) < .@price) {
			mes "[醴泉]";
			mes "您没有足够的 Barmeal 门票用于兑换。";
			close3;
		}
		mes "[醴泉]";
		mes "我把它换成了一个随机的自动模块。";
		delitem 1000103,.@price;
		getitem rand(1000105,1000149),1;
		close3;
	case 3:
		.@price = 45;
		.@zeny = 1500000;
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "我会将《^FF00001个物理自动提升装置^000000换成" + .@price + " " + mesitemlink(1000103) + "或" + .@zeny + " Zeny。";
		next;
		cutin "",255;
		switch( select( "兑换 Barmeal 券。", "兑换 1500000 Zeny。", "不兑换。" ) ) {
		case 1:
			cutin "ep172_omega",2;
			if (countitem(1000103) < .@price) {
				mes "[醴泉]";
				mes "您没有足够的 Barmeal 门票用于兑换。";
				close3;
			}
			delitem 1000103,.@price;
			break;
		case 2:
			cutin "ep172_omega",2;
			if (Zeny < .@zeny) {
				mes "[醴泉]";
				mes "您没有足够的 Zeny 来进行兑换。";
				close3;
			}
			Zeny -= .@zeny;
			break;
		case 3:
			mes "[醴泉]";
			mes "交易结束。";
			close3;
		}
		mes "[醴泉]";
		mes "我用它兑换了随机的体质自动提升装置一件。";
		.@r = rand(100);
		if (.@r < 50)
			getitem 100164,1;
		else if (.@r < 80)
			getitem 100165,1;
		else
			getitem 100166,1;
		close3;
	case 4:
		.@price = 45;
		.@zeny = 1500000;
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "我会将《^FF00001个魔法自动强化装置^000000换成" + .@price + " " + mesitemlink(1000103) + "或" + .@zeny + " Zeny。";
		next;
		cutin "",255;
		switch( select( "兑换 Barmeal 券。", "兑换 1500000 Zeny。", "不兑换。" ) ) {
		case 1:
			cutin "ep172_omega",2;
			if (countitem(1000103) < .@price) {
				mes "[醴泉]";
				mes "您没有足够的 Barmeal 门票用于兑换。";
				close3;
			}
			delitem 1000103,.@price;
			break;
		case 2:
			cutin "ep172_omega",2;
			if (Zeny < .@zeny) {
				mes "[醴泉]";
				mes "您没有足够的 Zeny 来进行兑换。";
				close3;
			}
			Zeny -= .@zeny;
			break;
		case 3:
			mes "[醴泉]";
			mes "交易结束。";
			close3;
		}
		mes "[醴泉]";
		mes "我用它兑换了随机的一件魔法自动强化装置。";
		.@r = rand(100);
		if (.@r < 50)
			getitem 100167,1;
		else if (.@r < 80)
			getitem 100168,1;
		else
			getitem 100169,1;
		close3;
	case 5:
		mes "[醴泉]";
		mes "交易结束。";
		close3;
	}
	end;
}

ba_in01,87,389,2	script	Butterfly Merchant#ba_in01	4_EP17_MASTER_A,{
	if (ep17_2_main < 8) {
		mes "[蝴蝶商人]";
		mes "对不起。购买蝴蝶需要注册。";
		close;
	}
	mes "[蝴蝶商人]";
	mes "欢迎。";
	mes "您需要翅膀才能搬到巴蒙德大厦吗？";
	close2;
	callshop "barter_Teleport_Ep17_02";
	end;
}
