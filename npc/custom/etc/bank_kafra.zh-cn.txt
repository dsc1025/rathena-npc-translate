//===== rAthena Script =======================================
//= The 2nd Bank of Prontera (with daily 0.01% income!)
//===== By: ==================================================
//= Lupus (1.0)
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A bank which has an interest %
//===== Additional Comments: =================================
// Look for this formula and setup your Bank daily % interest
// #kafrabank/1000 = 0.1% of interest per day
// #kafrabank/100  =  1%  of interest per day
// #kafrabank/10   =  10% of interest per day
//
// 1.1 Added log of bank operation -> logmes "some info";
// 1.2 Set max income to 100,000z. It would help to avoid
//     zeny exploits when you change DATE at your server
// 1.2a Corrected bad duplicate names. (bugreport:921) [Samuray22]
//===== Translated By: =======================================
//= dsc
//============================================================

-	script	Bank Clerk::bankg	833,{
	mes"[马尼斯]";
	mes strcharinfo(0)+"，欢迎来到普隆德拉第二银行！";

	set @kb_int,(gettime(DT_MONTH)*31)+gettime(DT_DAYOFMONTH); //today's number
	set @income,0;
	//calculate %
	if (#kafrabank<=0 || #kb_int>=@kb_int) goto L_NoIncomeToday;
	set @income,(#kafrabank/1000)*(@kb_int-#kb_int); //@income == % of the sum
	//max income constant:
	if (@income>100000) set @income,100000;
L_NoIncomeToday:
	set #kb_int,@kb_int; //reset days timer

	if(#kafrabank==0) mes "我们可以为您开设一个帐户。";
	if(@income>0) mes "今天的收入：^135445" + callfunc("F_InsertComma",@income) + "^000000 zeny。";
	set #kafrabank,#kafrabank+@income;
	if(#kafrabank>0) mes "您的帐户：^135445" + callfunc("F_InsertComma",#kafrabank) + "^000000 zeny。";
	mes "你要什么？";
	next;
	if(#kafrabank==0) menu "-Open an account",-,"-Quit",B_EXIT2;
	if(#kafrabank>0) menu "-Deposit money",-,"-Withdraw money",M_WITHDRAW,"-Quit",B_EXIT2;

	mes"[马尼斯]";
	mes "请告诉我您想存入多少 zeny。";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1000) goto L_LESS_1000;
	if(@kafrabank>zeny) goto L_NOT_ENOUGH;
	set Zeny, Zeny-@kafrabank;
	set #kafrabank,#kafrabank+@kafrabank;
	mes"[马尼斯]";
	mes "您已存款^135445" + callfunc("F_InsertComma",@kafrabank) + "z^000000。";
	//we log these zeny operations into the log db
	logmes "Bank %: " + @income +"z, Deposit: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

M_WITHDRAW:
	if(#kafrabank==0) goto L_ZERO_ACCOUNT;
	mes"[马尼斯]";
	mes "您的帐户：^135445" + callfunc("F_InsertComma",#kafrabank) + "^000000 zeny。";
	mes "您想提取多少 zeny？";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1) goto B_EXIT2;
	if(@kafrabank>#kafrabank) goto L_NOT_ENOUGH;
	set #kafrabank,#kafrabank-@kafrabank;
	set Zeny, Zeny+@kafrabank;
	mes"[马尼斯]";
	mes "这是您的 ^135445" + callfunc("F_InsertComma",@kafrabank) + "z^000000，请在此处输入您的符号...";
	//we log these zeny operations into the log db
	logmes "Bank %: " + @income +"z, Withdraw: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

L_NOT_ENOUGH:
	mes"[马尼斯]";
	mes "您没有足够的 zeny 来执行此操作。";
	next;
	goto B_EXIT2;

L_ZERO_ACCOUNT:
	mes"[马尼斯]";
	mes "您的帐户上没有任何 zeny！";
	next;
	goto B_EXIT2;

L_TOO_BIG_AMOUNT:
	mes"[马尼斯]";
	mes "对不起。您一次最多可以存入 10,000,000 zeny。";
	next;
	goto B_EXIT2;

L_LESS_1000:
	mes"[马尼斯]";
	mes "很抱歉，您可以存入的最低 zeny 金额为 1,000 zeny。";
	next;
	goto B_EXIT2;

B_EXIT:
	mes "很好……快点再来吧！";
	next;

B_EXIT2:
	mes"[马尼斯]";
	mes "感谢您使用我们的银行服务。我们希望很快能再次见到您。";
	close;
}

prontera,131,190,1	duplicate(bankg)	Bank Clerk#1-1	833
geffen,125,73,3	duplicate(bankg)	Bank Clerk#2-1	833
izlude,145,107,1	duplicate(bankg)	Bank Clerk#3-1	833
morocc,147,84,7	duplicate(bankg)	Bank Clerk#4-1	833
