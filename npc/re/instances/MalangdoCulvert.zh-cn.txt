//===== rAthena Script ======================================= 
//= Malangdo Culvert
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Clean the culverts and defeat the Coelacanth.
//= Daily and weekly quests are available.
//= Contains a normal mode and hard mode.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//=     Letters "n" and "h" in NPC names indicate difficulty.
//= 1.0a There is no minimum party size on official servers.
//= 1.0b Fixed incorrect use of 'close'. [Joseph]
//= 1.1 Instance system rewrite. [Euphy]
//===== Translated By: =======================================
//= dsc
//============================================================ 

// Quest NPCs :: mal_yong
//============================================================
mal_in01,172,28,2	script	Albo#mal	561,{
	if (checkweight(1201,1) == 0) {
		mes "你有太多种类的物体。减少这些物体后尝试继续。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你有太多重物。减轻体重后尝试继续。";
		close;
	}
	if (BaseLevel < 90) {
		mes "[或]";
		mes "你是谁？";
		mes "我们不需要弱化！！";
		close;
	}
	setarray .@quests[0],12271,12272,12273,12274;
	setarray .@names$[0],
		"General Culvert Daily Service","^990000Hard Culvert Daily Service^000000",
		"General Culvert Weekly Service","^990000Hard Culvert Weekly Service^000000";
	set .@menu$,"Why are doing this job?:";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		if (checkquest(.@quests[.@i],PLAYTIME) > -1) {
			set .@status[.@i],2;
			set .@menu$, .@menu$+"^aaaaaa- There is no new task -^000000:";
		} else if (.@i%2 && BaseLevel < 140) {
			set .@status[.@i],0;
			set .@menu$, .@menu$+"^aaaaaaCan't take this job because level is too low^000000:";
		} else {
			set .@status[.@i],1;
			set .@menu$, .@menu$+.@names$[.@i]+":";
		}
	}
	mes "[或]";
	mes "我们跨越了大海！";
	mes "我们是涵洞的伟大英雄！我们不原谅针对涵洞的海鲜！";
	next;
	set .@i, select(.@menu$)-1;
	if (.@i == 0) {
		mes "[或]";
		mes "我们曾经是失踪先生的追随者！我们几乎成为了这样一个历史遗址的守护者。";
		next;
		mes "[或]";
		mes "然而，当海水淹没了所有宝贵的资源时，我的梦想……我所有的梦想都消失了……";
		next;
		mes "[或]";
		mes "我们不会原谅入侵我们领土的海鲜。我要和辣酱一起吃！！！";
		next;
		mes "^0000ff阿尔博的声音因愤怒而颤抖。当他提到辣酱时，他咂了咂嘴。^000000";
		close;
	}
	switch(.@status[.@i-1]) {
	case 0:
		mes "[或]";
		mes "这个任务对你来说真的很难，所以我不能告诉你。你为什么不找另一个呢？";
		close;
	case 1:
		break;
	case 2:
		mes "[或]";
		mes "对不起。还没有新任务！如果我找到工作，我会告诉你的！";
		close;
	}
	switch(.@i) {
	case 1: // General Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12255,12271,"Deep Sea Crab";
			case 2: callsub L_GiveQuest,.@i,12256,12271,"Deep Sea Marse";
			case 3: callsub L_GiveQuest,.@i,12257,12271,"Deep Sea Vadon";
			case 4: callsub L_GiveQuest,.@i,12258,12271,"Deep Sea Shellfish";
			case 5: callsub L_GiveQuest,.@i,12259,12271,"Deep Sea Kukre";
			case 6: callsub L_GiveQuest,.@i,12260,12271,"Deep Sea Cornutus";
		}
	case 2: // Hard Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12261,12272,"Deep Sea Marc";
			case 2: callsub L_GiveQuest,.@i,12262,12272,"Deep Sea Sword Fish";
			case 3: callsub L_GiveQuest,.@i,12263,12272,"Deep Sea Strouf";
			case 4: callsub L_GiveQuest,.@i,12264,12272,"Deep Sea Anolian";
			case 5: callsub L_GiveQuest,.@i,12265,12272,"Deep Sea Obeaune";
			case 6: callsub L_GiveQuest,.@i,12266,12272,"Deep Sea Kapha";
		}
	case 3: // General Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12267,12273,"Weird Coelacanth";
			case 2: callsub L_GiveQuest,.@i,12268,12273,"Gloomy Coelacanth";
		}
	case 4: // Hard Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12269,12274,"Violent Coelacanth";
			case 2: callsub L_GiveQuest,.@i,12270,12274,"Mutant Coelacanth";
		}
	default:
		mes "[或]";
		mes "呃！？任务在哪里？让我快点找到那个。你必须和马德卡谈谈！";
		close;
	}
	end;

// callsub L_GiveQuest,.@i,<quest 1>,<quest 2>,<monster>;
L_GiveQuest:
	setquest getarg(1);
	setquest getarg(2);
	mes "[或]";
	mes "今天，英雄涵！";
	mes "我们大声疾呼并设定消灭^0000ff"+getarg(3)+"^000000的日期！";
	next;
	mes "[或]";
	switch(getarg(0)) {
	case 1:
	case 2:
		mes "加油，英雄们！别怕他们！继续吧，因为这是一日任务！";
		break;
	case 3:
		mes "我给你一周的时间来完成这项工作！在涵洞地下城普通模式中消灭他们所有人！";
		break;
	case 4:
		mes "我给你一周的时间来完成这项工作！在困难模式的涵洞地牢中消灭他们所有人！";
		break;
	}
	next;
	mes "您收到了消灭^005500"+getarg(3)+"^000000的请求。如果您需要更多详细信息，请检查任务窗口。";
	close;
}

mal_in01,172,26,2	script	Madeca#mal	544,{
	if (checkweight(1201,1) == 0) {
		mes "你有太多种类的物体。减少这些物体后尝试继续。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你有太多重物。减轻体重后尝试继续。";
		close;
	}
	if (BaseLevel < 90) {
		mes "[玛德卡]";
		mes "咔咔咔！！";
		mes "我们来吃一些可以煮或烤的劣质海鲜吧！";
		next;
		mes "[玛德卡]";
		mes "你是谁？ You are such a totterer.甚至海藻也会杀死你！";
		close;
	}
	mes "[玛德卡]";
	mes "哦？";
	mes "You're the one who took care of my big brother Albo's request.这是一项艰苦的工作！";
	next;
	set .@i, select("你在这里做什么？：普通涵洞日常服务补偿：硬涵日常服务补偿：普通涵洞每周服务补偿：硬涵每周服务补偿")-1;
	if (.@i == 0) {
		mes "[玛德卡]";
		mes "我是来帮助我的哥哥阿尔博的！";
		next;
		mes "[玛德卡]";
		mes "我们为那些用涵洞里的劣质海鲜制造污泥的人类提供了宝贵的材料！";
		next;
		mes "[玛德卡]";
		mes "我们提供A~B级金币作为日常服务的补偿，并提供珍贵的海神之怒作为每周服务的补偿。";
		next;
		mes "[玛德卡]";
		mes "如果你答应阿尔博哥哥的请求，我会经常见到你的。所以，让我们保持密切联系！";
		close;
	}
	mes "[玛德卡]";
	mes "我无法为您过期的请求提供补偿，所以让我检查一下您的请求是否已过期。";
	next;
	specialeffect2 EF_SPHERE;
	progressbar "0xFFFF00",3;
	specialeffect2 EF_STEAL;
	switch(.@i) {
	case 1: // General Culvert Daily Service
		if (checkquest(12271,PLAYTIME) == 2)
			callsub L_EraseQuest,12255,12256,12257,12258,12259,12260,12271;
		else {
			// Reward: 2x B Grade Coin
			callsub L_CheckQuest,12255,"Deep Sea Crab",6419,2;
			callsub L_CheckQuest,12256,"Deep Sea Marse",6419,2;
			callsub L_CheckQuest,12257,"Deep Sea Vadon",6419,2;
			callsub L_CheckQuest,12258,"Deep Sea Shellfish",6419,2;
			callsub L_CheckQuest,12259,"Deep Sea Kukre",6419,2;
			callsub L_CheckQuest,12260,"Deep Sea Cornutus",6419,2;
		}
		break;
	case 2: // Hard Culvert Daily Service
		if (checkquest(12272,PLAYTIME) == 2)
			callsub L_EraseQuest,12261,12262,12263,12264,12265,12266,12272;
		else {
			// Reward: 1x A Grade Coin
			callsub L_CheckQuest,12261,"Deep Sea Marc",6418,1;
			callsub L_CheckQuest,12262,"Deep Sea Sword Fish",6418,1;
			callsub L_CheckQuest,12263,"Deep Sea Strouf",6418,1;
			callsub L_CheckQuest,12264,"Deep Sea Anolian",6418,1;
			callsub L_CheckQuest,12265,"Deep Sea Obeaune",6418,1;
			callsub L_CheckQuest,12266,"Deep Sea Kapha",6418,1;
		}
		break;
	case 3: // General Culvert Weekly Service
		if (checkquest(12273,PLAYTIME) == 2)
			callsub L_EraseQuest,12267,12268,12273;
		else {
			// Reward: 1x Sea God's Wrath
			callsub L_CheckQuest,12267,"Weird Coelacanth",6423,1;
			callsub L_CheckQuest,12268,"Gloomy Coelacanth",6423,1;
		}
		break;
	case 4: // Hard Culvert Weekly Service
		if (checkquest(12274,PLAYTIME) == 2)
			callsub L_EraseQuest,12269,12270,12274;
		else {
			// Reward: 5x Sea God's Wrath
			callsub L_CheckQuest,12269,"Violent Coelacanth",6423,5;
			callsub L_CheckQuest,12270,"Mutant Coelacanth",6423,5;
		}
		break;
	default:
		mes "[玛德卡]";
		mes "有问题。请再检查一遍！";
		close;
	}
	mes "[玛德卡]";
	mes "我没有看到任何已完成的任务！";
	close;

L_EraseQuest:
	for(set .@j,0; .@j<getargcount(); set .@j,.@j+1) {
		if (checkquest(getarg(.@j)) > -1)
			erasequest getarg(.@j);
	}
	mes "[玛德卡]";
	mes "嗯，对不起。您超出了时间限制，所以我无法给您任何付款。";
	close;

// callsub L_CheckQuest,<quest ID>,<monster>,<reward item ID>,<reward item amount>;
L_CheckQuest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		mes "[玛德卡]";
		mes "你已经惩罚了^0000ff"+getarg(1)+"^000000！";
		mes "这是你的工资！";
		erasequest getarg(0);
		specialeffect2 EF_STEAL;
		getitem getarg(2),getarg(3);
		close;
	}
	return;
}

// Instance Creation :: pump
//============================================================
mal_in01,160,34,4	script	Missing, the Cleaner	545,{
	if (BaseLevel < 90) {
		mes "[清洁工失踪]";
		mes "你在跟我说话吗？！";
		next;
		mes "[清洁工失踪]";
		mes "我不知道你为什么来这里，但你不能进去！";
		mes "托马斯的命令...";
		next;
		mes "^770099您必须达到基础等级 90 才能进入。^000000";
		close;
	}
	if (in_canal_n == 0) {
		mes "[清洁工失踪]";
		mes "（发抖）";
		next;
		mes "^660066这只猫看起来状态很不好。它正在轻微颤抖。^000000";
		next;
		if(select("抚摸猫：你还好吗？") == 1) {
			mes "[清洁工失踪]";
			mes "你在跟我说话吗？！";
			next;
			mes "^660066一只颤抖的猫正在抬头看着你。^000000";
			close;
		}
		mes "[清洁工失踪]";
		mes "看！";
		next;
		mes "[清洁工失踪]";
		mes "期间我们遇到了很多问题";
		mes "涵洞的清洁。但一个";
		mes "猫必须这样做...";
		next;
		select("你要清理什么涵洞？");
		mes "[清洁工失踪]";
		mes "对于一个人来说可能看起来很陌生";
		mes "外人，但实际上这个地方";
		mes "是为了保存纪念品而制作的";
		mes "奢华的东西。";
		next;
		mes "[清洁工失踪]";
		mes "剧烈的颤抖之后";
		mes "这个岛上，事情开始泄漏";
		mes "在这附近。所以这就是为什么我成为";
		mes "这里有一个清洁工，虽然我用过";
		mes "成为一名经理。";
		next;
		mes "^660066猫看起来像是在哭……^000000";
		next;
		mes "[清洁工失踪]";
		mes "沿着这条路有一个涵洞";
		mes "到地下室。有很多不好的";
		mes "那些伤害我的人。我厌倦了";
		mes "和他们打了很多次";
		mes "天！";
		next;
		mes "[清洁工失踪]";
		mes "哦！你碰了我的皮毛！";
		mes "我的毛都掉了……就这样了";
		mes "都是那些人的错~";
		next;
		mes "^660066您现在注意到猫的皮毛在某些地方丢失了。^000000";
		next;
		select("你为什么不干脆放弃呢！");
		mes "[清洁工失踪]";
		mes "我还得谋生，不是吗？";
		next;
		mes "[清洁工失踪]";
		mes "有时还能抽到幸运箱，";
		mes "或者更好的是，我可以得到罐头......";
		next;
		if(select("好的，干杯！：如果可能的话我会帮忙......") == 1) {
			mes "[清洁工失踪]";
			mes "谢谢。我回去倒水了！";
			close;
		}
		mes "[清洁工失踪]";
		mes "（压低声音）";
		mes "如果你真的想帮忙，就靠近点！";
		next;
		select("（靠近点）");
		mes "[清洁工失踪]";
		mes "不是每个人都可以当助理";
		mes "这里。但我却忍不住选择";
		mes "此刻任何人...";
		next;
		mes "[清洁工失踪]";
		mes "如果你想去这里，让我得到这个代码。";
		next;
		mes "^660066猫给你写了一份代码备忘录。^000000";
		next;
		select("打开备忘录。");
		mes "^660000阿拉加姆侮辱了我。^000000";
		next;
		mes "[清洁工失踪]";
		mes "你一定是和朋友一起来的";
		mes "因为你不能一个人进去！和";
		mes "密码代码将来自";
		mes "党的领袖！";
		next;
		mes "[清洁工失踪]";
		mes "同样重要的是你必须";
		mes "有海神守护在身边。";
		next;
		mes "[清洁工失踪]";
		mes "别忘了！我不会打开";
		mes "如果没有海神护身的话就门！";
		set in_canal_n,1;
		close;
	}
	if (rentalcountitem(6436) == 0) {
		mes "[清洁工失踪]";
		mes "你看起来不像有海神防护。我现在打不开门！";
		close;
	}
	set .@party_id,getcharid(1);
	set .@md_name$,"Culvert";
	if (!.@party_id) {
		mes "^0000ff你必须组织一场聚会";
		mes "超过 1 名会员或已成为会员";
		mes "聚会并回来。^000000";
		close;
	}
	set .@playtime, checkquest(12254,PLAYTIME);
	if (.@playtime == -1) {
		if (is_party_leader() == true) {
			mes "[清洁工失踪]";
			mes "嗯？这是怎么回事？如果你是领导的话请告诉我密码！";
			next;
			switch(select("不...没什么。：阿拉加姆侮辱了我。：进入涵洞。")) {
			case 1:
				mes "[清洁工失踪]";
				mes "真是个菜鸟啊。";
				close;
			case 2:
				if (instance_create(.@md_name$) < 0) {
					mes "聚会名称："+getpartyname(.@party_id);
					mes "党魁："+strcharinfo(0);
					mes "^0000ff"+.@md_name$+"^000000 - 预订失败！";
					close;
				}
				mes "^3333FF"+.@md_name$+"^000000 - 保留";
				mes "预订后，您";
				mes "必须选择进入涵洞。";
				close;
			case 3:
				callsub L_Enter,0;
			}
		}
		if(select("进入涵洞：取消") == 2)
			end;
		callsub L_Enter,1;
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "如果门打开，您可以进入涵洞。";
		next;
		if(select("进入涵洞：取消") == 2)
			close;
		callsub L_Enter,0;
	} else if (.@playtime == 2) {
		mes "^0000ff涵洞的大门再次打开。^000000";
		erasequest 12254;
		close;
	}
	end;
L_Enter:
	switch(instance_enter("Culvert")) {
	case IE_OTHER:
		mes "发生未知错误。";
		close;
	case IE_NOINSTANCE:
		mes "涵洞的大门仍然关闭。";
		mes "您必须等待，直到您能够进入或找到可以创建实例的党魁。";
		close;
	case IE_NOMEMBER:
		mes "只有党员才能参加。";
		close;
	case IE_OK:
		mapannounce "mal_in01", strcharinfo(0)+" of the party "+getpartyname(getcharid(1))+" is entering the Culvert.",bc_map,"0x00ff99";
		if (checkquest(12254) == -1) setquest 12254;
		//warp "1@pump",63,98;
		if (getarg(0) == 0) close;
		else end;
	}
}

// Instance: Common Scripts
//============================================================
1@pump,63,100,4	script	Missing, the Cleaner#0	545,{
	if (is_party_leader() == false) {
		mes "[清洁工失踪]";
		mes "我现在正在尝试与你们的领导交谈。请不要打扰并等待！";
		close;
	}
	mes "[清洁工失踪]";
	mes "这是地下涵洞，你将和你的朋友一起清理！";
	next;
	mes "[清洁工失踪]";
	mes "我先去开门。顺便问一下，你知道怎么打架吗？";
	next;
	switch(select("我很擅长送面包。:我知道怎么战斗。:^ffffff给我钻石。^000000")) {
	case 1:
		mes "[清洁工失踪]";
		mes "好吧，那我就让你照常清理这里的涵洞吧！我会在右手边的角落准备，请跟我来！";
		next;
		if(select("等待！我改变主意了！：如你所愿~") == 1) {
			mes "[清洁工失踪]";
			mes "啊？还没准备好吗？等你准备好了再跟我说话。";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"Missing: Move toward the 3 o'clock direction and wait for my next order!",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("Missing, the Cleaner#0");
		enablenpc instance_npcname("Missing, the Cleaner#n");
		close;
	case 2:
		if (BaseLevel < 140) {
			mes "[清洁工失踪]";
			mes "嗯……Missing认为你很弱！告诉我真相！！";
			close;
		}
		mes "[清洁工失踪]";
		mes "啊？你有战斗技巧吗？那么，有一个地方我没能打扫干净……你为什么不去那里呢？！";
		next;
		mes "[清洁工失踪]";
		mes "我必须告诉你，我们已经很久没有清理那个地方了，140级以下的人根本无法进入！您确定还想去吗？";
		next;
		if(select("等待！我改变主意了！：当然，我已经准备好了~") == 1) {
			mes "[清洁工失踪]";
			mes "唔？等你准备好了再跟我说话。";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"Missing: I'll go in first, so follow me! I'll open up a gate at the 3 o'clock direction!",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("Missing, the Cleaner#0");
		enablenpc instance_npcname("Culvert Entrance#i");
		close;
	case 3:
		mes "[清洁工失踪]";
		mes "我都说了别玩我了~！";
		close;
	}
	end;
}

1@pump,84,105,0	script	Culvert Entrance#i	45,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("Culvert Entrance#i");
	disablenpc instance_npcname("Culvert Entrance#n");	// disable warp out
	end;
OnTouch:
	if (BaseLevel >= 140)
		warp instance_mapname("2@pump"),38,88;
	else
		warp instance_mapname("1@pump"),74,105;
	end;
}

function	script	F_mal_missing	{
	mes "[清洁工失踪]";
	mes "我很快就会告诉你如何清洁。你能看到你周围的涵洞吗？";
	next;
	if(select("什么涵洞？？：是的，我看到了。") == 1) {
		mes "[清洁工失踪]";
		mes "啊？这是您第一次看到涵洞吗？你会看到许多埋在地下的机器。你可以四处看看然后再回来！";
		close;
	}
	mes "[清洁工失踪]";
	mes "这是正确的！那些涵洞非常重要！我们必须清理涵洞，防止海藻堵塞。这就是我们清洁工所做的！";
	next;
	mes "[清洁工失踪]";
	mes "当心！当你清理涵洞时，一些怪物会追赶你！但不要屈服于他们！";
	next;
	mes "[清洁工失踪]";
	mes "并且...切勿让 6 个海藻堆叠！最多5个是可以接受的！如果有6个我就把你拖出去！！";
	next;
	mes "[清洁工失踪]";
	mes "我不在乎你是否得到朋友的帮助。我要你清理所有的涵洞！别忘了！我会观察你并给你指示。就按我说的做吧！！";
	next;
	if (is_party_leader() == false) {
		mes "[清洁工失踪]";
		mes "当你的领导者完成准备工作后，比赛就会开始，所以请做好准备吧！";
		close;
	}
	mes "[清洁工失踪]";
	mes "那么，你现在准备好打扫了吗？";
	next;
	switch(select("等待！还没有！：我们开始吧！")) {
	case 1:
		mes "[清洁工失踪]";
		mes "啊？还没准备好吗？等你准备好了再跟我说话。";
		close;
	case 2:
		mes "[清洁工失踪]";
		mes "好的！现在开始了！";
		return;
	}
}

// Instance: Normal Mode :: in_pump1
//============================================================
1@pump,84,105,4	script	Missing, the Cleaner#nf	545,{
	mes "[清洁工失踪]";
	mes "我从事清洁工作40年了，从来没有见过这么糟糕的团队！";
	next;
	mes "[清洁工失踪]";
	mes "我到处都看到海藻，因为你在扭动！所有的涵洞都会被海藻堵住！";
	next;
	if (is_party_leader() == false) {
		mes "[清洁工失踪]";
		mes "一旦你的领导完成准备，解决方案就会决定，所以你要做好准备！！";
		close;
	}
	mes "[清洁工失踪]";
	mes "如果你还有勇气再挑战一次，我就再给你一次机会！你会做出什么选择！？";
	next;
	switch(select("等待！还没有！：好吧，我们重新开始吧！")) {
	case 1:
		mes "[清洁工失踪]";
		mes "你还是慢了！等你准备好了再跟我说话。";
		close;
	case 2:
		mes "[清洁工失踪]";
		mes "好的！我们现在就开始吧！";
		set .@i$, charat(strnpcinfo(2),0);
		enablenpc instance_npcname("Missing, the Cleaner#"+.@i$);
		donpcevent instance_npcname("Missing, the Cleaner#"+.@i$)+"::OnStart";
		disablenpc instance_npcname("Culvert Entrance#"+.@i$);
		disablenpc instance_npcname("Missing, the Cleaner#"+.@i$+"o");
		donpcevent instance_npcname("Monster Hole#"+.@i$)+"::OnClear";
		disablenpc();
		close;
	}
	close;
OnInstanceInit:
	disablenpc();
	end;
}

1@pump,84,105,4	script	Missing, the Cleaner#n	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnStart";
	close;
OnInstanceInit:
	disablenpc instance_npcname("Missing, the Cleaner#n");
	end;
OnStart:
	killmonster instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	disablenpc instance_npcname("Missing, the Cleaner#n");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("1@pump");
	areamonster .@map$,55,99,61,105,"Contaminated Seaweed",2191,1,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		mapannounce .@map$,"Contaminated Seaweed: "+.@mob_dead_num+" stem",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#n")+"::OnClear";
	set .@map$, instance_mapname("1@pump");
	killmonster .@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	enablenpc instance_npcname("Missing, the Cleaner#nf");
	mapannounce .@map$,"What is this!! Seaweed is all over the culverts! You're done with cleaning! Get out!!",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("Missing, the Cleaner#n");
	end;
OnTimer100:
	mapannounce instance_mapname("1@pump"),"First culvert will be opened in 5 sec. Cleaner can find the opened drain and click to start cleaning.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("1@pump"),"The one who's in charge of cleaning the culvert cannot move or be attacked until the casting is over.",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer45000:
OnTimer95000:
OnTimer145000:
OnTimer195000:
OnTimer245000:
OnTimer295000:
OnTimer345000:
OnTimer395000:
OnTimer445000:
	mapannounce instance_mapname("1@pump"),"Next culvert will be opened in 5 sec. Please hurry up and find the position of the next culvert.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer50000:
OnTimer100000:
OnTimer150000:
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer200000:
OnTimer250000:
OnTimer300000:
OnTimer350000:
OnTimer400000:
OnTimer450000:
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer515000:
	set .@map$, instance_mapname("1@pump");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	mapannounce .@map$,"Contaminated Seaweed: "+.@mob_dead_num+" stem. Missing will come and inspect the results of the cleaning soon.",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer520000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#n")+"::OnEnable";
	end;
}

1@pump,1,1,4	script	Monster Hole#n	-1,{
	end;
OnSpawn:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	donpcevent instance_npcname("#Culvert_"+.@i$+rand(1,.@n))+"::OnEnable";
	end;
OnClear:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	for(set .@i,1; .@i<=.@n; set .@i,.@i+1)
		donpcevent instance_npcname("#Culvert_"+.@i$+.@n)+"::OnClear";
	end;
}

1@pump,36,111,4	script	#Culvert_n1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",10;
	stopnpctimer;
	disablenpc();
	end;
OnInstanceInit:
	disablenpc();
	end;
OnEnable:
	enablenpc();
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("1@pump");
	set .@index, atoi(charat(strnpcinfo(2),9));
	switch(.@index) {
		case 1: setarray .@c[0],32,107,40,115; break;
		case 2: setarray .@c[0],64,120,72,128; break;
		case 3: setarray .@c[0],76,110,84,118; break;
		case 4: setarray .@c[0],36,76,44,84; break;
		case 5: setarray .@c[0],71,76,79,84; break;
		case 6: setarray .@c[0],54,97,62,105; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Crab",2176,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Marse",2175,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Vadon",2174,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Shellfish",2178,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Kukre",2179,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Cornutus",2177,rand(1,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	getmapxy(.@map$,.@x,.@y,BL_NPC);
	getpartymember 'party_id,2;
	copyarray .@partymemberaid[0],$@partymemberaid[0],$@partymembercount;
	for(set .@i,0; .@i<$@partymembercount; set .@i,.@i+1) {
		if (attachrid(.@partymemberaid[.@i])) {
			if (strcharinfo(3) == .@map$)
				viewpoint 0,.@x,.@y,.@index,0xFFFF00;
			detachrid;
		}
	}
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("1@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer49500:	//OnTimer50000 clashes with the timer in "Missing, the Cleaner#h".
	donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc();
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
1@pump,68,124,4	duplicate(#Culvert_n1)	#Culvert_n2	844,14,14
1@pump,80,114,4	duplicate(#Culvert_n1)	#Culvert_n3	844,14,14
1@pump,40,80,4	duplicate(#Culvert_n1)	#Culvert_n4	844,14,14
1@pump,75,80,4	duplicate(#Culvert_n1)	#Culvert_n5	844,14,14
1@pump,58,101,4	duplicate(#Culvert_n1)	#Culvert_n6	844,14,14

1@pump,1,1,4	script	Boss Creation#n	-1,{
	end;
OnInstanceInit:
	disablenpc();
	end;
OnEnable:
	enablenpc();
	initnpctimer;
	end;
OnTimer100:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		mapannounce instance_mapname("1@pump"),"Hmm! That wasn't so bad!",bc_map,"0xff88ff",FW_NORMAL,15;
	else if (.@i$ == "h")
		mapannounce instance_mapname("2@pump"),"Hmm! You guys are pretty good!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer5000:
	mapannounce strnpcinfo(4),"Let's pack up and go... WHAT!!?",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer10000:
	mapannounce strnpcinfo(4),"I sense something strange!! Don't loosen up-- prepare for a fight!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer20000:
	stopnpctimer;
	set .@i$, charat(strnpcinfo(2),0);
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	if (rand(1,100) > 50) {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"Weird Coelacanth",2188,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"Mutant Coelacanth",2189,1,.@label$;
		mapannounce strnpcinfo(4),"Something big inside the abyss appears with a huge noise.",bc_map,"0x00ffcc",FW_NORMAL,15;
	} else {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"Gloomy Coelacanth",2187,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"Violent Coelacanth",2190,1,.@label$;
		mapannounce strnpcinfo(4),"Something savage inside the abyss appears with a huge noise.",bc_map,"0x00ffcc",FW_NORMAL,15;
	}
	end;
OnMyMobDead:
	if (mobcount(strnpcinfo(4),instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		mapannounce strnpcinfo(4),"You've destroyed all monsters in the culverts. There are trophies lying on the ground, please look for them.",bc_map,"0xffff00",FW_NORMAL,15;
		set .@i$, charat(strnpcinfo(2),0);
		set .@map$, strnpcinfo(4);
		enablenpc instance_npcname("Culvert Entrance#"+.@i$);
		enablenpc instance_npcname("Missing, the Cleaner#"+.@i$+"o");
		if (.@i$ == "n") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,6401);
				     if (.@j < 5001) set .@item,12636; //Malang_Sp_Can
				else if (.@j < 5501) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 6001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 6201) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 6401) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		} else if (.@i$ == "h") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,5001);
				     if (.@j < 2001) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 3001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 4001) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 4501) set .@item,12619; //Cgrade_Pocket
				else if (.@j < 5001) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		}
	} else
		mapannounce strnpcinfo(4),"There are still monsters alive.",bc_map,"0x00ff99",FW_NORMAL,20;
	end;
}

1@pump,84,105,4	script	Missing, the Cleaner#no	545,{
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n") {
		mes "[清洁工失踪]";
		mes "我很惊讶！";
		next;
		mes "[清洁工失踪]";
		mes "那条看起来很滑稽的鱼似乎是生活在附近的腔棘鱼之一。我还是第一次见到有人单独来这里！";
		next;
		mes "[清洁工失踪]";
		mes "我会清理残留物。收拾行李离开吧！入口在另一边，找找吧！";
		next;
	} else if (.@i$ == "h") {
		mes "[清洁工失踪]";
		mes "你已经消灭了一个巨大的怪物！";
		next;
		mes "[清洁工失踪]";
		mes "你消灭的那一种是腔棘鱼的一种。其他人试图摧毁它们但失败了。";
		next;
		mes "[清洁工失踪]";
		mes "我会清理残留物。你继续获得奖杯并离开！";
		next;
	}
	mes "[清洁工失踪]";
	mes "还有一件事！不要与任何人谈论你今天在这里看到的事情！";
	next;
	mes "[清洁工失踪]";
	mes "如果托马斯关闭这个，我们就会失去工作！";
	close;
OnInstanceInit:
	disablenpc();
	end;
}

1@pump,32,100,0	warp2	Culvert Entrance#n	3,3,mal_in01,161,32

//MD_Putmob "1@pump" 0 0 0 0 20 HYDRA 0 0 2
1@pump,1,1,4	script	Hydra#n	-1,{
OnInstanceInit:
	monster strnpcinfo(4),0,0,"Hydra",1068,20;
	end;
}

// Instance: Hard Mode :: in_pump2
//============================================================
2@pump,39,88,4	duplicate(Missing, the Cleaner#nf)	Missing, the Cleaner#hf	545

2@pump,39,88,4	script	Missing, the Cleaner#h	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnStart";
	close;
OnStart:
	killmonster instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	disablenpc instance_npcname("Missing, the Cleaner#h");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("2@pump");
	areamonster .@map$,75,78,85,88,"Contaminated Seaweed",2191,1,instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		mapannounce .@map$,"Proliferous Contaminated Seaweed: "+.@mob_dead_num+" stem",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#h")+"::OnClear";
	set .@map$, instance_mapname("2@pump");
	killmonster .@map$, instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	enablenpc instance_npcname("Missing, the Cleaner#hf");
	mapannounce .@map$,"What is this!! Seaweed is all over the culverts! You're done with cleaning! Get out!!",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("Missing, the Cleaner#h");
	end;
OnTimer100:
	mapannounce instance_mapname("2@pump"),"First culvert will be opened in 5 sec. Cleaner can find the opened drain and click to start cleaning.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("2@pump"),"The one who's in charge of cleaning the culvert cannot move or be attacked until the casting is over.",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer35000:
OnTimer75000:
OnTimer115000:
OnTimer155000:
OnTimer195000:
OnTimer235000:
OnTimer275000:
OnTimer315000:
OnTimer355000:
	mapannounce instance_mapname("2@pump"),"Next culvert will be opened in 5 sec. Please hurry up and find the position of the next culvert.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer40000:
OnTimer80000:
OnTimer120000:
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer160000:
OnTimer200000:
OnTimer240000:
OnTimer280000:
OnTimer320000:
OnTimer360000:
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer420000:
	mapannounce instance_mapname("2@pump"),"It seems Missing will come and inspect the results of the cleaning soon. Shall we clean up the mess around here?",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer425000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#h")+"::OnEnable";
	end;
}

2@pump,53,114,4	script	#Culvert_h1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",15;
	stopnpctimer;
	disablenpc();
	end;
OnInstanceInit:
	disablenpc();
	end;
OnEnable:
	enablenpc();
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("2@pump");
	set .@index, atoi(substr(strnpcinfo(2),9,getstrlen(strnpcinfo(2))-1));
	switch(.@index) {
		case 1: setarray .@c[0],49,110,57,118; break;
		case 2: setarray .@c[0],75,105,83,113; break;
		case 3: setarray .@c[0],110,110,118,118; break;
		case 4: setarray .@c[0],94,94,102,102; break;
		case 5: setarray .@c[0],58,92,66,100; break;
		case 6: setarray .@c[0],53,66,61,74; break;
		case 7: setarray .@c[0],43,45,51,53; break;
		case 8: setarray .@c[0],77,59,85,67; break;
		case 9: setarray .@c[0],96,70,104,78; break;
		case 10: setarray .@c[0],111,46,119,54; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Marc",2182,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Sword Fish",2181,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Strouf",2180,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Mutant Anolian",2183,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Obeaune",2184,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Metamorphous Kapha",2185,rand(2,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("2@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer39500:	//OnTimer40000 clashes with the timer in "Missing, the Cleaner#h".
	donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc();
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
2@pump,79,109,4	duplicate(#Culvert_h1)	#Culvert_h2	844,14,14
2@pump,114,114,4	duplicate(#Culvert_h1)	#Culvert_h3	844,14,14
2@pump,98,98,4	duplicate(#Culvert_h1)	#Culvert_h4	844,14,14
2@pump,62,96,4	duplicate(#Culvert_h1)	#Culvert_h5	844,14,14
2@pump,57,70,4	duplicate(#Culvert_h1)	#Culvert_h6	844,14,14
2@pump,47,49,4	duplicate(#Culvert_h1)	#Culvert_h7	844,14,14
2@pump,81,63,4	duplicate(#Culvert_h1)	#Culvert_h8	844,14,14
2@pump,100,74,4	duplicate(#Culvert_h1)	#Culvert_h9	844,14,14
2@pump,115,50,4	duplicate(#Culvert_h1)	#Culvert_h10	844,14,14

2@pump,1,1,4	duplicate(Monster Hole#n)	Monster Hole#h	-1
2@pump,1,1,4	duplicate(Boss Creation#n)	Boss Creation#h	-1
2@pump,39,88,4	duplicate(Missing, the Cleaner#no)	Missing, the Cleaner#ho	545
2@pump,38,100,0	duplicate(Culvert Entrance#n)	Culvert Entrance#h	3,3

//MD_Putmob "2@pump" 0 0 0 0 20 HYDRA 0 0 2
2@pump,1,1,4	duplicate(Hydra#n)	Hydra#h	-1
