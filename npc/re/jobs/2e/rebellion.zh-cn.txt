//===== rAthena Script =======================================
//= Rebellion Job Quest
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Gunslinger -> Rebellion.
//===== Changelogs: ==========================================
//= 1.0 First version. [exneval]
//= 1.1 Syntax change. [Capuche]
//============================================================

moc_fild12,261,318,3	script	Suspicious Man	4_M_MOCASS2,2,2,{
	end;
OnTouch:
	if (Class == Job_Gunslinger && BaseLevel >= 99 && JobLevel >= 70) {
		if (isbegin_quest(12340) == 0) {
			mes "[可疑男子]";
			mes "我终于找到你了，流浪者斯派克！你的脖子是我的了！";
			next;
			select("说什么？");
			mes "[可疑男子]";
			mes "*哼哼*别假装你不知道。看看这张海报！你就在其中！";
			next;
			select("我看到了相似之处，但这不是我。");
			mes "["+strcharinfo(0)+"]";
			mes "忘记相似之处。你从哪里得到那张海报？我从来没有犯罪过。";
			next;
			mes "[可疑男子]";
			mes "我知道你会试图用谎言逃脱。";
			next;
			mes "[可疑男子]";
			mes "^0000ffEinbroch私人酒馆^000000已经发布了通缉海报。你们身边有很多像我这样的赏金猎人。";
			next;
			mes "[可疑男子]";
			mes "我不在乎你是不是对的人，只要你看出来，我就可以带你死或活。";
		}
		else {
			mes "[可疑男子]";
			mes "又是你！你有勇气回来这里！";
		}
		next;
		if (select("逃跑吧。：跟他战斗。") == 1)
			mes "^0000ff我现在应该先跑一下，稍后再考虑这个问题。^000000";
		else
			mes "^0000ff如果他说的是实话的话，他并不是唯一追随我的赏金猎人。我需要时间制定策略。^000000";
		close2;
		if (isbegin_quest(12340) == 0)
			setquest 12340;// Rebellion Job Change Quest
		warp "moc_fild12",186,83;
	}
	end;
}

einbroch,54,97,3	script	Bouncer	4_M_REBELLION,{
	if (BaseClass != Job_Gunslinger) {
		mes "[保镖]";
		mes "不得在会场周围闲逛。";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[保镖]";
		mes "正在施工，进不去。";
		close;
	case 1:
		mes "[保镖]";
		mes "*喘气*你！我以为你已经被俘虏了哦，现在无所谓了。你是我的出狱免费卡！";
		next;
		select("我不是你想象的那个罪犯！");
		mes "[保镖]";
		mes "你不是吗？";
		next;
		mes "^0000ff保镖检查你的脸。^000000";
		next;
		mes "[保镖]";
		mes "嗯，你长得很像，不过你看起来不像那个罪犯斯派克那么危险";
		next;
		select("我想和海报出版商谈谈。");
		mes "[保镖]";
		mes "你的问题有点超出我的能力范围。进去吧。如果我发现你一直都是货真价实的人，我就让你再也见不到外面了。";
		close2;
		warp "job_gun",81,29;
		end;
	case 2:
		mes "[保镖]";
		mes "你回来了。为什么？";
		next;
		if (select("我想进来：我路过。") == 2) {
			mes "[保镖]";
			mes "你真傻。";
			close;
		}
		mes "[保镖]";
		mes "前进。远离麻烦。";
		close2;
		warp "job_gun",70,28;
		end;
	}
}

job_gun,68,33,3	script	Ruthless Rebellion	4_F_REBELLION,{
	mes "[无情的叛乱]";
	mes "你知道如何使用枪吗？";
	close;
}

job_gun,78,33,3	script	Suspicious Gunslinger	4_M_JOB_BLACKSMITH,{
	mes "[可疑的枪手]";
	mes "...";
	next;
	mes "[安静的枪手]";
	mes "别说我看起来像个铁匠，你知道的。";
	close;
}

job_gun,74,24,6	script	Drunken Man	4_M_LIEMAN,{
	mes "[醉汉]";
	mes "*打嗝*哦，我是这里的主人。";
	mes "哦等等，我是在酒吧还是其他地方？兹兹...";
	close;
}

job_gun,64,33,6	script	Anxious Man	4_M_ALCHE_A,{
	mes "[焦虑的人]";
	mes "也许我来错地方了。这个地方看起来不像酒吧。";
	close;
}

job_gun,86,29,3	script	Kulbertinov	4_M_REBELLION,{
	if (Class == Job_Rebellion) {
		mes "[库尔伯蒂诺夫]";
		mes "你想见老板吗？";
		next;
		if (select("是的。：没有。") == 1) {
			close2;
			warp "job_gun",220,130;
			end;
		}
		end;
	}
	if (Class != Job_Gunslinger) {
		mes "[库尔伯蒂诺夫]";
		mes "你在找我吗？";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[库尔伯蒂诺夫]";
		mes "我可以帮你吗？";
		close;
	case 1:
		mes "[库尔伯蒂诺夫]";
		mes "你想要什么？";
		next;
		select("我被误认为是罪犯！");
		mes "[库尔伯蒂诺夫]";
		mes "等一下...哇哈哈哈！";
		next;
		mes "^0000ff他扫描了你的脸，然后大笑起来。^000000";
		next;
		mes "[库尔伯蒂诺夫]";
		mes "对不起。有那么一刻，我以为斯派克又复活了。三天前他被发现死亡。";
		next;
		select("他已经被抓了？");
		mes "[库尔伯蒂诺夫]";
		mes "是的，赏金猎人已经得到报酬了。消息还没传出去，你就跟他一模一样了。去算算吧。";
		next;
		select("你必须做点什么！");
		mes "[库尔伯蒂诺夫]";
		mes "抱歉，我无法替你与每一个赏金猎人交谈。当然，我愿意补偿给您带来的不便。";
		next;
		mes "[库尔伯蒂诺夫]";
		mes "嘿，加入我们的行列怎么样？最后，你不会被枪杀。";
		next;
		select("你想让我成为赏金猎人吗？");
		mes "[库尔伯蒂诺夫]";
		mes "不，赏金狩猎只是一种消遣。我们是世界上所有枪械的专家。";
		next;
		mes "[库尔伯蒂诺夫]";
		mes "无忧无虑的精神！";
		mes "神奇的喷火！";
		mes "我们称自己为^0000ff叛乱^000000。";
		next;
		mes "[库尔伯蒂诺夫]";
		mes "你看起来好像知道如何使用枪。你想加入我们的行列吗？您也不必再担心其他赏金猎人了。";
		next;
		if (select("让我想一想：我想我别无选择。") == 1) {
			mes "[库尔伯蒂诺夫]";
			mes "你可以反复思考，但答案不会改变。只需接受报价即可。";
		}
		else {
			mes "[库尔伯蒂诺夫]";
			mes "好想法。不妨现在就做并结束它。";
		}
		next;
		mes "[库尔伯蒂诺夫]";
		mes "与恩温·科尼克交谈。她引导像你这样漂泊的灵魂走上叛乱之路。我会传话给她。啊，别用枪指着她。";
		completequest 12340;
		setquest 12341;// Rebellion Job Change Quest
		close2;
		warp "job_gun",19,15;
		end;
	case 2:
		mes "[库尔伯蒂诺夫]";
		mes "你和埃尔文·科尼克有生意吗？";
		next;
		if (select("否：请派我去找埃尔文·科尼克。") == 1) {
			mes "[库尔伯蒂诺夫]";
			mes "好的。想喝一杯伏特加吗？";
			close;
		}
		mes "[库尔伯蒂诺夫]";
		mes "好的，我会让她知道的。";
		close2;
		warp "job_gun",19,15;
		end;
	}
}

job_gun,19,24,3	script	Elwin Conick#1	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[埃尔文·科尼克]";
		mes "如果你不想加入叛乱，那就滚吧。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12341) == 1) {
		mes "[埃尔文·科尼克]";
		mes "你愿意加入我们的叛乱吗？";
		next;
		mes "^0000ff那个目光锐利的矮个子女人凝视着你的眼睛。^000000";
		next;
		mes "[埃尔文·科尼克]";
		mes "在这里签名。";
		next;
		mes "^0000ff签字方“_____”不会谈论测试，即使他或她在测试过程中可能发生不可预见的事故中受伤、致残或死亡。^000000";
		next;
		select("什么？！");
		mes "[埃尔文·科尼克]";
		mes "你想加入我们吗？";
		next;
		if (select("我放弃.:...我会签名。") == 1) {
			mes "[埃尔文·科尼克]";
			mes "你的选择。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[埃尔文·科尼克]";
		mes "好的。现在，跟我来。";
		close2;
		warp "job_gun",117,41;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		mes "[埃尔文·科尼克]";
		mes "您想再试一次吗？";
		next;
		if (select("我放弃了：做吧。") == 1) {
			mes "[埃尔文·科尼克]";
			mes "你的选择。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[埃尔文·科尼克]";
		mes "跟我来。";
		close2;
		warp "job_gun",117,41;
		end;
	}
	if (isbegin_quest(12343) > 0) {
		mes "[埃尔文·科尼克]";
		if (isbegin_quest(12343) == 1)
			mes "你还有伊万的投篮测试，你想再试一次吗？";
		else
			mes "伊万的射击测试还没有结束，你想再试一次吗？";
		next;
		if (select("我放弃了：做吧。") == 1) {
			mes "[埃尔文·科尼克]";
			mes "不值得放弃，现在出去放松一下。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[埃尔文·科尼克]";
		mes "记得尽力而为。";
		close2;
		warp "job_gun",120,130;
		end;
	}
	if (isbegin_quest(12345) == 1) {
		mes "[埃尔文·科尼克]";
		mes "伊万正在等你，去找他吧。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	mes "[埃尔文·科尼克]";
	mes "您无需再参加任何测试。你为什么不出去喝一杯呢？";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,117,46,3	script	Elwin Conick#2	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[埃尔文·科尼克]";
		mes "如果你不想加入叛乱，那就滚吧。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
	.@quest_12341 = isbegin_quest(12341);
	.@quest_12342 = isbegin_quest(12342);
	if (.@emt_chk == 3169) {
		mes "^0000ff您已经处于测试表格中。如果您已完成所有测试，请向大会室^000000 的 Elwin Conick 报到。";
		close;
	}
	if (.@emt_chk) {
		mes "[埃尔文·科尼克]";
		mes "看看你自己，你真的想测试吗？";
		next;
		mes "^0000ff埃尔文看到你变成了另一种形态，她很生气。等待当前表单结束，然后再次找到她。^000000";
		next;
		if (select("等等：走开。") == 2) {
			mes "[埃尔文·科尼克]";
			mes "当你不以任何形式回来时。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[埃尔文·科尼克]";
		mes "不值得放弃，现在出去放松一下。";
		close;
	}
	if (.@quest_12341 == 1 || .@quest_12342 == 1) {
		if (countitem(6746)) {
			mes "[埃尔文·科尼克]";
			mes "任何剩余的钢制品必须在测试后归还。";
			delitem 6746,countitem(6746);// Steel_Article
			close;
		}
		if (countitem(6747)) {
			mes "[埃尔文·科尼克]";
			mes "任何剩余的钢制品必须在测试后归还。";
			delitem 6747,countitem(6747);// Steel_Article_
			close;
		}
		if ((countitem(13118) - isequipped(13118)) > 0) {
			mes "[埃尔文·科尼克]";
			mes "时间到了。你没通过测试。让我拿走你剩下的所有考试材料。";
			delitem 13118,1;// Tiny_Flame
			close;
		}
		if (.@quest_12342 == 1)
			.@menu$ = "Got it.";
		else {
			.@menu$ = "Okay.";
			setquest 12342;// Rebellion Job Change Quest
			completequest 12341;
		}
		mes "[埃尔文·科尼克]";
		mes "听着。我不会竭尽全力向您解释测试。";
		next;
		select( .@menu$ );
		mes "[埃尔文·科尼克]";
		if (.@quest_12341 == 1) {
			mes "这是铸造枪械模具的地方。随处可见钢铁制品的生产。";
			next;
			mes "[埃尔文·科尼克]";
			mes "我们叛军本质上是武器专家。在这里您将学习制造和组装枪支。";
			next;
			mes "[埃尔文·科尼克]";
			mes "你会被要求控制入口附近的熔炉控制器并选择 5 个标准钢制品。";
			next;
			mes "[埃尔文·科尼克]";
			mes "然后你将把他们带到下一个装配室并将它们组装成枪支。您将有 3 分钟时间完成所有这些工作。你最好动作快、注意力集中，否则你就会考试不及格。";
			next;
			mes "[埃尔文·科尼克]";
			mes "我会在隔壁房间等你给我拿来一把完整的枪。为了检查经过的时间，你将被转换到不同的工作。 ^0000ff如果3分钟后恢复正常，请回到我身边并重新开始测试。^000000。";
		}
		else {
			mes "您将被要求控制机器上的开关并选择 5 个标准钢制品。";
			next;
			mes "[埃尔文·科尼克]";
			mes "然后，您将把它们带到下一个装配室并将它们组装成完整的枪支。您将有 3 分钟的时间来完成所有这些工作。 ^0000ff为了检查经过的时间，你将被转换成不同的工作^000000。";
			next;
			mes "[埃尔文·科尼克]";
			mes "我会在隔壁房间等你给我拿来一把完整的枪。请记住，^0000ff 当您的转换在 3 分钟后过期^000000 时，您将失败测试。";
		}
		transform 3169,180000;
		close;
	}
	mes "[埃尔文·科尼克]";
	mes "啊？你迷路了吗？您需要先注册，然后再回到这里。";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,198,39,3	script	Elwin Conick#3	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[埃尔文·科尼克]";
		mes "如果你不想加入叛乱，那就滚吧。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
		if (.@emt_chk == 3169) {
			if ((countitem(13118) - isequipped(13118)) < 1) {// don't count if equipped
				mes "[埃尔文·科尼克]";
				mes "你完成了吗？我在你的库存中没有看到任何已完成的枪支。";
				close;
			}
			mes "[埃尔文·科尼克]";
			mes "考虑到时间，它是劣质的，但还不错。你通过了。";
			delitem 13118,1;// Tiny_Flame
			next;
			mes "[埃尔文·科尼克]";
			mes "让我拿走你剩下的所有考试材料。";
			delitem 6746,countitem(6746);// Iron_Artifact
			delitem 6747,countitem(6747);// Steel_Artifact
			next;
			mes "[埃尔文·科尼克]";
			mes "现在您可以继续学习下一课程。 Ivan Sidorenko 将监督您的测试。";
			completequest 12342;
			close2;
			warp "job_gun",120,133;
			end;
		}
		if (.@emt_chk) {
			mes "[埃尔文·科尼克]";
			mes "看看你自己，你真的想测试吗？";
			next;
			mes "^0000ff埃尔文看到你变成了另一种形态，她很生气。等待当前表单结束，然后再次找到她。^000000";
			next;
			if (select("等等：走开。") == 2) {
				mes "[埃尔文·科尼克]";
				mes "当你不以任何形式回来时。";
				close2;
				warp "job_gun",81,29;
				end;
			}
			mes "[埃尔文·科尼克]";
			mes "不值得放弃，现在出去放松一下。";
			close;
		}
		mes "[埃尔文·科尼克]";
		mes "时间到了。你没通过测试。让我拿走你剩下的所有考试材料。";
		delitem 6746,countitem(6746);// Iron_Artifact
		delitem 6747,countitem(6747);// Steel_Artifact
		delitem 13118,(countitem(13118) - isequipped(13118));// Tiny_Flame
		next;
		mes "[埃尔文·科尼克]";
		mes "让我们重新开始吧。";
		close2;
		warp "job_gun",117,41;
		end;
	}
	mes "[埃尔文·科尼克]";
	mes "啊？你迷路了吗？您需要先注册，然后再回到这里。";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,165,18,0	script	Furnace Controller#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "只有授权用户才能进行操作。";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {// J_REB_SHECIL1
		if (isbegin_quest(12342) == 1) {
			mes "您想将模具从熔炉中挖出来吗？";
			next;
			if (select("退出：开始。") == 1) {
				mes "切换至待机模式。";
				close;
			}
			mes "正在初始化炉控制器 ZX-3100...";
			disablenpc "Furnace Controller#1";
			donpcevent "Furnace Controller#2::OnStart";
			close;
		}
	}
	mes "不是授权用户。中止操作。";
	close;
}

job_gun,214,36,0	script	Auto Anvil#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "只有授权用户才能进行操作。";
		close;
	}
	if ((countitem(13118) - isequipped(13118)) > 0) {// Tiny_Flame
		mes "已发现未经授权制造枪支的情况。向经理埃尔文汇报。";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {
		if (countitem(6747) < 5) {// Steel_Article_
			mes "您没有标准的钢铁制品。";
			close;
		}
		if (isbegin_quest(12342) == 1) {
			mes "您想使用自动铁砧并将标准钢制品塑造成枪支吗？";
			next;
			if (select("退出：开始。") == 1) {
				mes "为了您的安全，请远离自动砧。";
				close;
			}
			if (rand(1,10) > 6) {
				mes "您已经使用自动砧成功创建了一把枪支。 ^ff0000请在转换到期之前报告^000000。";
				specialeffect EF_REPAIRWEAPON;
				getitem 13118,1;// Tiny_Flame
			}
			else {
				mes "制造枪支失败。";
				specialeffect EF_SUI_EXPLOSION;
			}
			delitem 6747,5;// Steel_Article_
			close;
		}
	}
	mes "不是授权用户。中止操作。";
	close;
}

job_gun,165,18,0	script	Furnace Controller#2	CLEAR_NPC,{
	mes "系统正忙着从熔炉中取出已完成的模具。";
	close;

OnStart:
	enablenpc "Furnace Controller#2";
	sleep 3000;
	mapannounce "job_gun","Furnace Controller: Dredging a mold up from the furnace... Workers, stand by at the production line.",bc_map,"0x00ff44";
	sleep 500;
	for ( .@i = 0; .@i < 30; ++.@i ) {
		.@eff_rnd = rand(1,100);
		if (.@eff_rnd < 33)
			donpcevent "Heating Furnace#3::OnStart";
		else if (.@eff_rnd > 66)
			donpcevent "Heating Furnace#4::OnStart";
		else
			donpcevent "Heating Furnace#5::OnStart";
		sleep 200;
		.@d_itemx = rand(127,143);
		.@d_itemy = rand(26,29);
		makeitem 6746,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article
		if (rand(1,10) > 7)
			makeitem 6747,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article_
		sleep 500;
	}
	sleep 2000;
	mapannounce "job_gun","Furnace Controller: Finishing the operation... Entering Standby mode in 15 seconds.",bc_map,"0x00ff44";
	sleep 15000;
	disablenpc "Furnace Controller#2";
	enablenpc "Furnace Controller#1";
	end;
OnInit:
	disablenpc "Furnace Controller#2";
	end;
}

job_gun,130,31,0	script	Heating Furnace#3	CLEAR_NPC,{
	end;
OnStart:
	specialeffect EF_FIREPILLAR;
	sleep 500;
	specialeffect EF_FIREPILLARBOMB;
	end;
}
job_gun,136,31,0	duplicate(Heating Furnace#3)	Heating Furnace#4	CLEAR_NPC
job_gun,141,31,0	duplicate(Heating Furnace#3)	Heating Furnace#5	CLEAR_NPC

job_gun,120,138,3	script	Ivan Sidorenko#1	4_M_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[伊万·西多连科]";
		mes "这里是射击试验场，你不能随意来。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) < 2) {
		mes "[伊万·西多连科]";
		mes "看来有一个错误，你不能留在这里。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12343) == 0) {
		mes "[伊万·西多连科]";
		mes "嘿，保持专注——我们这里使用的是实弹，你随时都可能被枪杀。";
		next;
		mes "[伊万·西多连科]";
		mes "让我快点做这件事。基本上，您将在这个由 5 个部分组成的射击场中奔跑。";
		next;
		mes "[伊万·西多连科]";
		mes "目标是在人群中快速且准确地找到敌人。";
		next;
		mes "[伊万·西多连科]";
		mes "在所有目标中，您只能射击^0000ff标准目标^000000。如果你犯了错误，你就会失去分数。";
		next;
		mes "[伊万·西多连科]";
		mes "我们来试试吧。没有很多^0000ff标准问题目标^000000，所以请保持警惕。";
		next;
		mes "[伊万·西多连科]";
		mes "^0000ff您有 3 分钟时间完成此测试。如果您无法在转换到期^000000之前完成，您就会失败。如果发生这种情况，请沿着路径回到我身边。";
		next;
		mes "[伊万·西多连科]";
		mes "^0000ff如果你错过了一个标准目标，或者射中了 3 个不正确的目标^000000，你就会失败。让我们开始吧。";
		setquest 12343;// Rebellion Job Change Quest
		setquest 12344;// Rebellion Job Change Quest
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	if (isbegin_quest(12343) == 1) {
		if (checkquest(12343,HUNTING) == 2)
			.@j_r_s = 10;
		if (checkquest(12344,HUNTING) == 2)
			.@j_r_f = 2;
		if (getstatus(SC_MONSTER_TRANSFORM,1) != 3169)
			.@j_r_t = 5;
		.@j_r_c = .@j_r_s - .@j_r_f - .@j_r_t;
		if (.@j_r_c > 9) {
			mes "[伊万·西多连科]";
			mes "好工作。我没想到你能这么快就过去了。";
			completequest 12343;
			completequest 12344;
			setquest 12345;// Rebellion Job Change Quest
			next;
			mes "[伊万·西多连科]";
			mes "您可以离开并在外面等候。我会带着其他导师的评估报告来见你。";
			close2;
			warp "job_gun",210,132;
			end;
		}
		if (.@j_r_c == 8) {
			mes "[伊万·西多连科]";
			mes "你的速度足够快，但你射中了太多不正确的目标。你失败了。";
		}
		else if (.@j_r_c == 5) {
			mes "[伊万·西多连科]";
			mes "你足够准确，但你花了太多时间。缓慢的叛乱无法持续足够长的时间。";
		}
		else if (.@j_r_c < 5) {
			mes "[伊万·西多连科]";
			mes "你在速度和准确性上都失败了。你怎么活了这么久？";
		}
		else {
			mes "[伊万·西多连科]";
			mes "有问题，统计好像有错。嗯...有时会发生这种情况。";
		}
		erasequest 12343;
		erasequest 12344;
		next;
		mes "[伊万·西多连科]";
		mes "让我们再试一次。这次要小心一些。";
		setquest 12343;
		setquest 12344;
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	mes "[伊万·西多连科]";
	mes "你怎么没有离开？";
	next;
	mes "[伊万·西多连科]";
	mes "您可以离开并在外面等候。我会带着其他导师的评估报告来见你。";
	close2;
	warp "job_gun",210,132;
	end;
}

job_gun,1,4,0	script	#target	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#target::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#target";
	while( mobcount("job_gun","#target::OnMyMobDead") < 16 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
	}
	end;
}

job_gun,1,5,0	script	#nontarget	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#nontarget::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#nontarget";
	while( mobcount("job_gun","#nontarget::OnMyMobDead") < 56 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
	}
	end;
}

job_gun,220,132,3	script	Ivan Sidorenko#2	10062,{
	if (!checkweight(1201,1)) {
		mes "您的项目太多，请减少项目以继续。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "库存中的物品正在压垮你。先减轻体重。";
		close;
	}
	if (isbegin_quest(12345) == 0) {
		mes "[伊万·西多连科]";
		mes "一定是出了什么差错。你不应该留在这里。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Gunslinger) {
		if (BaseLevel < 99 || JobLevel < 70) {
			mes "[伊万·西多连科]";
			mes "啊？你看起来这么弱吗？我没什么可说的了。";
			close;
		}
		if (SkillPoint) {
			mes "[伊万·西多连科]";
			mes "哦不~你还有技能点。";
			close;
		}
		mes "[伊万·西多连科]";
		mes "库尔伯蒂诺夫、埃尔文和我评估了你们的 Rebellion 测试结果。";
		next;
		mes "[伊万·西多连科]";
		mes "和...";
		next;
		mes "[伊万·西多连科]";
		mes "您已经满足了我们所有的要求，包括态度和测试表现。";
		next;
		mes "[伊万·西多连科]";
		mes "没错，你现在是叛军的一员了。准备好惊叹吧。";
		next;
		mes "[伊万·西多连科]";
		mes "埃尔文希望你拥有这个。好好照顾它。他为你做的。";
		jobchange Job_Rebellion;
		completequest 12345;
		getitem 13119,1;// Freedom_Flame
		next;
		mes "[伊万·西多连科]";
		mes "稍后见。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Rebellion) {
		mes "[伊万·西多连科]";
		mes "这样的生活应该是有趣的，而不是无聊的。";
		next;
		mes "[伊万·西多连科]";
		mes "通过成为叛逆者，你的生活应该得到升级。那么，去创造一段伟大的旅程，让我们骄傲吧！";
		close2;
		warp "job_gun",81,29;
		end;
	}
	mes "[伊万·西多连科]";
	mes "你有什么话要对我说吗？";
	close;
}

job_gun,220,138,4	shop	Prop Vending Machine	2_VENDING_MACHINE1,13210:-1,13211:-1,13212:-1,13213:-1,13214:-1,7663:-1,7664:-1,7665:-1

job_gun,216,138,4	script	Butler Karlex	1_M_01,{
	mes "[管家卡莱克斯]";
	mes "使用此临时存储空间可以在高级测试期间保证您的物品安全。";
	next;
	.@s = select("退出：打开存储。");
	mes "[管家卡莱克斯]";
	mes "感谢您使用该服务。";
	close2;
	if (.@s == 2)
		openstorage;
	end;
}

job_gun,56,26,0	warp	rebelroom#1	2,2,einbroch,49,97
job_gun,18,10,0	warp	rebelroom#2	2,2,job_gun,81,29
job_gun,165,22,0	warp	rebelroom#3	2,2,job_gun,196,35
job_gun,197,49,0	warp	rebelroom#4	2,2,job_gun,157,21
job_gun,88,144,0	warp	shootingrange#1	3,3,job_gun,93,123
job_gun,105,97,0	warp	shootingrange#2	3,3,job_gun,125,98
job_gun,156,115,0	warp	shootingrange#3	3,3,job_gun,157,137
job_gun,152,154,0	warp	shootingrange#4	3,3,job_gun,135,154
job_gun,130,146,0	warp	shootingrange#5	3,3,job_gun,120,130


job_gun,1,1,0	script	Admin1	CLEAR_NPC,{
	callfunc "F_GM_NPC";
	if (callfunc("F_GM_NPC",1854,0) != 1)
		end;
	mes "[时间管理器]";
	mes "您想什么时候返回？";
	next;
	switch( select("取消：测试怪物生成：从头开始：熔炉区：射击区：完成测试") ) {
	case 1:
		end;
	case 2:
		donpcevent "#target::OnStart";
		donpcevent "#nontarget::OnStart";
		end;
	case 3:
		callsub S_Quest,0,0;
	case 4:
		callsub S_Quest,12341,12341;
	case 5:
		callsub S_Quest,12342,12342;
	case 6:
		callsub S_Quest,12345,12344;
	}
	end;

S_Quest:
	setarray .@arguments[0], getarg(0), getarg(1);
	for ( .@quest = 12340; .@quest <= 12345; ++.@quest ) {
		if (isbegin_quest(.@quest) > 0)
			erasequest .@quest;
		if (.@quest <= .@arguments[0])
			setquest .@quest;
		if (.@quest <= .@arguments[1])
			completequest .@quest;
	}
	end;
}
