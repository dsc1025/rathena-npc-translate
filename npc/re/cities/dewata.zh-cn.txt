//===== rAthena Script ======================================= 
//= Dewata Town
//===== By: ================================================== 
//= Gennosuke Kouga, Muad_Dib
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Dewata Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Standardized. [Euphy]
//= 1.2 Fixed Zeny check. [Joseph]
//= 1.3 Updated to match the official script. [Lemongrass/Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================

// Dewata Transportation :: dewata_trans
//============================================================
alberta,212,202,4	script	Dewata Sailor#alberta	536,{
	mes "[水手之神]";
	mes "您想参观^8B4513德瓦塔岛^000000吗？";
	mes "这里拥有令人眼花缭乱的海浪和迷人的景色，是放松身心的好地方。";
	if (VIP_SCRIPT) {
		set .@cost, vip_status(VIP_STATUS_ACTIVE)?1000:10000;
		mes "过境费10,000 Zeny，VIP 1,000。";
	} else {
		set .@cost,10000;
		mes "过境费为10,000 Zeny。";
	}
	next;
	switch(select("是的！：不。")) {
	case 1:
		if (Zeny >= .@cost) {
			mes "[水手之神]";
			mes "^8B4513德瓦塔^000000是一个美丽而宁静的岛国。";
			mes "祝旅途愉快~";
			close2;
			set Zeny, Zeny - .@cost;
			warp "dewata",232,53;
			end;
		} else {
			mes "[水手之神]";
			mes "你没有钱，孩子。";
			mes "等你钱包鼓起来了再回来。";
			close;
		}
	case 2:
		mes "[水手之神]";
		mes "让我知道";
		mes "当你准备好去旅行的时候~";
		close;
	}
}

dewata,229,49,6	script	Alberta Sailor#dewata	536,{
	mes "[阿尔伯塔水手]";
	mes "你想回艾伯塔省吗？";
	next;
	switch(select("是的。：没有。")) {
	case 1:
		mes "[阿尔伯塔水手]";
		mes "我希望你玩得很开心";
		mes "您的^8B4513德瓦塔^000000岛之旅。";
		mes "请再来吧！";
		close2;
		warp "alberta",192,215;
		end;
	case 2:
		mes "[阿尔伯塔水手]";
		mes "出发前享受更多乐趣。";
		mes "^8B4513Dewata^000000 岛上还有更多精彩。";
		close;
	}
}

// Generic Dewata NPCs :: dewata_npc
//============================================================ 
dewata,221,237,5	script	Young Man#dew-1	536,{
	mes "[水果爱好者]";
	mes "你的faaaaaace怎么了？你一定是真的无知吧！";
	next;
	mes "[水果爱好者]";
	mes "让我给你一些知识。";
	next;
	mes "[水果爱好者]";
	mes "我虽然是个年轻人，但没有人比我更了解^777700棕榈果^000000。好的！你想知道什么？";
	next;
	switch(select("关于壳...：关于果肉...：关于棕榈仁...：不用了，谢谢。")) {
	case 1:
		mes "[水果爱好者]";
		mes "那么您想了解棕榈果肉吗？让我解释一下。";
		next;
		mes "[水果爱好者]";
		mes "通常，棕榈果壳用于提炼油。棕榈油是许多菜肴的有用成分。";
		next;
		mes "[水果爱好者]";
		if (dew_drink > 0)
			mes "嗯...你见过 Memo 了吗？那么你应该很容易理解。";
		else
			mes "如果您想了解如何使用棕榈油，请前往德瓦塔岛北侧的 Memo。他每天都用棕榈油制作传统饮料。";
		next;
		mes "[水果爱好者]";
		mes "无论如何，我们将来自棕榈果的油称为棕榈油或椰子油，因其品质而闻名。";
		next;
		mes "[水果爱好者]";
		mes "石油生产的剩余物被干燥，并用作烹饪或取暖的燃料。";
		next;
		mes "[水果爱好者]";
		mes "它燃烧得很好，气味也很好闻，没有比这更好的了。";
		next;
		mes "[水果爱好者]";
		mes "好的！！这就是关于果肉的全部内容，需要很长时间才能解释棕榈果壳的所有这些有用作用。";
		next;
		mes "[水果爱好者]";
		mes "这不是我的知识范围。别误会我的意思！";
		close;
	case 2:
		mes "[水果爱好者]";
		mes "那么您想了解 Palm 的内饰吗？让我解释一下。";
		next;
		mes "[水果爱好者]";
		mes "棕榈树果实有2层果肉。外面很粗糙，里面很光滑……";
		next;
		mes "[水果爱好者]";
		mes "嗯……是的！棕榈树果肉比核桃壳硬一点，一定是里面含有水分。";
		next;
		mes "[水果爱好者]";
		mes "贝壳内含有约300cc的水，由于出羽田岛天气炎热，可以起到解渴的作用。";
		next;
		mes "[水果爱好者]";
		mes "而且我们并不是喝了里面的水就扔掉它！";
		next;
		mes "[水果爱好者]";
		mes "我们还可以用它打造一个优质食堂！就像我说的，它的外壳足够厚，可以充当水瓶。";
		next;
		mes "[水果爱好者]";
		mes "制作木炭也很棒。由棕榈果制成的木炭以其持久且优质的火力而闻名，";
		next;
		mes "[水果爱好者]";
		mes "人们喜欢用棕榈果木炭烹饪海鲜和鸡肉。";
		next;
		mes "[水果爱好者]";
		mes "它还有一个额外的好处，就是燃烧时会发出令人愉快的棕榈味";
		next;
		mes "[水果爱好者]";
		mes "好的！！这就是纸浆信息的全部内容，解释所有这些有用的效果需要很长时间。";
		next;
		mes "[水果爱好者]";
		mes "这并不是我所知道的全部。别误会我的意思！";
		close;
	case 3:
		mes "[水果爱好者]";
		mes "那么，你想了解里面的嫩仁吗？让我为你解释一下";
		next;
		mes "[水果爱好者]";
		mes "硬果肉内部的果仁被用作天然消化剂。";
		next;
		mes "[水果爱好者]";
		mes "里面的内核就不多说了..";
		next;
		mes "[水果爱好者]";
		mes "但我们确实确保使用棕榈果的每一部分。";
		next;
		mes "[水果爱好者]";
		mes "你不同意吗？";
		close;
	case 4:
		mes "[水果爱好者]";
		mes "哎呀！你对这个世界没有兴趣！";
		close;
	}
}

dewata,114,243,6	script	Lazy Young Man#dew	536,1,1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 500) {
		mes "[懒惰的年轻人]";
		mes "你为什么背这么多东西？是不是很重？";
		close;
	}
	if (countitem(11534)) {
		mes "[懒惰的年轻人]";
		mes "呜呜~!!这不是棕榈汁吗？";
		mes "你能给我那个吗？我刚醒";
		mes "起来，我的喉咙都干了！";
		next;
		switch(select("给予：不给予")) {
		case 1:
			mes "[懒惰的年轻人]";
			mes "真的吗？为我？？我就给你这个作为回报...谢谢...呵呵~";
			delitem 11534,1; //Coco_Juice
			getgroupitem(IG_GiftBox);
			close;
		case 2:
			mes "[懒惰的年轻人]";
			mes "就这样吧~好吧...";
			close;
		}
	}
	mes "[懒惰的年轻人]";
	mes "啊~渴了~谁有手掌啊";
	mes "给我喝果汁吗？~";
	close;
OnTouch:
	if (countitem(11534) == 0) {
		mes "[懒惰的年轻人]";
		mes "哎哟~我渴了~~好渴~~";
		mes "给我棕榈汁...棕榈汁~~";
		emotion ET_FRET;
		next;
		emotion ET_FRET;
		next;
		emotion ET_FRET;
		next;
		emotion ET_FRET;
		close;
	}
	end;
}

dewata,159,81,4	script	Tourist#dew-1	881,{
	mes "[纳索罗]";
	mes "哇~纯净的海洋，温暖的沙滩……";
	mes "非常适合和女朋友约会...";
	next;
	mes "[纳索罗]";
	mes "如果我有一个就好了...";
	emotion ET_CRY;
	next;
	mes "[纳索罗]";
	mes "我想知道我的公主什么时候来……";
	emotion ET_OHNO;
	close;
}

dewata,146,109,5	script	Restauranteur#dew	886,{
	if (checkweight(11532,1) == 0 || checkweight(11533,1) == 0 || MaxWeight - Weight < 500) {
		mes "[餐厅]";
		mes "你是来尝尝我的菜的吗？空腹吃东西总是好的。";
		close;
	}
	mes "[餐厅]";
	mes "欢迎来到德瓦塔岛。";
	mes "我希望你表现得像个贵族";
	mes "旅行者，与其中一些人不同";
	mes "青少年朋克...";
	next;
	switch(select("关于Dawata菜肴：今天的菜单？：我还好。")) {
	case 1:
		mes "[餐厅]";
		mes "需要有关我们食品的信息吗？嗯……我没什么可说的，但如果你愿意的话，请随意听。";
		next;
		mes "[餐厅]";
		mes "要讲的内容太多了，让我开始吧。";
		next;
		mes "[餐厅]";
		mes "首先，Dawata菜肴根据每种烹饪食谱都有特殊的名称，";
		next;
		mes "[餐厅]";
		mes "如果菜名中有“Goreng”，那么它要么是炒菜，要么是油炸食品。";
		next;
		mes "[餐厅]";
		mes "如果它是“Bakar”的一种，那么它就是烧烤食品。";
		next;
		mes "[餐厅]";
		mes "如果是“沙爹”，那么你吃的是串烧菜。";
		next;
		mes "[餐厅]";
		mes "其次，我们有以基本食材命名的菜肴，";
		next;
		mes "[餐厅]";
		mes "在德瓦塔岛，我们将米饭称为 Nasi，将面条称为 Mie，将面包称为 Roti，将鸡肉称为 Ayam，将鱼称为 Ikan，将玉米称为 Jagung。";
		next;
		mes "[餐厅]";
		mes "因此，如果米饭是炒的，我们称之为“Nasi Goreng”，而炒面则称为“Mie Goreng”。";
		next;
		mes "[餐厅]";
		mes "嗯...这足以享受我们在出瓦田的美食了！如果您饿了，我们有特别套餐，仅需 ^0000FF5000^000000 Zeny。";
		close;
	case 2:
		mes "[餐厅]";
		mes "我们有 2 种商品“炒饭”和“沙爹”。您想要哪一款？";
		next;
		switch(select("炒饭：沙爹")) {
		case 1:
			mes "[餐厅]";
			mes "这是用我特制的酱汁炒的炒饭，上面还有煎蛋。只需要 ^0000FF5000^000000 Zeny。";
			set .@food,11532;
			break;
		case 2:
			mes "[餐厅]";
			mes "这是沙爹，一片肉串在棕榈果木炭上烤。仅需 ^0000FF5000^000000 Zeny。";
			set .@food,11533;
			break;
		}
		next;
		switch(select("味道：走开")) {
		case 1:
			if (Zeny < 5000) {
				mes "[餐厅]";
				mes "钱小子哪去了？哎呀！这里没有免费食物！好的？";
				close;
			}
			mes "[餐厅]";
			mes "这是你的"+getitemname(.@food)+"，你不会后悔的！";
			set Zeny, Zeny - 5000;
			getitem .@food,1;
			close;
		case 2:
			mes "[餐厅]";
			mes "可惜你没能品尝到这道美味佳肴……";
			close;
		}
	case 3:
		mes "[餐厅]";
		mes "你是认真的？这一点都不好笑。";
		close;
	}
}

dewata,147,107,6	script	Gourmet#dew	85,{
	mes "[美食]";
	mes "哦...这就像我的聚会";
	mes "嘴...每个人都受到邀请！";
	emotion ET_CRY;
	next;
	mes "[美食]";
	mes "大师~再来一张~";
	emotion ET_BEST;
	emotion ET_OK, getnpcid(0, "Restauranteur#dew");
	next;
	mes "[美食]";
	mes "你一定要试试这个！这里的食物肉质鲜美~";
	close;
}

dewata,154,107,4	script	Sightseer#dew	86,{
	mes "[游客]";
	mes "哇...";
	mes "他已经吃了多少了？";
	emotion ET_SCRATCH;
	next;
	mes "[游客]";
	mes "他一定是个大胃王，而不是美食家……";
	next;
	mes "[游客]";
	mes "你去过普隆德拉吗？我好像在哪里见过他..";
	close;
}

dewata,95,203,6	script	Travel Guide#dew	536,{
	mes "[指南]";
	mes "我身后的这座寺庙叫婆罗浮屠。";
	mes "婆罗浮屠寺庙建于9世纪";
	mes "供奉古代宗教人物大佛。";
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "[指南]";
	mes "这座寺庙由6个方形平台和3个圆形平台组成，有2672块手工雕刻的浮雕面板以及504尊各种佛像。";
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "[指南]";
	mes "中央佛塔位于高台中央，四周环绕着72尊佛像。";
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "[指南]";
	mes "即使在现代，婆罗浮屠寺庙也是许多僧人礼佛的朝圣地。正如您可以想象的那样，它也是一个主要的旅游景点。";
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "[指南]";
	mes "这就是我对婆罗浮屠寺庙的全部了解，还有什么问题吗？";
	mes "如果没有，我们可以转移到下一个地点。";
	close;
}

dewata,97,207,4	script	Tourist#dew1	803,{
	mes "[游客]";
	mes "哦，该死的……我走了，";
	mes "选错导游了...";
	next;
	mes "[游客]";
	mes "这家伙真是个贪睡的人……";
	mes "为什么要加长..";
	emotion ET_CONFUSE;
	next;
	mes "[游客]";
	mes "我要换旅行社了...";
	emotion ET_OHNO;
	close;
}

dewata,100,206,4	script	Tourist#dew2	59,{
	mes "[游客]";
	mes "嘿，我们去那里吧！赶快，";
	mes "导游在讲解的时候~";
	next;
	mes "[游客]";
	mes "来吧...听起来不像";
	mes "他很快就会停止说话。";
	close;
}

dewata,101,206,4	script	Tourist#dew3	90,{
	mes "[游客]";
	mes "没有导游我们会不会迷路？";
	next;
	mes "[游客]";
	mes "上次我们就已经迷路了...";
	close;
}

dewata,99,203,4	script	Tourist#dew4	755,{
	mes "[游客]";
	mes "哦~这个很有趣。一个";
	mes "供奉佛陀的寺庙...";
	emotion ET_OK;
	next;
	mes "[游客]";
	mes "但我并没有真正理解所有";
	mes "这里的符号学。";
	next;
	mes "[游客]";
	mes "好漂亮啊。。不知道是谁建的";
	mes "这个地方？";
	close;
}

dewata,67,186,8	script	Monk#dew1	834,{
	mes "[武僧]";
	mes "我是一名僧侣，满足于花时间进行宗教研究。";
	next;
	mes "[武僧]";
	mes "我刚刚和我的僧侣们一起从婆罗浮屠寺庙朝圣回来......";
	next;
	mes "[武僧]";
	mes "婆罗浮屠寺庙有许多朝圣者参观，因为它是仅存的少数供奉大佛的寺庙之一。";
	close;
}

dewata,65,188,8	script	Monk#dew2	834,{
	mes "[武僧]";
	mes "婆罗浮屠寺庙是仅存的少数供奉大佛的寺庙之一。";
	next;
	mes "[武僧]";
	mes "我不明白为什么遵循我们道路的寺庙只剩下很少了......";
	close;
}

dewata,63,190,8	script	Monk#dew3	834,{
	mes "[武僧]";
	mes "您是游客吗？您可以向其中一个神社供奉并许愿。";
	next;
	mes "[武僧]";
	mes "我们伟大的佛陀可能会满足你的愿望..";
	close;
}

dewata,62,192,8	script	Monk#dew4	834,{
	mes "[武僧]";
	mes "我们伟大的佛陀是灵魂之王，是一切善神的领袖。";
	next;
	mes "[武僧]";
	mes "德瓦塔部落信仰佛陀，奇怪的是他们都将佛陀描述为不同的生物。";
	next;
	mes "[武僧]";
	mes "我不太确定其中的原因。也许是因为德瓦塔有很多不同的部落，各有不同的特点。";
	close;
}

dewata,89,191,6	script	Small Shrine#dew1	844,{
	mes "这里有一个小神社。许愿的人很多。";
	next;
	switch(select("捐款。：许愿。：走开。")) {
	case 1:
		mes "捐赠案例说道：";
		mes "^FF00001,000 ~ 100,000 名女性^000000";
		mes "您想捐赠多少？";
		next;
		input .@input,0,100000;
		if (.@input < 1000) {
			mes "- 取消了。 -";
			close;
		} else if (.@input <= 50000)
			set .@good_luck, rand(1,10000);
		else
			set .@good_luck, rand(1,5000);
		if (.@input > Zeny) {
			mes "- 没有足够的 Zeny。 -";
			close;
		}
		mes "你的愿望是什么？";
		next;
		input(.@wish$);

		setarray .@wishes$[0],
			"Power","Strength", //1
			"Faster","Speed","Quickness","Agility", //2
			"Skillful Hands","Dex", //4
			"Healthy","Fitness","Vital", //8
			"Wisdom","Brain","Study","Int","1st place", //16
			"Luck","Wealth","Items","Get Item","Goods","Lotto", //32
			"Full Level","Level","LV", //64
			"Lover","Girl friend","Boy friend"; //128
		setarray .@index[0],
			1,1,2,2,2,2,4,4,8,8,8,16,16,16,16,16,32,32,32,32,32,32,64,64,64,128,128,128;
			
		if (.@good_luck == 7 || .@good_luck == 77 || .@good_luck == 777 || .@good_luck == 7777)
			set .@bonus,1;
		for(set .@i,0; .@i<getarraysize(.@wishes$); set .@i,.@i+1) {
			if (compare(.@wish$,.@wishes$[.@i])) {
				set .@stat, .@stat | .@index[.@i];
				break;
			}
		}

		mes "您向小神社许愿^0000FF" + .@wish$ + "^000000。";
		next;
		callsub L_Wish;
		set Zeny, Zeny - .@input;
		if ((.@stat & 1) && .@bonus)
			consumeitem 12043; //Str_Dish03
		else if ((.@stat & 2) && .@bonus)
			consumeitem 12058; //Agi_Dish03
		else if ((.@stat & 4) && .@bonus)
			consumeitem 12063; //Dex_Dish03
		else if ((.@stat & 8) && .@bonus)
			consumeitem 12053; //Vit_Dish03
		else if ((.@stat & 16) && .@bonus)
			consumeitem 12048; //Int_Dish03
		else if ((.@stat & 32) && .@bonus)
			consumeitem 12068; //Luk_Dish03
		else if (.@stat & 64) {
			specialeffect2 EF_ANGEL;
			mes "- 一个天体给你祝福。 -";
			next;
		} else if (.@stat & 128) {
			mes "- 神社将你笼罩在爱的光环中。 -";
			if ((Sex == SEX_FEMALE && compare(.@wish$,.@wishes$[26])) || (Sex == SEX_MALE && compare(.@wish$,.@wishes$[27])))
				specialeffect2 EF_LIGHTSPHERE;
			next;
		}
		mes "感觉这个愿望可以实现了。";
		close;
	case 2:
		mes "你的愿望是什么？";
		next;
		input .@wish$;
		mes "您已经向小神社许下了^0000FF" + .@wish$ + "^000000的愿望。";
		next;
		callsub L_Wish;
		mes "感觉这个愿望可以实现了。";
		close;
	case 3:
		close;
	}

L_Wish:
	mes "神秘的能量从神殿中散发出来。";
	specialeffect EF_FLASHER,AREA,"Small Shrine#dew1";
	specialeffect EF_FLASHER,AREA,"Small Shrine#dew2";
	specialeffect EF_FLASHER,AREA,"Small Shrine#dew3";
	specialeffect EF_FLASHER,AREA,"Small Shrine#dew4";
	next;
	mes "你已经得到了神社的祝福。";
	specialeffect2 EF_BLESSING;
	next;
	return;
}
dewata,89,212,6	duplicate(Small Shrine#dew1)	Small Shrine#dew2	844
dewata,68,212,6	duplicate(Small Shrine#dew1)	Small Shrine#dew3	844
dewata,68,191,6	duplicate(Small Shrine#dew1)	Small Shrine#dew4	844

dewata,75,122,6	script	Monk#dewt1	834,{
	mes "[武僧]";
	mes "你是这里的陌生人。是什么让我们来到这个安静的地方？";
	next;
	switch(select("我正在观光：没有理由。")) {
	case 1:
		mes "[武僧]";
		mes "噢，你是游客。这是供奉着大佛的‘^0000FF宝塔^000000。请尊重在这里祈祷的僧侣。";
		close;
	case 2:
		mes "[武僧]";
		mes "祝你有美好的一天...";
		close;
	}
}

dewata,69,101,6	script	Monk#dewt2	834,{
	mes "[武僧]";
	mes "我们有一座佛塔供奉佛陀。每个寺庙都有一座这样的塔，尽管除了其他僧侣之外，来这里的人并不多。";
	next;
	switch(select("我明白了……：解释一下佛：啊，好的。")) {
	case 1:
		mes "[武僧]";
		mes "你一定感受到我们佛陀的慈悲吧！";
		close;
	case 2:
		mes "[武僧]";
		mes "佛陀是德瓦塔神话中的神，他是灵魂之王，也是善神的领袖。";
		next;
		mes "[武僧]";
		mes "佛陀是灵魂的守护者，在传统舞蹈中经常被描述为与魔术师让达战斗。";
		next;
		mes "[武僧]";
		mes "在德瓦塔，我们相信有许多神保护每个岛屿和部落地区，";
		next;
		mes "[武僧]";
		mes "所以我们确实有不同的佛陀显现，通常被描述为熊、虎、龙、蛇或狮子";
		next;
		mes "[武僧]";
		mes "大狮子是出瓦塔供奉的著名佛之一。";
		next;
		mes "[武僧]";
		mes "希望这对您来说是一个很好的解释。如果您还有其他想了解的信息，请告诉我。";
		close;
	case 3:
		mes "[武僧]";
		mes "祝你有美好的一天...";
		close;
	}
}

dewata,71,79,6	script	Monk#dewt3	834,{
	mes "[武僧]";
	mes "这座塔建在沿海悬崖边缘。您可以在凉爽的海风拂过头发的情况下祈祷。";
	next;
	mes "[武僧]";
	mes "来吧，在这宜人的天气里，你应该感受我们佛陀的慈悲。";
	close;
}

dewata,211,272,4	script	Lodge Owner#dew	534,2,3,{
OnTouch:
	mes "[旅馆主人]";
	mes "这些天，有很多度蜜月的新人来这里。";
	next;
	mes "[旅馆主人]";
	mes "一定是美丽的风景吧？";
	next;
	mes "[旅馆主人]";
	mes "我对我的生意很满意。希望我们有更多这样的访客。";
	next;
	mes "[旅馆主人]";
	mes "啊!您应该带您的爱人来德瓦塔度蜜月。";
	mes "我会给你一个好价钱-";
	close;
}

dewata,245,244,4	script	Lodge Employee#dew	536,2,3,{
OnTouch:
	mes "[旅馆员工]";
	mes "Argg~这真是让我很恼火！";
	emotion ET_FRET;
	next;
	mes "[旅馆员工]";
	mes "怎么越来越多的情侣来到这里？";
	next;
	mes "[旅馆员工]";
	mes "而他们为何表现得如此恩爱呢？这是怎么回事？";
	next;
	mes "[旅馆员工]";
	mes "来吧，对在这里工作的人们表示尊重......";
	close;
}

dewata,233,263,8	script	Sweet Married Couple#1	828,2,3,{
OnTouch:
	mes "[甜蜜老公]";
	mes "宝贝你是世界上最美的东西~";
	emotion ET_CHUP;
	next;
	mes "[甜蜜的妻子]";
	mes "真的吗？我有那么美吗？我爱你，亲爱的~";
	emotion ET_CHUPCHUP, getnpcid(0, "Sweet Married Couple#2");
	next;
	mes "[甜蜜夫妻]";
	mes "哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈";
	mes "哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈";
	emotion ET_SMILE;
	emotion ET_SMILE, getnpcid(0, "Sweet Married Couple#2");
	next;
	mes "来吧，我已经厌倦了这个。我们走吧。";
	close;
}

dewata,234,263,8	script	Sweet Married Couple#2	727,2,3,{
OnTouch:
	mes "[甜蜜的妻子]";
	mes "宝贝~你知道那是什么花吗~？";
	emotion ET_QUESTION;
	next;
	mes "[甜蜜老公]";
	mes "唔？我不知道。但它确实很漂亮。";
	next;
	mes "[甜蜜的妻子]";
	mes "Baby~我和花谁更漂亮？";
	emotion ET_QUESTION;
	next;
	mes "[甜蜜老公]";
	mes "不管花多美，你永远更美~";
	emotion ET_CHUP, getnpcid(0, "Sweet Married Couple#1");
	next;
	mes "[甜蜜的妻子]";
	mes "真的吗？我有那么漂亮吗？我爱你，亲爱的~";
	emotion ET_CHUPCHUP;
	next;
	mes "[甜蜜夫妻]";
	mes "哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈";
	mes "哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈哈";
	emotion ET_SMILE, getnpcid(0, "Sweet Married Couple#1");
	emotion ET_SMILE;
	next;
	mes "来吧，这让我很无聊。让我们继续吧。";
	close;
}

dewata,279,213,4	script	Tourist#dew-2	887,2,3,{
OnTouch:
	mes "[斜倚游客]";
	mes "哇~这真是太舒服了~";
	next;
	mes "[斜倚游客]";
	mes "我只是放松地度过了一段美好的时光。";
	next;
	mes "[斜倚游客]";
	mes "你也应该躺在这里。";
	mes "一旦你这么做了，你就再也不想起来了。";
	next;
	mes "[斜倚游客]";
	mes "这就是为什么我整整一周都没有动过！哈哈哈哈~";
	emotion ET_SMILE;
	close;
}

dewata,269,208,4	script	Little Kid in Town#dew	538,2,3,{
OnTouch:
	mes "[孩子]";
	mes "嘿"+((Sex == SEX_MALE)?"兄弟":"姐姐")+"，看看那个男人！";
	next;
	mes "[孩子]";
	mes "他已经躺在那里一个星期了，什么也没做……";
	next;
	mes "[孩子]";
	mes "那个人是谁？";
	emotion ET_QUESTION;
	next;
	mes "[孩子]";
	mes "我不想长大后也像他一样懒惰。";
	close;
}

dewata,227,129,4	script	Sarr#dew	538,2,3,{
OnTouch:
	mes "[萨尔]";
	mes "他们躲起来了，所以我得去寻找。";
	mes "我一定会找到他们的！-";
	next;
	mes "[萨尔]";
	mes "一个~";
	next;
	mes "[萨尔]";
	mes "二~三~";
	next;
	mes "[萨尔]";
	mes "四~五~六~";
	next;
	mes "[萨尔]";
	mes "…………";
	next;
	mes "[萨尔]";
	mes "九十九~一百！！！现在我来了~";
	close;
}

dewata,239,140,4	script	Siyak#dew	539,2,3,{
OnTouch:
	mes "[西亚克]";
	mes "嘿来吧，走过去~他会找到我的~~";
	next;
	mes "[萨尔]";
	mes "我找到了西亚克！！出来吧，出来吧！！";
	emotion ET_KIK, getnpcid(0, "Sarr#dew");
	emotion ET_HUK;
	emotion ET_KIK, getnpcid(0, "Sipo#dew");
	emotion ET_HUK, playerattached();
	next;
	mes "[西亚克]";
	mes "啊啊！！这都是你的错~！！";
	emotion ET_FRET;
	close;
}

dewata,193,145,4	script	Sipo#dew	538,2,3,{
OnTouch:
	mes "[四坡]";
	mes "萨尔不会在这里找到我，对吧？";
	next;
	mes "[萨尔]";
	mes "我找到了西亚克！！快出来快出来！！";
	emotion ET_KIK, getnpcid(0, "Sarr#dew");
	emotion ET_HUK, getnpcid(0, "Siyak#dew");
	emotion ET_KIK;
	emotion ET_HUK, playerattached();
	next;
	mes "[四坡]";
	mes "呵呵~现在Siyak就是IT了~我就继续躲在这里吧。";
	close;
}

dewata,228,177,4	script	Majya#dew	538,2,3,{
OnTouch:
	mes "[玛吉亚]";
	mes "我和朋友们玩捉迷藏...";
	next;
	mes "[玛吉亚]";
	mes "我就站在这里，他们已经三个小时没有找到我了……";
	emotion ET_SCRATCH;
	next;
	mes "[玛吉亚]";
	mes "我应该怎么办？出来？我不想当IT...";
	close;
}

dewata,278,100,4	script	Missing Child#dew	703,2,3,{
OnTouch:
	mes "[爷爷]";
	mes "纳阿阿阿阿凯~";
	emotion ET_CRY;
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "["+strcharinfo(0)+"]";
	mes "嘿，孩子……";
	next;
	mes "[爷爷]";
	mes "纳阿阿阿阿凯~";
	emotion ET_CRY;
	next;
	mes "[爷爷]";
	mes "妈妈~爸爸~你在哪里uu~哇~";
	emotion ET_CRY;
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "["+strcharinfo(0)+"]";
	mes "…………";
	emotion ET_THINK, playerattached();
	close;
}

dewata,249,87,4	script	Mum#dew	101,2,3,{
OnTouch:
	mes "[妈妈]";
	mes "爷爷~爷爷~她去哪儿了？";
	emotion ET_KEK;
	next;
	mes "[妈妈]";
	mes "我的目光一刻也不敢离开她……";
	next;
	mes "[妈妈]";
	mes "爷爷~";
	emotion ET_KEK;
	close;
}

dewata,251,85,6	script	Poppa#dew	97,2,3,{
OnTouch:
	mes "[船尾]";
	mes "亲爱的，Ukki又迷路了吗？";
	emotion ET_QUESTION;
	next;
	mes "[船尾]";
	mes "呃……我不能和她去任何地方……已经多少次了？";
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "[爸爸]";
	mes "她迷失在洛阳、阿松、莫斯科、巴西利斯、昆仑、";
	mes "而在大城...";
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "[爸爸]";
	mes "什么！！包括这次在Dawata已经7次了...7次!!!!!!";
	emotion ET_OTL;
	close;
}

dewata,278,281,4	script	Happily Married Grandpa	534,{
	mes "[爷爷结婚幸福]";
	mes "今天，我应该请我的女士";
	mes "给我煮点^006400炒饭^000000。";
	next;
	mes "[爷爷结婚幸福]";
	mes "我老婆的^006400炒饭^000000是";
	mes "总是很特别！";
	next;
	mes "[爷爷结婚幸福]";
	mes "这让我更加爱她。";
	mes "你无法想象那美妙的味道！";
	emotion ET_THROB;
	close;
}

dewata,280,277,4	script	Happily Married Grandma	535,{
	mes "[奶奶婚姻幸福]";
	mes "在我年轻的时候，";
	mes "有很多绅士来电";
	mes "谁想向我求爱。";
	emotion ET_PROFUSELY_SWEAT;
	next;
	mes "[奶奶婚姻幸福]";
	mes "然而，我遇到了对的人，";
	mes "并结婚了。";
	mes "我问他最喜欢什么";
	mes "关于我，他说是";
	mes "我自制的^006400炒饭^000000";
	next;
	mes "[奶奶婚姻幸福]";
	mes "从那以后，他只问我";
	mes "做晚餐^006400炒饭^000000。";
	mes "结婚40多年";
	mes "他仍然不厌倦。";
	next;
	mes "[奶奶婚姻幸福]";
	mes "我一直认为这是幸运的";
	mes "和一个男人一起变老";
	mes "谁喜欢我做的菜。";
	emotion ET_THROB;
	close;
}

dewata,165,103,6	script	Adventurer Semangat#dew	534,{
	mes "[热情的冒险家]";
	mes "我也环游世界";
	mes "当我像你一样年轻的时候。";
	mes "^FF0000猴子、墨鱼^000000";
	mes "^FF0000鹰，和蛇^000000，";
	mes "都是我遇到的好朋友";
	mes "在我的旅行期间。";
	next;
	mes "[热情的冒险家]";
	mes "我仍然对冒险和旅行充满热情。";
	mes "但是，我不能就这样离开";
	mes "我心爱的妻子和";
	mes "后面的孙子...";
	next;
	mes "[热情的冒险家]";
	mes "你也会有一些东西";
	mes "或同样珍贵的人";
	mes "有一天在你心里。";
	close;
}

dewata,139,114,6	script	Semangat's Wife#dew	535,{
	mes "[塞曼加特的妻子]";
	mes "那个老人在哪里";
	mes "又去了？";
	mes "你见过我丈夫吗？";
	next;
	switch(select("我想我在那儿见过他..：不，我没有..")) {
	case 1:
		mes "[塞曼加特的妻子]";
		mes "当他逃跑时";
		mes "我正在找他。";
		mes "应该在某个地方，讲一些荒唐的故事";
		mes "对于一些旅行者来说，一如既往。";
		next;
		mes "[塞曼加特的妻子]";
		mes "我道歉";
		mes "代表我丈夫。";
		mes "请不要相信他的愚蠢故事";
		mes "说他遇到了^FF0000猴子和蛇^000000，或者";
		mes "他与^FF0000墨鱼^000000并肩作战。";
		next;
		mes "[塞曼加特的妻子]";
		mes "他一直住在这里";
		mes "一生。";
		mes "然而，总能找到一些旅人，";
		mes "谁会听他编造的故事。";
		close;
	case 2:
		mes "[塞曼加特的妻子]";
		mes "哦。请告诉我";
		mes "如果你在附近见过他。";
		close;
	}
}

dewata,181,88,6	script	Young Man#dew-2	536,{
	mes "[年轻人]";
	mes "不是^8B4513Dewata^000000海滩吗";
	mes "真的很漂亮吗？";
	mes "孩子们总是在玩耍";
	mes "在这个海滩。真的是";
	mes "让我想起了我的青春。";
	next;
	mes "[年轻人]";
	mes "晚上可以看到";
	mes "海滩上的烟花。";
	mes "伴随着海风、汹涌的海浪，";
	mes "和照亮天空的烟花，";
	mes "夜晚是这里的美丽时光！";
	close;
}

dewata,179,204,4	script	Young Lady#dew	537,{
	mes "[年轻女士]";
	mes "当你在城外时，";
	mes "并遇到^FF0000老虎、猴子或猪^000000，";
	mes "确保避开它们。";
	next;
	select("你是什​​么意思？");
	mes "[年轻女士]";
	mes "我还没见过，";
	mes "但传说说有一个";
	mes "名为^FF0000泄漏^000000的残酷怪物";
	mes "可以变成动物";
	mes "并催眠人们杀人。";
	next;
	mes "[年轻女士]";
	mes "你知道这个世界的危险。";
	mes "小心一点总没有坏处";
	mes "哪怕这只是一个都市传说。";
	//emotion ET_SCRATCH; //TODO
	close;
}

dewata,280,236,4	script	Settler#dew	726,{
	mes "[定居者]";
	mes "我并不是真正来自^8B4513Dewata^000000。";
	mes "但在旅行的过程中，";
	mes "我找到了这个地方...";
	mes "并做出了我的决定";
	mes "生活在这个美丽的小镇。";
	next;
	mes "[定居者]";
	mes "我很高兴住在这里";
	mes "并去发现和体验";
	mes "所有这些美丽的景点。";
	mes "这是梦想成真。";
	next;
	mes "[定居者]";
	mes "你有没有想过";
	mes "也在这里定居？";
	close;
}

dewata,204,230,6	script	Dieting Lady#dew	537,{
	if (countitem(11533) == 0) {
		mes "[节食女士]";
		mes "啊~我饿了！！";
		mes "我现在应该节食了。";
		mes "为什么我还饿？";
		next;
		mes "[节食女士]";
		mes "别跟我说太多！！";
		mes "我想尝尝那个烤^006400沙爹^000000";
		mes "就这么多~！！！！！！";
		close;
	}
	mes "[节食女士]";
	mes "是不是让人垂涎欲滴";
	mes "^006400沙爹^000000给我？";
	next;
	switch(select("是的，想尝尝吗？：不，这是给我的！")) {
	case 1:
		delitem 11533,1; //Satay
		mes "[节食女士]";
		mes "非常感谢！";
		mes "我已经好饿了~！！";
		mes "这个^006400沙爹^000000的味道";
		mes "真好啊~！";
		next;
		getitem 11533,1; //Satay
		mes "[节食女士]";
		mes "啊……我得回过神来，";
		mes "好事太多反而是坏事……";
		mes "我应该坚持我的饮食。";
		mes "对不起，我会把它还给你。";
		emotion ET_CRY;
		close;
	case 2:
		mes "[节食女士]";
		mes "Arrgg..你太贪心了！";
		mes "因为你不想分享。";
		mes "无论如何，我不应该在饮食上作弊";
		mes "即使是看起来和闻起来都很好的东西......";
		emotion ET_HNG;
		close;
	}
}

dewata,117,156,6	script	Dreaming Kid#dew	538,{
	mes "[做梦的孩子]";
	mes "我奶奶说";
	mes "从前有一只鸟，名叫^FF0000Garuda^000000";
	mes "谁飞过这个城镇的上空。";
	mes "后来我们有了很大的收获";
	mes "并能够幸福地生活。";
	next;
	mes "[做梦的孩子]";
	mes "我想看看那只鸟";
	mes "再次飞翔在我们小镇上空！";
	mes "然后我的妈妈、爸爸";
	mes "奶奶和爷爷";
	mes "可以像以前一样快乐。";
	mes "你怎么认为？";
	close;
}

dewata,144,216,4	script	Energetic Kid#dew	538,{
	mes "[精力充沛的孩子]";
	mes "我父母曾经告诉我...";
	next;
	mes "- ^006400儿子，^000000 -";
	mes "- ^006400你要好好学习。^000000 -";
	mes "- ^006400快乐。^000000 -";
	mes "- ^006400慢慢生活^000000 -";
	mes "- ^006400 和安全生活。^000000 -";
	mes "- ^006400那边的山^000000 -";
	mes "- ^006400将永远在你身边。^000000 -";
	next;
	mes "[精力充沛的孩子]";
	mes "这不是一个伟大的哲学吗？";
	mes "我想那样生活";
	mes "并成为一个伟大的人，";
	mes "并帮助我们镇上的所有人";
	mes "从此幸福快乐地生活！";
	close;
}

dewata,127,248,6	script	Wishing Kid#dew	539,{
	mes "[希望成为成年人的孩子]";
	mes "我的家人一直在酝酿";
	mes "多年来的传统德瓦塔葡萄酒。";
	mes "我想自己喝";
	mes "但我爸爸说我太年轻了。";
	mes "他说我可以喝";
	mes "只有在我睡了超过^FF0000100天^000000之后。";
	next;
	mes "[希望成为成年人的孩子]";
	mes "所有大人都爱喝酒";
	mes "我爸爸的酒。";
	mes "我很想尝试一些。";
	mes "但是，我该怎么办……？";
	mes "我已经忘记了多少个^FF0000夜晚^000000";
	mes "我已经睡了……呜呜！";
	emotion ET_CRY;
	close;
}

dew_fild01,101,259,6	script	Jaty Tribe Gatekeeper	541,{
	mes "[贾蒂部落守门人]";
	mes "欢迎来到^FF0000贾蒂^000000镇。";
	mes "我们的部落崇尚力量和勇敢。";
	mes "我们祝您在这里度过安全、宁静的时光。";
	close;
}

dew_fild01,106,306,4	script	R. Jaty Tribe Warrior	541,{
	mes "[保留贾蒂战士]";
	mes "我想成为最好的";
	mes "就像从来没有人那样。";
	next;
	mes "[保留贾蒂战士]";
	mes "我正在学习狩猎技能";
	mes "这些日子。我越来越";
	mes "越来越好";
	mes "每天！";
	next;
	mes "[保留贾蒂战士]";
	mes "我不会停下来直到";
	mes "我成为一名真正的战士！";
	close;
}
