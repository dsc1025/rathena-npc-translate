//===== rAthena Script =======================================
//= Penal Servitude 
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A simple Penal Servitude Script.
//= It could cheer up your prisoners a bit.
//===== Additional Comments: =================================
// This script uses CHEQUES of the 2nd KAFRA_BANK.TXT
// var PRISON - it counts number of your imprisonments.
// 1.1 English translation
// 1.2 Stricted the conditions a bit
//===== Translated By: =======================================
//= dsc
//============================================================

sec_pri,36,58,1	script	Chief Warder	105,{ 
	mes "[萨迪厄斯]";
	emotion ET_QUESTION;
	mes (Sex==SEX_MALE?"":"女士")+" 囚犯#"+BaseLevel+JobLevel+"，";
	mes "那边有什么声音？";
	next;
	menu "Let me free, I'll pay!",-, "Let me to work off my freedom!",M_Q1, "Nothing",M_NO_THANKS;

M_PAY:
	mes "[萨迪厄斯]";
	set .@MUSTPAY,(PRISON+1)*1000000;
	if (.@MUSTPAY<1000000) set .@MUSTPAY,1000000;
	if (.@MUSTPAY>100000000) set .@MUSTPAY,100000000;
	if (PRISON==0) mes "我记不起你了。这是你第一次吗？";
	if (PRISON>0) mes "又是你吗？从第一次访问开始，有些人并没有变得更聪明......";
	mes "好的，我们可以释放您 ^FF0000"+.@MUSTPAY+"z^000000。";
	next;
	menu "I pay cash!",M_PAYCASH,"Do you accept cheques?",M_PAYBANK,"I've changed my mind.",-;

	mes "[萨迪厄斯]";
	mes "你有时间考虑一下...";
	close;

M_PAYCASH:
	if (.@MUSTPAY>Zeny) goto L_NOCASH;
	set Zeny,Zeny-.@MUSTPAY;
	mes "[萨迪厄斯]";
	mes "好吧，到处唱。";
	goto L_RELEASE;

L_NOCASH:
	mes "[萨迪厄斯]";
	mes "这是什么？这还不够！";
	close;

M_PAYBANK:
	if (.@MUSTPAY>#kafrabank) goto L_NOBANK;
	set #kafrabank,#kafrabank-.@MUSTPAY;
	mes "[萨迪厄斯]";
	mes "好的，在你的支票上签名。并把你的名字记在我的书里。";
	goto L_RELEASE;

L_NOBANK:
	mes "[萨迪厄斯]";
	if (#kafrabank==0) mes "您的案件没有提及您的银行账户。";
	if (#kafrabank!=0) mes "唉，箱子里有你的银行账户信息... ^FF0000"+#kafrabank+"z^000000...。这还不够。";
	mes "立即停止你的愚蠢游戏吧！";
	close;

L_RELEASE:
	set PRISON,PRISON+1;
	next;
	mes "[萨迪厄斯]";
	mes "你现在自由了！";
	next;
	savepoint "izlude",105,112;
	warp "izlude",105,112;
	close;

M_Q1:
	mes "[萨迪厄斯]";
	mes "我们的库存缺少伞菌。  还记得你早上的技巧吗？  我们赞助的蘑菇农场的一些工作怎么样？";
	next;
	menu "OK, I'll work off!",-, "I love this prison!",M_NO_THANKS;

	mes "[萨迪厄斯]";
	if(checkcart() || checkfalcon() || checkriding()){
		emotion ET_SCRATCH;
		mes "抱歉，您应该释放您的 Cart、PecoPeco 或 Falcon！";
		close;
	}
	mes "与我们的监督奥利弗交谈。";
	next;

	nude;

	set .@rw,rand(1,4);
	if (.@rw==2) goto L_W2;
	if (.@rw==3) goto L_W3;
	if (.@rw==4) goto L_W4;

L_W1:
	savepoint "sec_in02",179,76;
	warp "sec_in02",179,76;
	close;

L_W2:
	savepoint "sec_in02",139,32;
	warp "sec_in02",139,32;
	close;

L_W3:
	savepoint "sec_in02",100,28;
	warp "sec_in02",100,28;
	close;

L_W4:
	savepoint "sec_in02",107,75;
	warp "sec_in02",107,75;
	close;

M_NO_THANKS:
	mes "[萨迪厄斯]";  
	if (rand(2)) mes "今天是圣诞节吧？";
	mes "现在闭嘴并退后！";
	if (rand(2)) emotion ET_HUK;
	close;  
}

sec_in02,137,57,1	script	Overseer	708,{ 
	mes "[奥利弗]";

	delitem 4002,countitem(4002);//Items: Fabre_Card,
	delitem 4009,countitem(4009);//Items: Chonchon_Card,
	delitem 4022,countitem(4022);//Items: Spore_Card,
	delitem 4048,countitem(4048);//Items: Poison_Spore_Card,
	
	if (PRISON_Q <= 0 ) goto L_GET_Q;

	mes "我要你收集^FF0000"+PRISON_Q+"橙网蘑菇^000000。";
	if ( countitem(1069)<PRISON_Q ) close;//Items: Orange_Net_Mushroom,
	mes "好吧...把所有的蘑菇都给我...";
	mes "让我看看..."+countitem(1069)+" 几乎可以食用的...";//Items: Orange_Net_Mushroom,
	mes "和"+countitem(1070)+"无用的致命的。";//Items: Orange_Gooey_Mushroom_,
	next;
	mes "[奥利弗]";
	mes "谢谢。你自由了！";
	set PRISON_Q,0;
	delitem 1069,countitem(1069);//Items: Orange_Net_Mushroom,
	delitem 1070,countitem(1070);//Items: Orange_Gooey_Mushroom_,
	next;
	savepoint "izlude",105,112;
	warp "izlude",105,112;
	close;

L_GET_Q:
	set PRISON,PRISON+1;
	if (PRISON > 1) mes "嗯...你已经来过这里了...好吧好吧...";

	if ( countitem(1069) || countitem(1070) ) mes "放下这些蘑菇！！它们有毒！";//Items: Orange_Net_Mushroom, Orange_Gooey_Mushroom_,
	delitem 1069,countitem(1069);//Items: Orange_Net_Mushroom,
	delitem 1070,countitem(1070);//Items: Orange_Gooey_Mushroom_,

	set PRISON_Q, PRISON*3 + BaseLevel/3;
	set PRISON_Q, PRISON_Q * (readparam(bAspd)/55 + 1);
	set PRISON_Q, PRISON_Q+rand(50,60);
	mes "好吧，你必须给我带来^FF0000"+PRISON_Q+"^000000可食用的^FF0000橙网蘑菇^000000toadstoo..蘑菇。";
	if(readparam(bAspd)>=100) mes "你真是个快人！那你就得去买更多的蘑菇了。";
	if (rand(10)<4) mes "你可能会在早餐时吃那些致命的东西。";
	close;
}


sec_in02	mapflag	nomemo
sec_in02	mapflag	nosave	SavePoint
sec_in02	mapflag	noteleport
sec_in02	mapflag	nobranch
sec_in02	mapflag	nowarp
sec_in02	mapflag	nowarpto
sec_in02	mapflag	noexp
sec_in02	mapflag	noskill
//sec_in02	mapflag	pvp
//sec_in02	mapflag	pvp_noparty
//sec_in02	mapflag	gvg
sec_in02	mapflag	pvp_nightmaredrop	random,all,300
sec_in02	mapflag	hidemobhpbar

sec_in02,138,55,100,100	monster	Toadstool	1182,30,10000,10000
sec_in02,138,55,100,100	monster	Fabre	1184,10,20000,20000
sec_in02,138,55,100,100	monster	Chonchon	1183,11,20000,20000
sec_in02,138,55,100,100	monster	Spore	1014,12,20000,20000
sec_in02,138,55,100,100	monster	Poison Spore	1077,3,20000,20000
