//===== rAthena Script =======================================
//= Kagerou/Oboro Job Quest
//===== By: ==================================================
//= M45T3R, Dastgir Pojee, Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Ninja -> Kagerou/Oboro.
//===== Additional Comments: =================================
//= 1.0 First version. Uses official script structure and text
//=     from iRO (ripped by M45T3R/Dastgir Pojee). [Euphy]
//===== Translated By: =======================================
//= dsc
//============================================================

// Starting the Quest
//============================================================
que_ng,28,156,0	script	Start#ko	139,1,1,{
	end;
OnTouch:
	if (BaseLevel >= 99 && JobLevel >= 70 && job_kagero == 0 && Class == Job_Ninja) {
		mes "通往^25C18D忍者公会^25C18D的秘密通道";
		mes "你会听到熟悉的声音互相交谈。";
		next;
		mes "[未知声音A]";
		mes "好久不见。你好吗？你收养的孩子还好吗？";
		next;
		mes "[未知声音B]";
		mes "我必须把它交给你。你已经选择了一些有竞争力的。";
		next;
		mes "[未知声音A]";
		mes "在我耳中听起来就像音乐。被送到空气污染严重的异国他乡已经够糟糕的了，现在又对这个地方的记忆耿耿于怀……";
		next;
		mes "[未知声音A]";
		mes "你怎么认为？我们现在应该轮换吗？";
		next;
		mes "[未知声音B]";
		mes "还没有放弃，对吧。你真的认为他会同意吗？";
		next;
		mes "[未知声音A]";
		mes "无需苦涩。嘿！来来往往的孩子中有没有找到^BD0408地点^BD0408的孩子？";
		next;
		mes "[未知声音B]";
		mes "因为寻找隐藏的地方也是考验的一部分。";
		next;
		mes "[未知声音A]";
		mes "你就像我第一次见到你时一样固执。它只是某处的一堵墙...";
		next;
		mes "[未知声音B]";
		mes "嘘！我认为有人在窃听。匆忙！回到任务区。";
		next;
		mes "谈话突然停止了。隐藏的地方？墙？测试的一部分？这到底是怎么回事？";
		setquest 5131;
		set job_kagero,1;
		close;
	} else if (BaseJob != Job_Ninja && job_kagero > 0) {
		for (set .@i,5131; .@i<=5146; set .@i,.@i+1)
			if (isbegin_quest(.@i)) erasequest .@i;
		set job_kagero,0;
	}
	end;
}

que_ng,21,76,0	script	Wall with a Drawing#ko	844,{
	if (Class == Job_Oboro || Class == Job_Kagerou) {
		mes "你不应该再使用这条秘密通道。";
		close;
	} else if (job_kagero == 1 && Class == Job_Ninja) {
		mes "您找到了一个类似于隐藏地点的位置。";
		next;
		mes "从那时起，这堵墙就在这里了。我之前怎么没注意到呢？";
		next;
		mes "现在我知道这是一条秘密通道，我该怎么做才能进去呢？";
		next;
		if(select("撕下图画。：用手触摸图画。") == 1) {
			mes "我不能把它撕下来，因为我不拥有它。";
			close;
		}
		mes "你用手尖触碰画作，突然感觉到一种被拉扯的感觉。";
		close2;
		warp "job_ko",26,111;
		end;
	} else if (job_kagero > 1 && Class == Job_Ninja) {
		mes "进入^25C18D忍者公会^000000藏身处。";
		next;
		if(select("输入：不输入。") == 2)
			close;
		mes "触摸图画的时候，有一种被拉扯的感觉……"; //custom translation
		close2;
		warp "job_ko",26,111;
		end;
	} else {
		mes "有一堵墙，上面有一幅伟大的图画。";
		close;
	}
	end;
}

job_ko,25,115,4	script	Old Man#ko	588,{
	if (MaxWeight - Weight < 1000 || checkweight(1201,1) == 0) {
		mes "Error 500 (Server Error)!!1500.That’s an error.There was an error. Please try again later.That’s all we know.";
		mes "不需要带那么多东西。"; //custom translation
		close;
	}
	if (BaseJob != Job_Ninja) {
		for (set .@i,5131; .@i<=5146; set .@i,.@i+1)
			if (isbegin_quest(.@i)) erasequest .@i;
		set job_kagero,0;
		mes "[与月盖]";
		mes "你不属于忍者家族。";
		close2;
		warp "amatsu",147,136;
		end;
	}
	if (job_kagero == 1) {
		cutin "job_ko03",2;
		mes "^1A95E6一位看上去精神矍铄的老人正坐着，目光茫然。^1A95E6";
		next;
		if(select("忽略：跟他说话。") == 1) {
			close2;
			cutin "",255;
			end;
		}
		mes "["+strcharinfo(0)+"]";
		mes "打扰一下...";
		next;
		mes "^1A95E6您尝试与老人交谈，但没有任何反应。就在你准备转身离开时……^1A95E6";
		next;
		mes "[老人]";
		mes "终于有顾客了！嗯……还有活生生的！";
		next;
		select("对不起……我在哪儿？");
		mes "[老人]";
		mes "^1A95E6他一直在说话，不停地回答你的问题。^1A95E6";
		next;
		mes "[老人]";
		mes "在古代天松时代，曾经有一个历史书或故事中从未提及过的安静的氏族。";
		next;
		mes "[老人]";
		mes "他们生活在阴影之下，却总是像向日葵一样向往灿烂的阳光。";
		next;
		mes "[老人]";
		mes "一个忠于他们的领主的氏族，他们将领主视为他们的光明太阳。";
		next;
		mes "[老人]";
		mes "……一个非常值得信赖的部落……";
		next;
		mes "[老人]";
		mes "....忠于他们的核心...";
		next;
		mes "[老人]";
		mes "...正直的家族...";
		next;
		select("他们怎么了？");
		mes "^1A95E6老人一脸忧郁地看着你。^1A95E6";
		next;
		mes "[老人]";
		mes "你为什么对一个被领主抛弃并从历史中消失的氏族感兴趣？";
		next;
		if(select("我是忍者：我很无聊。") == 2) {
			mes "[老人]";
			mes "很高兴认识你。";
			close2;
			warp "amatsu",147,136;
			end;
		}
		cutin "job_ko02",2;
		mes "[老人]";
		mes "忍者！曾经有一段时间，氏族也被称为忍者。";
		next;
		erasequest 5131;
		setquest 5132;
		set job_kagero,2;
		mes "[老人]";
		mes "你必须侧耳倾听，因为我有很多关于部落的故事要告诉你。";
		close2;
		cutin "",255;
		end;
	} else if (job_kagero == 2) {
		cutin "job_ko01",2;
		mes "^1A95E6老人以一种非常舒缓的语气开始讲故事，就像在给他的孙子讲故事一样。^1A95E6";
		next;
		mes "[老人]";
		mes "这可以追溯到远古时代，但在阿松没有人记得这件事。";
		next;
		mes "[老人]";
		mes "这个家族在幕后工作，基本上为他们的领主而活。";
		next;
		mes "[老人]";
		mes "他们非常忠诚，做主人要求的任何事情。";
		next;
		mes "[老人]";
		mes "忍者，这个家族被称为黑暗家族，但这并不意味着他们想隐藏在黑暗中。";
		next;
		mes "[老人]";
		mes "他们足够忠诚，作为领主的仆人感到满意，但他们的忠诚却成了问题。";
		next;
		select("问题？");
		mes "[老人]";
		mes "他们是一个连领主都不太了解的秘密组织。";
		next;
		mes "[老人]";
		mes "少数知道该氏族存在的人试图调查他们，但没有人能够透露他们的真实身份。";
		next;
		mes "[老人]";
		mes "这就是为什么这个氏族从忠诚的仆人成长为一个因其秘密而令人畏惧的群体。";
		next;
		mes "[老人]";
		mes "领主避开了这个氏族，不再召唤他们为他们服务，但他们从未背叛他。";
		next;
		select("他们是非常忠诚的人。");
		cutin "job_ko03",2;
		mes "[老人]";
		mes "是的，他们是。非常忠诚。";
		next;
		erasequest 5132;
		setquest 5133;
		set job_kagero,3;
		mes "^1A95E6老人看上去更加凄凉。^1A95E6";
		close2;
		cutin "",255;
		end;
	} else if (job_kagero == 3) {
		cutin "job_ko03",2;
		mes "[老人]";
		mes "自古以来，这个家族就一直隐居在隐秘的地方。现任领主根本不知道他们的存在。";
		next;
		select(".........");
		cutin "job_ko01",2;
		mes "[老人]";
		mes "我是黑暗忍者家族最后的只园队长。";
		next;
		if(select("我想你的时代已经结束了：我需要你的教导。") == 1) {
			cutin "job_ko04",2;
			mes "[领袖只园]";
			mes "你是要结束这位老人生命的刺客吗？";
			next;
			mes "[领袖只园]";
			mes "这就是为什么你对我的部落表现出兴趣。我不会轻易放弃。";
			next;
			percentheal -99,0;
			mes "噗！！";
			mes "^1A95E6巨大的冲击让你失去了良知。^000000";
			close2;
			warp "amatsu",147,136;
			end;
		}
		cutin "job_ko02",2;
		mes "[领袖只园]";
		mes "教学...";
		mes "自从我上次听到这个词已经很久了。";
		next;
		mes "[领袖只园]";
		mes "我想这次小小的访问并不是巧合，而是一种联系的开始。";
		next;
		mes "[领袖只园]";
		mes "抱歉我不是老师。但！";
		next;
		mes "[领袖只园]";
		mes "我可以在你选择的道路上帮助你，^33CC71"+((Sex == SEX_MALE)?"Kagerou":"Oboro")+"^000000路径。";
		next;
		mes "^1A95E6当你溜走时，你隐约听到只园队长的声音。^1A95E6";
		next;
		erasequest 5133;
		setquest 5134;
		set job_kagero,4;
		mes "[领袖只园]";
		mes "如果你准备好跟随我，祗园队长，走上"+((Sex == SEX_MALE)?"Kagerou":"Oboro")+"之路，我们还会再见面的。";
		close2;
		warp "amatsu",147,136;
		end;
	} else if (job_kagero == 4) {
		cutin "job_ko02",2;
		mes "[领袖只园]";
		mes "我以为你害怕^33CC71的路径"+((Sex == SEX_MALE)?"影郎":"奥博罗")+"^33CC71并且不会回来。";
		next;
		mes "[领袖只园]";
		mes "但从你的眼神来看，我想我误判了你。";
		next;
		mes "[领袖只园]";
		mes "你要训练自己的身心，在未知的世界中稳步前行，不被诱惑留在路上。";
		next;
		mes "[领袖只园]";
		mes "我们的祖先有四次考验来训练我们的人民。";
		next;
		select("4 次测试？");
		cutin "job_ko01",2;
		mes "[领袖只园]";
		mes "是的，有 4 项测试。";
		mes "我的祖先通过 4 个测试来训练我的人民，涉及^087FF8知识^000000、^087FF8生存^000000、^087FF8武器^000000和^087FF8战斗^000000。";
		next;
		mes "[领袖只园]";
		mes "我知道你很好奇这些测试是什么。我来一一解释一下。";
		next;
		while(1) {
			set .@i, select("知识测试：生存测试：武器测试：战斗测试");
			set .@test, .@test | (1<<(.@i-1));
			switch (.@i) {
			case 1:
				mes "[领袖只园]";
				mes "我们需要有知识才能协助主。本次测试就是为了这个目的。";
				next;
				mes "[领袖只园]";
				mes "如果您成功解决了 10 个问题中的 9 个问题，您就可以通过测试。";
				next;
				if (.@test != 15) {
					mes "[领袖只园]";
					mes "如果你学习稳定的话，考试很容易通过。现在您还对什么其他测试感到好奇？";
					next;
				}
				break;
			case 2:
				mes "[领袖只园]";
				mes "任务并不总是轻松和安全的。这就是为什么生存本能对我们至关重要。";
				next;
				mes "[领袖只园]";
				mes "我的祖先将这种测试称为骰子测试。这是一个根据骰子结果前进的测试。";
				next;
				mes "[领袖只园]";
				mes "将其视为人们玩的简单骰子游戏。";
				next;
				mes "[领袖只园]";
				mes "但在测试过程中千万不要放松警惕，因为它并不是无缘无故地被称为生存测试的。";
				next;
				mes "[领袖只园]";
				mes "有些方块会帮助你，有些方块会干扰你。";
				next;
				if (.@test != 15) {
					mes "[领袖只园]";
					mes "如果你能明智地处理各种情况，你就能通过考验。现在您还对什么其他测试感到好奇？";
					next;
				}
				break;
			case 3:
				mes "[领袖只园]";
				mes "我的部落因使用我们创造的独特武器而闻名。";
				next;
				mes "[领袖只园]";
				mes "如果你创造了自己独特的武器，你将被认为是有福的。";
				next;
				mes "[领袖只园]";
				mes "为自己打造一把武器并精炼它，就是这次测试的目的。";
				next;
				if (.@test != 15) {
					mes "[领袖只园]";
					mes "我希望你能得到祝福并找到最适合自己的武器。现在您还对什么其他测试感到好奇？";
					next;
				}
				break;
			case 4:
				mes "[领袖只园]";
				mes "任务并不总是单独完成的。您通常会为了一个共同的目标而组成 2 或 3 人的团队。";
				next;
				mes "[领袖只园]";
				mes "战斗测试仅适用于通过知识、生存和武器测试的人。所以！这是最后一次测试。";
				next;
				mes "[领袖只园]";
				mes "与其他三个单独完成的测试不同，您必须在这个最终测试中与其他人竞争。";
				next;
				mes "[领袖只园]";
				mes "目标只有一个！！";
				mes "只有第一个到达目标的人才能通过测试。";
				next;
				mes "[领袖只园]";
				mes "如果在测试过程中没有竞争对手，那你就很幸运了。";
				next;
				if (.@test != 15) {
					mes "[领袖只园]";
					mes "一个挑战比解释一百遍更好。真正的体验会让你变得更好。";
					next;
				}
				break;
			}
			if (.@test == 15) break;
		}
		cutin "job_ko02",2;
		mes "[领袖只园]";
		mes "眼见为实，所以继续接受挑战。"; //custom translation
		next;
		erasequest 5134;
		setquest 5135;
		set job_kagero,5;
		mes "[领袖只园]";
		mes "准备工作完成后我们就开始吧。";
		close2;
		cutin "",255;
		end;
	} else if (job_kagero == 5) {
		set .@ko_test_01, isbegin_quest(5136);
		set .@ko_test_02, isbegin_quest(5137);
		set .@ko_test_03, isbegin_quest(5138);
		set .@ko_test, .@ko_test_01 + .@ko_test_02 + .@ko_test_03;
		if (.@ko_test == 0) {
			cutin "job_ko03",2;
			mes "[领袖只园]";
			mes "已经有一段时间了。";
			next;
			mes "[领袖只园]";
			mes "既然这样我就感到幸福了。看到像你们这样的年轻人挑战自我，走上新的道路，我感到自己年轻、充满活力。";
			next;
			mes "[领袖只园]";
			mes "我们已经解释完了有关测试的内容，现在我应该告诉你我的氏族故事吗？";
			next;
			cutin "job_ko01",2;
			mes "[领袖只园]";
			mes "我的氏族是从两个战士开始的。";
			next;
			mes "[领袖只园]";
			mes "阳炎，一位如同太阳舞动的火焰的战士。";
			next;
			mes "[领袖只园]";
			mes "奥波罗，如朦胧月光般的战士。";
			next;
			mes "[领袖只园]";
			mes "太阳和月亮。";
			mes "阳光照亮世界，月光照亮黑夜。两人都是非常相似但又不同的战士。";
			next;
			mes "[领袖只园]";
			mes "曾经有一段时间，两位战士之间存在着敌意。";
			next;
			mes "[领袖只园]";
			mes "但没过多久，他们就成为了一个氏族。";
			next;
			select("后来进展如何？");
			cutin "job_ko02",2;
			mes "[领袖只园]";
			mes "哈哈哈。一次性听完整个故事一点也不有趣，不是吗？通过测试后再回来，我会继续我的故事。";
			next;
			mes "[领袖只园]";
			mes "您将选择哪个测试作为您的第一个测试？";
			next;
			callsub L_StartTest,select("知识测试：生存测试：武器测试"),1;
			end;
		} else if (.@ko_test == 2) {
			cutin "job_ko01",2;
			mes "[领袖只园]";
			if (.@ko_test_01 == 2) {
				set .@menu$,":Test of Survival:Test of Weaponry";
				mes "您已经通过了知识测试。";
				next;
				mes "[领袖只园]";
				mes "我的朋友不太容易认可别人，但我猜他喜欢你。";
				next;
			} else if (.@ko_test_02 == 2) {
				set .@menu$,"Test of Knowledge::Test of Weaponry";
				mes "你已经通过了生存考验。";
				next;
				mes "[领袖只园]";
				mes "看起来你在这次测试中经历了地狱般的考验。";
				next;
			} else if (.@ko_test_03 == 2) {
				set .@menu$,"Test of Knowledge:Test of Survival:";
				mes "你已经通过了武器测试。";
				next;
				mes "[领袖只园]";
				mes "我不知道乔对你的表现是否满意。";
				next;
			}
			select("请继续讲部落的故事。");
			cutin "job_ko02",2;
			mes "[领袖只园]";
			mes "看来您非常渴望听到更多信息。我哪里中断了……啊！我记得。";
			next;
			mes "[领袖只园]";
			mes "阳炎，一位如同太阳舞动的火焰的战士。";
			next;
			mes "[领袖只园]";
			mes "奥波罗，如朦胧月光般的战士。";
			next;
			mes "[领袖只园]";
			mes "两个战士一开始并不亲近，因为性格和其他一切都完全相反。";
			next;
			mes "[领袖只园]";
			mes "他们首先相遇的地方就是战场。你也知道敌人在战场上如何互相打招呼。";
			next;
			mes "[领袖只园]";
			mes "他们最终互相重伤。";
			next;
			mes "[领袖只园]";
			mes "但你能做什么呢？战争就是战争。";
			next;
			mes "[领袖只园]";
			mes "昨天与你一起欢笑的朋友是你今天必须在战争中与之战斗的敌人。";
			next;
			mes "[领袖只园]";
			mes "所以在战争期间没有人能与任何人和睦相处。";
			next;
			mes "[领袖只园]";
			mes "等你通过另一次测试后我会继续讲故事。";
			next;
			mes "[领袖只园]";
			mes "您会选择哪个测试进行第二次测试？";
			next;
			callsub L_StartTest,select(.@menu$),2;
			end;
		} else if (.@ko_test == 4) {
			cutin "job_ko04",2;
			mes "[领袖只园]";
			if (.@ko_test_01 == 2 && .@ko_test_02 == 2) {
				set .@last_test,3;
				mes "您已经通过了^339CCC知识和生存测试^000000！";
			} else if (.@ko_test_01 == 2 && .@ko_test_03 == 2) {
				set .@last_test,2;
				mes "你已经通过了^339CCC知识和武器测试^000000！";
			} else if (.@ko_test_02 == 2 && .@ko_test_03 == 2) {
				set .@last_test,1;
				mes "您已经通过了^339CCC生存和武器测试^000000！";
			}
			next;
			mes "[领袖只园]";
			mes "您已经完成了两项测试。希望你从他们身上学到了很多东西。";
			next;
			cutin "job_ko01",2;
			mes "[领袖只园]";
			mes "我们继续讲故事吗？";
			next;
			mes "[领袖只园]";
			mes "阳炎，一位如同太阳舞动的火焰的战士。";
			next;
			mes "[领袖只园]";
			mes "奥波罗，如朦胧月光般的战士。";
			next;
			mes "[领袖只园]";
			mes "我想当这两个战士在战场上成为敌人时我就停止了。";
			next;
			mes "[领袖只园]";
			mes "漫长的战争最终结束了，但幸存者的创伤和痛苦才刚刚开始。";
			next;
			mes "[领袖只园]";
			mes "于是，这两位战士开始拥抱并治愈战争创伤，合而为一。";
			next;
			mes "[领袖只园]";
			mes "有一个背景故事，一个男人出现在他们面前并赢得了两位战士的忠诚。";
			next;
			select("这个人是谁？");
			mes "[领袖只园]";
			mes "关于这个人，人们知之甚少。只是简短地提到了两位宣誓效忠的战士。";
			next;
			mes "[领袖只园]";
			mes "也许像我这样的当代人就是当今天松领主的祖先？但这只是一个假设。";
			next;
			mes "[领袖只园]";
			mes "我的氏族故事即将结束。等你通过第三关后再回来，剩下的我会告诉你。";
			next;
			callsub L_StartTest,.@last_test,3;
			end;
		} else if (.@ko_test == 6) {
			cutin "job_ko01",2;
			mes "[领袖只园]";
			mes "您已经完成了三项测试，只剩下一项需要通过。";
			next;
			mes "[领袖只园]";
			mes "正如我之前告诉过你的，最后一次测试是不同的，因为你必须与其他人竞争。";
			next;
			select("你会继续讲故事吗？");
			cutin "job_ko02",2;
			mes "[领袖只园]";
			mes "哈哈哈。我会讲完这个故事。";
			next;
			mes "[领袖只园]";
			mes "阳炎，一位如同太阳舞动的火焰的战士。";
			next;
			mes "[领袖只园]";
			mes "奥波罗，如朦胧月光般的战士。";
			next;
			mes "[领袖只园]";
			mes "据说，赢得两位武士忠诚的人是一个幽默的人。";
			next;
			mes "[领袖只园]";
			mes "比起争吵，他更喜欢笑话和对话，而且他喜欢女人胜过男人。";
			next;
			mes "[领袖只园]";
			mes "嗯……我不知道为什么这部分故事多年来一直被保留，但这个人想把这两个战士聚集在一起。";
			next;
			mes "[领袖只园]";
			mes "战后，两位战士确实一起旅行，但彼此保持着尴尬的距离。";
			next;
			mes "[领袖只园]";
			mes "他们肯定有他们的理由，但他们的主人却会派他们一起去执行一项艰巨的任务，把他们一起关在密室里，一起经历各种情况。";
			next;
			select("听起来是个奇怪的人。");
			mes "[领袖只园]";
			mes "是的，他是。而他的努力也没有白费，两位勇士最终相识并结婚了。";
			next;
			mes "[领袖只园]";
			mes "部落就是这样开始的。";
			next;
			select("之后发生了什么？");
			mes "[领袖只园]";
			mes "可悲的是，故事的下一部分被故意中断。";
			next;
			mes "[领袖只园]";
			mes "我想这是因为有人希望我们放下过去，继续前进。";
			next;
			mes "[领袖只园]";
			mes "就是这样。现在你不是应该准备最后一次考试吗？";
			next;
			set job_kagero,6;
			mes "[领袖只园]";
			mes "但到目前为止，你一定已经厌倦了所有的测试。休息一下。";
			close2;
			cutin "",255;
			end;
		} else if (.@ko_test == 1 || .@ko_test == 3 || .@ko_test == 5) {
			cutin "job_ko04",2;
			if (.@ko_test_01 == 1)
				set .@test_ko$, "Knowledge";
			else if (.@ko_test_02 == 1)
				set .@test_ko$, "Survival";
			else if (.@ko_test_03 == 1)
				set .@test_ko$, "Weaponry";
			mes "[领袖只园]";
			mes "你刚才不是在参加" + .@test_ko$ + "的测试吗？"; //custom translation
			next;
			switch(select("我想回到测试站点。：啊...不。")) {
			case 1:
				mes "[领袖只园]";
				mes "的测试" + .@test_ko$ + "网站在这里。"; //custom translation(?)
				close2;
				if (.@ko_test_01 == 1)
					warp "job_ko",72,128;
				else if (.@ko_test_02 == 1)
					warp "job_ko",62,16;
				else if (.@ko_test_03 == 1)
					warp "job_ko",121,129;
				end;
			case 2:
				mes "[领袖只园]";
				mes "村子就在这儿。";
				close2;
				warp "amatsu",147,136;
				end;
			}
		}
	} else if (job_kagero == 6) {
		cutin "job_ko01",2;
		mes "[领袖只园]";
		mes "你已经回来了吗？你本来可以多休息一下。有理由匆忙吗？";
		next;
		input .@inputstr$;
		cutin "job_ko03",2;
		mes "[领袖只园]";
		mes "因为^B24E59" + .@inputstr$ + "^000000？";
		next;
		mes "[领袖只园]";
		mes "我必须承认，我现在不理解你了。";
		next;
		mes "[领袖只园]";
		mes "但既然你已经经历了很多，我相信你会在期末考试中取得好成绩。";
		next;
		cutin "job_ko01",2;
		mes "[领袖只园]";
		mes "现在！不再闲聊。我会让你知道战斗测试的目标是什么。";
		next;
		mes "[领袖只园]";
		mes "目标是名为^FF0000氏族秘密^000000的怪物。";
		next;
		mes "[领袖只园]";
		mes "你必须小心，因为测试场地中有许多形状和名称相似的怪物。";
		next;
		mes "[领袖只园]";
		mes "如果你幸运的话，还会有其他人瞄准这个怪物。";
		next;
		mes "[领袖只园]";
		mes "愿“" + ((Sex == SEX_MALE)?"阳炎舞动的太阳":"敖波罗，雾蒙蒙的月光") + "”的祝福与你同在。";
		next;
		setquest 5146;
		set job_kagero,7;
		mes "[领袖只园]";
		mes "那我们就去战斗试验场吧。";
		close2;
		switch(rand(3)) {
			case 0: warp "job_ko",142,20; break;
			case 1: warp "job_ko",141,80; break;
			case 2: warp "job_ko",107,60; break;
		}
		end;
	} else if (job_kagero == 7 || job_kagero == 8) {
		cutin "job_ko03",2;
		mes "[领袖只园]";
		mes "我看到你正在进行^339CCC战斗测试^000000。你会回到考场吗？";
		next;
		switch(select("返回考点：参观村庄。")) {
		case 1:
			mes "[领袖只园]";
			mes "测试地点就在这里。"; //custom translation(?)
			close2;
			switch(rand(3)) {
				case 0: warp "job_ko",142,20; break;
				case 1: warp "job_ko",141,80; break;
				case 2: warp "job_ko",107,60; break;
			}
			end;
		case 2:
			mes "[领袖只园]";
			mes "村子就在这儿。";
			close2;
			cutin "",255;
			end;
		}
	} else {
		cutin "job_ko03",2;
		mes "[领袖只园]";
		mes "你不应该在这里。离开！";
		close2;
		warp "amatsu",147,136;
		end;
	}
	end;

//callsub L_StartTest,<1=Knowledge,2=Survival,3=Weaponry>,<test number [1..3]>;
L_StartTest:
	switch (getarg(1)) {
		case 1: set .@str$,"You are starting with the ^339CCCTest of %s^000000? "; break;
		case 2: set .@str$,"You are taking the ^339CCCTest of %s^000000 as the second test? "; break; //custom translation
		case 3: set .@str$,"Your third test is the ^339CCCTest of %s^000000! "; break;
	}
	mes "[领袖只园]";
	switch (getarg(0)) {
	case 1:
		mes sprintf(.@str$ + "那么时隔这么久我就会看到一张熟悉的面孔...","知识");
		next;
		select("熟悉的面孔？");
		setquest 5136;
		mes "[领袖只园]";
		mes "哈哈哈。当我们到达那里你就会知道。知识测试在这里进行。";
		close2;
		warp "job_ko",72,128;
		end;
	case 2:
		mes sprintf(.@str$ + "这是一场孤独的考验，你必须独自面对。","生存");
		next;
		setquest 5137;
		mes "[领袖只园]";
		mes "但我相信你能处理好。生存考验在这里进行。";
		close2;
		warp "job_ko",62,16;
		end;
	case 3:
		mes sprintf(.@str$ + "在你走之前我有东西要给你。","武器");
		next;
		setquest 5138;
		getitem 1002,5; //Iron_Ore
		getitem 1010,1; //Phracon
		mes "您收到 5 个铁矿石和 1 个 Phracon。";
		next;
		mes "[领袖只园]";
		mes "你会发现这些很有用。武器装备的测试就在这里进行。";
		close2;
		warp "job_ko",121,129;
		end;
	}
}

// Test of Knowledge
//============================================================
job_ko,81,124,4	script	Kuuga Gai#ko	730,{
	if (BaseJob != Job_Ninja) {
		for (set .@i,5131; .@i<=5146; set .@i,.@i+1)
			if (isbegin_quest(.@i)) erasequest .@i;
		set job_kagero,0;
		mes "[与月盖]";
		mes "抱歉，你们的家族和我们的家族不一样，有什么问题吗？";
		close2;
		warp "amatsu",147,136;
		end;
	}
	if (job_kagero != 5) {
		goto L_Kick;
	}
	if (MaxWeight - Weight < 1000 || checkweight(1201,1) == 0) {
		mes "[与月盖]";
		mes "这是考验知识的，那你为什么带这么多东西呢？"; //custom translation
		close;
	}
	set .@ko_test_01, isbegin_quest(5136);
	set .@ko_test_01_1, isbegin_quest(5139);
	if (.@ko_test_01 == 1 && .@ko_test_01_1 == 0) {
		mes "[与月盖]";
		mes "已经有一段时间了。";
		next;
		select("你不是...");
		mes "[与月盖]";
		mes "我记得你在寻找忍者之道之前。";
		next;
		mes "[与月盖]";
		mes "你已经这么强了？";
		next;
		mes "[与月盖]";
		mes "哈哈哈-";
		mes "真正有决心的年轻人！我喜欢这样。";
		next;
		mes "[与月盖]";
		mes "好的！您即将参加的测试是^339CCC知识测试^000000。";
		next;
		mes "[与月盖]";
		mes "希望你在专注于变强的同时，没有偷懒学习吧？";
		next;
		switch(select("是：否")) {
		case 1:
			setquest 5139;
			mes "[与月盖]";
			mes "这真是一种解脱。当您准备好开始测试时请告诉我。";
			close;
		case 2:
			setquest 5139;
			mes "[与月盖]";
			mes "所以你们都是说说而已？好吧，那么当你准备好时请告诉我。";
			close;
		}
	} else if (.@ko_test_01 == 1 && .@ko_test_01_1 == 1) {
		mes "[与月盖]";
		mes "我已经准备好了。你准备好参加测试了吗？";
		next;
		if(select("是：否") == 2) {
			mes "[与月盖]";
			mes "好吧，我除了等你还能做什么。";
			close;
		}
		mes "[与月盖]";
		mes "这不是你的第一次测试，是吗？";
		next;
		mes "[与月盖]";
		mes "您只需选择我的问题的正确答案即可。";
		next;
		mes "[与月盖]";
		mes "让我们开始吧。";
		next;

		set .@questions,10;  // number of questions to ask

		// shuffle array an array of questions to be asked
		freeloop(1);
		setarray .@n[0],
			1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,
			26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50;
		for (set .@i,getarraysize(.@n)-1; .@i>0; set .@i,.@i-1) {
			set .@rand, rand(0,.@i);
			set .@tmp, .@n[.@i];
			set .@n[.@i], .@n[.@rand];
			set .@n[.@rand], .@tmp;
		}
		deletearray .@n[10],getarraysize(.@n) - .@questions;
		freeloop(0);

		set @job_ko_Kuuga,0;
		for (set .@i,1; .@i<=.@questions; set .@i,.@i+1) {
			mes "[与月盖]";
			mes (.@i < .@questions)?"问题编号"+.@i+"：":"最后一个问题：";
			switch (.@n[.@i-1]) {
				case 1: callsub L_Question,"What is the DEX + LUK total for a Job Master?",2,"8:10:12:14"; break;
				case 2: callsub L_Question,"What is the highest job level for a ninja?",1,"70:50:99:100"; break;
				case 3: callsub L_Question,"How many skill points do you need to master the Throw Coins skill?",3,"25:30:35:37"; break;
				case 4: callsub L_Question,"Which of the following is the correct number of Evasion and effect duration for Mirror Image level 10?",4,"Up to 7 and 200 seconds:Up to 5 and 220 seconds:Up to 6 and 240 seconds:Up to 5 and 240 seconds"; break;
				case 5: callsub L_Question,"How many skill points do you need to master the Cicada Skin Shed?",1,"5:3:1:4"; break;
				case 6: callsub L_Question,"If Dagger Throwing Practice is at 7, how much ATK is added to Shuriken attacks?",2,"18:21:27:30"; break;
				case 7: callsub L_Question,"What is the basic attack range for Throw Shuriken?",3,"7 blocks:8 blocks:9 blocks:10 blocks"; break;
				case 8: callsub L_Question,"What is the maximum moving range of Shadow Leap?",4,"6 blocks:7 blocks:8 blocks:9 blocks"; break;
				case 9: callsub L_Question,"How much money is used for Throw Coins level 6?",4,"2000-8000:1000-6000:3000-8000:3000-6000"; break;
				case 10: callsub L_Question,"What is the most important aspect of increasing ATK for the Killing Strike skill?",2,"VIT:MAXHP:INT:MAXSP"; break;
				case 11: callsub L_Question,"What is not the effect you get after reaching the STR status?",3,"Close range attack increase:Weight limit increase:Magic attack increase:Weapon attack increase"; break;
				case 12: callsub L_Question,"What is not the effect you get after reaching the INT status?",4,"MAXSP increase:MATK increase:Magic damage increase:Staff attack increase"; break;
				case 13: callsub L_Question,"What is not the effect you get after reaching the DEX status?",2,"Casting time decrease:Evasion increase:Accuracy increase:Long range attack increase"; break;
				case 14: callsub L_Question,"What is not the effect you get after reaching the AGI status?",1,"Accuracy increase:Flee increase:Attack speed increase:Delay after attack decrease"; break;
				case 15: callsub L_Question,"What is not the effect you get after reaching the VIT status?",4,"Weapon defense increase:MAXHP increase:Magic defense increase:Attack increase"; break;
				case 16: callsub L_Question,"What is not the effect you get after reaching the LUK status?",3,"Accuracy increase:Complete evasion increase:Magic damage decrease:Critical rate increase"; break;
				case 17: callsub L_Question,"You need to equip a card to your shoes to enhance Killing Strike attack. Which of the following is the appropriate card to equip?",2,"Gold Acidus Card:Matyr Card:Odium of Thantos Card:Eddga Card"; break;
				case 18: callsub L_Question,"You need a catalyst to use ninja skills. Which of the following skills does not need a catalyst?",3,"Blaze Shield:Watery Evasion:Flaming Petals:Snow Flake Draft"; break;
				case 19: callsub L_Question,"How much does STR/INT go up when Ninja Aura level 5 is used?",2,"4:5:6:7"; break;
				case 20: callsub L_Question,"You are hunting the Orc Warrior. Which Kunai would you use? ",1,"Heat Wave Kunai:Icicle Kunai:Fell Poison Kunai:High Wind Kunai"; break;
				case 21: callsub L_Question,"Which village is the Ninja Guild located in?",2,"Lutie:Amatsu:Dewata:Comodo"; break;
				case 22: callsub L_Question,"How do Ninjas get promoted?",3,"By Val Kiwi:By Randgris:None:By Valkyrie"; break;
				case 23: callsub L_Question,"What was next to me when you first met me?",4,"A cushion:A folding screen:A flowerpot:A brazier"; break;
				case 24: callsub L_Question,"What is the level of Dagger Throwing Practice you have to reach to learn the Killing Strike skill?",1,"7:5:6:8"; break;
				case 25: callsub L_Question,"How many hits does a Throw Kunai give to a monster?",2,"2:3:4:5"; break;
				case 26: callsub L_Question,"When your First Wind is at level 4, what will go with the MATK and range?",3,"MATK400, 8 blocks:MATK500, 7 blocks:MATK500, 8 blocks:MATK400, 7 blocks"; break;
				case 27: callsub L_Question,"Which of the following skills can't you learn at Dagger Throwing Practice level 7?",4,"Throw Kunai:Throw Huuma Shuriken:Killing Strike:Throw Coins"; break;
				case 28: callsub L_Question,"What level do you have to be in Dagger Throwing Practice in order to learn the Throw Coins skill?",4,"4:6:8:10"; break;
				case 29: callsub L_Question,"What is the attack range for the Exploding Dragon?",3,"3*3:4*4:5*5:6*6"; break;
				case 30: callsub L_Question,"How long is the effect time for the Watery Evasion level 7 skill?",2,"40 seconds:45 seconds:50 seconds:55 seconds"; break;
				case 31: callsub L_Question,"What is the attack effect for Haze Slasher level 4?",1,"140%:150%:160%:170%"; break;
				case 32: callsub L_Question,"How much power is in the Huuma Blaze Shuriken weapon?",1,"Fireball 5 , DEX-2:Fireball 5 , DEX-3:Fireball 4 , DEX-2:Fireball 4 , DEX-3"; break;
				case 33: callsub L_Question,"How much power is in the Murasame weapon?",2,"Human attack 10%:Human critical +10:Demon attack 10%:Demon critical +10"; break;
				case 34: callsub L_Question,"What is the maximum hit number for Blaze Shield level 10?",3,"7:8:9:10"; break;
				case 35: callsub L_Question,"What is the property of the Lightning Jolt?",4,"Fire:Water:Ground:Wind"; break;
				case 36: callsub L_Question,"You are creating a weapon for small monsters. What is the best card to use?",4,"Hydra Card:Minorous Card:Skel Woker Card:Desert Wolf Card"; break;
				case 37: callsub L_Question,"You are creating a weapon for mid-sized monsters. What is the best card to use?",3,"Hydra Card:Minorous Card:Skel Woker Card:Desert Wolf Card"; break;
				case 38: callsub L_Question,"You are creating a weapon for huge monsters. What is the best card to use?",2,"Hydra Card:Minorous Card:Skel Woker Card:Desert Wolf Card"; break;
				case 39: callsub L_Question,"Which NPC promotes you?",1,"Valkyrie:Val Kiwi:Balp Hiri:Elephant"; break;
				case 40: callsub L_Question,"Which catalyst do you need to use the Blaze Shield skill?",2,"Ice Stone:Flame Stone:Wind Stone:Earth Stone"; break;
				case 41: callsub L_Question,"Which of the following is not an effect of the Watery Evasion skill?",4,"Movement speed decrease:Waterball availability:Holy Water craftability:VIT decrease"; break;
				case 42: callsub L_Question,"Which of the following matches are incorrect for skill and property?",3,"Flaming Petals-Fire:Snow Flake Draft-Water:Freezing Spear-Ground:Lightning Jolt-Wind"; break;
				case 43: callsub L_Question,"Which of the following is the best skill to use when attacking a ground property enemy?",2,"First Wind:Exploding Dragon:Snow Flake Draft:Wind Blade"; break;
				case 44: callsub L_Question,"Which Ninja Mastery skill level do you need to learn the Exploding Dragon skill?",1,"10:9:8:1"; break;
				case 45: callsub L_Question,"What is the attack range when you master Flip Tatami?",3,"5:7:3:1"; break;
				case 46: callsub L_Question,"What weapon cannot be made by the blacksmith Khaibara?",4,"Khukri:Murasame:Hakujin:Humma Wing Shuriken"; break;
				case 47: callsub L_Question,"Which of the following correctly matches material needed to make a Icicle Kunai?",2,"4 Nimbus Shuriken, 2 Ice Stone:8 Nimbus Shuriken, 2 Ice Stone:2 Nimbus Shuriken, 1 Ice Stone:1 Nimbus Shuriken, 2 Ice Stone"; break;
				case 48: callsub L_Question,"Which of the following Kunai will give the most damage to the ground property monster, Porcellio?",1,"Heat Wave Kunai:Icicle Kunai:Fell Poison Kunai:High Wind Kunai"; break;
				case 49: callsub L_Question,"Which of the following blacksmiths do not create ninja items?",2,"Khaibara:Aiku:Tetsu:Toshu"; break;
				case 50: callsub L_Question,"What is the name of the suspicious man you can meet during the Ninja job change quest?",3,"Red Leopard Jack:Black Leopard Jack:Red Leopard Joe:Black Leopard Joe"; break;
				default:
					mes "[与月盖]";
					mes "发生未知错误。";
					mes "请联系客服。";
					close;
			}
		}
		mes "[与月盖]";
		mes "您已答完全部 10 个问题。没那么糟糕！重要的部分现在开始。";
		next;
		mes "[与月盖]";
		mes "…………";
		next;
		if (@job_ko_Kuuga < 90) {
			mes "[与月盖]";
			mes "你这个傻瓜！！";
			mes "这些你都解决不了？";
			next;
			mes "[与月盖]";
			mes "不敢相信一个走上新道路的人会如此可悲。";
			next;
			mes "[与月盖]";
			mes "我再给你一次机会。";
			mes "您将带着新问题再次参加考试。这次还是通过吧。";
		} else {
			mes "[与月盖]";
			mes "唔。" + (@job_ko_Kuuga) + "？";
			next;
			mes "[与月盖]";
			mes "嗯，看来你的学习并不偷懒。";
			next;
			mes "[与月盖]";
			mes "什么？为自己解决这些问题感到自豪吗？";
			next;
			mes "[与月盖]";
			mes "你还有很长的路要走，这只是其中的一小部分。";
			next;
			mes "[与月盖]";
			mes "嗯……我很好奇你的坚强意志能带你通过其他考验到什么程度。";
			next;
			completequest 5136;
			erasequest 5139;
			mes "[与月盖]";
			mes "我现在就放你走了，你去向祗园队长报告你的结果吧。";
			close2;
			warp "job_ko",16,113;
			end;
		}
		set @job_ko_Kuuga,0;
		close;
	} else if (.@ko_test_01 == 2 && .@ko_test_01_1 == 0) {
		mes "[与月盖]";
		mes "我现在就放你走了，你去向祗园队长报告你的结果吧。";
		close2;
		warp "job_ko",16,113;
		end;
	} else {  //custom translation
	L_Kick:
		mes "[与月盖]";
		mes "你是怎么到这里来的？";
		next;
		mes "[与月盖]";
		mes "摆脱你是我的责任。";
		mes "^000099（他是个矮小、沉默寡言的人。）^000000";
		mes "这会让你退缩！";
		close2;
		warp "amatsu",147,136;
		end;
	}
	end;

//callsub L_Question,"<Question>",<correct answer choice>,"<Choice 1>:<Choice 2>:<Choice 3>:<Choice 4>";
L_Question:
	mes getarg(0);
	next;
	if(select(getarg(2)) == getarg(1))
		set @job_ko_Kuuga, @job_ko_Kuuga + 10;
	return;
}

// Test of Survival
//============================================================

// -----------------------------------------------------------
//  Functions
// -----------------------------------------------------------
function	script	F_KO_Survival_mes	{
	mes "[没有。" + getarg(0) + "]";
	mes "^1A95E6其他字母太小而无法阅读。让我们仔细看看。^000000";
	close;
}

function	script	F_KO_Survival_warp	{
	close2;
	switch (getarg(0)) {
		case  0: warp "job_ko",63,16; break;
		case  1: warp "job_ko",54,16; break;
		case  2: warp "job_ko",46,16; break;
		case  3: warp "job_ko",38,16; break;
		case  4: warp "job_ko",30,16; break;
		case  5: warp "job_ko",22,16; break;
		case  6: warp "job_ko",17,19; break;
		case  7: warp "job_ko",17,27; break;
		case  8: warp "job_ko",17,35; break;
		case  9: warp "job_ko",17,43; break;
		case 10: warp "job_ko",17,51; break;
		case 11: warp "job_ko",17,60; break;
		case 12: warp "job_ko",25,60; break;
		case 13: warp "job_ko",33,60; break;
		case 14: warp "job_ko",41,60; break;
		case 15: warp "job_ko",49,60; break;
		case 16: warp "job_ko",57,60; break;
		case 17: warp "job_ko",62,56; break;
		case 18: warp "job_ko",62,48; break;
		case 19: warp "job_ko",62,40; break;
		case 20: warp "job_ko",62,32; break;
		case 21: warp "job_ko",62,23; break;
		case 22: warp "job_ko",54,23; break;
		case 23: warp "job_ko",46,23; break;
		case 24: warp "job_ko",38,23; break;
		case 25: warp "job_ko",30,23; break;
		case 26: warp "job_ko",25,27; break;
		case 27: warp "job_ko",25,35; break;
		case 28: warp "job_ko",25,43; break;
		case 29: warp "job_ko",25,52; break;
		case 30: warp "job_ko",33,52; break;
		case 31: warp "job_ko",41,52; break;
		case 32: warp "job_ko",49,52; break;
		case 33: warp "job_ko",54,48; break;
		case 34: warp "job_ko",54,40; break;
		case 35: warp "job_ko",54,31; break;
		case 36: warp "job_ko",46,31; break;
		case 37: warp "job_ko",38,31; break;
		case 38: warp "job_ko",33,35; break;
		case 39: warp "job_ko",33,44; break;
		case 40: warp "job_ko",41,44; break;
	}
	end;
}

//callfunc "F_KO_Survival_dice",<location>{,<LP (default:1)>{,<0:auto-warp (default), 1:do not warp>}}
//Return: dice value [1..6] ONLY IF arg2 = 1
function	script	F_KO_Survival_dice	{
	mes "地点：" + getarg(0);
	mes "总LP:" + job_kagero_lp;
	mes "要掷骰子，您需要消耗" + getarg(1,1) + "^FD0260LP^000000。";
	next;
	select("掷骰子。");
	if (job_kagero_lp < getarg(1,1)) {
		mes "由于^FD0260LP^000000不足，您将返回到起始位置。";
		callfunc "F_KO_Survival_warp",0;
		end;
	}
	set job_kagero_lp, job_kagero_lp - getarg(1,1);
	set .@dice, rand(1,6);
	emotion ET_OTL + .@dice, playerattached();
	mes "骰子的结果是" + .@dice + ".";
	if (getarg(2,0))
		return .@dice;
	else {
		mes "按“关闭”向前移动 " + .@dice + " 块。";
		callfunc "F_KO_Survival_warp", .@dice + getarg(0);
		end;
	}
}

//callfunc "F_KO_Survival_dice2",<location>{,<LP (default:2)>}
function	script	F_KO_Survival_dice2	{
	mes "地点：" + getarg(0);
	mes "总LP:" + job_kagero_lp;
	mes "要掷骰子，您需要消耗" + getarg(1,2) + "^FD0260LP^000000。";
	next;
	select("掷第一个骰子。");
	if (job_kagero_lp < getarg(1,2)) {
		mes "由于^FD0260LP^000000不足，您将返回到起始位置。";
		callfunc "F_KO_Survival_warp",0;
		end;
	}
	set job_kagero_lp, job_kagero_lp - getarg(1,2);
	mes "<二手" + getarg(1,2) + " LP >";
	next;
	set @job_ko_dice1, rand(1,6);
	emotion ET_OTL + @job_ko_dice1, playerattached();
	mes "第一个骰子结果是" + @job_ko_dice1 + "。你想掷第二个骰子吗？";
	next;
	select("掷第二个骰子。");
	set @job_ko_dice2, rand(1,6);
	emotion ET_OTL + @job_ko_dice2, playerattached();
	return;
}

//Return: 1:win, 0:draw, -1:lose
function	script	F_KO_Survival_rps	{
	setarray .@rps$[1], "Scissors", "Rock", "Paper";
	setarray .@emote[1], ET_SCISSOR, ET_ROCK, ET_WRAP;  //might be iRO custom(?)

	mes "岩石！纸！剪刀！";
	next;
	set .@pc, select("给剪刀：给石头。：给布。");
	set .@npc, rand(1,3);
	emotion .@emote[.@pc], playerattached();
	emotion .@emote[.@npc];
	mes "Error 500 (Server Error)!!1500.That’s an error.There was an error. Please try again later.That’s all we know.";
	mes "机器：" + .@rps$[.@npc] + ".";
	mes "你：" + .@rps$[.@pc] + ".";
	next;
	if ((.@npc == 1 && .@pc == 2) || (.@npc == 2 && .@pc == 3) || (.@npc == 3 && .@pc == 1)) {
		mes "你赢了";
		return 1;
	} else if (.@pc == .@npc) {
		mes "画";
		return 0;
	} else if ((.@npc == 1 && .@pc == 3) || (.@npc == 2 && .@pc == 1) || (.@npc == 3 && .@pc == 2)) {
		mes "你输了";
		return -1;
	} else {
		mes "发生未知错误。";
		mes "请联系客服。";
		close;
	}
}

// -----------------------------------------------------------
//  Templates
// -----------------------------------------------------------
-	script	Sight#ko_trap	-1,{
	callfunc "F_KO_Survival_mes", atoi(substr(strnpcinfo(2),3,4));
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	switch (atoi(substr(strnpcinfo(2),3,4))) {
		case  2: set .@id,0; break;
		case  7: set .@id,5; break;
		case 11: set .@id,9; break;
		case 23: set .@id,18; break;
		case 26: set .@id,20; break;
		case 30: set .@id,9; break;
		case 35: set .@id,29; break;
	}
	mes "这个方块有一个陷阱。";
	mes "按“关闭”返回块" + .@id + ".";
	callfunc "F_KO_Survival_warp", .@id;
	end;
}

-	script	Sight#ko_trap2	-1,{
	callfunc "F_KO_Survival_mes", atoi(substr(strnpcinfo(2),3,4));
	end;
OnTouch:
	set .@id, atoi(substr(strnpcinfo(2),3,4));
	switch (.@id) {
		case 10: set .@var$, "$20110808_vko01"; break;
		case 28: set .@var$, "$20110808_vko03"; break;
		//case 38: set .@var$, "$20110808_vko04"; break;
	}
	mes "牌子上有小字。";
	next;
	mes "如果你不想被抓住，就设置一个陷阱。";
	next;
	if (getd(.@var$) == 0) {
		mes "目前没有安装陷阱。";
		next;
		mes "您想使用 10 个^FD0260LP^000000 来安装陷阱吗？";
		next;
		if(select("安装陷阱。：掷骰子。") == 1) {
			if (job_kagero_lp > 9) {
				mes "<已使用10张LP>";
				set job_kagero_lp, job_kagero_lp - 10;
				setd .@var$,1;
				next;
				mes "陷阱已安装。请掷骰子。";
				next;
			} else {
				mes "< 未使用 10 LP >";
				mes "^FD0260LP^000000 不足！请掷骰子。";
				next;
			}
		}
	} else {
		set job_kagero_lp,0;
		setd .@var$,0;
		mes "你被困在安装的陷阱里了。";
		mes "按关闭返回到块 0。";
		callfunc "F_KO_Survival_warp",0;
		end;
	}
	callfunc "F_KO_Survival_dice",.@id;
	end;
}

-	script	Sight#ko_heal	-1,{
	callfunc "F_KO_Survival_mes", atoi(substr(strnpcinfo(2),3,4));
	end;
OnTouch:
	set .@id, atoi(substr(strnpcinfo(2),3,4));
	mes "牌子上有小字。";
	next;
	mes "这个街区是有福的。";
	mes "生命点恢复 7 ^FD0260LP^000000。";
	mes "不能超过最大值 100 ^FD0260LP^000000。";
	next;
	mes "<已恢复" + ((job_kagero_lp + 7 > 100) ? "" : "7") + " LP >";
	next;
	set job_kagero_lp, ((job_kagero_lp + 7 > 100) ? 100 : job_kagero_lp + 7);
	callfunc "F_KO_Survival_dice",.@id;
	end;
}

-	script	Sight#ko_curse	-1,{
	callfunc "F_KO_Survival_mes", atoi(substr(strnpcinfo(2),3,4));
	end;
OnTouch:
	set .@id, atoi(substr(strnpcinfo(2),3,4));
	set .@playtime, checkquest(5141,PLAYTIME);
	if (.@playtime == -1) {
		mes "牌子上有小字。";
		next;
		mes "如果您不支付5个^FD0260LP^000000，您将被诅咒并无法动弹2分钟。";
		next;
		if(select("使用5 LP：不付费。") == 1) {
			if (job_kagero_lp > 4) {
				set job_kagero_lp, job_kagero_lp - 5;
				mes "<已使用的 5 张唱片>";
				mes "你可以掷骰子。";
				next;
				callfunc "F_KO_Survival_dice",.@id;
				end;
			} else {
				mes "<未使用5张LP>";
				mes "无法承受诅咒。";  //custom translation
				next;
			}
		}
		setquest 5141;
		mes "你被诅咒了，在 2 分钟内不能移动到任何其他区块。";
		close;
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "你被诅咒了，在 2 分钟内不能移动到任何其他区块。";
		close;
	} else if (.@playtime == 2) {
		mes "定身诅咒持续2分钟，然后被解除。";
		next;
		erasequest 5141;
		callfunc "F_KO_Survival_dice",.@id;
		end;
	} else {
		erasequest 5141;
		mes "发生未知错误。";
		mes "请联系客服。";
		close;
	}
}

-	script	Sight#ko_gravity	-1,{
	callfunc "F_KO_Survival_mes", atoi(substr(strnpcinfo(2),3,4));
	end;
OnTouch:
	set .@id, atoi(substr(strnpcinfo(2),3,4));
	mes "牌子上有小字。";
	next;
	mes "受到10倍正常重力诅咒的区域。您将需要 10 个 ^FD0260LP^000000 来掷骰子。";
	next;
	callfunc "F_KO_Survival_dice",.@id,10;
	end;
}

// -----------------------------------------------------------
//  Blocks (00-40)
// -----------------------------------------------------------

job_ko,57,16,4	script	Sight#ko_00	858,1,1,{
	callfunc "F_KO_Survival_mes",0;
	end;
OnTouch:
	if (job_kagero != 5) {
		mes "你不应该在这里。";  //custom translation
		close2;
		warp "amatsu",147,136;
		end;
	}
	if (isbegin_quest(5137) == 1 && isbegin_quest(5140) == 0) {
		mes "牌子上有小字。";
		next;
		callsub L_Description;
		setquest 5140;
		set job_kagero_lp,100;
		mes "您已获得 100 点生命点 (LP)。";
		next;
	} else {
		if (checkquest(5141,PLAYTIME) > -1)
			erasequest 5141;
		set job_kagero_lp,100;
		mes "您已返回到第一个块。您的 LP 已恢复。";
		next;
		callsub L_Description;
	}
	callfunc "F_KO_Survival_dice",0;
	end;

L_Description:
	while (1) {
		if(select("阅读说明：开始测试。") == 2)
			break;
		mes "生存测试从这里开始，你使用^FD0260100点生命点（LP）^000000并掷骰子来前进通过测试。";
		next;
		mes "整个测试从区块0开始到区块40，你得到的每个区块都可以给予或夺走你的LP。";
		next;
		mes "你需要有一些^FD0260LP^000000直到你到达最后的到达块才能通过测试。";
		next;
		mes "但！当您到达到达区块时，如果您的 ^FD0260LP^000000 变为 0，您仍然通过测试。";
		next;
		mes "游戏说明到此结束。祝你好运。";
		next;
	}
	return;
}

job_ko,49,16,4	script	Sight#ko_01	858,1,1,{
	callfunc "F_KO_Survival_mes",1;
	end;
OnTouch:
	set .@life_p, job_kagero_lp;
	mes "牌子上有小字。";
	next;
	mes "你掷骰子的技术真的很差。怎么才能从一开始就得到1呢？如果您在该区块中支付了 10 LP，则可以移动到区块 9。";
	next;
	if(select("使用 10 LP：掷骰子。") == 1) {
		if (job_kagero_lp > 9) {
			set .@reset_p, .@life_p - 10;
			set job_kagero_lp, .@reset_p;
			mes "<已使用10张LP>";
			mes "按关闭移动到块 9。";
			callfunc "F_KO_Survival_warp",9;
			end;
		} else {
			mes "< 未使用 10 LP >";
			mes "^FD0260LP^000000 不足！";
			next;
		}
	}
	mes "掷骰子。";
	next;
	callfunc "F_KO_Survival_dice",1;
	end;
}

job_ko,41,16,4	duplicate(Sight#ko_trap)	Sight#ko_02	858,1,1

job_ko,33,16,4	script	Sight#ko_03	858,1,1,{
	callfunc "F_KO_Survival_mes",3;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "使用了5张LP，所以你必须抵抗代代相传的诅咒才能继续前进。";
	next;
	select("使用 5 LP");
	if (job_kagero_lp > 4) {
		mes "<已使用的5张唱片>";
		set job_kagero_lp, job_kagero_lp - 5;
		callfunc "F_KO_Survival_dice",3;
	} else {
		mes "<未使用5张LP>";
		mes "^FD0260LP^000000 不足！返回起始位置。";
		callfunc "F_KO_Survival_warp",0;
	}
	end;
}

job_ko,25,16,4	script	Sight#ko_04	858,1,1,{
	callfunc "F_KO_Survival_mes",4;
	end;
OnTouch:
	mes "标牌上写着一些小字。";
	next;
	if ($20110808_sko01$ != "") {
		mes "^0000FF" + $20110808_sko01$ +" 在这里。^000000";
		next;
	}
	mes "由于参加考试是一个特殊的场合，我应该留下我的名字吗？";
	next;
	if(select("使用5 LP并留下你的名字。：继续掷骰子。") == 1) {
		if (job_kagero_lp > 4) {
			mes "<已使用的5张唱片>";
			set job_kagero_lp, job_kagero_lp - 5;
			next;
			set $20110808_sko01$, strcharinfo(0);
			mes strcharinfo(0)+"就在这里。";
			mes "你留下了你的名字。";
			next;
		} else {
			mes "<未使用5张LP>";
			mes "^FD0260LP^000000 不足！你可以掷骰子。";
			next;
		}
	}
	callfunc "F_KO_Survival_dice",4;
	end;
}

job_ko,17,16,4	duplicate(Sight#ko_curse)	Sight#ko_05	858,1,1

job_ko,17,24,4	duplicate(Sight#ko_heal)	Sight#ko_06	858,1,1

job_ko,17,32,4	duplicate(Sight#ko_trap)	Sight#ko_07	858,1,1

job_ko,17,40,4	script	Sight#ko_08	858,1,1,{
	callfunc "F_KO_Survival_mes",8;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "剪刀石头布机：";
	mes "如果你赢了，请移至第 15 格。";
	mes "如果是平局，就掷骰子。";
	mes "如果输了，则移动到区块 0。";
	next;
	switch (callfunc("F_KO_Survival_rps")) {
	case 1:
		mes "按 Close 移至块 15。";
		callfunc "F_KO_Survival_warp",15;
		end;
	case 0:
		mes "平局，所以掷骰子吧！";
		next;
		callfunc "F_KO_Survival_dice",8;
		end;
	case -1:
		mes "按关闭移动到块 0。";
		callfunc "F_KO_Survival_warp",0;
		end;
	}
}

job_ko,17,48,4	script	Sight#ko_09	858,1,1,{
	callfunc "F_KO_Survival_mes",9;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "请继续。";
	next;
	callfunc "F_KO_Survival_dice",9;
	end;
}

job_ko,17,56,4	duplicate(Sight#ko_trap2)	Sight#ko_10	858,1,1

job_ko,22,60,4	duplicate(Sight#ko_trap)	Sight#ko_11	858,1,1

job_ko,30,60,4	script	Sight#ko_12	858,1,1,{
	callfunc "F_KO_Survival_mes",12;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "剪刀石头布机：";
	mes "如果你赢了，请移至第 20 格。";
	mes "如果是平局，就掷骰子。";
	mes "如果你输了，请移至区块 9。";
	next;
	switch (callfunc("F_KO_Survival_rps")) {
	case 1:
		mes "按关闭移动到块 20。";
		callfunc "F_KO_Survival_warp",20;
		end;
	case 0:
		mes "平局，所以掷骰子吧！";
		next;
		callfunc "F_KO_Survival_dice",12;
		end;
	case -1:
		mes "按关闭移动到块 9。";
		callfunc "F_KO_Survival_warp",9;
		end;
	}
}

job_ko,38,60,4	duplicate(Sight#ko_heal)	Sight#ko_13	858,1,1

job_ko,46,60,4	duplicate(Sight#ko_curse)	Sight#ko_14	858,1,1

job_ko,54,60,4	script	Sight#ko_15	858,1,1,{
	callfunc "F_KO_Survival_mes",15;
	end;
OnTouch:
	set .@playtime, checkquest(5141,PLAYTIME);
	if (.@playtime == -1) {
		mes "牌子上有小字。";
		next;
		mes "你将受到强大的重力诅咒的诅咒，使你在 2 分钟内无法动弹。";
		setquest 5141;
		close;
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "你被诅咒了，在 2 分钟内不能移动到任何其他区块。";
		close;
	} else if (.@playtime == 2) {
		mes "定身诅咒持续2分钟，然后被解除。";
		next;
		erasequest 5141;
		callfunc "F_KO_Survival_dice",15;
		end;
	} else {
		erasequest 5141;
		mes "发生未知错误。";
		mes "请联系客服。";
		close;
	}
}

job_ko,62,60,4	script	Sight#ko_16	858,1,1,{
	callfunc "F_KO_Survival_mes",16;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "消耗 2 个 ^FD0260LP^000000 并掷骰子两次。";
	mes "如果你得到相同的数字，那么你可以移动到第30块，如果数字不同，消耗2LP再次滚动......直到你得到相同的数字。";
	next;
	while (1) {
		callfunc "F_KO_Survival_dice2",16;
		if (@job_ko_dice1 == @job_ko_dice2) break;
		else {
			mes "第二个骰子结果是" + @job_ko_dice2 + "，这不是同一个数字。";  //custom translation
			next;
		}
	}
	set @job_ko_dice1,0;
	set @job_ko_dice2,0;
	//mes "Second dice result is " + @job_ko_dice2 + ", which is the same number. Press Close to move block 30.";
	mes "你有相同的号码。按 Close 移动块 30。";
	callfunc "F_KO_Survival_warp",30;
	end;
}

job_ko,62,51,4	duplicate(Sight#ko_gravity)	Sight#ko_17	858,1,1

job_ko,62,43,4	script	Sight#ko_18	858,1,1,{
	callfunc "F_KO_Survival_mes",18;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "消耗 2 个 ^FD0260LP^000000 并掷骰子两次。";
	next;
	mes "如果两个骰子的数字之和小于 5，则移至块 30，如果大于 5，则移至块 19。";
	next;
	callfunc "F_KO_Survival_dice2",18;
	mes "第二个骰子结果是" + @job_ko_dice2 + ".";
	next;
	set .@dice_sum, @job_ko_dice1 + @job_ko_dice2;
	mes @job_ko_dice1 + " + " + @job_ko_dice2 + " = " + .@dice_sum;
	set @job_ko_dice1,0;
	set @job_ko_dice2,0;
	if (.@dice_sum < 5) {
		mes "您的总数低于 5。按“关闭”移动到块 30。";
		callfunc "F_KO_Survival_warp",30;
		end;
	} else {
		mes "您的总数大于 5。按“关闭”移至块 19。";
		callfunc "F_KO_Survival_warp",19;
		end;
	}
}

job_ko,62,35,4	script	Sight#ko_19	858,1,1,{
	callfunc "F_KO_Survival_mes",19;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "继续前进。";
	next;
	callfunc "F_KO_Survival_dice",19;
	end;
}

job_ko,62,27,4	duplicate(Sight#ko_heal)	Sight#ko_20	858,1,1

job_ko,57,23,4	duplicate(Sight#ko_gravity)	Sight#ko_21	858,1,1

job_ko,49,23,4	script	Sight#ko_22	858,1,1,{
	callfunc "F_KO_Survival_mes",22;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "留下所有剩余的^FD0260LP^000000并返回区块0，然后下一个人将被赎回。";
	next;
	if ($20110808_vko02 == 0) {
		mes "您想离开您的^FD0260LP^000000吗？";
		mes "如果您离开您的^FD0260LP^000000，那么到达该块的下一个测试人员将自动通过测试。";
		next;
		if(select("留下你所有的LP：继续掷骰子。") == 1) {
			set job_kagero_lp,0;
			set $20110808_vko02,1;
			mes "将所有剩余的 ^FD0260LP^000000 保留在此处。按“关闭”并返回到块 0。";
			callfunc "F_KO_Survival_warp",0;
			end;
		}
	} else if ($20110808_vko02 == 1) {
		set $20110808_vko02,0;
		mes "在你之前有人牺牲了他们所有的^FD0260LP^000000，所以按关闭并转到最后一个块。"; //custom translation
		callfunc "F_KO_Survival_warp",40;
		end;
	} else {
		mes "发生未知错误。";
		mes "请联系客服。";
		close;
	}
	callfunc "F_KO_Survival_dice",22;
	end;
}

job_ko,41,23,4	duplicate(Sight#ko_trap)	Sight#ko_23	858,1,1

job_ko,33,23,4	script	Sight#ko_24	858,1,1,{
	callfunc "F_KO_Survival_mes",24;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "消耗 2 个 ^FD0260LP^000000 并掷骰子两次。";
	next;
	mes "如果两个骰子的数字总和大于 8，那么您可以移动到 25 到 30 之间的任何区块。但是！如果总和低于 8，则必须返回到块 19。";
	next;
	callfunc "F_KO_Survival_dice2",24;
	mes "第二个骰子结果是" + @job_ko_dice2 + ".";
	next;
	set .@dice_sum, @job_ko_dice1 + @job_ko_dice2;
	if (.@dice_sum > 8) {
		mes @job_ko_dice1 + " + " + @job_ko_dice2 + " = " + .@dice_sum;
		set @job_ko_dice1,0;
		set @job_ko_dice2,0;
		mes "您的总数超过 8。请选择一个块在第 25 块到第 30 块之间移动。";
		callfunc "F_KO_Survival_warp", select("移至块 25：移至块 26：移至块 27：移至块 28：移至块 29：移至块 30") + 24;
		end;
	} else {
		set @job_ko_dice1,0;
		set @job_ko_dice2,0;
		mes "您的总数低于 8。按“关闭”移至块 19。";
		callfunc "F_KO_Survival_warp",19;
		end;
	}
}

job_ko,25,23,4	script	Sight#ko_25	858,1,1,{
	callfunc "F_KO_Survival_mes",25;
	end;
OnTouch:
	set .@playtime, checkquest(5141,PLAYTIME);
	if (.@playtime == -1) {
		mes "牌子上有小字。";
		next;
		mes "这太蹩脚了。";
		mes "无法阅读文字。应该有一种方法来阅读它。";
		next;
		switch(select("尝试一下：什么都不做。")) {
		case 1:
			specialeffect2 EF_TALK_FROSTJOKE;
			mes "尝试最近的普隆德拉笑话来做点什么。";
			next;
			setquest 5141;
			mes "被诅咒了，动弹不得2分钟。";
			close;
		case 2:
			if (rand(1,10) == 4) {
				mes "什么都不做是明智的决定。";
				next;
			} else {
				mes "什么也没发生。还是需要做点什么。";
				close;
			}
			break;
		}
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "无法移动，因为你被诅咒了。";
		close;
	} else if (.@playtime == 2) {
		mes "定身诅咒持续2分钟，然后被解除。";
		next;
		erasequest 5141;
	} else {
		erasequest 5141;
		mes "发生未知错误。";
		mes "请联系客服。";
		close;
	}
	callfunc "F_KO_Survival_dice",25;
	end;
}

job_ko,25,32,4	duplicate(Sight#ko_trap)	Sight#ko_26	858,1,1

job_ko,25,40,4	duplicate(Sight#ko_heal)	Sight#ko_27	858,1,1

job_ko,25,48,4	duplicate(Sight#ko_trap2)	Sight#ko_28	858,1,1

job_ko,30,52,4	script	Sight#ko_29	858,1,1,{
	callfunc "F_KO_Survival_mes",29;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "往前走吧。";
	next;
	callfunc "F_KO_Survival_dice",29;
	end;
}

job_ko,38,52,4	duplicate(Sight#ko_trap)	Sight#ko_30	858,1,1

job_ko,46,52,4	duplicate(Sight#ko_curse)	Sight#ko_31	858,1,1

job_ko,54,52,4	script	Sight#ko_32	858,1,1,{
	callfunc "F_KO_Survival_mes",32;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "剪刀石头布机：";
	mes "如果你赢了，你就可以移动到 33 到 38 之间的任何一个区块；如果你输了或者是平局，你必须移动到区块0。";
	next;
	switch (callfunc("F_KO_Survival_rps")) {
	case 1:
		mes "请选择一个块以在块 33 到 38 之间移动。";
		next;
		callfunc "F_KO_Survival_warp", select("移至块 33：移至块 34：移至块 35：移至块 36：移至块 37：移至块 38") + 32;
		end;
	case 0:
	case -1:
		mes "按关闭移动到块 0。";
		callfunc "F_KO_Survival_warp",0;
		end;
	}
}

job_ko,54,43,4	script	Sight#ko_33	858,1,1,{
	callfunc "F_KO_Survival_mes",33;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "如果您支付除最后 1 个 ^FD0260LP^000000 之外的所有点数来最后一次掷骰子，您可以移至块 39。";
	next;
	switch(select("Pay LP.：一次一点地前进。")) {
	case 1:
		set .@sell_p, job_kagero_lp - 1;
		set job_kagero_lp,1;
		mes "<二手" + .@sell_p + " LP >";
		next;
		mes "按 Close 移至块 39。";
		callfunc "F_KO_Survival_warp",39;
		end;
	case 2:
		mes "请掷骰子。";
		next;
		break;
	}
	callfunc "F_KO_Survival_dice",33;
	end;
}

job_ko,54,35,4	duplicate(Sight#ko_heal)	Sight#ko_34	858,1,1

job_ko,49,31,4	duplicate(Sight#ko_trap)	Sight#ko_35	858,1,1

job_ko,41,31,4	script	Sight#ko_36	858,1,1,{
	callfunc "F_KO_Survival_mes",36;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "距离最后一个区块只剩下 4 个区块了。";
	next;
	mes "规则 1（普通掷骰子）"; //iRO custom dialogue(?)
	mes "如果你得到 4 或以上，你就通过了，但没有人知道掷骰子会产生什么结果。";
	next;
	mes "规则2"; //iRO custom dialogue(?)
	mes "如果你得到 1、2 或 3，你就向右移动到到达点；如果你得到 4、5 或 6，你可以选择移动到区块 29。";
	next;
	switch(select("换个方式试试：遵守规则。")) {
	case 1:
		//mes "The Other Way (2nd Rule)"; //iRO custom dialogue(?)
		set .@dodoripo,0;
		break;
	case 2:
		set .@dodoripo,1;
		break;
	}
	set .@dice, callfunc("F_KO_Survival_dice",36,1,1);
	if (.@dodoripo == 0) {
		if (.@dice <= 3) {
			mes "按“关闭”移至末尾。";
			callfunc "F_KO_Survival_warp",40;
			end;
		} else {
			mes "按 Close 移至块 29。";
			callfunc "F_KO_Survival_warp",29;
			end;
		}
	} else {
		mes "按“关闭”可向前移动" + .@dice + " 块。";
		callfunc "F_KO_Survival_warp", ((.@dice <= 3) ? .@dice + 36 : 40);
		end;
	}
}

job_ko,33,31,4	script	Sight#ko_37	858,1,1,{
	callfunc "F_KO_Survival_mes",37;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "距离终点线只剩下 3 个街区了，但你不能这么轻松地走完。";
	next;
	mes "如果得到 1、3 或 5，则移动到到达点；如果你得到 2、4 或 6，则返回到起点。";
	next;
	set .@dice, callfunc("F_KO_Survival_dice",37,1,1);
	if (.@dice % 2) {
		mes "按关闭移动到最后一个块。";
		callfunc "F_KO_Survival_warp",40;
		end;
	} else {
		mes "按关闭移动到起点。";
		callfunc "F_KO_Survival_warp",0;
		end;
	}
}

job_ko,33,40,4	script	Sight#ko_38	858,1,1,{
	callfunc "F_KO_Survival_mes",38;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "留下 1 个 LP，然后用剩余的 LP 来安装陷阱来打倒其他玩家！";
	next;
	if ($20110808_vko04 == 0) {
		mes "目前没有安装陷阱。您想安装陷阱吗？";
		next;
		switch(select("安装陷阱。：不安装。")) {
		case 1:
			set .@reset_p, job_kagero_lp - 1;
			set job_kagero_lp,1;
			set $20110808_vko04,1;
			mes "<二手" + .@reset_p + " LP >";
			next;
			mes "陷阱已安装。请掷骰子。";
			next;
			break;
		case 2:
			mes "没有设置陷阱。请掷骰子。";
			next;
			break;
		}
	} else {
		set $20110808_vko04,0;
		set job_kagero_lp,0;
		mes "你被困在安装的陷阱里了。按关闭返回到块 0。";
		callfunc "F_KO_Survival_warp",0;
		end;
	}
	set .@dice, callfunc("F_KO_Survival_dice",38,1,1);
	mes "按“关闭”向前移动 " + .@dice + " 块。";
	callfunc "F_KO_Survival_warp", ((.@dice == 1) ? .@dice + 38 : 40);
	end;
}

job_ko,38,44,4	script	Sight#ko_39	858,1,1,{
	callfunc "F_KO_Survival_mes",39;
	end;
OnTouch:
	mes "牌子上有小字。";
	next;
	mes "现在距离结束只剩下 1 个区块了。恭喜你经历了一切才到达这里。";
	next;
	mes "但你不能这么轻易走。如果你得到 3，你将返回到块 3。";
	next;
	set .@dice, callfunc("F_KO_Survival_dice",39,1,1);
	if (.@dice == 3) {
		mes "按关闭移动到第三个块。";
		callfunc "F_KO_Survival_warp",3;
		end;
	} else {
		mes "按关闭移动到到达点。";
		callfunc "F_KO_Survival_warp",40;
		end;
	}
}

job_ko,46,41,4	script	Sight#ko_40	858,1,1,{
	callfunc "F_KO_Survival_mes",40;
	end;
OnTouch:
	if (job_kagero != 5) {
		mes "你不应该在这里。";  //custom translation
		close2;
		warp "amatsu",147,136;
		end;
	}
	if (isbegin_quest(5137) != 1 || isbegin_quest(5140) != 1) {
		mes "你一定是误会到这里了。";  //custom translation
		close2;
		warp "amatsu",147,136;
		end;
	}
	mes "牌子上有小字。";
	next;
	mes "对于那些正在为新的转折做准备的人...";
	mes "好的开始就成功了一半。但永远记住这不是结束。";
	next;
	completequest 5137;
	erasequest 5140;
	if (isbegin_quest(5141)) erasequest 5141;
	set job_kagero_lp,0;
	mes "让我们读一下标牌上的所有文字。";
	mes "你感觉自己被转移到了另一个地方。";
	close2;
	warp "job_ko",35,113;
	end;
}

// Test of Weaponry
//============================================================
job_ko,127,125,4	script	Red Leopard Joe#ko	730,{
	if (MaxWeight - Weight < 1000 || checkweight(1201,1) == 0) {
		mes "[红豹乔]";
		mes "快去减肥吧！不然这个测试就太难了……";  //custom translation
		close;
	}
	if (BaseJob != Job_Ninja) {
		for (set .@i,5131; .@i<=5146; set .@i,.@i+1)
			if (isbegin_quest(.@i)) erasequest .@i;
		set job_kagero,0;
		mes "[红豹乔]";
		mes "抱歉，你们的家族和我们的不一样，有什么问题吗？";
		close2;
		warp "amatsu",147,136;
		end;
	}
	if (job_kagero != 5) {  //custom translation
		mes "[红豹乔]";
		mes "你不应该在这里...";
		mes "摆脱你是我的责任";
		mes "所以我现在就送你回去！";
		close2;
		warp "amatsu",147,136;
		end;
	}
	set .@ko_test_03_1, isbegin_quest(5138);
	set .@ko_test_03_2, isbegin_quest(5142);
	set .@ko_test_03_3, isbegin_quest(5143);
	set .@ko_test_03_4, isbegin_quest(5144);
	set .@ko_test_03_5, isbegin_quest(5145);
	if (.@ko_test_03_1 == 1 && .@ko_test_03_2 == 0 && .@ko_test_03_3 == 0 && .@ko_test_03_4 == 0 && .@ko_test_03_5 == 0) {
		mes "[红豹乔]";
		mes "已经有一段时间了！";
		next;
		select("确实很久了，乔。");
		mes "[红豹乔]";
		mes "我在这里你并不感到惊讶。你来这里之前见过^0237FD空我盖^000000吗？";
		next;
		mes "[红豹乔]";
		mes "不喜欢我不是你第一个来的人，但这并不重要。";
		next;
		mes "[红豹乔]";
		mes "好吧，" + strcharinfo(0) + "！";
		mes "欢迎来到为部落制造武器的工作室。";
		next;
		mes "[红豹乔]";
		mes "自古以来，我们的氏族一直在创造和使用独特的武器来执行我们的秘密任务。";
		next;
		select("真的吗？在艾因布洛赫？");
		mes "[红豹乔]";
		mes "唔？你没有我想象的那么慢！";
		next;
		mes "[红豹乔]";
		mes "是的。艾因布罗赫被誉为工业城市，伟大铁匠创造新技术的城市。";
		next;
		mes "[红豹乔]";
		mes "我的任务是分析他们的技术并向部落报告。";
		next;
		mes "[红豹乔]";
		mes "我说的话超出了我的本意。";
		next;
		mes "[红豹乔]";
		mes "你带了足够的^6848B7铁矿石、铁、钢、Phracon、Emveretaron、Rough Oridecon和Rough Elunium^000000吗？";
		next;
		mes "[红豹乔]";
		mes "现在让我们创建一个新的原型武器。";
		next;
		specialeffect EF_GUIDEDATTACK,AREA,"Crafting Tools#ko_01";
		mes "[红豹乔]";
		mes "请随意使用这里的工具，并且...";
		next;
		specialeffect EF_GUIDEDATTACK,AREA,"Crafting Tools#ko_02";
		mes "[红豹乔]";
		mes "...那里...";
		next;
		specialeffect EF_GUIDEDATTACK,AREA,"Crafting Tools#ko_03";
		setquest 5142;
		mes "[红豹乔]";
		mes "...以及其他地方。现在让我们开始吧！";
		close;
	} else if (.@ko_test_03_1 == 1 && .@ko_test_03_2 == 1 && .@ko_test_03_3 == 0 && .@ko_test_03_4 == 0 && .@ko_test_03_5 == 0) {
		callsub L_Weapon,":";
		end;
	} else if (.@ko_test_03_1 == 1 && .@ko_test_03_2 == 0 && (.@ko_test_03_3 == 1 || .@ko_test_03_4 == 1) && .@ko_test_03_5 == 0) {
		callsub L_Weapon,":Yes, I'm done.";
		if (countitem(13074)) {
			mes "[红豹乔]";
			mes "嗯……有趣……是苦无形状的匕首吗？";
			next;
			mes "[红豹乔]";
		} else if (countitem(13312)) {
			mes "[红豹乔]";
			mes "嗯……这是普通的修玛吗？";
			next;
			mes "[红豹乔]";
			mes "啊啊，这可不是普通的休玛啊。";
		} else {
			mes "[红豹乔]";
			mes "啊啊，怎么可能？你没有武器？也许你装备了？";
			close;
		}
		mes "非常有创意。我确实看到了改进的空间，但武器本身在稍微平滑之后可能会变得有用。";
		next;
		mes "[红豹乔]";
		mes "好的。我想你已经准备好了。";
		next;
		mes "[红豹乔]";
		mes "现在是时候升级了吗？";
		mes "我的两侧都有精炼工具。用它们来精炼武器然后回来。";
		next;
		mes "[红豹乔]";
		mes "我自己花了很大力气创建了这些优化工具，因此您在使用它们时不会遇到任何问题。";
		next;
		setquest 5145;
		mes "[红豹乔]";
		mes "啊……不过以防万一，";
		mes "^FF0000请小心，不要将这些东西展示给这里外面的铁匠，以免造成麻烦。^000000";
		close;
	} else if (.@ko_test_03_1 == 1 && .@ko_test_03_2 == 0 && (.@ko_test_03_3 == 1 || .@ko_test_03_4 == 1) && .@ko_test_03_5 == 1) {
		disable_items;
		mes "[红豹乔]";
		mes "你得到成功的结果了吗？";
		next;
		switch(select("还没有。：在这里。：实际上...")) {
		case 1:
			mes "[红豹乔]";
			mes "一开始想要得到满意的结果并不容易。";
			close;
		case 2:
			break;
		case 3:
			mes "[红豹乔]";
			mes "即使我警告过你，你还是丢失了武器。";
			next;
			mes "[红豹乔]";
			mes "我们无能为力。等待！";
			next;
			mes "红豹乔嘟囔了一句，突然所有武器都没有装备了。";
			next;
			nude;
			if (countitem(13074) || countitem(13312)) {
				mes "[红豹乔]";
				mes "这是什么？你仍然有原型。别再玩了。";
				close;
			}
			mes "[红豹乔]";
			mes "哦！你真的失去了。";
			next;
			setquest 5142;
			if (isbegin_quest(5143)) erasequest 5143;
			if (isbegin_quest(5144)) erasequest 5144;
			erasequest 5145;
			mes "[红豹乔]";
			mes "出色地！让我们再尝试一下吧！";
			close;
		}
		mes "[红豹乔]";
		mes "看起来你已经得到了一些结果。";
		next;
		set .@part, EQI_HAND_R;
		if (!getequipisequiped(.@part)) {
			mes "[红豹乔]";
			mes "啊！你还没有装备任何武器！";
			close;
		} else {
			set .@equip_id, getequipid(.@part);
			set .@equip_refine, getequiprefinerycnt(.@part);
			if (.@equip_id == 13074 || .@equip_id == 13312) {
				mes "[红豹乔]";
				mes "那把武器是你的吗？让我看看。";
				next;
			} else {  //custom translation
				mes "[红豹乔]";
				mes "你确定是那个武器？";
				next;
				mes "[红豹乔]";
				mes "你打算用这个来欺骗我吗？再次！！";
				close;
			}
		}
		mes "乔仔细地看了看封印。";
		next;
		if (.@equip_refine < 7) {
			mes "[红豹乔]";
			mes "你有那么低吗？";
			mes "一定是我误判了你。你可能对自己很满意，但我不满意。再做一次！！";
			close;
		} else if (.@equip_refine > 6 && .@equip_refine < 9) {
			mes "[红豹乔]";
			mes "嗯……这就是你的极限了吗？";
			mes "你已经非常接近通过了。我再问一次，你满意吗？";
			next;
			if(select("是：否") == 2) {
				mes "[红豹乔]";
				mes "你还在这里。为什么？";
				close;
			}
			mes "[红豹乔]";
			mes "你的能力就足够了。";
			next;
		} else if (.@equip_refine > 8 && .@equip_refine < 13) {  //custom translation(?)
			mes "[红豹乔]";
			mes "啊，正如我所料。";
			mes "我仍然相信你不会就此止步。";
			next;
			if(select("我想停下来：我想继续。") == 2) {
				mes "[红豹乔]";
				mes "我会期待你的结果。";
				close;
			}
			mes "[红豹乔]";
			mes "你的能力就足够了。";
			next;
		} else {  //custom translation(?)
			mes "[红豹乔]";
			mes "比预期的要好。";
			next;
			mes "[红豹乔]";
			mes "对于初学者来说，相当不错。这个测试可能对你来说意义不大，但你的能力就足够了。";
			next;
		}
		mes "[红豹乔]";
		mes "我能感觉到。";
		mes "我会拿这把武器做一些最后的修饰。";
		next;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
			close;

		delequip .@part;
		erasequest 5145;
		completequest 5138;
		if (isbegin_quest(5143) == 1) completequest 5143;
		if (isbegin_quest(5144) == 1) completequest 5144;
		mes "[红豹乔]";
		mes "'" + strcharinfo(0) + "'";
		mes "通过武器测试！！";
		close;
	} else if (.@ko_test_03_1 == 2 && .@ko_test_03_2 == 0 && (.@ko_test_03_3 == 2 || .@ko_test_03_4 == 2) && .@ko_test_03_5 == 0) {
		mes "[红豹乔]";
		mes "哦，我差点忘了。";
		mes "去只园报告化验结果吧！";
		close2;
		warp "job_ko",26,104;
		end;
	} else {  //custom translation
		mes "[红豹乔]";
		mes "你不应该来这里...";
		mes "摆脱你是我的责任。";
		mes "这会让你退缩！";
		close2;
		warp "amatsu",147,136;
		end;
	}
	end;

L_Weapon:
	mes "[红豹乔]";
	mes "新武器的原型制作完成了吗？";
	next;
	switch(select("制作工具？：我没有足够的材料。：你希望我如何制作武器？" + getarg(0))) {
	case 1:
		specialeffect EF_GUIDEDATTACK,AREA,"Crafting Tools#ko_01";
		mes "[红豹乔]";
		mes "请随意使用这里的工具，并且...";
		next;
		specialeffect EF_GUIDEDATTACK,AREA,"Crafting Tools#ko_02";
		mes "[红豹乔]";
		mes "...那里...";
		next;
		specialeffect EF_GUIDEDATTACK,AREA,"Crafting Tools#ko_03";
		mes "[红豹乔]";
		mes "...以及其他地方。";
		close;
	case 2:
		mes "[红豹乔]";
		mes "你带的材料还不够吗？";
		next;
		specialeffect EF_GUIDEDATTACK,AREA,"Drawer#ko";
		mes "[红豹乔]";
		mes "然后我们就要去村里看看。试试那边的^FF0000抽屉^000000。它会带你去村庄。";
		close;
	case 3:
		mes "[红豹乔]";
		mes "嗯...我猜我期待太多了？让我解释一下。";
		next;
		mes "[红豹乔]";
		mes "你首先必须熔化矿石才能用于武器。而获得高纯度金属的最佳方法是熔化同类材料。";
		next;
		mes "[红豹乔]";
		mes "例如，铁矿石、铁和钢具有相似的性质，因此您可以从中提取高纯度的金属。";
		next;
		mes "[红豹乔]";
		mes "熔化高纯度金属后，将其倒入模具中以获得武器的形状，然后通过研磨和回火使其坚固。";
		next;
		mes "[红豹乔]";
		mes "顺便说一句，通过彻底旋转、研磨和回火，您将获得最佳效果。";
		next;
		mes "[红豹乔]";
		mes "我认为这足以让你开始，你不觉得吗？现在去开发那个原型吧！";
		close;
	case 4:
		return;
	}
}

job_ko,117,128,0	script	Drawer#ko	844,{
	mes "抽屉上有某种装置。";
	next;
	if(select("停止：尝试操作。") == 2) {
		mes "叮叮当当";
		close2;
		warp "amatsu",147,136;
		end;
	}
	close;
}

job_ko,131,124,0	script	Crafting Tools#ko_01	844,{
	if (MaxWeight - Weight < 1000 || checkweight(1201,1) == 0) {
		mes "太重了！请减轻体重。";  //custom translation
		close;
	}
	if (isbegin_quest(5138) == 1 && isbegin_quest(5142) == 1 && isbegin_quest(5143) == 0 && isbegin_quest(5144) == 0 && isbegin_quest(5145) == 0) {
		// fall through
	} else {
		mes "一种制作工具。";
		mes "现在确实不需要它。";
		close;
	}
	mes "制造武器的工具。";
	next;

	// Part 1: Melting Metal
	// Input similar materials to proceed to next step.
	mes "首先需要熔化铁和铁矿石等金属来制造武器。";
	next;
	callsub L_CheckMaterials;
	while(1) {
		switch(select("熔化铁矿石。：熔化铁。：熔化钢。：熔化Phracon。：熔化Emveretarcon。：熔化粗糙的Oridecon。：熔化粗糙的Elunium。：停止。")) {
			case 1: set .@item,1002; setarray .@val[0],1,0,0; break;  //Iron_Ore
			case 2: set .@item, 998; setarray .@val[0],2,0,0; break;  //Iron
			case 3: set .@item, 999; setarray .@val[0],3,0,0; break;  //Steel
			case 4: set .@item,1010; setarray .@val[0],0,1,0; break;  //Phracon
			case 5: set .@item,1011; setarray .@val[0],0,2,0; break;  //Emveretarcon
			case 6: set .@item, 756; setarray .@val[0],0,3,0; break;  //Oridecon_Stone
			case 7: set .@item, 757; setarray .@val[0],0,0,5; break;  //Elunium_Stone
			case 8: close;
		}
		if (countitem(.@item)) {
			delitem .@item,1;
			if (.@val[0]) {			//Iron Ore, Iron, Steel
				set .@ston_t01, .@ston_t01 + .@val[0];
				if (.@ston_t02 > 0)
					set .@ston_t02, .@ston_t02 - .@val[0];
			} else if (.@val[1]) {	//Phracon, Emveretarcon, Rough Oridecon
				set .@ston_t02, .@ston_t02 + .@val[1];
				if (.@ston_t01 > 0)
					set .@ston_t01, .@ston_t01 - .@val[1];
			} else if (.@val[2]) {	//Rouch Elunium
				if (.@ston_t01 > 0)
					set .@ston_t01, .@ston_t01 - .@val[2];
				if (.@ston_t02 > 0)
					set .@ston_t02, .@ston_t02 - .@val[2];
			}
			specialeffect EF_DEMONSTRATION;
			mes "在高温下融化了" + getitemname(.@item) + "。";
			next;
			if (.@ston_t01 > 49 || .@ston_t02 > 49)
				break;
		} else {
			mes "没有" + getitemname(.@item) + ".";
			next;
		}
		callsub L_CheckMaterials;
		mes "接下来会熔化哪种金属？";
		next;
	}
	set .@boll_01, ((.@ston_t01 > 49) ? .@ston_t01 : .@ston_t02);

	// Part 2: Forging
	// Grind and temper a weapon.
	mes "现在金属被熔化用于制造武器。";
	next;
	mes "接下来是将熔化的金属倒入模具中使其坚固。您将使用哪种模具形状？";
	next;
	switch(select("匕首模具:手里剑模具")) {
	case 1:
		set .@weapon_who,0;
		mes "将熔化的金属倒入匕首模具中。";
		break;
	case 2:
		set .@weapon_who,1;
		mes "将熔化的金属倒入手里剑模具中。";
		break;
	}
	next;
	progressbar "ffff00",3;
	mes "看起来金属正在成形。现在下一步怎么办？";
	next;
	set @job_ko_forge,0;
	set @job_ko_lastaction,0;
	callsub L_ForgeWeapon,":";
	while(1) {
		mes "现在下一步怎么办？";
		next;
		if (callsub(L_ForgeWeapon,":Final Touches") == 0)
			break;
	}
	set .@boll_02, @job_ko_forge;
	set @job_ko_forge,0;
	set @job_ko_lastaction,0;

	// Part 3: Create the Weapon
	// Success rate based on previous two parts.
	set .@boll_00, .@boll_01 + .@boll_02;
	mes "开始使用磨刀石打磨工具进行最后的润色。";
	next;
	progressbar "ffff00",3;
	set .@success_pp, rand(1,100);
	if (.@boll_00 < 100) {
		if (.@success_pp == 77) set .@success,1;
	} else {
		if (.@success_pp != 44) set .@success,1;
	}
	if (.@success) {
		specialeffect2 EF_PERFECTION;
		erasequest 5142;
		if (.@weapon_who == 0) {
			setquest 5143;
			getitem 13074,1; //Ninja_Cutter
		} else {
			setquest 5144;
			getitem 13312,1; //Huuma_Job_Test
		}
		mes "武器制作成功。";
		next;
		mes "我们把它交给红豹乔吧。";
		close;
	} else {
		specialeffect2 EF_SUI_EXPLOSION;
		mes "这把武器在最后阶段被毁坏了。";
		next;
		mes "一定是在制造武器的时候遗漏了一些东西。发生这样的事情非常令人难过，但你必须重新开始。";
		close;
	}
	end;

L_CheckMaterials:
	if (!countitem(1002) && !countitem(998) && !countitem(999) && !countitem(1010) && !countitem(1011) && !countitem(756) && !countitem(757)) {
		mes "但你没有更多的金属可以熔化。获得更多金属后我们再试一次。";
		close;
	}
	return;

L_ForgeWeapon:
	set .@i, select("磨练武器：淬炼武器" + getarg(0));
	switch (.@i) {
	case 1:
		specialeffect2 EF_DETOXICATION;
		mes "将武器接地。";
		break;
	case 2:
		specialeffect2 EF_REPAIRWEAPON;
		mes "淬炼了武器。";
		break;
	case 3:
		return 0;
	}
	next;
	if (@job_ko_lastaction == .@i)
		set @job_ko_forge, @job_ko_forge + 1;
	else
		set @job_ko_forge, @job_ko_forge + 2;
	set @job_ko_lastaction, .@i;
	return 1;
}
job_ko,129,129,0	duplicate(Crafting Tools#ko_01)	Crafting Tools#ko_02	844
job_ko,116,124,0	duplicate(Crafting Tools#ko_01)	Crafting Tools#ko_03	844

job_ko,121,121,0	script	Refinement Tools#ko_01	844,{
	if (isbegin_quest(5138) == 1 && isbegin_quest(5142) == 0 && (isbegin_quest(5143) == 1 || isbegin_quest(5144) == 1) && isbegin_quest(5145) == 1) {
		disable_items;
	} else {
		mes "这个工具可以帮助你自动完善你的武器。但是，您现在不需要这个。";
		close;
	}
	mes "您打开细化工具。";
	mes "设备启动时会发出令人愉快的振动。";
	next;
	mes "看起来你可以立即使用该工具进行优化，因为它包含大量的 Elunium 和 Oridecon。";
	next;
	if(select("开始精炼：不开始精炼。") == 2) {
		mes "您关闭了该工具。";
		close;
	}
	set .@part, EQI_HAND_R;
	if (!getequipisequiped(.@part)) {
		mes "嗡嗡声";
		mes "该工具发出警告声。";
		next;
		mes "[红豹乔]";
		mes "如果没有装备物品进行精炼，该装备将无法工作。";
		close;
	} else {
		set .@equip_id, getequipid(.@part);
		set .@equip_refine, getequiprefinerycnt(.@part);
		setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
		if (.@equip_id == 13074 || .@equip_id == 13312) {
			mes "分析武器。";
			next;
		} else {
			mes "[红豹乔]";
			mes "等等……这把武器不是定制的。";
			next;
			mes "[红豹乔]";
			mes "你为什么不和那些头脑发热的铁匠一起改进这个呢？";
			close;
		}
	}
	if (!getequipisenableref(.@part)) {
		mes "嗡嗡声";
		mes "该工具发出警告声。";
		next;
		mes "[红豹乔]";
		mes "那件武器是无法炼制的。你从哪里得到的？";  //custom translation
		close;
	}
	if (.@equip_refine >= 20) {
		mes "嗡嗡声";
		mes "该工具发出警告声。";
		next;
		mes "[红豹乔]";
		mes "这装备堪称完美精炼。自己留着吧！";  //custom translation
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "这是非常精炼的装备。您想继续吗？如果精炼失败，精炼技能会减少1。";
		next;
		if(select("继续：停止。") == 2) {
			mes "您关闭了该工具。";
			close;
		}
	} else {
		mes "开始精炼。";  //custom translation
		next;
	}
	progressbar "ffff00",1;
	set .@rand, rand(1,100);
	if (.@equip_refine < 5) {			// 0>5 100%
		set .@wlevel_up,1;
	} else if (.@equip_refine == 5) {	// 5>6  40%
		if (.@rand < 41) set .@wlevel_up,1;
	} else if (.@equip_refine == 6) {	// 6>7  30%
		if (.@rand < 31) set .@wlevel_up,1;
	} else if (.@equip_refine == 7) {	// 7>8  20%
		if (.@rand < 21) set .@wlevel_up,1;
	} else if (.@equip_refine == 8) {	// 8>9  10%
		if (.@rand < 11) set .@wlevel_up,1;
	} else if (.@equip_refine == 9) {	// 9>10  5%
		if (.@rand < 6) set .@wlevel_up,1;
	} else {							// 10>20 2%
		if (.@rand < 3) set .@wlevel_up,1;
	}

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
		close;

	if (.@wlevel_up) {
		successrefitem .@part;
		mes "炼化成功。";
		mes "关闭了精炼工具。";
		close;
	} else {
		downrefitem .@part;
		mes "炼化失败。";
		mes "你的武器精炼技能下降1。";
		mes "关闭了精炼工具。";
		close;
	}
	end;
}
job_ko,127,121,0	duplicate(Refinement Tools#ko_01)	Refinement Tools#ko_02	844

// Test of Battle
//============================================================
job_ko,148,46,4	script	Leader Gion#ko2	588,{
	if (MaxWeight - Weight < 2000 || checkweight(1201,1) == 0) {
		mes "[领袖只园]";
		mes "现在你不需要那么多东西了！"; //custom translation
		close;
	}
	if (job_kagero == 7) {
		cutin "job_ko01",2;
		mes "[领袖只园]";
		mes "看来你还没有处理好目标。";
		next;
		mes "[领袖只园]";
		mes "我再告诉你一次。目标是^FF0000氏族秘密^000000。";
		next;
		mes "[领袖只园]";
		mes "当然，这条路会很困难，所以继续寻找吧！"; //custom translation
		close2;
		cutin "",255;
		end;
	} else if (job_kagero == 8) {
		cutin "job_ko02",2;
		mes "[领袖只园]";
		mes "看起来你已经解决了目标。唔。";
		next;

		//custom translations
		if (ismounting()) {
			mes "[领袖只园]";
			mes "但请从你的青蛙身上下来。不然我无法继续！";
			close2;
			cutin "",255;
			end;
		}
		if (BaseLevel < 99 || JobLevel < 70) {
			mes "[领袖只园]";
			mes "你怎么变弱了？一旦你恢复到以前的水平就回来。";
			close2;
			cutin "",255;
			end;
		}
		if (SkillPoint != 0) {
			mes "[领袖只园]";
			mes "请用完所有技能点后回来。";
			close2;
			cutin "",255;
			end;
		}

		mapannounce "job_ko","[Gion] Interrupting the Test of Battle.",bc_map;
		cutin "job_ko04",2;
		mes "[领袖只园]";
		mes "战斗测试将推迟一段时间。不用担心，因为这不会影响其他测试。";
		next;
		mapannounce "job_ko","[Gion] My Friend " + strcharinfo(0) + " made it to " + ((Sex == SEX_MALE)?"Kagerou":"Oboro") + " Path. Congratulations!!",bc_map;
		mes "[领袖只园]";
		mes "我的朋友" + strcharinfo(0) + "到达了" + ((Sex == SEX_MALE)?"Kagerou":"Oboro") + "路径。恭喜！";
		next;
		cutin "job_ko02",2;
		mes "[领袖只园]";
		mes "很高兴见到一个正在走上新道路的年轻人。";
		next;
		mes "[某人的声音]";
		mes "等一下，祗园。";
		next;
		donpcevent "Kuuga Gai#ko2::OnEnable";
		donpcevent "Red Leopard Joe#ko2::OnEnable";
		cutin "job_ko04",2;
		mes "[领袖只园]";
		mes "抱歉我差点忘了你们两个。您有什么要与" + strcharinfo(0) + "分享的吗？";
		next;
		cutin "",255;
		mes "[与月盖]";
		mes "嗯……不好意思……突然说话……";
		next;
		mes "[与月盖]";
		mes strcharinfo(0) + "，你现在是我们部落的一名自豪的成员。始终昂首挺胸，并...";
		next;
		mes "[与月盖]";
		mes "^777777（凯的声音逐渐消失。）^000000。";
		mes "对不起，我们第一次见面时我就掐死了你。";
		next;
		donpcevent "Kuuga Gai#ko2::OnDisable";
		mes "[红豹乔]";
		mes "普哈哈哈！盖都是说说而已。我知道我对你来说比那个盖更有价值。";
		next;
		if (isbegin_quest(5143) == 2) {
			set .@item,13075; //Kurenai
			set .@weapon$,"dagger";
		} else {
			set .@item,13311; //Huuma_Shadow
			set .@weapon$,"shuriken";
		}
		mes "[红豹乔]";
		mes "我对你制作的原型" + .@weapon$ + "做了一些工作。";
		next;
		mes "[红豹乔]";
		mes getitemname(.@item) + "在我完成最后的润色后，现在它闪耀着美丽的猩红色光芒。";
		next;
		mes "[红豹乔]";
		mes "我自己给你的武器命名，但我知道你会喜欢这个名字。";
		next;
		mes "[红豹乔]";
		mes "祗园队长，请继续。";
		next;
		cutin "job_ko01",2;
		donpcevent "Red Leopard Joe#ko2::OnDisable";
		mes "[领袖只园]";
		mes "我没有什么要补充的，因为盖和乔都说了。";
		next;
		mes "[领袖只园]";
		mes "我只是很遗憾，所有这些测试的环境对于我们的后代来说本来可以更好。";
		next;
		mes "[领袖只园]";
		mes "你只需要向前看，永远不要回头。";
		next;
		for (set .@i,5131; .@i<=5146; set .@i,.@i+1)
			if (isbegin_quest(.@i)) erasequest .@i;
		set job_kagero,9;
		getnameditem .@item,strcharinfo(0);
		jobchange (Sex == SEX_MALE)?Job_Kagerou:Job_Oboro;
		donpcevent "Summon Target#ko::OnEnable";
		mes "[领袖只园]";
		mes "希望阳阳舞动的阳炎和月光朦胧的敖包的祝福伴随着你前行。";
		close2;
		warp "que_ng",21,71;
		end;
	} else if (job_kagero == 9) {
		cutin "job_ko01",2;
		mes "[领袖只园]";
		mes "希望阳阳舞动的阳炎和月光朦胧的敖包的祝福伴随着你前行。";
		close2;
		warp "que_ng",21,71;
		end;
	} else {
		cutin "job_ko01",2;
		mes "[领袖只园]";
		mes "你不应该在这里。请离开！"; //custom translation
		close2;
		warp "amatsu",147,136;
		end;
	}
}

job_ko,151,47,4	script	Kuuga Gai#ko2	730,{
	mes "[与月盖]";
	mes "恭喜您选择了发展之路！";
	close;
OnInit:
OnDisable:
	disablenpc "Kuuga Gai#ko2";
	end;
OnEnable:
	enablenpc "Kuuga Gai#ko2";
	end;
}

job_ko,145,47,6	script	Red Leopard Joe#ko2	730,{
	mes "[红豹乔]";
	mes "你还没有找到目标吗？继续寻找吧。";
	close;
OnInit:
OnDisable:
	disablenpc "Red Leopard Joe#ko2";
	end;
OnEnable:
	enablenpc "Red Leopard Joe#ko2";
	end;
}

job_ko,1,1,4	script	Summon Target#ko	844,{
	end;
OnInit:
OnTimer180000:
	donpcevent "Summon Target#ko::OnEnable";
	end;
OnEnable:
	stopnpctimer;
	switch (rand(1,7)) {
		case 1: set .@mob,1002; break;
		case 2: set .@mob,1031; break;
		case 3: set .@mob,1113; break;
		case 4: set .@mob,1063; break;
		case 5: set .@mob,1010; break;
		case 6: set .@mob,1049; break;
		case 7: set .@mob,1050; break;
	}
	areamonster "job_ko",120,30,160,70,"Clan Secret",.@mob,1,"Summon Target#ko::OnMyMobDead";
	end;
OnDisable:
	initnpctimer;
	end;
OnMyMobDead:
	if (mobcount("job_ko","Summon Target#ko::OnMyMobDead") < 1) {
		if (job_kagero == 7) {
			donpcevent "Summon Target#ko::OnDisable";
			set job_kagero,8;
		} else
			donpcevent "Summon Target#ko::OnEnable";
	}
	end;
}

job_ko,1,2,4	script	Summon Fake#ko	844,{
	end;
OnInit:
	donpcevent "Summon Fake#ko::OnEnable";
	end;
OnEnable:
	areamonster "job_ko",120,30,160,70,"Clan's Legacy",1002,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Kagerou's Memory",1002,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Oboro's Memory",1002,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Clan's Mistake",1031,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Oboro's Mistake",1031,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Kagerou's Mistake",1031,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Clan's Memory",1113,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Oboro's Past",1113,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Kagerou's Past",1113,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Clan's Legacy",1063,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Oboro's Legacy",1063,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Kagerou's Legacy",1063,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Clan's Mistake",1010,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Oboro's Memory",1010,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Kagerou's Memory",1010,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Clan's Past",1049,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Oboro's Mistake",1049,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Kagerou's Mistake",1049,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Clan's Memory",1050,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Oboro's Past",1050,5,"Summon Fake#ko::OnMyMobDead";
	areamonster "job_ko",120,30,160,70,"Kagerou's Past",1050,5,"Summon Fake#ko::OnMyMobDead";
	end;
OnDisable:
	killmonster "job_ko","Summon Fake#ko::OnMyMobDead";
	end;
OnMyMobDead:
	if (mobcount("job_ko","Summon Fake#ko::OnMyMobDead") < 100) {
		donpcevent "Summon Fake#ko::OnDisable";
		donpcevent "Summon Fake#ko::OnEnable";
		warp "job_ko",16,113;
	}
	end;
}

// GM Control Panel (custom translations)
//============================================================
job_ko,3,1,4	script	Battle Test Control#ko	588,{
	callfunc "F_GM_NPC";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "这是战斗测试控制NPC。";
		next;
	} else {
		mes "密码不正确。";
		close;
	}
	switch(select("召唤目标：召唤目标禁用：召唤假冒：召唤假冒禁用")) {
	case 1:
		donpcevent "Summon Target#ko::OnEnable";
		mes "召唤目标已激活。";
		break;
	case 2:
		donpcevent "Summon Target#ko::OnDisable";
		mes "召唤目标已禁用。";
		break;
	case 3:
		donpcevent "Summon Fake#ko::OnEnable";
		mes "召唤假货已激活。";
		break;
	case 4:
		donpcevent "Summon Fake#ko::OnDisable";
		mes "召唤假货已禁用。";
		break;
	}
	next;
	mes "控制已完成。";
	close;
}

job_ko,4,1,4	script	Guide#ko_helper	588,{
	callfunc "F_GM_NPC";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "这是阳炎和敖包转职的辅助NPC。";
		next;
	} else {
		mes "密码不正确。";
		close;
	}
	mes "我可以解决工作变动任务中的任何问题。";
	next;
	switch(select("当前状态：测试集：预传输：初始化")) {
	case 1:
		mes "可变状态（当前/最大）";
		mes "乙" + job_kagero + "/9)";
		mes "b_lp 你构建_p (" + job_kagero_lp + "/100）";
		next;
		mes "任务状态（当前/最大）";
		mes "5131（" + isbegin_quest(5131) + "/1)";
		mes "5132（" + isbegin_quest(5132) + "/1)";
		mes "5133（" + isbegin_quest(5133) + "/1)";
		mes "5134（" + isbegin_quest(5134) + "/1)";
		mes "5135（" + isbegin_quest(5135) + "/1)";
		mes "5136（" + isbegin_quest(5136) + "/2)";
		mes "5139（" + isbegin_quest(5139) + "/1)";
		mes "5137（" + isbegin_quest(5137) + "/2)";
		mes "5140（" + isbegin_quest(5140) + "/1)";
		mes "5141（" + checkquest(5141,PLAYTIME) + "/1)";
		mes "5138（" + isbegin_quest(5138) + "/2)";
		mes "5142（" + isbegin_quest(5142) + "/1)";
		mes "5143（" + isbegin_quest(5143) + "/2)";
		mes "5144（" + isbegin_quest(5144) + "/2)";
		mes "5145（" + isbegin_quest(5145) + "/1)";
		mes "5146（" + isbegin_quest(5146) + "/1)";
		close;
	case 2:
		mes "您想将哪项测试设置为已完成？";
		mes "换工作之前必须完成三项测试。如果您想进入转职状态，请直接在上一级菜单中选择[预转]！";
		next;
		switch(select("知识完整测试：生存完整测试：武器装备完整测试")) {
		case 1:
			set job_kagero,5;
			setquest 5136;
			completequest 5136;
			mes "知识测试已完成。我希望您已经完成初始化。";
			break;
		case 2:
			set job_kagero,5;
			set job_kagero_lp,0;
			setquest 5137;
			completequest 5137;
			mes "生存考验已经完成。我希望您已经完成初始化。";
			break;
		case 3:
			set job_kagero,5;
			setquest 5138;
			completequest 5138;
			mes "武器装备测试已经完成。我希望您已经完成初始化。";
			break;
		}
		close;
	case 3:
		mes "我会将你的任务状态更改为预转移，但在此之前......";
		next;
		mes "请选择武器。";
		next;
		switch(select("匕首：手里剑")) {
			case 1: set .@quest,5143; break;
			case 2: set .@quest,5144; break;
		}
		setquest 5136;
		completequest 5136;
		setquest 5137;
		completequest 5137;
		setquest 5138;
		completequest 5138;
		setquest .@quest;
		completequest .@quest;
		setquest 5146;
		set job_kagero,8;
		mes "我已设置预传输状态。祝你好运！！";
		close;
	case 4:
		set job_kagero,0;
		set job_kagero_lp,0;
		for (set .@i,5131; .@i<=5146; set .@i,.@i+1)
			if (isbegin_quest(.@i)) erasequest .@i;
		mes "初始化任务完成。您可以通过[当前状态]选项确认这一点！";
		close;
	}
	end;
}
