//===== rAthena Script =======================================
//= Coin Merchants
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Renewal coin redemption NPCs.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy/Lemongrass]
//= 1.1 Added remaining Malangdo traders. [Euphy/Lemongrass]
//= 1.2 Added Eclage traders (not fully complete). [Euphy]
//= 1.3 Completed Eclage Traders. [Dastgir]
//===== Translated By: =======================================
//= dsc
//============================================================

// Malangdo
//============================================================
malangdo,220,167,5	script	Coin Exchanger CX-1	564,{
	if (checkweight(1201,1) == 0) {
		mes "你有太多种类的物体。让我们在减少这些对象后尝试继续。";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "无法继续，因为您有太多重物。减肥后我们继续努力吧。";
		close;
	}
	mes "[硬币兑换机CX-1]";
	mes "欢迎来到 CX-1。如果您有金币，可以将其兑换成高品级或低品级金币。";
	next;
	setarray .@coins[1],
		6417, //Silvervine
		6418, //Agrade_Coin
		6419, //Bgrade_Coin
		6420, //Cgrade_Coin
		6421, //Dgrade_Coin
		6422; //Egrade_Coin
	.@menu$ = "Stop:";
	for(.@i = 1; .@i<=6; .@i++) {
		if (countitem(.@coins[.@i]))
			.@menu$ = .@menu$ + getitemname(.@coins[.@i]) + ":";
		else
			.@menu$ = .@menu$ + "^aaaaaa"+getitemname(.@coins[.@i])+" (None)^000000:";
	}
	.@i = select(.@menu$);
	switch(.@i) {
	case 1:
		mes "[硬币兑换机CX-1]";
		mes "谢谢您的光临。";
		close;
	default:
		.@coin = .@coins[.@i-1];
		.@coin_select = .@i-1;
		break;
	}
	if (countitem(.@coin) == 0) {
		mes "[硬币兑换机CX-1]";
		mes "您选择了一枚您没有的硬币。";
		mes "谢谢您的光临。";
		close;
	}
	mes "[硬币兑换机CX-1]";
	mes "----汇率----";
	mes "更高的交换 - 30 至 10";
	mes "较低的交换 - 10 至 30";
	mes "------------------------";
	mes "^ff3333您兑换时，我们按照当前市场价格收取费用。^000000";
	next;
	setarray .@exchange_name$[0],"higher","lower";
	setarray .@exchange_rate[0],30,10;
	setarray .@exchange_loss[0],rand(1,4),rand(1,2); // Amount deducted per exchange.
	setarray .@exchange_id[0],.@coins[.@coin_select-1],.@coins[.@coin_select+1];
	if (.@exchange_id[0] == .@coins[1]) .@exchange_id[0] = 0; // Cannot exchange for Silvervine.
	.@menu$ = "Stop:";
	for(.@i = 0; .@i<2; .@i++) {
		if (.@exchange_id[.@i] == 0)
			.@menu$ = .@menu$ + "^ff3333Unavailable exchange to "+.@exchange_name$[.@i]+" coin^000000:";
		else if (countitem(.@coin) < .@exchange_rate[.@i])
			.@menu$ = .@menu$ + "^aaaaaaExchange to "+.@exchange_name$[.@i]+" coin (null)^000000:";
		else
			.@menu$ = .@menu$ + "Exchange to "+.@exchange_name$[.@i]+" coin - "+getitemname(.@coin)+" ("+.@exchange_rate[.@i]+" needed):";
	}
	.@i = select(.@menu$)-2;
	if (.@i == -1) {
		mes "[硬币兑换机CX-1]";
		mes "谢谢您的光临。";
		close;
	}
	if (.@exchange_id[.@i] == 0) {
		mes "[硬币兑换机CX-1]";
		mes "该硬币不可兑换为"+.@exchange_name$[.@i]+"硬币。";
		mes "谢谢您的光临。";
		close;
	}
	if (countitem(.@coin) < .@exchange_rate[.@i]) {
		mes "[硬币兑换机CX-1]";
		mes "您没有足够的硬币来兑换"+.@exchange_name$[.@i]+"硬币。";
		mes "谢谢您的光临。";
		close;
	}
	.@exchange_total = .@exchange_rate[(!.@i)] - .@exchange_loss[(!.@i)];
	delitem .@coin, .@exchange_rate[.@i];
	getitem .@exchange_id[.@i], .@exchange_total;
	mes "[硬币兑换机CX-1]";
	mes "^003399"+.@exchange_rate[.@i]+"^000000 ^003399 "+getitemname(.@coin)+"^000000 单元。";
	mes "我用它们兑换了^003399"+.@exchange_name$[.@i]+"硬币^000000，扣除^003399"+.@exchange_loss[(!.@i)]+"^000000单位后，剩余^003399"+.@exchange_total+"^000000单位。";
	close;
}

malangdo,218,165,5	script	Special Vending Machine	562,{
	if (checkweight(1201,1) == 0) {
		mes "你有太多种类的物体。让我们在减少这些对象后尝试继续。";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "无法继续，因为您有太多重物。减肥后我们继续努力吧。";
		close;
	}
	mes "[特殊自动售货机]";
	mes "欢迎来到CX-82。你有银藤果实，所以你可以购买具有特殊能力的马兰多特产。";
	next;
	callsub L_AddItem, //DisplayName,ItemID,Cost
		"Seagod's Protection [23 hours]",16740,4,
		"Seagod's Protection [7 days]",16763,12,
		"Seagod's Protection [15 days]",16764,24,
		"Octopus Hunting Stick [23 hours]",16765,2,
		"Octopus Hunting Stick [3 days]",16766,5,
		"Octopus Hunting Stick [7 days]",16767,10,
		"Premium Lubricant",6441,56,
		"Ordinary Lubricant",6440,36,
		"Sillit Pong",6443,192;
L_AddItem:
	.@menu$ = "Explanation:";
	for(.@i = 0; .@i<getargcount(); .@i += 3)
		.@menu$ = .@menu$ + getarg(.@i)+":";
	.@i = select(.@menu$)-2;
	if (.@i == -1) {
		mes "[特殊自动售货机]";
		mes "海神守护是在一定时期内进入涵洞纪念副本时使用的物品。";
		next;
		mes "[特殊自动售货机]";
		mes "章鱼狩猎棒是在一定时期内进入涵洞纪念地牢时使用的物品。";
		next;
		mes "[特殊自动售货机]";
		mes "高级和普通润滑剂是用于分离设备和卡片的物品。如果您带着任一物品拜访专家杰里米，他会解释更多效果细节。";
		next;
		mes "[特殊自动售货机]";
		mes "Sillit Pong 是一种将插槽与 MVP 卡分开的特殊物品。无法通过高级和普通润滑剂分离 MVP 卡。";
		close;
	}
	.@cost = getarg(.@i*3+2);
	mes "[特殊自动售货机]";
	mes "您选择^005500"+getarg(.@i*3)+"^000000。购买时，您需要 ^005500"+.@cost+"^000000 单位银藤果。";
	next;
	if (countitem(6417) < .@cost)
		.@ven_menu$ = "^999999Silvervine Fruit (missing "+(.@cost - countitem(6417))+")^000000";
	else
		.@ven_menu$ = "Purchase - Silvervine Fruit (have "+countitem(6417)+")";
	switch(select("停止："+.@ven_menu$)) {
	case 1:
		mes "[特殊自动售货机]";
		mes "谢谢您的光临。";
		close;
	case 2:
		if (countitem(6417) < .@cost) {
			mes "[特殊自动售货机]";
			mes "银藤果不够。";
			mes "结束交易。";
			mes "谢谢您的光临。";
			close;
		}
		mes "[特殊自动售货机]";
		mes "交易完成。谢谢您的光临。";
		delitem 6417,.@cost; //Silvervine
		getitem getarg(.@i*3+1),1;
		close;
	}
}

malangdo,236,179,5	script	Dark Merchant K	554,{
	if (checkweight(1201,1) == 0) {
		mes "看来你的库存太多了。处理掉一些物品后，我们再试一次。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你现在的库存太重了。请处理掉一些物品后回来。";
		close;
	}
	mes "[商人K]";
	mes "谢谢你的交易。";
	mes "我们将^ff000010 C级硬币^000000兑换为^3333ff1 Mora硬币^000000。那我们开始吧？";
	next;
	setarray .@coin_amount[0],10,100,500,1000;
	while(1) {
		.@menu$ = "";
		for(.@i = 0; .@i<getarraysize(.@coin_amount); .@i++) {
			if (countitem(6420) >= .@coin_amount[.@i])
				.@menu$ = .@menu$ + "Exchange "+(.@coin_amount[.@i]/10)+" Mora Coin:";
			else
				.@menu$ = .@menu$ + "^aaaaaaExchange "+(.@coin_amount[.@i]/10)+" Mora Coin (Not Enough)^000000:";
		}
		.@i = select(.@menu$+"辞职")-1;
		if (.@i == getarraysize(.@coin_amount)) {
			mes "[商人K]";
			mes "我们改天再交换吧。";
			close;
		}
		if (countitem(6420) < .@coin_amount[.@i]) {
			mes "[商人K]";
			mes "您需要^ff0000"+.@coin_amount[.@i]+"^000000 C级硬币来执行兑换。但你现在似乎还不够。";
			close;
		}
		delitem 6420,.@coin_amount[.@i]; //Cgrade_Coin
		getitem 6380,.@coin_amount[.@i]/10; //Mora_Coin
		mes "[商人K]";
		mes "C级硬币^003399"+.@coin_amount[.@i]+"^000000深受好评。"; //FIXME: Dialogue.
		mes "兑换为^003399"+(.@coin_amount[.@i]/10)+"^000000莫拉币。";
		next;
		mes "[商人K]";
		mes "您想继续吗？";
		next;
	}
}

malangdo,233,180,3	script	Dark MachineTX100	564,{
	if (checkweight(1201,1) == 0) {
		mes "看来你的库存太多了。处理掉一些物品后，我们再试一次。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你现在的库存太重了。请处理掉一些物品后回来。";
		close;
	}
	mes "[暗机TX100]";
	mes "感谢您访问TX100。您可以将金币兑换成更高或更低等级的金币。";
	next;
	mes "[暗机TX100]";
	mes "对于较低级别的硬币兑换，您最多可以进行 500 个硬币兑换，对于较高级别的硬币兑换，您可以进行 1 次最多 99 个硬币兑换。";
	next;
	setarray .@coins[1],
		6417, //Silvervine
		6418, //Agrade_Coin
		6419, //Bgrade_Coin
		6420, //Cgrade_Coin
		6421, //Dgrade_Coin
		6422; //Egrade_Coin
	.@menu$ = "Quit:";
	for(.@i = 1; .@i<=6; .@i++) {
		if (countitem(.@coins[.@i]))
			.@menu$ = .@menu$ + getitemname(.@coins[.@i])+" (have "+countitem(.@coins[.@i])+"):";
		else
			.@menu$ = .@menu$ + "^aaaaaa"+getitemname(.@coins[.@i])+" (None)^000000:";
	}
	.@i = select(.@menu$)-1;
	if (.@i == 0) {
		mes "[暗机TX100]";
		mes "谢谢您的光临。";
		close;
	}
	.@coin = .@coins[.@i];
	setarray .@exchange_name$[0],"higher","lower";
	setarray .@exchange_id[0],.@coins[.@i-1],.@coins[.@i+1];
	if (.@exchange_id[0] == .@coins[1]) .@exchange_id[0] = 0; // Cannot exchange for Silvervine.
	if (.@i > getarraysize(.@coins)) {
		mes "[暗机TX100]";
		mes "您选择了不正常的菜单。";
		mes "正在中止进程。谢谢您的光临。";
		close;
	}
	if (countitem(.@coin) == 0) {
		mes "[暗机TX100]";
		mes "您选择了一枚您没有的硬币。";
		mes "正在中止进程。谢谢您的光临。";
		close;
	}
	mes "[暗机TX100]";
	mes "-  - 汇率 -  -";
	mes "更高的硬币 - 3 比 1";
	mes "较低的硬币 - 1 到 3";
	mes "--------------------";
	mes "^ff3333我们从您兑换的硬币中收取少量供应商费用。^000000";
	next;
	.@menu$ = "Quit:";
	if (.@exchange_id[0] == 0)
		.@menu$ = .@menu$ + "^ff3333Cannot exchange to higher level coin^000000:";
	else {
		if (countitem(.@coin) >= 99)
			.@menu$ = .@menu$ + "Exchange 99 "+getitemname(.@coin)+" into higher level coin:";
		else
			.@menu$ = .@menu$ + "^aaaaaaExchange to higher level coin (Not enough)^000000:";
	}
	if (.@exchange_id[1] == 0)
		.@menu$ = .@menu$ + "^ff3333Cannot exchange to lower level coin^000000:";
	else {
		if (countitem(.@coin) > 500)
			.@menu$ = .@menu$ + "Exchange 500 "+getitemname(.@coin)+" into lower level coin:";
		else if (countitem(.@coin))
			.@menu$ = .@menu$ + "Exchange "+countitem(.@coin)+" "+getitemname(.@coin)+" into lower level coin:";
		else
			.@menu$ = .@menu$ + "^aaaaaaExchange to lower level coin (Not enough)^000000:";
	}
	.@i = select(.@menu$)-2;
	if (.@i == -1) {
		mes "[暗机TX100]";
		mes "谢谢您的光临。";
		close;
	}
	if (.@exchange_id[.@i] == 0) {
		mes "[暗机TX100]";
		mes "该硬币无法兑换为"+.@exchange_name$[.@i]+"级别的硬币。";
		mes "正在中止进程。谢谢您的光临。";
		close;
	}
	if ((.@i == 0 && countitem(.@coin) < 100) || (.@i == 1 && countitem(.@coin) == 0)) {
		mes "[暗机TX100]";
		mes "您没有足够的硬币来进行 "+.@exchange_name$[.@i]+" 级别的硬币兑换。";
		mes "正在中止进程。谢谢您的光临。";
		close;
	}
	if (countitem(.@exchange_id[.@i]) > 28000) {
		mes "[暗机TX100]";
		mes "您已经拥有太多 "+.@exchange_name$[.@i]+" 级别的硬币。请少带一些硬币回来。";
		close;
	}
	switch(.@i) {
	case 0:
		.@payment_amount = 99;
		.@reward_amount = .@payment_amount/3;
		.@coin_text$ = "High";
		break;
	case 1:
		.@payment_amount = (((countitem(.@coin) > 500))?500:countitem(.@coin));
		.@reward_amount = .@payment_amount*3;
		.@coin_text$ = "Low";
		break;
	}
	.@fee = rand(1,3);
	delitem .@coin, .@payment_amount;
	getitem .@exchange_id[.@i], .@reward_amount - .@fee;
	mes "[暗机TX100]";
	mes "已收到 <<<CLR1>>>"+getitemname(.@coin)+"^000000 单位的 ^000000"+.@payment_amount+"^000000。";
	mes "我们已将您的币兑换为^003399"+(.@reward_amount-.@fee)+"^000000单位^003399"+.@coin_text$+"等级币^000000，扣除^003399"+.@fee+"^000000单位作为兑换费用。";
	close;
}

malangdo,175,145,4	script	Can Agency Guard	549,{
	if (checkweight(1201,1) == 0) {
		mes "看来你的物品太多了。整理好物品种类后尝试一下。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "由于您携带的物品很重，您不太可能无法继续前进。减轻体重后尝试一下。";
		close;
	}
	mes "[可以代理守卫]";
	mes "嘿，你在那儿...你在这附近看到过^0000ff马兰多猫罐头^000000吗？";
	next;
	switch(select("你为什么问？：哦，你是说这些吗？")) {
	case 1:
		mes "[可以代理守卫]";
		mes "嗯。";
		mes "我们 Can Agency 最近收到了一些关于他们的可疑情报......";
		next;
		mes "[可以代理守卫]";
		mes "马兰多特产的罐头生产线上存在大量不合格产品。这些被丢弃的产品似乎落入了零售经销商的手中。";
		next;
		mes "[可以代理守卫]";
		mes "事实上，它们与普通产品很难区分。但外面明显写着<<<CLR0>>>Malangdo Cat Can^000000。";
		next;
		mes "[可以代理守卫]";
		mes "由于有缺陷的产品在市场上流通，我们一直在以收集它们为己任。你见过有人有我所描述的情况吗？";
		next;
		mes "[可以代理守卫]";
		mes "如果您发现有人出售这些产品，请过来告诉我。";
		close;
	case 2:
		if (countitem(12633) == 0) {
			mes "[可以代理守卫]";
			mes "嗯？我看不到你的^0000ff马兰多猫罐^000000。如果你发现了，别忘了给我带来……";
			close;
		}
		break;
	}
	mes "[可以代理守卫]";
	mes "Malangdo 猫罐头是有缺陷的产品。我把它兑换成3枚E级硬币吧。怎么样？";
	next;
	switch(select("我不会兑换：^0000ff1^000000 可以 -> ^0000ff3^000000 E 级硬币：^0000ff10^000000 可以 -> ^0000ff30^000000 E 级硬币：^0000ff100^000000 可以 -> ^0000ff300^000000 E级硬币：兑换我所有的")) {
	case 1:
		mes "[可以代理守卫]";
		mes "嗯，我不会永远在这里，所以如果你想在我还在的时候来找我交换。";
		close;
	case 2:
		.@check = 1;
		.@count = 1;
		break;
	case 3:
		.@check = 10;
		.@count = 10;
		break;
	case 4:
		.@check = 100;
		.@count = 100;
		break;
	case 5:
		.@check = 1;
		.@count = countitem(12633);
		break;
	}
	if (countitem(12633) < .@check) {
		if (.@check == 1) {
			mes "[可以代理守卫]";
			mes "嗯？我看不到你的^0000ff马兰多猫罐^000000。如果你发现了，别忘了给我带来……";
			close;
		} else {
			mes "[可以代理守卫]";
			mes "嗯？我觉得你没有足够的钱来兑换……等金额确定后再来找我。";
			close;
		}
	}
	delitem 12633,.@count; //Malang_Cat_Can
	getitem 6422,.@count*3; //Egrade_Coin
	mes "[可以代理守卫]";
	mes "将"+.@count+"马兰多猫罐头兑换为"+(.@count*3)+" E级硬币。一探究竟。";
	close;
}

function	script	F_mal_coin	{

// Initial dialogue and checks:
// -- callfunc "F_mal_coin",0,"[<NPC Name>]",<NPC ID>;
	.@npc_name$ = getarg(1);
	if (getarg(0) == 0) {
		if (getarg(2) != 2 && BaseLevel < 50) {
			mes .@npc_name$;
			mes "啊啊~";
			mes "如果你想买这个东西，";
			mes "你可以跟我说话";
			mes "当你再长大一点的时候。";
			mes "这东西太贵了";
			mes "给孩子的~";
			close;
		}
		mes .@npc_name$;
		mes "很高兴认识你~~";
		mes "我是！是的~~没错！";
		mes "我卖的是特别的东西";
		mes "只有在喵喵岛上才能看到！";
		next;
		mes .@npc_name$;
		mes "怎么样？你还在等什么？";
		mes "你为什么不看一下";
		mes "我有哪些东西？";
		next;
		switch(getarg(2)) {
		case 1:
			mes "[流浪商人]";
			mes "哦~";
			mes "这些是租赁物品，因此请确保您知道您是否拥有^0000FF1小时^000000或^0000FF7天^000000版本。";
			next;
			break;
		case 2:
			break;
		case 3:
			mes "[流浪商人]";
			mes "仅供您参考，";
			mes "母猪虫、海星、鱿鱼干和飞鱼是可以使用 1 小时的物品。";
			next;
			break;
		}
		mes .@npc_name$;
		mes "那么，你想看看我现在有什么吗？";
		next;
		switch(select("是：否")) {
		case 1:
			if (checkweight(1201,1) == 0) {
				mes .@npc_name$;
				mes "您的库存中似乎有太多物品。";
				mes "请腾出空间再回来~";
				close;
			}
			if (MaxWeight - Weight < 2500) {
				mes .@npc_name$;
				mes "看来你体重超重了。";
				mes "请减肥后再回来吧~";
				close;
			}
			mes .@npc_name$;
			mes "那就好好看看吧~~~";
			next;
			return;
		case 2:
			mes .@npc_name$;
			mes "好吧，那么。下次见。";
			mes "再见。";
			close;
		}
	}

// Exchange function:
// -- callfunc "F_mal_coin",1,"[<NPC Name>]","<Display Name>",<Item ID>,<Egrade_Coin>,<Malang_Sp_Can>,<Silvervine>
	if (getarg(0) == 1) {
		mes .@npc_name$;
		mes "^0000FF["+getarg(2)+"]^000000 的价格为";
		if (getarg(4) && getarg(5)) { // Type 1: Egrade_Coin or Malang_Sp_Can
			mes getarg(4)+"E 级硬币或"+getarg(5)+" 马兰多罐头特产。";
			.@type = 1;
			.@menu$ = "Yes.:No, I'll purchase with cans.:I don't want to purchase any.";
		} else if (getarg(5)) { // Type 2: Malang_Sp_Can only
			mes getarg(5)+"马兰多罐头特产。";
			.@type = 2;
			.@menu$ = "Yes.::No.";
		} else { // Type 3: Silvervine only
			mes getarg(6)+"银藤果。";
			.@type = 3;
			.@menu$ = "Yes.::I don't want to purchase any.";
		}
		next;
		mes .@npc_name$;
		if (.@type == 1) {
			mes "您想购买吗";
			mes "^0000FF["+getarg(2)+"]^000000 带有 E 级硬币？";
		} else {
			mes "你是否想要";
			mes "购买^0000FF["+getarg(2)+"]^000000？";
		}
		next;
		switch(select(.@menu$)) {
		case 2:
			.@type = 2;
		case 1:
			switch(.@type) {
			case 1:
				.@item = 6422; //Egrade_Coin
				.@amount = getarg(4);
				.@str$ = "coins";
				break;
			case 2:
				.@item = 12636; //Malang_Sp_Can
				.@amount = getarg(5);
				.@str$ = "cans";
				break;
			case 3:
				.@item = 6417; //Silvervine
				.@amount = getarg(6);
				.@str$ = "Silvervine Fruit";
				break;
			}
			if (countitem(.@item) < .@amount) {
				mes .@npc_name$;
				mes "抱歉，您需要更多"+.@str$+".";
				close;
			}
			if (MaxWeight - Weight < getiteminfo(getarg(3), ITEMINFO_WEIGHT)) {
				mes .@npc_name$;
				mes "抱歉，您买的太多了。";
				mes "您需要在库存中腾出更多空间。请稍后再回来。";
				close;
			}
			mes .@npc_name$;
			mes "谢谢。";
			delitem .@item,.@amount;
			getitem getarg(3),1;
			return;
		case 3:
			mes .@npc_name$;
			mes "时刻小心";
			mes "当您购买物品时。";
			close;
		}
	}
}
malangdo,162,146,5	script	Wandering Merchant#mal	495,{
	callfunc "F_mal_coin",0,"[Wandering Merchant]",1;
	setarray .@items[1],
		16743,16747,16741,16745,16749,16751, // 1 hour items.
		16742,16744,16746,16748,16752,16750; // 7 day items.
	setarray .@names$[1],
		"Spearfish","Tuna","Hairtail","Saurel","Malang Snow Crab","Brindle Eel",
		"Hairtail (7Days)","Spearfish (7Days)","Saurel (7Days)","Tuna (7Days)","Brindle Eel (7Days)","Malang Snow Crab (7Days)";
	while(1) {
		.@i = select(
			"[Spearfish(1hr)] 8 E-Coins/50 Cans",
			"[Tuna(1hr)] 8 E-Coins/50 Cans",
			"[Hairtail(1hr)] 8 E-Coins/50 Cans",
			"[Saurel(1hr)] 8 E-Coins/50 Can",
			"[Malang Snow Crab(1hr)] 8 E-Coins/50 Can",
			"[Brindle Eel(1hr)] 8 E-Coins/50 Can",
			"[Hairtail(7Days)] 10 Silvervine Fruit",
			"[Spearfish(7Days)] 10 Silvervine Fruit",
			"[Saurel(7Days)] 10 Silvervine Fruit",
			"[Tuna(7Days)] 10 Silvervine Fruit",
			"[Brindle Eel(7Days)] 10 Silvervine Fruit",
			"[Malang Snow Crab(7Days)] 10 Silvervine Fruit",
			"End purchasing."
		);
		if (.@i == 13) {
			mes "[流浪商人]";
			mes "好吧，那么。下次见。";
			mes "再见。";
			close;
		}
		mes "[流浪商人]";
		mes "您对^0000FF["+.@names$[.@i]+"]^000000感兴趣吗？";
		switch(.@i) {
		case 1: //Spearfish_Box1
		case 8: //Spearfish_Box2
			mes "这是……随季节变化的鱼，非常好吃！";
			mes "但这只是我说的一种食物，好吃~";
			next;
			mes "[流浪商人]";
			mes "当然，这是^DC143C双手枪^000000。";
			mes "它很有可能";
			mes "导致出血状态";
			mes "当你使用穿刺或冲突螺旋时~";
			next;
			mes "[流浪商人]";
			mes "更多细节是...";
			mes "螺旋冲击攻击力增加100%，灵感攻击力增加50%，";
			mes "基础Lv.1时攻击力+30超过100。";
			mes "基础攻击力为220。";
			next;
			break;
		case 2: //Tuna_Box1
		case 10: //Tuna_Box2
			mes "这是一种很受欢迎的鱼";
			mes "用于各种菜肴。";
			mes "剑士、商人和侍僧职业都可以使用它";
			mes "作为^DC143C狼牙棒^000000。";
			next;
			mes "[流浪商人]";
			mes "它有很高的机会执行眩晕或自动法术重击";
			mes "使用近距离物理攻击时。";
			mes "基础攻击力为180。";
			next;
			mes "[流浪商人]";
			mes "更多细节是...";
			mes "基础Lv.1时攻击力+20超过100，";
			mes "以低速率冻结自身。";
			next;
			break;
		case 3: //Hairtail_Box1
		case 7: //Hairtail_Box2
			mes "这条鱼看起来像一条长带子。";
			next;
			mes "[流浪商人]";
			mes "这是^DC143C双手剑^000000";
			mes "大多数剑士都可以使用。";
			mes "攻击时暴击伤害增加50%，CRI+20。";
			mes "基础攻击力为220。";
			next;
			mes "[流浪商人]";
			mes "更多细节是...";
			mes "基础Lv.1时攻击力+50超过100。";
			next;
			break;
		case 4: //Saurel_Box1
		case 9: //Saurel_Box2
			mes "这是一个 ^DC143CDagger^000000 ，它给出 MATK + 100";
			mes "物理攻击时，攻击力+30，持续7秒。";
			next;
			mes "[流浪商人]";
			mes "更多细节是...";
			mes "魔法攻击时以一定速度给予MATK+20，持续7秒";
			mes "基础Lv.1时追加MATK+10、ATK+10超过100。";
			mes "基础攻击力为160。";
			next;
			break;
		case 5: //Malang_Crab_Box1
		case 12: //Malang_Crab_Box2
			mes "这是一把^DC143C弓^000000，增加LUK + 3和50%的暴击伤害，";
			mes "基础Lv.1时，长怒物理攻击力增加20%超过100。";
			mes "基础攻击力为120。";
			next;
			mes "[流浪商人]";
			mes "更多细节是...";
			mes "可供弓箭手、盗贼和盗贼职业使用。";
			next;
			break;
		case 6: //Brindle_Eel_Box1
		case 11: //Brindle_Eel_Box2
			mes "这是一把^DC143C弓^000000。";
			mes "因为它有弹力，";
			mes "我们开始用它作为弓。";
			mes "基础攻击力为180。";
			next;
			mes "[流浪商人]";
			mes "更多细节是...";
			mes "AGI+3，ASPD+2，一定速度持续5秒，远程物理攻击。";
			mes "基础Lv.1时，远程物理攻击力额外增加20%。超过100。";
			mes "猎人、吟游诗人和舞者职业都可以使用它。";
			next;
			break;
		}
		if (.@i < 7)
			setarray .@price[0],8,50,0; // 1 hour items cost 8 Egrade_Coin or 50 Malang_Sp_Can.
		else
			setarray .@price[0],0,0,10; // 7 day items cost 10 Silvervine.
		callfunc "F_mal_coin",1,"[Wandering Merchant]",.@names$[.@i],.@items[.@i],.@price[0],.@price[1],.@price[2];
		next;
	}
}

malangdo,173,145,4	script	Stinky Merchant	496,{
	callfunc "F_mal_coin",0,"[Stinky Merchant]",2;
	setarray .@items[1],2873,16015;
	setarray .@names$[1],"Cat Paw Glove","Cat Club";
	while(1) {
		.@i = select(
			"[Cat Paw Glove] 32 E-Coins/200 Cans",
			"[Cat Club] 32 E-Coins/200 Cans",
			"End purchasing."
		);
		if (.@i == 3) {
			mes "[臭商人]";
			mes "好吧，那么。下次见。";
			mes "再见。";
			close;
		}
		mes "[臭商人]";
		mes "您对^0000FF["+.@names$[.@i]+"]^000000感兴趣吗？";
		switch(.@i) {
		case 1: //Cat_Hand_Glove
			mes "这是一款可以带来幸福的手套";
			mes "只要穿上它。";
			next;
			mes "[臭商人]";
			mes "这是一个^DC143C配件^000000";
			mes "防御力为5。";
			mes "它还给出";
			mes "VIT + 1、AGI + 1、DEX + 1、LUK + 1。";
			next;
			mes "[臭商人]";
			mes "更多细节是...";
			mes "插槽 : 1 / 精炼 : 无法 / 破坏 : 无法";
			next;
			break;
		case 2: //Cat_Club
			mes "看起来不会痛，你不这么认为吗？";
			mes "但我告诉你，这可能会伤害某人。";
			next;
			mes "[臭商人]";
			mes "这是对动物类怪物伤害增加15%的^DC143C狼牙棒^000000。";
			mes "基础攻击力为88。";
			mes "其他细节是";
			mes "武器 Lv : 1 / 槽位 : 3 / 精炼 : 能 / 破坏 : 不能";
			next;
			break;
		}
		callfunc "F_mal_coin",1,"[Stinky Merchant]",.@names$[.@i],.@items[.@i],32,200,0;
		next;
	}
}

malangdo,150,135,5	script	Roving Merchant	495,{
	callfunc "F_mal_coin",0,"[Roving Merchant]",3;
	setarray .@items[1],12639,12637,12638,12640;
	setarray .@names$[1],"Flying Fish","Sow Bug","Dried Squid","Starfish";
	while(1) {
		.@i = select(
			"[Flying Fish] 200 Cans",
			"[Sow Bug] 200 Cans",
			"[Dried Squid] 200 Cans",
			"[Starfish] 200 Cans",
			"End purchasing."
		);
		if (.@i == 5) {
			mes "[流浪商人]";
			mes "好吧，那么。下次见。";
			mes "再见。";
			close;
		}
		mes "[流浪商人]";
		mes "您对^0000FF["+.@names$[.@i]+"]^000000感兴趣吗？";
		switch(.@i) {
		case 1: //Flying_Fish_Box
			mes "这是会飞的";
			mes "有一个大胸。";
			next;
			mes "[流浪商人]";
			mes "这是^DC143C投掷武器^000000。";
			mes "当然也可以给予敌人致命伤";
			mes "攻击时以一定的速度。";
			next;
			mes "[流浪商人]";
			mes "其属性为中性，攻击力为50。";
			next;
			break;
		case 2: //Gong_Bug_Pocket
			mes "既然如此，我就给你解释一下。";
			mes "这是^DC143C子弹^000000，攻击时会以一定的速度眩晕敌人。";
			next;
			mes "[流浪商人]";
			mes "我感觉有些不好";
			mes "正在我的口袋里移动，但是...";
			mes "中立属性，攻击力50。";
			next;
			break;
		case 3: //Dried_Squid_Box
			mes "我将解释一下这个项目。";
			mes "这是^DC143C投掷武器^000000，会以一定的速度造成黑暗。";
			mes "中性属性，攻击力50。";
			next;
			mes "[流浪商人]";
			mes "它被压缩了";
			mes "然后放进一个很小的盒子里，";
			mes "所以打开它时要小心。";
			next;
			break;
		case 4: //Starfish_Box
			mes "我将解释一下这个项目。";
			mes "这是^DC143C投掷武器^000000，能以一定的速度使敌人昏迷。";
			next;
			mes "[流浪商人]";
			mes "它组织得很好，包装得很好";
			mes "所以你甚至看不到里面。";
			mes "中性属性，攻击力110。";
			next;
			break;
		}
		mes "[流浪商人]";
		mes "供你参考，";
		mes "一盒包含";
		mes "200 单位。";
		next;
		callfunc "F_mal_coin",1,"[Roving Merchant]",.@names$[.@i],.@items[.@i],0,200,0;
		next;
	}
}

// Eclage
//============================================================
ecl_in01,66,95,3	script	Armor Merchant Naphara#e::glove	436,{
	if (!checkweight(1301,3)) {
		mes "- 停在这里！！ -";
		mes "- 你的物品太多了。 -";
		mes "- 您不能携带更多物品。 -";
		mes "- 减轻你的负担并且 -";
		mes "- 再试一次。 -";
		close;
	}
	mes "[盔甲商人]";
	mes "您好，这里是Naphara的商店，是一家出售高级商品的地方。";
	mes "你需要什么？";
	next;
	.@choice = select("Str 手套：Int 手套：Agi 手套：Vit 手套：Dex 手套：Luk 手套") -1;
	mes "[盔甲商人]";
	switch(.@choice) {
	case 0: // Str Glove
		mes "^3131FF强力手套^000000";
		mes "^3131FFMHP + 100，MSP + 20^000000";
		mes "^3131FF每STR+10ATK+1增加^000000";
		mes "^3131FF在STR 110之上添加攻击力+1%^000000";
		break;
	case 1:
		mes "^3131FF智力手套^000000";
		mes "^3131FFMHP + 100，MSP + 20^000000";
		mes "^3131FF每 INT+10，MATK+1 就会增加^000000";
		mes "^3131FF在 INT 110 之上添加 MATK +1%^000000";
		break;
	case 2:
		mes "^3131FF敏捷手套^000000";
		mes "^3131FFMHP + 100，MSP + 20^000000";
		mes "^3131FF每 AGI+10，FLEE+1 就会增加^000000";
		mes "^3131FF在 AGI 110 之上添加完整逃离 +1^000000";
		break;
	case 3:
		mes "^3131FF维生素手套^000000";
		mes "^3131FFMHP + 100，MSP + 20^000000";
		mes "^3131FF每 VIT+10，MHP+50^000000";
		mes "^3131FF在 VIT 110 之上添加 MHP+1%^000000";
		break;
	case 4:
		mes "^3131FFDex 手套^000000";
		mes "^3131FFMHP + 100，MSP + 20^000000";
		mes "^3131FFDEX每+10，HIT+1增加^000000";
		mes "^3131FFDEX 110以上增加远程攻击力+1%^000000";
		break;
	case 5:
		mes "^3131FF陆克手套^000000";
		mes "^3131FFMHP + 100，MSP + 20^000000";
		mes "^3131FF每 LUK+10，CRI+1 就会增加^000000";
		mes "^3131FF在 LUK 110 之上增加 +1% 暴击伤害^000000";
		break;
	}
	mes "^3131FF所需等级：100^000000";
	mes "^3131FF插槽：0^000000";
	mes "^3131FF权重：10^000000";
	mes "是特征选项。";
	next;
	mes "[盔甲商人]";
	mes "花费 10 枚 Splendide 硬币。";
	mes "你想买吗？";
	next;
	if (select("买：不买。")==1) {
		if (countitem(6081) < 10) {
			mes "[盔甲商人]";
			mes "看来你的金币还不够。";
			close;
		}
		delitem 6081,10; //Splendide_Coin
		getitem (.@choice+2917),1;
		mes "[盔甲商人]";
		mes "太好了，它是你的了。谢谢。";
	}
	close;
}

ecl_in01,64,97,5	script	Slot Expert Nattuer#ecl	436,{
	disable_items;
	mes "[老虎机专家]";
	mes "我是老虎机专家 Naturer。";
	mes "如果你给我带来我弟弟 Naphara 的一件物品和 5 个 Splendide 硬币，我将为你打开一个槽位。";
	next;
	if (select("激活一个插槽。：算了。") == 2){
		close;
	}
	mes "[老虎机专家]";
	mes "插槽激活是一个极其困难的过程，即使非常小心并使用放大镜也可能会失败。";
	next;
	mes "[老虎机专家]";
	mes "当然，如果你的心像那片开满鲜花的土地一样大，你就不会介意。正确的？我应该去争取吗？";
	next;
	if (select("继续：算了。") == 2) {
		close;
	}
	mes "[老虎机专家]";
	mes "您想用插槽激活哪个项目？";
	next;
	.@choice = select("Str 手套：Int 手套：Ag 手套：Vit 手套：Dex 手套：Luk 手套");
	.@choice += 2916;
	mes "[老虎机专家]";
	mes "我们最后一次确认一下吧。是你想要的" + getitemname(.@choice) + "吗？";
	next;
	if (select("是：否")==2) {
		mes "[老虎机专家]";
		mes "当你确定的时候为什么不跟我谈谈呢？";
		close;
	}
	if (countitem(.@choice) < 1) {
		mes "[老虎机专家]";
		mes "你没有那个物品。当你确定的时候为什么不跟我谈谈呢？";
		close;
	}
	if (countitem(6081) < 5) {
		mes "[老虎机专家]";
		mes "你没有足够的金币。当你确定的时候为什么不跟我谈谈呢？";
		close;
	}
	progressbar "0xFFFF00",3;
	mes "[老虎机专家]";
	delitem 6081,5; //Splendide_Coin
	.@luckyday = rand(1,100);
	if (.@luckyday % 20){
		emotion ET_THINK;
		delitem .@choice,1;
		mes "射击，对不起。它失败了。";
		mes "但我确信下次一定会成功。我有一种感觉。";
		close;
	}
	emotion ET_BEST;
	delitem .@choice,1;
	getitem (.@choice+6),1;
	mes "我感觉眼睛快要掉出来了。";
	mes "尽管如此，还是恭喜你。插槽激活成功。";
	close;
}

ecl_in01,33,98,5	script	Herb Merchant Plafina#a	443,{
	if (checkweight(1301,1)==0) {
		mes "- 停在这里！！ -";
		mes "- 你的物品太多了。 -";
		mes "- 您不能携带更多物品。 -";
		mes "- 减轻你的负担并且 -";
		mes "- 再试一次。 -";
		close;
	}
	mes "[药草商人]";
	mes "欢迎光临，这里是Plafina's Herb Store，这里只提供最新鲜的香草~！";
	next;
	.@item = select("雪翻转：牡丹妈妈：拍打草药：世界树之尘：结束对话")-1;
	if (.@item == 4){
		mes "[药草商人]";
		mes "随时回来。";
		close;
	}
	mes "[药草商人]";
	switch (.@item){
	case 0:
		mes "雪翻对^3131FF燃烧、流血、深度睡眠、睡眠^000000有特殊效果。";
		break;
	case 1:
		mes "牡丹妈咪对^3131FF霜、冰冻、冰冻^000000有特效";
		break;
	case 2:
		mes "拍打草药对^3131FF眩晕、恐惧、混乱、幻觉^000000有特效";
		break;
	case 3:
		mes "世界之尘对^3131FF致盲、诅咒、降低敏捷、逆兽人^000000有特殊效果。";
		break;
	}
	mes "使用之间需要 5 秒。";
	mes "每个花费"+ .cost[.@item] +" Splendide Coins。";
	next;
	.@buy = select("买1.：买10.：不买。");
	if (.@buy == 3){
		close;
	}
	if (.@buy == 2){
		.@buy = 10;
	}
	mes "[药草商人]";
	mes "您想购买"+ .@buy +" "+ getitemname(.items[.@item]) +"吗？";
	next;
	if (select("买：不买。") == 2){
		close;
	}
	if (countitem(6081) < (.cost[.@item]*.@buy) ) {
		mes "[药草商人]";
		mes "你没有足够的金币。";
		close;
	}
	mes "[药草商人]";
	mes "感谢您的惠顾。";
	delitem 6081,.cost[.@item]*.@buy; //Splendide_Coin
	getitem .items[.@item],.@buy;
	close;
	
OnInit:
	setarray .items[0],12812,12813,12814,12815;
	setarray .cost[0],5,5,1,1;	//Splendide Coins
	end;
}

ecl_in01,67,39,4	script	Replication Expert Paltu	445,{
	if (checkweight(1301,3) == 0) {
		mes "您有太多项目无法继续。";
		close;
	}
	emotion ET_THROB;
	mes "[帕尔图]";
	mes "呜呼-问候。你要买什么？这里的商品很多。";
	next;
	switch (select("不，谢谢。：复制？：我知道你有很多锦囊妙计！")) {
	case 1:
		emotion ET_FRET;
		mes "[帕尔图]";
		mes "真的吗？唔？";
		close;
	case 2:
		mes "[帕尔图]";
		mes "可以说，在Eclage中，没有人比我更善于利用魔抗了。";
		next;
		mes "[帕尔图]";
		mes "不管怎样我都会给你一个好价钱。相信我。";
		next;
		select("你能为我做点什么吗？");
		emotion ET_BEST;
		mes "[帕尔图]";
		mes "嘿，我喜欢你的态度。";
		next;
		mes "[帕尔图]";
		mes "啊――你知道你戴的帽子吗？";
		next;
		emotion ET_QUESTION;
		mes "[帕尔图]";
		mes "你的储藏室里没有那些坚固但太难戴的帽子吗？你知道，对吧？";
		next;
		select(".........");
		emotion ET_THINK, playerattached();
		emotion ET_SMILE;
		mes "[帕尔图]";
		mes "看！我就知道。我从第一分钟就知道了。";
		next;
		mes "[帕尔图]";
		mes "如果你给我带来^ff0000 50个辉煌币和原料^000000，我会给你制作一顶形状相同的新帽子，但作为^ff0000服装^000000穿着。";
		next;
		mes "[帕尔图]";
		mes "嗯，那是什么……？";
		mes "我想你们冒险家称它们为^0571B0服装^000000？正确的？";
		next;
		select("是的~需要什么？");
		mes "[帕尔图]";
		mes "我需要 1 ^0571B0 帽子来复制 ^000000 并且";
		mes "我需要^E1281E 4块水晶镜^000000用于魔抗，^E1281E 1个仙魔粉^000000作为催化剂，以及^E1281E祖母绿、红宝石、蓝宝石、黄玉、锆石各10个^000000用于增强魔力。";
		next;
		emotion ET_STARE_ABOUT;
		mes "[帕尔图]";
		mes "成分还蛮多的吧？好吧，我对此无能为力。对付魔力可不是闹着玩的。";
		next;
		select("哪些帽子可以复制？");
		mes "[帕尔图]";
		mes "哦！你可真够犀利的！";
		mes "差点忘记了最重要的事情。";
		next;
		mes "[帕尔图]";
		mes "我可以复制的帽子有：";
		mes "^0571B0青蛙帽、浪漫花、魔山羊、腮红、女武神头盔、恶魔帽、刺客面具、精灵耳朵、河回面具、男孩帽^000000共10种。";
		next;
		mes "[帕尔图]";
		mes "你怎么认为？只要我有原料和补偿，我就会实现它。";
		next;
		if (select("我稍后会做：让我看看你得到了什么！") == 1) {
			emotion ET_THINK;
			mes "[帕尔图]";
			mes "什么？你在开玩笑吧？请告诉我你是！";
			close;
		}
		break;
	}
	disable_items;
	emotion ET_BIGTHROB;
	mes "[帕尔图]";
	mes "哦哇。";
	mes "你做出了正确的决定。我以为我会饿死，因为拉菲人对漂亮的帽子不感兴趣。";
	next;
	mes "[帕尔图]";
	mes "我会为你做一件大事。你真的应该知道我在这里给你优惠。";
	next;
	mes "[帕尔图]";
	mes "你有什么想法吗？你为什么不看看呢？";
	next;
	setarray .@items[0], 5447, 2269, 2256, 5040, 5171, 5038, 5096, 2286, 5176, 5016;
	setarray .@costumes[0], 19548, 19552, 19549, 19550, 19546, 19547, 19553, 19551, 19554, 19545;
	if ((countitem(6081) >= 50) && (countitem(747) >= 4) && countitem(6395) && (countitem(721) >= 10) && (countitem(723) >= 10) && (countitem(726) >= 10) && (countitem(728) >= 10) && (countitem(729) >= 10))
		.@item_check =1;
	for(.@i = 0; .@i<getarraysize(.@items); .@i++) {
		if (.@item_check && countitem(.@items[.@i]))
			.@menu$ = .@menu$+getitemname(.@items[.@i])+" (^2502FDAble to Replicate^000000):";
		else
			.@menu$ = .@menu$+getitemname(.@items[.@i])+" (^777777Insufficient ingredients^000000):";
	}
	.@i = select(.@menu$)-1;
	mes "[帕尔图]";
	mes "^0571B0"+getitemname(.@items[.@i])+"^000000 是您想要的吗？";
	next;
	if (.@item_check == 0 || countitem(.@items[.@i]) == 0) {
		emotion ET_CRY;
		mes "[帕尔图]";
		mes "哎...信用购买对我来说很难。";
		next;
		mes "[帕尔图]";
		mes "^0571B0"+getitemname(.@items[.@i])+"^000000 和";
		mes "需要^E1281E50个辉煌币、4个水晶镜子、1个仙女魔法粉、绿宝石、红宝石、蓝宝石、黄玉、锆石各10个^000000。";
		next;
		mes "[帕尔图]";
		mes "我自己也陷入困境。也请考虑一下我的情况。";
		close;
	}
	emotion ET_QUESTION;
	mes "[帕尔图]";
	mes "是的，这已经足够了。我现在应该做吗？";
	next;
	if (select("请现在就做：哎呀，我必须处理一些事情......") == 2) {
		emotion ET_HUK;
		mes "[帕尔图]";
		mes "嘿..嘿！嘿！你要去哪里？";
		close;
	}
	emotion ET_HUK;
	mes "[帕尔图]";
	mes "我得到了它。哦，对了！差点又忘记了。";
	next;
	mes "[帕尔图]";
	mes "^FF0000在复制过程中，魔法干预现象导致帽子失去所有的精炼、卡牌和隐藏结界。^000000";
	next;
	mes "[帕尔图]";
	mes "^FF0000如果您拥有多顶相同类型的帽子，您不想要的帽子可以进行复制过程，因此请检查以确保。^000000";
	next;
	mes "[帕尔图]";
	mes "那么这有什么问题吗？";
	next;
	if (select("让我去检查一下：启动复制过程。") == 1) {
		mes "[帕尔图]";
		mes "是的，请确认一下！";
		close;
	}
	emotion ET_KIK;
	mes "[帕尔图]";
	mes "呜呼-明白了。";
	mes "终于到了我再次施展我的技能的时候了。";
	next;
	emotion ET_PROFUSELY_SWEAT;
	mes "[帕尔图]";
	mes "在这里做这个，在那里做那个，然后在这里再做这个……";
	next;
	emotion ET_THINK;
	emotion ET_PROFUSELY_SWEAT;
	mes "[帕尔图]";
	mes "…………";
	specialeffect EF_BEGINSPELL;
	progressbar "0xFFFF00",1;
	specialeffect EF_BEGINSPELL2;
	progressbar "0xFFFF00",1;
	specialeffect EF_BEGINSPELL3;
	progressbar "0xFFFF00",1;
	specialeffect EF_BEGINSPELL4;
	progressbar "0xFFFF00",1;
	specialeffect EF_MVP;
	next;
	delitem 6081,50; //Splendide_Coin
	delitem 747,4; //Crystal_Mirror
	delitem 6395,1; //Angel_Magic_Power
	delitem 721,10; //Azure_Jewel
	delitem 723,10; //Cardinal_Jewel
	delitem 726,10; //Blue_Jewel
	delitem 728,10; //Golden_Jewel
	delitem 729,10; //Bluish_Green_Jewel
	delitem .@items[.@i],1;
	getitem .@costumes[.@i],1;
	mes "[帕尔图]";
	mes "好的。这是一个令人满意的结果。";
	mes "下次我会做得更好。请向其他人介绍我的工作。祝你有美好的一天~";
	close;
}
