//===== rAthena Script =======================================
//= Cluck! Cluck! Boom!
//===== By: ==================================================
//= Keale
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Click the chicken and try retrieve the item at a low 
//= chance. If you fail he will nuke, freeze, stone, 
//= stun, or make you fall asleep.
//= The prize is configurable and triggered by the NPC.
//===== Additional Comments: =================================
//= 1.0 First release.
//= 1.1 Using 'switch rand' instead.
//= 1.2 Cleaned and standardized. [Euphy]
//= 1.2a Switched 'atcommand' to 'unitskilluseid'.
//= 1.3 Switched 'unitskilluseid' to 'unitkill'.
//===== Translated By: =======================================
//= dsc
//============================================================

prontera,156,219,4	script	Cluckers	800,{

	set .@GMAccess,80;	// GM level required to access NPC

	if (.startcluck) {
		specialeffect2 EF_HIT3;
		switch(rand(15)) {
			case 0: npctalk "咯咯的叫声：咕噜咕噜！！！"; unitkill getcharid(3); skilleffect "NPC_自我毁灭",1; emotion ET_HUK; break;
			case 1: npctalk "咕噜咕噜：咕咕咕！~"; break;
			case 2: unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; break;
			case 3: sc_start SC_Freeze,10000,0; break;
			case 4: npctalk "咯咯的：咕噜咕噜！！！"; unitkill getcharid(3); skilleffect "NPC_自我毁灭",1; emotion ET_HUK; break;
			case 5: sc_start SC_Sleep,10000,0; break;
			case 6: sc_start SC_Stone,10000,0; emotion ET_KIK; break;
			case 7: npctalk "咯咯的叫声：咕噜咕噜！！！"; unitkill getcharid(3); skilleffect "NPC_自我毁灭",1; emotion ET_HUK; break;
			case 8: npctalk "咯咯：咯咯！咕咕！！"; unitkill getcharid(3); skilleffect "NPC_自我毁灭",1; emotion ET_HUK; break;
			case 9: sc_start SC_Stun,10000,0; break;
			case 10: sc_start SC_Sleep,10000,0; emotion ET_KIK; break;
			case 11: npctalk "咯咯：咯咯！咕!"; break;
			case 12: sc_start SC_Stun,10000,0; break;
			case 13: unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; break;
			default:
				if (rand(50) < 4) {
					npctalk "咯咯声：呜呜！............";
					specialeffect2 EF_SPHERE;
					announce "[Cluck! Cluck! Boom!] " + strcharinfo(0) + " squeezed out the prize! Well done!",0;
					getitem $cluck_item_id,$cluck_item_amount;
					set .startcluck,0;
				} else {
					npctalk "咯咯：咯咯！咕咕！！";
					unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1;
				}
				break;
		}
		end;
	}
	mes "[咯咯]";
	if (getgmlevel() >= .@GMAccess) {
		mes "咯咯咯！克鲁鲁克？ ^FF0000~嗨，GM" + strcharinfo(0) + "，今天想玩吗？~^000000";
		mes "咯咯咯...咯咯！ ^FF0000~告诉我该怎么做！~^000000";
		next;
		while(1) switch(select("开始活动：检查奖品：设置奖品：今天不行 Cluckers")) {
			case 1:
				mes "[咯咯]";
				mes "咕！ ^FF0000~当然可以！~^000000";
				emotion ET_OK;
				close2;
				announce "[Cluck! Cluck! Boom!] is about to start in Prontera!",bc_blue;
				initnpctimer;
				end;
			case 2:
				mes "[咯咯]";
				mes "咯咯咯咯……咯咯！ ^FF0000~当前奖品是^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +"。^000000";
				next;
				break;
			case 3:
				mes "[咯咯]";
				mes "咯咯咯？咕？？？ ^FF0000~获胜的奖品应该是什么？请输入ID~^000000";
				input $cluck_item_id;
				next;
				mes "[咯咯]";
				mes "咕？克鲁克？ ^FF0000~如果这个物品我应该赠送多少？~^000000";
				input $cluck_item_amount;
				next;
				mes "[咯咯]";
				mes "咯咯咯..？咕。 ^FF0000~那么，奖品是^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +"^000000？ ^FF0000太棒了。~^000000";
				emotion ET_OK;
				next;
				break;
			case 4:
				mes "[咯咯]";
				mes "咯咯咯咯...";
				close;
		}
	}
	mes "咯咯咯！克鲁鲁克？";
	mes "咕....";
	close;

OnTimer10000:
	announce "Please hurry behind the fountain if you want to play with the crazy chicken!",bc_blue;
	end;
OnTimer20000:
	announce "Cluckers has eaten one of my items! I'm too scared to retrieve it!",bc_blue;
	end;
OnTimer30000:
	announce "Click the insane chicken and try squeeze out the item, if you're lucky you'll win! Are you ready?",bc_blue;
	end;
OnTimer40000:
	announce "GO! Click the chicken to get the prize!",bc_blue;
	stopnpctimer;
	if (!$cluck_item_id) set $cluck_item_id,512;
	if (!$cluck_item_amount) set $cluck_item_amount,1;
	set .startcluck,1;
	end;
}
