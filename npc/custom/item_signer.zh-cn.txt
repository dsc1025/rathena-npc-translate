//===== rAthena Script =======================================
//= Sign Your Items
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.1a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Write you name on your rare equipment or weapon ^_-
//===== Additional Comments: =================================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//===== Translated By: =======================================
//= dsc
//============================================================

prt_in,24,61,7	script	Perchik	47,{
	
	setarray .@Item[0],644,3;	// Item requirements: <ID>,<Count>{,...} (0 to disable)
	setarray .@Cost[0],0,5000;	// Zeny requirements: <base price>,<price per refine>

	disable_items;
	mes "[佩奇克]";
	mes "我可以^0055FF在你持有的几乎任何稀有物品上签上你的名字^000000。";
	next;
	if(select("告诉我更多...：请在我的物品上签名！") == 1) {
		mes "[佩奇克]";
		mes "我可以把你的名字写在任何无槽装备或武器上。";
		emotion ET_AHA;
		next;
		mes "[佩奇克]";
		if (getarraysize(.@Item) || getarraysize(.@Cost)) {
			mes "对于我的工作，我接受：";
			if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
				mes "～"+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (.@Cost[0]) mes " ~ "+callfunc("F_InsertComma",.@Cost[0])+" Zeny";
			if (.@Cost[1]) mes " ~ "+callfunc("F_InsertComma",.@Cost[1])+" 每次精炼 Zeny";
		}
		else mes "我免费工作，但是...";
		next;
		emotion ET_CRY;
		mes "[佩奇克]";
		mes "唉，我有 12 个饥饿的孩子";
		mes "和一个非常生气的妻子。";
		next;
		mes "[佩奇克]";
		mes "或者是12个愤怒的孩子";
		mes "还有一个非常饥饿的妻子......";
		emotion ET_SCRATCH;
		close;
	}
	mes "[佩奇克]";
	mes "显示您要签名的物品...";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i]))
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) +" [^0055FF"+getequipname(.@indices[.@i])+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, .@indices[ select(.@menu$) ];
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[佩奇克]";
	if (!getequipisequiped(.@part)) {
		mes "那里什么装备都没有！";
		emotion ET_KEK;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "唉，这个项目已经签名了。";
		mes "我永远不会碰大师的作品。";
		emotion ET_SCRATCH;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "一张卡？这里？！";
		mes "正如我之前所说，我不会用卡片签署物品。";
		emotion ET_SCRATCH;
		close;
	}
	if (getiteminfo(.@id, ITEMINFO_SLOT)) {
		mes "抱歉，我不签署已开槽的物品。";
		emotion ET_SORRY;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "抱歉，我不签署租赁物品！";
			emotion ET_SCRATCH;
			close;
		}
	set .@price, .@Cost[0]+(.@Cost[1]*.@ref);
	if (getarraysize(.@Item) || .@price) {
		mes "我需要：";
		if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2) {
			mes "～"+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (countitem(.@Item[.@i]) < .@Item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes "～"+.@price+"女性";
	}
	mes "我可以在您的^0055FF"+getitemname(.@id)+"^000000 上签字吗？";
	next;
	if(select("好的！：离开")==2) {
		mes "[佩奇克]";
		mes "再见...";
		emotion ET_SLEEPY;
		close;
	}
	mes "[佩奇克]";
	if (Zeny < .@price || .@nr) {
		mes "我工作不是为了‘谢谢’。";
		emotion ET_SORRY;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "哪里是"+getitemname(@id)+"...？";
		npctalk "Perchik：你是个贪睡的骗子！";
		logmes "Hack: Tried to sign an item not having it: "+getitemname(@id);
		emotion ET_KEK;
		close;
	}
	if (.@price) set Zeny, Zeny-.@price;
	if (getarraysize(.@Item))  for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
		delitem .@Item[.@i], .@Item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "完毕！";
	emotion ET_OK;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	close;
}
