//===== rAthena Script ======================================= 
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================

// Main NPC :: safety_Ref_NPC
//============================================================
prontera,184,177,6	script	Refine Master	851,{
	disable_items;
	setarray .@cert_weapon[0],
		6864,	// Guarantee_Weapon_19Up
		6875,	// Guarantee_Weapon_18Up
		6874,	// Guarantee_Weapon_17Up
		6873,	// Guarantee_Weapon_16Up
		6872,	// Guarantee_Weapon_15Up
		6871,	// Guarantee_Weapon_14Up
		6870,	// Guarantee_Weapon_13Up
		6584,	// Guarantee_Weapon_12Up
		6238,	// Guarantee_Weapon_11Up
		6228,	// Guarantee_Weapon_9Up
		6229,	// Guarantee_Weapon_8Up
		6230,	// Guarantee_Weapon_7Up
		6231,	// Guarantee_Weapon_6Up
		6456;	// Guarantee_Weapon_5Up
	setarray .@cert_armor[0],
		6865,	// Guarantee_Armor_19Up
		6881,	// Guarantee_Armor_18Up
		6880,	// Guarantee_Armor_17Up
		6879,	// Guarantee_Armor_16Up
		6878,	// Guarantee_Armor_15Up
		6877,	// Guarantee_Armor_14Up
		6876,	// Guarantee_Armor_13Up
		6585,	// Guarantee_Armor_12Up
		6239,	// Guarantee_Armor_11Up
		6232,	// Guarantee_Armor_9Up
		6233,	// Guarantee_Armor_8Up
		6234,	// Guarantee_Armor_7Up
		6235,	// Guarantee_Armor_6Up
		6457;	// Guarantee_Armor_5Up
	setarray .@cert_level[0],19,18,17,16,15,14,13,12,11,9,8,7,6,5;
	.@size_cert = getarraysize(.@cert_weapon);

	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@cert_weapon[.@i]) > 0 || countitem(.@cert_armor[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}

	if (.@check == 0) {
		mes "[炼器大师]";
		mes "你好！";
		mes "这是怎么回事？";
		mes "我是专家";
		mes "用于精炼物品，";
		mes "但我不再工作了。";
		next;
		switch(select("我会继续我的路。：嗯...这让我很好奇。")) {
		case 1:
			mes "[炼器大师]";
			mes "小心点，冒险家。";
			close;
		case 2:
			mes "[炼器大师]";
			mes "其实我有时候也会用《精炼券》^000000为冒险者提供精炼服务……";
			mes "再见~！";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[炼器大师]";
	mes "问候！";
	mes "我可以将物品精炼到与您的票^000000相同的级别^006400。";
	mes "您不必担心！没有机会损坏您的物品。";
	next;
	if(select("我稍后再回来。：用票精炼物品。") == 1) {
		mes "[炼器大师]";
		mes "好的。";
		mes "你可以稍后再来。";
		close;
	}
	mes "[炼器大师]";
	mes "您想精炼哪些装备？";
	next;
	setarray .@position$[1],"Head upper","Armor","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head middle","Head lower";
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for ( .@i = 1; .@i <= 10; ++.@i )
		.@menu$ += (getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : .@position$[.@i] + "- [Empty]") + ":";

	.@part = .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[炼器大师]";
		mes "你必须装备你想要精炼的物品。";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[炼器大师]";
		mes "哦，对不起。";
		mes "此物品无法精炼。";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );
	if( .@itemtype == IT_WEAPON ){
		switch( getequipweaponlv( .@part ) ){
			case 1:
			case 2:
			case 3:
			case 4:
				copyarray .@tickets[0], .@cert_weapon[0], .@size_cert;
				.@type$ = "Weapon";
				break;
			default:
				// TODO:
				close;
		}
	}else if( .@itemtype == IT_ARMOR ){
		switch( getequiparmorlv( .@part ) ){
			case 1:
				copyarray .@tickets[0], .@cert_armor[0], .@size_cert;
				.@type$ = "Armor";
				break;
			default:
				// TODO:
				close;
		}
	}else{
		// TODO:
		close;
	}
	.@check = 0;
	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@tickets[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}
	if (.@check == 0) {
		emotion ET_THINK;
		mes "[炼器大师]";
		mes "如果您想精炼此^006400"+.@type$+"^000000，请附上^006400"+.@type$+"精炼单^000000。";
		mes "稍后见！";
		close;
	}
	mes "[炼器大师]";
	mes "请选择您要使用的^006400"+.@type$+" Refine Ticket^000000。";
	next;
	.@menu$ = "";
	for ( .@i = 0; .@i < .@size_cert; ++.@i )
		.@menu$ += getitemname(.@tickets[.@i]) + ":";
	.@select = select(.@menu$)-1;
	.@ticket_lv = .@cert_level[.@select];
	.@ticket_id = .@tickets[.@select];

	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[炼器大师]";
		mes getitemname(.@ticket_id)+"不在您的库存中。你把它放在你的仓库里了吗？";
		mes "请再次检查。";
		mes "稍后见！";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[炼器大师]";
		mes "^8B4513该物品已与您的行为一样精炼。^000000";
		mes "请携带一件比您的门票精致的物品。";
		close;
	}
	mes "[炼器大师]";
	mes "我要精炼^006400"+getequipname(.@part)+"^8B4513 到 +"+.@ticket_lv+"级别 ^000000 与 ^006400"+getitemname(.@ticket_id)+"^000000。";
	mes "我可以继续吗？";
	next;
	if(select("否：是的。") == 1) {
		emotion ET_THINK;
		mes "[炼器大师]";
		mes "哦，你改变主意了。";
		mes "好的。";
		mes "你可以稍后再回来。";
		close;
	}
	mes "[炼器大师]";
	mes "伟大的。";
	mes "如你所愿！";
	mes "我有自己独特的炼化方法……";
	mes "……轰！";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id) < 1) {
		next;
		mes "错误！";
		mes "请报告此事。";
		close;
	}
	delitem .@ticket_id,1;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) ||
		callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
		mes "[炼器大师]";
		emotion ET_FRET;
		mes "等一下...";
		mes "你以为我傻吗？！";
		mes "你趁我不注意的时候把东西换了！离开这里！";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[炼器大师]";
	mes "好啦，就到这里啦~";
	mes "好吧，^0000FF"+strcharinfo(0)+"^000000！";
	mes "恭喜你闪耀的"+.@type$+".";
	mes "你看起来棒极了！";
	mes "再见~！";
	close;
}
