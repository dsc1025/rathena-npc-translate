//===== rAthena Script ======================================= 
//= Valhallen Items Quests NPC
//===== By: ================================================== 
//= Avaji
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= All-in-one: Mjolnir,Sleipnir,Brisingamen,Gleipnir,Megingjard
//= Custom quest, ingredients based on official RO FAQ
//===== Additional Comments: ================================= 
//= 1.1 Optimized. [Euphy]
//= 2.0 Adds SE and TE Godlikes. Fixed Mixing Itemlist. [Jey]
//= Sources for the mixing lists 2016-08-15:
//= http://irowiki.org/wiki/God_Items_Quest
//= http://irowiki.org/wiki/God_Items_Quest_2
//= http://irowiki.org/wiki/God_Items_Quest_3
//===== Translated By: =======================================
//= dsc
//============================================================ 

prontera,147,171,5	script	Lenneth	811,{
	mes "[雷尼思]";
	mes "我从瓦尔哈拉来帮助你。";
	mes "我可以为你转化许多瓦尔哈伦的物品，";
	mes "尽管它们需要许多稀有元素来制作。";
	next;
	switch(select("雷神之锤:斯莱普尼尔:布里辛加门:格莱普尼尔:梅金贾德:阿斯普里卡:布伦希尔德:赫沃尔:耶蒙刚德:穆斯佩里姆:符文精华:关闭")) {
		case 1:
			mes "[雷尼思]";
			mes "^3355FF雷神锤^000000是强大的雷神之锤。";
			setarray .@Items[0],1530,1522,2,984,20,985,5,969,40,7074,2,7075,4,7078,5,7091,5,7089,5;
			break;
		case 2:
			mes "[雷尼思]";
			mes "^3355FFSleipnir^000000是根据奥丁战马制作的靴子。";
			setarray .@Items[0],2410,2406,2,969,20,985,10,7076,3,7079,5,7083,3,7086,4,7087,3;
			break;
		case 3:
			mes "[雷尼思]";
			mes "^3355FFBrisingammen^000000是美丽女神芙蕾雅的神奇项链。";
			setarray .@Items[0],2630,2603,1,726,2,722,3,727,10,724,5,969,20,7073,4,7077,4,7088,3,7090,3,7092,3;
			break;
		case 4:
			mes "[雷尼思]";
			mes "^3355FFGleipnir^000000是制作^3355FFMegingjard^000000所需的轻质而坚固的绳索";
			setarray .@Items[0],7058,7080,4,7081,5,7082,4,7084,3,7085,3;
			break;
		case 5:
			mes "[雷尼思]";
			mes "^3355FFMegingjard^000000 是强大的雷神腰带。";
			setarray .@Items[0],2629,7058,1,2627,1,969,20,726,10,984,10;
			break;
		case 6:
			mes "[雷尼思]";
			mes "^3355FFAsprika^000000是众神之光。";
			setarray .@Items[0],2541,7835,1,7836,1,7837,1,7838,1,2513,1,7063,100,7291,10,7293,10,985,20;
			break;
		case 7:
			mes "[雷尼思]";
			mes "^3355FF布伦希尔德^000000是战士的盔甲。";
			setarray .@Items[0],2383,7830,1,7831,1,7832,1,7833,1,7834,1,7510,100,2357,1,969,10,985,20;
			break;
		case 8:
			mes "[雷尼思]";
			mes "^3355FF赫沃尔^000000以确保战士们胜利的瓦尔基里命名。";
			setarray .@Items[0],2181,6595,2,6596,1,6594,4,6597,3,6602,1,6604,1,6605,1,2115,1;
			break;
		case 9:
			mes "[雷尼思]";
			mes "^3355FFJormungand^000000以其毒害敌人的能力而闻名。";
			setarray .@Items[0],2020,6603,4,6599,1,6598,1,6601,4,6600,1,6604,1,6605,1,1473,1;
			break;
		case 10:
			mes "[雷尼思]";
			mes "制造^3355FFJormungand^000000和^3355FFHervor^000000需要^3355FFMuspellium^000000。";
			setarray .@Items[0],6605,984,50,985,50;
			break;
		case 11:
			mes "[雷尼思]";
			mes "制作^3355FFJormungand^000000和^3355FFHervor^000000需要^3355FF符文精华^000000。";
			setarray .@Items[0],6604,12735,200,12736,100;
			break;
		default:
			close;
	}
	
	mes "^FF0000我需要的物品如下：^000000";
	set .@itemmissing,0;
	for(set .@i,1; .@i<getarraysize(.@Items); set .@i,.@i+2) {
		set .@amount,.@Items[.@i+1];
		set .@id,.@Items[.@i];
		mes " ~ "+.@amount+"x "+((countitem(.@id)<.@amount)?("^dd3355"):("^33dd55"))+getitemname(.@id)+((getitemslots(.@id)>0)?" ["+getitemslots(.@id)+"]":"")+"^000000";
		if (countitem(.@id) < .@amount) 
			set .@itemmissing,1;
	}
	if (.@itemmissing) {
		next;
		mes "[雷尼思]";
		mes "继续寻找所有这些物品。";
		mes "我会在这里等你来帮助你。";
		close;
	}
	next;
	mes "[雷尼思]";
	mes "您想要^3355FF"+getitemname(.@Items[0])+"^000000吗？";
	next;
	if(select("不","是的")==1) {
		mes "[雷尼思]";
		mes "如果您改变主意，请返回。";
		close;
	}
	mes "[雷尼思]";
	mes "这是您的^3355FF"+getitemname(.@Items[0])+"^000000。";
	mes "愿它为您服务。";
	//second check if the player tries to bypass the first
	for(set .@i,1; .@i<getarraysize(.@Items); set .@i,.@i+2) {
		set .@amount,.@Items[.@i+1];
		set .@id,.@Items[.@i];
		if (countitem(.@id) < .@amount) 
			close;
	}
	// Delete the items _after_ the second check
	for(set .@i,1; .@i<getarraysize(.@Items); set .@i,.@i+2) {
		set .@amount,.@Items[.@i+1];
		set .@id,.@Items[.@i];
		delitem .@id, .@amount;
	}
	getitem .@Items[0],1;
	close;
}
