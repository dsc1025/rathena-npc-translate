//===== rAthena Script =======================================
//= Vending Machine NPCs for Novice.
//===== Description: ========================================= 
//= [Walkthrough Conversion]
//= Exchangers NPCs for Novice.
//===== Additional Comments: =================================
//= 1.0 Firt version. [Aleos] [Secret]
//= 1.1 Additionnal clean-up. [Capuche]
//============================================================

-	script	novice_vending_machine#main	-1,{
	.@storage_ticket = 7059;	// Cargo_Free_Ticket
	.@storage_ticket_name$ = getitemname(.@storage_ticket);

	mes "新手自动售货机。";
	mes "有很多按钮。";
	next;
	switch( select( "手册", "交易" + .@storage_ticket_name$, "交易新手装备" ) ) {
	case 1:
		mes "[交换" + .@storage_ticket_name$ + "]";
		mes "4个" + .@storage_ticket_name$ + "可兑换1张曲速、飞艇或马车服务免费门票，或1张佩科骑行免费门票、15个新手飞翼、5个新手蝴蝶翅膀、30个新手药水。";
		next;
		mes "[交易新手装备]";
		mes "克里阿图拉学院的盔甲、兜帽、凉鞋、主装和新手守卫可以兑换成4个" + .@storage_ticket_name$ + ".";
		next;
		mes "[交易新手装备]";
		mes "新手蛋壳可以兑换成Criatura发卡，用于更换发型。";
		close;
	case 2:
		setarray .@exchange[0],
			7060,1,		// Warp_Free_Ticket
			7311,1,		// Free_Flying_Ship_Ticket
			7061,1,		// Cart_Free_Ticket
			7310,1,		// Free_Peco_Ticket
			12323,15,	// N_Fly_Wing
			12324,5,	// N_Butterfly_Wing
			569,30;		// Novice_Potion
		.@size = getarraysize(.@exchange);

		for ( .@i = 0; .@i < .@size; .@i += 2 ) {
			if (.@exchange[.@i+1] > 1)
				.@menu$ += .@exchange[.@i+1] + " ";
			.@menu$ += getitemname(.@exchange[.@i]) + ":";
		}
		.@s = (select(.@menu$) - 1) * 2;
		if (countitem(.@storage_ticket) < 4) {
			mes "" + .@storage_ticket_name$ + " 数量不足以继续兑换。";
			close;
		}
		if (.@exchange[.@s+1] > 1)
			.@amount$ = .@exchange[.@s+1] + " ";
		mes "交换 4 " + .@storage_ticket_name$ + " 至" + .@amount$ + getitemname(.@exchange[.@s]);
		delitem .@storage_ticket,4;
		getitem .@exchange[.@s],.@exchange[.@s+1];
		close;
	case 3:
		mes "将所有新手装备兑换成优惠券。";
		mes "选择继续继续。";
		next;
		if (select( "继续", "取消。" ) == 2) {
			mes "贸易被中断。";
			close;
		}
		mes "将所有装备兑换成优惠券。";
		setarray .@list[0],2352,2510,2414,1243,2112,5055;
		.@size = getarraysize(.@list);
		
		for ( .@i = 0, .@i < .@size; .@i++ ) {
			.@count = countitem(.@list[.@i]);	// note: maybe check if equipped items are skipped
			if (.@count < 1)
				continue;
			switch(.@list[.@i]) {
			case 2352:	// Novice_Plate
			case 2510:	// Novice_Hood
			case 2414:	// Novice_Boots
			case 1243:	// Novice_Knife
			case 2112:	// Novice_Guard
				delitem .@list[.@i], .@count;
				getitem .@storage_ticket,(4*.@count);	// Cargo_Free_Ticket
				break;
			case 5055:	// Novice_Egg_Cap
				delitem .@list[.@i], .@count;
				getitem 6593,.@count;	// Cryptura_Hair_Coupon
				break;
			}
		}
		close;
	}
}
izlude_in,69,177,5	duplicate(novice_vending_machine#main)	Vending Machine for Nov#iz	2_VENDING_MACHINE1
payon_in02,71,75,5	duplicate(novice_vending_machine#main)	Vending Machine for Nov#pay	2_VENDING_MACHINE1
prt_church,187,27,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#prt	2_VENDING_MACHINE1

// unknown facing
alberta_in,60,52,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#alb	2_VENDING_MACHINE1
geffen_in,158,107,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#gef	2_VENDING_MACHINE1
moc_para01,17,37,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#para	2_VENDING_MACHINE1
moc_prydb1,38,124,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#pryd	2_VENDING_MACHINE1
payon_in01,59,20,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#pay2	2_VENDING_MACHINE1
que_ng,28,73,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#que_ng1	2_VENDING_MACHINE1
que_ng,168,39,3	duplicate(novice_vending_machine#main)	Vending Machine for Nov#que_ng2	2_VENDING_MACHINE1
