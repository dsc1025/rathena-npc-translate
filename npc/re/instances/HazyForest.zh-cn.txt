//===== rAthena Script ======================================= 
//= Hazy Forest
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Cross through the Hazy Forest to reach the Bifrost.
//= Contains the "Wandering Guardian" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Instance system rewrite. [Euphy]
//===== Translated By: =======================================
//= dsc
//============================================================ 

// Instance Creation :: md_mistwoods_enter
//============================================================
bif_fild01,158,340,5	script	Laphine Soldier#mist	447,{

	set .@party_id,getcharid(1);
	set .@md_name$,"Mistwood Maze";

	mes "[拉芬士兵]";
	mes "你要去森林吗？";
	next;
	if (!.@party_id) {
		mes "[拉芬士兵]";
		mes "好吧好吧，你这也太鲁莽了吧？";
		mes "你为什么不至少组织一个聚会呢？";
		close;
	}
	set .@playtime, checkquest(7211,PLAYTIME);
	if (is_party_leader() == true) {
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[拉芬士兵]";
			mes "……你确定你没有做得太过分吗？";
			mes "我想你最好休息一下。";
			mes "尝试做太多事情只会让自己筋疲力尽";
			next;
			mes "[拉芬士兵]";
			mes "我认为当局也不会给予你许可。";
			mes "……即使是萨法人也会被阻止进入森林。";
			next;
			select("你我们的语言说得很好，不是吗？");
			mes "[拉芬士兵]";
			mes "...我是吗？";
			mes "事实上我不会说你们的语言。";
			mes "我说的是拉芬语。";
			next;
			mes "[拉芬士兵]";
			mes "但这是 Bifrost 的谜团之一。";
			mes "即使我们说不同的语言，我们也能互相理解。";
			next;
			mes "[拉芬士兵]";
			mes "能够和大家交流...";
			mes "也许这就是 Bifrost 的力量。";
			close;
		} else if (.@playtime == 2) {
			mes "[拉芬士兵]";
			mes "太棒了，太棒了。";
			mes "现在森林不会拒绝你了。";
			mes "你现在要做什么？";
			erasequest 7211;
		} else {
			mes "[拉芬士兵]";
			mes "你们人类冒险家真是不可思议。";
			mes "你总是要走进别人不敢去的森林。";
			next;
			mes "[拉芬士兵]";
			mes "唷。但我必须向当局报告";
			mes "每个进入森林的人，以防万一。";
			next;
			mes "[拉芬士兵]";
			mes "你会得到许可";
			mes "注册后。";
			mes "你要去迷雾森林冒险吗？";
		}
		next;
		switch(select("冒险进入朦胧森林。：放弃。")) {
		case 1:
			mes "[拉芬士兵]";
			mes "政党名称 "+getpartyname(.@party_id)+"...";
			mes "党魁"+strcharinfo(0)+"...";
			if (instance_create(.@md_name$) < 0) {
				mes "唔...";
				next;
				mes "[拉芬士兵]";
				if (.@playtime == -1)
					mes "森林里很危险。";
				else
					mes "森林里的气氛有些紧张。";
				mes "……你今天怎么不回去？";
				close;
			}
			mes "我明白了。我已将它们写在此处的报告中。";
			next;
			mes "[拉芬士兵]";
			mes "你很快就会得到许可。";
			mes "现在前往原木隧道，这是进入朦胧森林的唯一途径。";
			mes "你明白吗？";
			next;
			mes "[拉芬士兵]";
			mes "我们不能违背森林，";
			mes "但也许你们人类可以。";
			mes "祝你好运！";
			close;
		case 2:
			mes "[拉芬士兵]";
			mes "嗯，你做出了正确的决定。";
			if (.@playtime == -1)
				mes "这对你有好处。";
			close;
		}
	} else {
		mes "[拉芬士兵]";
		mes "你们的党魁在哪里？";
		mes "那里很危险。";
		next;
		if (.@playtime == 2) {
			mes "[拉芬士兵]";
			mes "我发现你以前去过森林。";
			mes "好吧，至少森林不会拒绝你。";
			mes "然而...";
			erasequest 7211;
			next;
		}
		mes "[拉芬士兵]";
		mes "设身处地为我着想，门卫，让我见见你们党的代表。";
		mes "我必须和他/她讨论一些事情。";
		close;
	}
}

bif_fild01,161,355,0	script	Log Tunnel	844,{
	mes "我透过圆木隧道看到了森林。";
	mes "看来可以通过圆木隧道进入森林了。";
	next;
	if(select("进入隧道。：放弃。") == 2)
		close;
	switch(instance_enter("Mistwood Maze")) {
	case IE_OTHER:
		mes "[拉芬士兵]";
		mes "感觉有些不对劲。看来有什么危险的事情发生，你今天还是回去吧。";
		close;
	case IE_NOINSTANCE:
		mes "你试图爬进圆木里，但某种神秘的力量用一阵风将你推了回来。";
		mes "看来你不能强行进入森林。";
		close;
	case IE_NOMEMBER:
		mes "[拉芬士兵]";
		mes "嘿，看！";
		mes "你一个人去吗？";
		mes "那是不可能的。太鲁莽了。";
		mes "与一些朋友组队，一起去吧！";
		close;
	case IE_OK:
		if (checkquest(7211,PLAYTIME) == 2) erasequest 7211;
		if (checkquest(7211,PLAYTIME) == -1) setquest 7211;
		mapannounce "bif_fild01",getpartyname(getcharid(1))+" party's "+strcharinfo(0)+" member is entering the Mistwood Maze.",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		//warp "1@mist",89,29;
		close;
	default:
		mes "圆木隧道之外的世界看起来黑暗而令人窒息。";
		mes "你的本能尖叫着这片森林很危险。";
		close;
	}
}

// Instance Scripts :: md_mistwoods_in
//============================================================
1@mist,89,29,0	script	#Whisper_mist	844,1,1,{
	end;
OnInstanceInit:
	enablenpc instance_npcname("#Whisper_mist");
	end;
OnTouch_:
	disablenpc instance_npcname("#Whisper_mist");
	initnpctimer;
	end;
OnTimer10000:
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"Whisper: Who's this? Who's this!? The forest is open! Tom, Tomba, Tired Rem, Remi! Wake up!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer60000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: What are the forest keepers doing? What are the gardeners doing??",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer120000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: Don't go out, wander around forever, and play with us!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer180000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: Useless, useless. Wandering around and around, you'll meet your end!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
}

// callfunc "F_Mora_Mist",<warp to enable>,<success message>,<fail message>{,<end flag>}
function	script	F_Mora_Mist	{
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMyMobDead") == 0) {
		mes "显然没有人照顾它。";
		mes "看来你可以砍倒花园里的树。";
		next;
		if(select("砍倒花园里的树。：放弃。") == 2)
			close;
		mes "你砍倒了挡住迷宫道路的树，"+((getarg(3,0))?"清理出森林的道路。":"所以现在你可以继续了。");
		mapannounce .@map$,getarg(1),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
		enablenpc instance_npcname(getarg(0));
		disablenpc();
		if (getarg(4,0))
			initnpctimer;
		close;
	} else
		mapannounce .@map$,((getarg(3,0))?getarg(2):getarg(2)+"'s Cry: Huh? Who's doing bad things to my tree?!"),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
}

1@mist,251,121,3	script	Tom's Garden	858,{ end; }
1@mist,247,123,0	script	Tom's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a25_a18-1",
		"Roaring Cry: Tom's bolt is broken! Watch out, Tomba! Watch out, Tomba!",
		"Tom";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),249,120,"Tom",2136,1,instance_npcname("Tom's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,226,95,3	script	Tomba's Garden	858,{ end; }
1@mist,225,98,0	script	Tomba's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a27-2_af1-2",
		"Roaring Cry: Tomba's Garden Tree is gone with Tomba. Remi, what are you going to do now?",
		"Tomba";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),200,64,"Tomba",2136,1,instance_npcname("Tomba's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,157,183,3	script	Remi's Garden	858,{ end; }
1@mist,159,184,0	script	Remi's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a15-2_a4-1",
		"Roaring Cry: Remi, Remi's Garden Tree was chopped down- Rem, it's your turn next. Poor Rem!",
		"Remi";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),154,184,"Remi the Tired",2137,1,instance_npcname("Remi's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,58,39,3	script	Tired Rem's Garden	858,{ end; }
1@mist,61,39,0	script	Tired Rem's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a4-2_a11",
		"Rem's Desperate Cry: Argh... Rem will sleep. Rem will sleep now, and won't wake up forever!",
		"Rem",
		0,1;
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),101,107,"Rem the Gardener",2136,1,instance_npcname("Tired Rem's Garden Tree")+"::OnMyMobDead";
	end;
OnTimer10000:
	mapannounce instance_mapname("1@mist"),"Whisper of the Forest: So Tom's, Tomba's, Remi's, and Rem's garden trees were all chopped down?",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@mist"),"Whisper of the Forest: Trouble, trouble, we're in big trouble.",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: They've come to the second deepest forest. Gardeners of the deep forest, watch out!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,227,179,3	script	Ron's Garden	858,{ end; }
1@mist,230,179,0	script	Ron's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b4-2_b2",
		"Ron the Gardener's Cry: I'm getting off work! Don't look for Ron, the second gardener, any more!!",
		"Ron";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),227,178,"Ron the Gardener",2134,1,instance_npcname("Ron's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,288,226,3	script	Rover's Garden	858,{ end; }
1@mist,285,225,0	script	Rover's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b7_bf1-2",
		"Roaring Whisper: Ron's, and Rover's trees were chopped down as well. Who is forcing his way into the forest? Who is it?",
		"Rover";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),304,237,"Rover the Strutter",2134,1,instance_npcname("Rover's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,159,318,3	script	Mona's Garden	858,{ end; }
1@mist,161,316,0	script	Mona's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b31_b16",
		"Clamoring Whisper: Mona the Seedseeker's garden tree is gone! Mona is gone too! Namon will be okay, because he is brave, right? Namon, are you okay?",
		"Mona";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),239,253,"Mona the Seedseeker",2133,1,instance_npcname("Mona's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,205,300,3	script	Namon's Garden	858,{ end; }
1@mist,204,299,0	script	Namon's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b27-3_bN-1",
		"Clamoring Whisper: Brave Namon met his fate not so bravely... *giggle*",
		"Namon";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),89,173,"Brave Namon",2134,1,instance_npcname("Namon's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,219,232,3	script	Sad Neoron's Garden	858,{ end; }
1@mist,221,236,0	script	Sad Neoron's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b6-1_b33",
		"Lamenting Whisper: Sad Neoron has become cheerful! Spyder the Superior Spider, what are you going to do? What are you going to do now?",
		"Neoron";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),143,265,"Sad Neoron",2137,1,instance_npcname("Sad Neoron's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,206,202,3	script	Spyder's Garden	858,{ end; }
1@mist,206,200,0	script	Spyder's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b5_b14",
		"Depressed Whisper: Now it's all over with the second deepest forest. Gardeners are dying out-",
		"Spyder",
		0,1;
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),209,200,"Spyder the Eight-Legged",2132,1,instance_npcname("Spyder's Garden Tree")+"::OnMyMobDead";
	end;
OnTimer5000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: The path is about to open. The tight bolts have been removed!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer9000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: A forest opened, another opened, and the other is going to open as well. Tito and Pumba, they are heading your way!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer13000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: Where you get after wandering around, around and around, everybody you've met while wandering around, around and around,",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer17000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: all the gardeners will be waiting for you, with their clippers in their hands-!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,96,288,3	script	Tito's Garden	858,{ end; }
1@mist,95,287,0	script	Tito's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"c1_c13-1",
		"Tito's Cry: *sob* *sob* Tito's gone now! Tito's done for! Boo hoo!!",
		"Tito";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),264,291,"Tito the Flipper",2133,1,instance_npcname("Tito's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,326,325,3	script	Pumba's Garden	858,{ end; }
1@mist,324,325,0	script	Pumba's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"c13-3_c4",
		"Roaring Whisper: Tito the Hard Worker was defeated! Stop playing and go to work, Tete! Start working!",
		"Pumba";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),309,165,"Diligent Pumba",2134,1,instance_npcname("Pumba's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,278,345,3	script	Tete's Garden	858,{ end; }
1@mist,280,344,0	script	Tete's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"c19_c4",
		"Whisper: They are strong, strong indeed. What are we going to do now? Are there any gardeners left? Are there any?",
		"Tete";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),277,343,"Carefree Tete",2136,1,instance_npcname("Tete's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,345,186,0	script	The Gardeners' Tree	527,{
	callfunc "F_Mora_Mist",
		"#to_bif02",
		"Roaring Whisper: The forest will remember you! It will remember you, the one who broke the clippers of all the gardeners!",
		"Gardeners: We're not dead, yet!!!!",1;
	end;
OnInstanceInit:
	set .@map$, instance_mapname("1@mist");
	monster .@map$,318,137,"Baby Tom",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,321,137,"Tomba the Baby",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,324,137,"Exhausted Remi",2137,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,319,135,"Rem the Exhausted",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,323,135,"Ron the Ex-Gardener",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,320,133,"Rover the Strutter",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,323,133,"Mona the Seedpicker",2133,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,317,132,"Timid Namon",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,326,132,"Indifferent Neoron",2137,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,317,129,"Spyder the Seven-Legged",2132,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,320,129,"Tito the Flapper",2133,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,324,129,"Lazy Pumba",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,327,129,"Careless Tete",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,73,290,0	script	Mysterious Flower#1	844,{
	specialeffect EF_LEVEL99_4;
	disablenpc();
	getitem 12561,1; //Mysterious_Seed
	end;
}
1@mist,69,295,0	duplicate(Mysterious Flower#1)	Mysterious Flower#2	844
1@mist,62,291,0	duplicate(Mysterious Flower#1)	Mysterious Flower#3	844
1@mist,54,293,0	duplicate(Mysterious Flower#1)	Mysterious Flower#4	844
1@mist,48,286,0	duplicate(Mysterious Flower#1)	Mysterious Flower#5	844
1@mist,54,277,0	duplicate(Mysterious Flower#1)	Mysterious Flower#6	844
1@mist,52,270,0	duplicate(Mysterious Flower#1)	Mysterious Flower#7	844
1@mist,62,268,0	duplicate(Mysterious Flower#1)	Mysterious Flower#8	844
1@mist,72,271,0	duplicate(Mysterious Flower#1)	Mysterious Flower#9	844
1@mist,72,276,0	duplicate(Mysterious Flower#1)	Mysterious Flower#10	844
1@mist,118,194,0	duplicate(Mysterious Flower#1)	Mysterious Flower#11	844
1@mist,122,204,0	duplicate(Mysterious Flower#1)	Mysterious Flower#12	844
1@mist,137,209,0	duplicate(Mysterious Flower#1)	Mysterious Flower#13	844
1@mist,144,203,0	duplicate(Mysterious Flower#1)	Mysterious Flower#14	844
1@mist,146,186,0	duplicate(Mysterious Flower#1)	Mysterious Flower#15	844
1@mist,142,182,0	duplicate(Mysterious Flower#1)	Mysterious Flower#16	844
1@mist,132,179,0	duplicate(Mysterious Flower#1)	Mysterious Flower#17	844
1@mist,123,184,0	duplicate(Mysterious Flower#1)	Mysterious Flower#18	844
1@mist,194,118,0	duplicate(Mysterious Flower#1)	Mysterious Flower#19	844
1@mist,188,109,0	duplicate(Mysterious Flower#1)	Mysterious Flower#20	844
1@mist,193,94,0	duplicate(Mysterious Flower#1)	Mysterious Flower#21	844
1@mist,205,92,0	duplicate(Mysterious Flower#1)	Mysterious Flower#22	844
1@mist,213,96,0	duplicate(Mysterious Flower#1)	Mysterious Flower#23	844
1@mist,216,103,0	duplicate(Mysterious Flower#1)	Mysterious Flower#24	844
1@mist,212,116,0	duplicate(Mysterious Flower#1)	Mysterious Flower#25	844
1@mist,207,120,0	duplicate(Mysterious Flower#1)	Mysterious Flower#26	844
1@mist,169,310,0	duplicate(Mysterious Flower#1)	Mysterious Flower#27	844
1@mist,172,296,0	duplicate(Mysterious Flower#1)	Mysterious Flower#28	844
1@mist,191,301,0	duplicate(Mysterious Flower#1)	Mysterious Flower#30	844
1@mist,195,301,0	duplicate(Mysterious Flower#1)	Mysterious Flower#31	844
1@mist,192,316,0	duplicate(Mysterious Flower#1)	Mysterious Flower#32	844
1@mist,187,320,0	duplicate(Mysterious Flower#1)	Mysterious Flower#33	844
1@mist,174,318,0	duplicate(Mysterious Flower#1)	Mysterious Flower#34	844
1@mist,308,136,0	duplicate(Mysterious Flower#1)	Mysterious Flower#35	844
1@mist,314,122,0	duplicate(Mysterious Flower#1)	Mysterious Flower#36	844
1@mist,321,118,0	duplicate(Mysterious Flower#1)	Mysterious Flower#37	844
1@mist,331,123,0	duplicate(Mysterious Flower#1)	Mysterious Flower#38	844
1@mist,335,126,0	duplicate(Mysterious Flower#1)	Mysterious Flower#39	844
1@mist,332,141,0	duplicate(Mysterious Flower#1)	Mysterious Flower#40	844
1@mist,328,144,0	duplicate(Mysterious Flower#1)	Mysterious Flower#41	844
1@mist,314,144,0	duplicate(Mysterious Flower#1)	Mysterious Flower#42	844
1@mist,238,224,0	duplicate(Mysterious Flower#1)	Mysterious Flower#43	844
1@mist,244,231,0	duplicate(Mysterious Flower#1)	Mysterious Flower#44	844
1@mist,257,232,0	duplicate(Mysterious Flower#1)	Mysterious Flower#45	844
1@mist,262,229,0	duplicate(Mysterious Flower#1)	Mysterious Flower#46	844
1@mist,265,216,0	duplicate(Mysterious Flower#1)	Mysterious Flower#47	844
1@mist,260,214,0	duplicate(Mysterious Flower#1)	Mysterious Flower#48	844
1@mist,252,206,0	duplicate(Mysterious Flower#1)	Mysterious Flower#49	844
1@mist,244,211,0	duplicate(Mysterious Flower#1)	Mysterious Flower#50	844

// Wandering Guardian Quest
//============================================================

// ----------------- Part of md_mistwoods_in -----------------
1@mist,2,2,0	script	#Dragon Herder	844,{
	end;
OnInstanceInit:
	switch(rand(1,6)) {
		case 1: setarray .@c[0],77,240; break;
		case 2: setarray .@c[0],147,261; break;
		case 3: setarray .@c[0],182,263; break;
		case 4: setarray .@c[0],194,350; break;
		case 5: setarray .@c[0],268,183; break;
		case 6: setarray .@c[0],65,278; break;
	}
	monster instance_mapname("1@mist"),.@c[0],.@c[1],"Wandering Purple Dragon",2131,1,instance_npcname("#Dragon Herder")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname("#Dragon Herder")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("Collapsed Girl#mist")+"::OnEnable";
		donpcevent instance_npcname("Mysterious Young Man#mis")+"::OnDisable";
		donpcevent instance_npcname("Loki#mist")+"::OnEnable";
		mapannounce .@map$,"The Dragon's Cry: Am I being punished... for abandoning the way... of the guardian...?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
		disablenpc instance_npcname("#Dragon Herder");
		initnpctimer;
	}
	end;
OnTimer3000:
	stopnpctimer;
	mapannounce instance_mapname("1@mist"),"Voice in your head: ...Will you... save... me... please......?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	end;
}

1@mist,97,30,3	script	Mysterious Young Man#mis	512,{
	if (BaseLevel < 98) {
		mes "[神秘青年]";
		mes "啊？你打算经过这里吗？";
		mes "……恐怕这超出了你的能力范围，但我不会插手你的事情。";
		mes "如果你还活着的话我们还会再见面的。";
		close;
	}
	if (ep14_1_mistwoods == 0) {
		if (ep13_3_secret > 23) {
			mes "[神秘青年]";
			mes "你……我记得在营地见过你。";
			mes "您提供的有关阿哈特的信息很有用。";
			mes "谢谢！";
			next;
			mes "[神秘青年]";
			mes "但你打算经过这里吗？";
			mes "穿过 Bifrost 有什么好处呢？";
			next;
		} else {
			mes "[神秘青年]";
			mes "你打算经过这里吗？";
			mes "如果您只是好奇的话，我会建议您不要这样做。";
			mes "只会带来你的英年早逝……";
			next;
		}
		mes "[神秘青年]";
		mes "如果你决心要经过这里，";
		mes "我会向你寻求一些帮助。";
		next;
		select("你是谁？");
		mes "[洛基]";
		mes "洛基……来自刺客公会。";
		mes "那是我的名字。";
		mes "你的呢？";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "....";
		mes "...."+strcharinfo(0)+"先生。";
		next;
		mes "[洛基]";
		mes "我懂了。"+strcharinfo(0)+".";
		mes "我又问你了。";
		mes "你打算穿过这片森林吗？";
		next;
		switch(select("是的，我愿意。：不，我不这样做。")) {
		case 1:
			mes "[洛基]";
			mes "那我就问你一个问题。";
			mes "你见过守卫……紫色头发的女孩吗？";
			next;
			switch(select("不，我没有：是的，我可能有……")) {
			case 1:
				mes "[洛基]";
				mes "所以你还没有。";
				mes "你说的是实话。";
				next;
				break;
			case 2:
				mes "[洛基]";
				mes "等待。";
				mes "（洛基走近了。）";
				mes "... 不。";
				mes "你说的是实话。";
				next;
				break;
			}
			mes "[洛基]";
			mes "现在我们必须在这里说再见了。";
			mes "当然，如果你能在这片森林里生存下来。";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		case 2:
			mes "[洛基]";
			mes "我明白。";
			mes "如果你还没准备好，就退后一步。";
			mes "这片森林不喜欢陌生人。";
			set ep14_1_mistwoods,1;
			close;
		}
	} else if (ep14_1_mistwoods == 1) {
		mes "[洛基]";
		mes "我告诉过你――如果你不想死就退后一步。";
		mes "穿过森林并不容易。";
		mes "这可能会让你付出生命的代价。";
		next;
		switch(select("好的：你想要什么？")) {
		case 1:
			mes "- 当你退后一步时，";
			mes "Loki轻轻地点点头，然后移开视线。 -";
			close;
		case 2:
			mes "[洛基]";
			mes "我正在找人。";
			mes "你见过守卫……紫色头发的女孩吗？";
			next;
			switch(select("不，我没有：是的，我可能有……")) {
			case 1:
				mes "[洛基]";
				mes "所以你还没有。";
				mes "你说的是实话。";
				next;
				break;
			case 2:
				mes "[洛基]";
				mes "等待。";
				mes "（洛基走近了。）";
				mes "... 不。";
				mes "你说的是实话。";
				next;
				break;
			}
			mes "[洛基]";
			mes "现在我们必须在这里说再见了。";
			mes "希望我们能在莫拉再次见面。";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		}
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛基]";
		mes "我不想和你一起去。";
		mes "希望我们能在莫拉再次见面。";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "于是我们又见面了。";
		mes "你习惯这个地方吗？";
		mes "来到莫拉的旅馆。";
		mes "我有东西要给你。";
		close;
	} else {
		mes "[洛基]";
		mes "这些天我们见面很多。";
		mes "看起来你对这个地方非常满意。";
		mes "你最好不要太为自己骄傲。";
		close;
	}
	end;
OnEnable:
	enablenpc instance_npcname("Mysterious Young Man#mis");
	end;
OnDisable:
	disablenpc instance_npcname("Mysterious Young Man#mis");
	end;
}

1@mist,183,304,3	script	Loki#mist	512,{
	if (ep14_1_mistwoods < 2) {
		mes "[洛基]";
		mes "森林里的冒险家。";
		mes ".....";
		mes "穿过这片森林就可以去莫拉。";
		close;
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛基]";
		mes "是你照顾这个不守规矩的女孩吗？";
		mes "我想我必须感谢你。";
		mes "你已经让她平静下来了。";
		next;
		select("这是怎么回事？：这个女孩……？");
		mes "[洛基]";
		mes "你也许可以改变你的外表，但你无法改变你独特的、与生俱来的光芒。";
		mes "一股浓烈的气味足以灼伤你的鼻子。";
		next;
		mes "[洛基]";
		mes "这是尼德霍格，世界树的守护者，";
		mes "我在找谁。";
		mes "现在她不会再疯狂地到处乱逛了。";
		next;
		select("你现在要做什么？");
		mes "[洛基]";
		mes "我会等她醒来，离开这片森林。";
		mes "我可以自己处理。";
		mes "我们在莫拉见。";
		set ep14_1_mistwoods,3;
		erasequest 7212;
		setquest 7213;
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "我不想和你一起去。";
		mes "我一个人就能管理她。";
		mes "如果你有什么话要说，等我们走出森林后再说。";
		close;
	} else {
		mes "[洛基]";
		mes "我不想和你一起去。";
		mes "如果你有什么话要说，等我们走出森林后再说。";
		close;
	}
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Loki#mist");
	end;
OnEnable:
	enablenpc instance_npcname("Loki#mist");
	end;
}

1@mist,181,303,3	script	Collapsed Girl#mist	511,{
	mes "你看到一个有着神秘头发的女孩倒在地上。";
	mes "当你走近她时，你闻到了浓烈的花香，让你头晕目眩。";
	close;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Collapsed Girl#mist");
	end;
OnEnable:
	enablenpc instance_npcname("Collapsed Girl#mist");
	end;
}

// --------------- Part of md_mistwoods_enter ----------------
mora,46,152,5	script	Flower Smelling Lady	510,{
	if (ep14_1_mistwoods < 3) {
		mes "[花香女士]";
		mes "我需要休息。";
		mes "我太累了，不想和你说话。";
		mes "对不起。";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[守护者尼多格]";
		mes "我一直在等你。";
		mes "我仍然很困惑，但我知道我做错了什么。";
		next;
		mes "[守护者尼多格]";
		mes "如果没有你，我可能还在黑暗的深渊里徘徊。";
		mes "我从心底里感谢你。";
		next;
		mes "[守护者尼多格]";
		mes "Error 500 (Server Error)!!1500.That’s an error.There was an error. Please try again later.That’s all we know.";
		mes "....";
		next;
		mes "[守护者尼多格]";
		mes "我不配再当监护人了。";
		mes "发疯的龙不再是守护者了。";
		next;
		mes "[洛基]";
		mes "Yggdrasil 不这么认为。";
		mes "尼迪，不要忘记你的职责。";
		next;
		mes "[守护者尼多格]";
		mes "你是...？";
		mes "我懂了...";
		mes "我以为我被抛弃了...";
		next;
		mes "守护者缓缓移开视线，仿佛陷入了沉思。";
		set ep14_1_mistwoods,4;
		close2;
	} else if (ep14_1_mistwoods == 4) {
		mes "――他似乎陷入了沉思。 -";
		close;
	} else {
		mes "[守护者尼多格]";
		mes "我还没有恢复所有的力量。";
		mes "很抱歉我无法保护你。";
		next;
		mes "[守护者尼多格]";
		mes "洛基说……";
		mes "我不会再因为孤独而哭泣";
		mes "我将永远有像你这样的新朋友......";
		next;
		mes "[守护者尼多格]";
		mes "……我失去了一切，但我现在很幸福。";
		close;
	}
}

mora,48,152,3	script	Sharp Eyed Man	512,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- 你的物品太多，无法完成此任务。 -";
		close;
	}
	if (ep14_1_mistwoods < 3) {
		mes "他不会看你。就好像他根本不感兴趣一样。";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "所以你已经穿过了森林。";
		mes "尼迪似乎有话要对你说。";
		close;
	} else if (ep14_1_mistwoods == 4) {
		mes "[洛基]";
		mes "尼多格是世界树的守护者之一。";
		mes "但当他屈服于莫罗克的诱惑时，他放弃了监护人的职责。";
		next;
		mes "[洛基]";
		mes "一些傻瓜举行了仪式来打破摩洛哥的封印。";
		mes "但仪式并没有发挥作用。摩洛哥的仆人未能打破封印。";
		next;
		mes "[洛基]";
		mes "但封印如此脆弱，随时都有可能破开。";
		mes "正是世界之树尤格德拉西尔的意志才阻止了封印的破裂。";
		next;
		mes "[守护者尼多格]";
		mes "这是正确的。世界树的力量，与整个世界相连。";
		mes "当守护者和世界树之间的联系破裂时，世界树变得不稳定。";
		next;
		mes "[守护者尼多格]";
		mes "这就是为什么所有这些灾难发生在世界树上。";
		mes "随着世界树变得不稳定，它对封印的影响也越来越弱。";
		next;
		mes "[洛基]";
		mes "摩洛哥也挣脱了封印。";
		mes "虽然看起来他用了大部分力量只是为了打破封印……";
		next;
		mes "[守护者尼多格]";
		mes "都是我的错。";
		mes "我无法摆脱摩洛哥的诱惑。";
		mes "太……太久了……";
		next;
		mes "[守护者尼多格]";
		mes "……我想摆脱几千年来的孤独。";
		mes "那时我神志不清。";
		mes "断绝与世界树的羁绊的不是别人，正是我。";
		next;
		mes "[守护者尼多格]";
		mes "我失去了对自己的控制，做了不该做的事。";
		mes "我的另一面变成了阴影和鲁莽的暴君。";
		next;
		mes "[守护者尼多格]";
		mes "面对这个世界，我发现...";
		mes "我的失落感太大了……比我承受了几千年的孤独还要痛苦得多。";
		next;
		mes "[守护者尼多格]";
		mes "就这样我就被留下了。";
		mes "我的失落感太大了……比我承受了几千年的孤独还要痛苦得多。";
		next;
		mes "[守护者尼多格]";
		mes "于是我成了一个罪人，被世界遗忘了。";
		next;
		mes "[洛基]";
		mes "还不算太晚。";
		mes "你仍然是一个监护人。";
		mes "你还有事情要做。";
		next;
		mes "[守护者尼多格]";
		mes "...哦是的。虽然很晚了，但我还是要尽到监护人的职责。";
		mes "现在我知道了。我会亲手毁灭摩洛哥。";
		mes "我会履行作为监护人的职责。";
		next;
		switch(select("你并不孤单：这些都是非常重要的职责。")) {
		case 1:
			mes "[守护者尼多格]";
			mes "你说得对。";
			mes "这可能就是我没有被抛弃的原因――意识到我并不孤单。";
			mes "你是一个善良的灵魂。";
			next;
			break;
		case 2:
			mes "[守护者尼多格]";
			mes "但这是我的赎罪。";
			mes "不过我很高兴。";
			mes "我不再需要与孤独作斗争。";
			next;
			break;
		}
		mes "[守护者尼多格]";
		mes strcharinfo(0)+"...";
		mes "谢谢你听我的长篇故事。";
		next;
		mes "[守护者尼多格]";
		mes "这是我一直随身携带的东西。";
		mes "我相信有一天它会保护你。";
		next;
		mes "[洛基]";
		mes "（摘下脖子上的围巾，递给你。）";
		next;
		mes "[守护者尼多格]";
		mes "愿尤格德拉希尔母亲的祝福与你同在……";
		set ep14_1_mistwoods,10;
		completequest 7213;
		getitem 2568,1; //Muffler_Of_Roki
		getitem 2858,1; //Pendant_Of_Guardian
		getexp 400000,400000;
		close;
	} else {
		mes "[洛基]";
		mes "我会在这里呆一段时间。";
		mes "我们可能会再次见面。";
		next;
		mes "[洛基]";
		mes "摩洛哥露出卑鄙本性的那一天……";
		mes "我会等他。";
		close;
	}
}

// Monster Spawns :: md_mist_mob
//============================================================
1@mist,1,1,0	script	#Mist Mobs Respawn1	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Miming",2137,15,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Miming",2137,.@num_mobs,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
}

1@mist,1,2,0	script	#Mist Mobs Respawn2	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Pom Spider",2132,35,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Pom Spider",2132,.@num_mobs,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
}

1@mist,1,3,0	script	#Mist Mobs Respawn3	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Angra Mantis",2133,35,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Angra Mantis",2133,.@num_mobs,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
}

1@mist,1,4,0	script	#Mist Mobs Respawn4	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Parus",2134,35,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Parus",2134,.@num_mobs,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
}

1@mist,1,5,0	script	#Mist Mobs Respawn5	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Little Fatum",2136,15,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Little Fatum",2136,.@num_mobs,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
}

1@mist,1,6,0	script	#Fragments of Memory	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Fragments of Memory");
	monster instance_mapname("1@mist"),0,0,"Guardian's Fragments of Memory",2138,15;
	end;
}

// Warp Portals :: md_mist_in_link
//============================================================
bif_fild01,38,374,0	script	Mysterious Flower#ep14_1	844,{
	mes "这是一朵巨大的花朵，蕴含着神秘的能量。";
	if (countitem(12561)) {
		mes "它会对你随身携带的神秘花种子做出反应。";
		next;
		switch(select("观察反应：停止反应。")) {
		case 1:
			mes "花朵开始剧烈摇晃";
			mes "然后向前摆动，好像要把种子送到尽可能远的地方！";
			close2;
			delitem 12561,1; //Mysterious_Seed
			warp "bif_fild02",160,230;
			end;
		case 2:
			mes "当你紧紧握住神秘花种时，";
			mes "花朵猛烈地摇晃了一下，然后又恢复了平静。";
			close;
		}
	}
	mes "看起来它有一些特殊的用途，但你不确定是什么。";
	close;
}

1@mist,104,23,0	warp	#to_bif01	1,1,bif_fild01,160,352

1@mist,109,70,0	script	a1_a2	45,1,1,{
	end;
OnTouch_:
	if (checkquest(7211,PLAYTIME) == -1)
		setquest 7211;
	warp instance_mapname("1@mist"),116,40;
	end;
}

1@mist,113,40,0	warp	a2_a7	1,1,1@mist,90,81
1@mist,93,81,0	warp	a7_a14	1,1,1@mist,108,152
1@mist,111,154,0	warp	a14_a24	1,1,1@mist,236,108
1@mist,236,110,0	warp	a24_a1	1,1,1@mist,106,69
1@mist,161,67,0	warp	a18-1_a22	1,1,1@mist,159,85
1@mist,180,116,0	warp	a23_a18-1	1,1,1@mist,164,66
1@mist,180,63,0	warp	a18-2_a9	1,1,1@mist,141,90
1@mist,116,84,0	warp	a8_a26	1,1,1@mist,222,133
1@mist,220,133,0	warp	a26_a27-1	1,1,1@mist,200,64
1@mist,197,64,0	warp	a27-1_a23	1,1,1@mist,177,116
1@mist,146,194,0	warp	af1-2_a27-2	1,1,1@mist,225,96
1@mist,133,195,0	warp	af1-1_af2-1	1,1,1@mist,202,110
1@mist,202,107,0	warp	af2-1_af1-1	1,1,1@mist,134,189
1@mist,217,110,0	warp	af2-2_a20-1	1,1,1@mist,240,56
1@mist,240,54,0	warp	a20-1_af2-2	1,1,1@mist,214,110
1@mist,234,67,0	warp	a20-2_a21	1,1,1@mist,239,80
1@mist,242,77,0	warp	a21_a15-1	1,1,1@mist,153,137
1@mist,156,139,0	warp	a15-1_a3	1,1,1@mist,123,55
1@mist,124,51,0	warp	a3_a20-1	1,1,1@mist,240,56
1@mist,48,41,0	warp	a4-1_a17	1,1,1@mist,174,165
1@mist,171,165,0	warp	a17_a16	1,1,1@mist,168,143
1@mist,169,141,0	warp	a16_a10	1,1,1@mist,101,107
1@mist,99,107,0	warp	a10_a4-2	1,1,1@mist,57,38
1@mist,65,113,0	warp	a11_a28	1,1,1@mist,218,151
1@mist,221,151,0	warp	a28_a13	1,1,1@mist,117,126
1@mist,114,126,0	warp	a13_a5	1,1,1@mist,36,62
1@mist,61,88,0	warp	a6_a19	1,1,1@mist,189,139
1@mist,186,140,0	warp	a19_a12	1,1,1@mist,98,126
1@mist,101,126,0	warp	a12_a29-1	1,1,1@mist,238,151
1@mist,235,152,0	warp	a29-1_af1-2	1,1,1@mist,144,194
1@mist,254,151,0	warp	a29-2E_b1	1,1,1@mist,271,164
1@mist,275,211,0	warp	b1_b13	1,1,1@mist,277,112
1@mist,301,141,0	warp	b14_b24	1,1,1@mist,99,232
1@mist,99,230,0	warp	b24_b25	1,1,1@mist,103,255
1@mist,102,253,0	warp	b25_b4-1	1,1,1@mist,215,177
1@mist,212,177,0	warp	b4-1_b1	1,1,1@mist,276,208
1@mist,289,167,0	warp	b2_b19	1,1,1@mist,152,219
1@mist,149,219,0	warp	b19_b20	1,1,1@mist,119,227
1@mist,115,227,0	warp	b20_b23	1,1,1@mist,230,353
1@mist,233,353,0	warp	b23_b7	1,1,1@mist,289,225
1@mist,76,273,0	warp	bf1-2_b7	1,1,1@mist,289,225
1@mist,61,282,0	warp	bf1-1_bf2-1	1,1,1@mist,256,220
1@mist,252,220,0	warp	bf2-1_bf1-1	1,1,1@mist,65,282
1@mist,267,213,0	warp	bf2-2_b28	1,1,1@mist,214,268
1@mist,212,265,0	warp	b28_b12	1,1,1@mist,288,266
1@mist,285,266,0	warp	b12_b21	1,1,1@mist,143,234
1@mist,143,231,0	warp	b21_b29	1,1,1@mist,222,281
1@mist,223,279,0	warp	b29_b31	1,1,1@mist,158,317
1@mist,134,287,0	warp	b30_b3	1,1,1@mist,293,195
1@mist,295,197,0	warp	b3_b10	1,1,1@mist,239,253
1@mist,236,253,0	warp	b10_b12	1,1,1@mist,288,266
1@mist,111,203,0	warp	b16_b34	1,1,1@mist,202,333
1@mist,199,333,0	warp	b34_b27-1	1,1,1@mist,203,251
1@mist,206,249,0	warp	b27-1_b22	1,1,1@mist,168,239
1@mist,165,239,0	warp	b22_b15	1,1,1@mist,89,173
1@mist,177,265,0	warp	b27-2_a14	1,1,1@mist,108,152
1@mist,166,314,0	warp	bN-1_b27-3	1,1,1@mist,206,297
1@mist,197,299,0	warp	bN-2_b17	1,1,1@mist,167,200
1@mist,165,200,0	warp	b17_bN-2	1,1,1@mist,193,299
1@mist,86,173,0	warp	b15_b32	1,1,1@mist,218,313
1@mist,215,313,0	warp	b32_b28	1,1,1@mist,214,268
1@mist,230,324,0	warp	b33_b6-1	1,1,1@mist,221,233
1@mist,230,230,0	warp	b6-2_b11	1,1,1@mist,263,261
1@mist,263,258,0	warp	b11_b9	1,1,1@mist,272,246
1@mist,269,246,0	warp	b9_b26	1,1,1@mist,143,265
1@mist,140,265,0	warp	b26_b5	1,1,1@mist,209,200
1@mist,195,206,0	warp	b18_b35	1,1,1@mist,169,341
1@mist,166,341,0	warp	b35_b36	1,1,1@mist,198,354
1@mist,201,354,0	warp	b36_b37-1	1,1,1@mist,83,224
1@mist,87,224,0	warp	b37-1_b35	1,1,1@mist,169,341
1@mist,84,273,0	warp	b37-2_c1	1,1,1@mist,98,287
1@mist,111,298,0	warp	c2_c3	1,1,1@mist,82,307
1@mist,79,307,0	warp	c3_c7	1,1,1@mist,284,89
1@mist,281,89,0	warp	c7_c12	1,1,1@mist,264,291
1@mist,261,291,0	warp	c12_c2	1,1,1@mist,115,298
1@mist,326,275,0	warp	c13-1_b13	1,1,1@mist,277,112
1@mist,294,293,0	warp	c13-2_c5	1,1,1@mist,78,330
1@mist,81,330,0	warp	c5_c17	1,1,1@mist,308,330
1@mist,308,327,0	warp	c17_c10	1,1,1@mist,309,165
1@mist,306,165,0	warp	c10_c13-2	1,1,1@mist,298,293
1@mist,46,316,0	warp	c4_c6	1,1,1@mist,113,328
1@mist,95,328,0	warp	c6_c9	1,1,1@mist,342,157
1@mist,339,157,0	warp	c9_c16	1,1,1@mist,343,328
1@mist,343,325,0	warp	c16_c8-2	1,1,1@mist,346,120
1@mist,344,123,0	warp	c8-2_c19	1,1,1@mist,277,343
1@mist,256,313,0	warp	c18_c15	1,1,1@mist,343,307
1@mist,342,305,0	warp	c15_cKEY	1,1,1@mist,332,121
1@mist,335,120,0	warp	cKEY_c18	1,1,1@mist,259,313
1@mist,317,89,0	warp	c8-1_c14	1,1,1@mist,339,284
1@mist,339,281,0	warp	c14_c11-1	1,1,1@mist,332,172
1@mist,329,171,0	warp	c11-1_c8-2	1,1,1@mist,346,120
1@mist,301,237,0	warp	b8_b2	1,1,1@mist,289,170

// Disable Garden Tree warps.
// Custom, but saves processing and lines.
1@mist,1,1,0	script	#mist_warp_init	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("a25_a18-1");
	disablenpc instance_npcname("a27-2_af1-2");
	disablenpc instance_npcname("a15-2_a4-1");
	disablenpc instance_npcname("a4-2_a11");
	disablenpc instance_npcname("b4-2_b2");
	disablenpc instance_npcname("b7_bf1-2");
	disablenpc instance_npcname("b31_b16");
	disablenpc instance_npcname("b27-3_bN-1");
	disablenpc instance_npcname("b6-1_b33");
	disablenpc instance_npcname("b5_b14");
	disablenpc instance_npcname("c1_c13-1");
	disablenpc instance_npcname("c13-3_c4");
	disablenpc instance_npcname("c19_c4");
	disablenpc instance_npcname("#to_bif02");
	disablenpc instance_npcname("#mist_warp_init");
	end;
}
1@mist,247,123,0	warp	a25_a18-1	1,1,1@mist,164,66	//Tom
1@mist,225,98,0	warp	a27-2_af1-2	1,1,1@mist,144,194	//Tomba
1@mist,159,184,0	warp	a15-2_a4-1	1,1,1@mist,49,37	//Remi
1@mist,61,39,0	warp	a4-2_a11	1,1,1@mist,68,113	//Rem
1@mist,230,179,0	warp	b4-2_b2	1,1,1@mist,289,170	//Ron
1@mist,285,225,0	warp	b7_bf1-2	1,1,1@mist,72,274	//Rover
1@mist,161,316,0	warp	b31_b16	1,1,1@mist,108,203	//Mona
1@mist,204,299,0	warp	b27-3_bN-1	1,1,1@mist,169,314	//Namon
1@mist,221,236,0	warp	b6-1_b33	1,1,1@mist,233,324	//Neoron
1@mist,206,200,0	warp	b5_b14	1,1,1@mist,298,142	//Spyder
1@mist,95,287,0	warp	c1_c13-1	1,1,1@mist,323,276	//Tito
1@mist,324,325,0	warp	c13-3_c4	1,1,1@mist,49,316	//Pumba
1@mist,280,344,0	warp	c19_c4	1,1,1@mist,49,316	//Tete
1@mist,345,186,0	warp	#to_bif02	1,1,bif_fild02,151,121	//The Gardeners' Tree
