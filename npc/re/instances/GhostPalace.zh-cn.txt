//===== rAthena Script =======================================
//= Ghost Palace
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Ghost Palace Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//= 1.1 Fixed NPC names. [zackdreaver]
//===== Translated By: =======================================
//= dsc
//============================================================

dali02,43,129,5	script	Unpleasant Royal Guard#	4_M_SAKRAYROYAL,{
	if (BaseLevel < 120) {
		mes "[令人不快的皇家守卫]";
		mes "虽然我有任务需要帮忙，但是你的实力还不够，菜鸟~！";
		next;
		mes "[令人不快的皇家守卫]";
		mes "等你完成一些训练并至少达到 120 级后再回来！";
		close;
	}
	.@party_id = getcharid(1);
	if (is_party_leader() == false || .@party_id < 1) {
		mes "[令人不快的皇家守卫]";
		mes "我只能为党的领导人打开秘密通道。请让你们的党魁跟我谈谈。";
		close;
	}
	switch( checkquest(1261,PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		mes "[令人不快的皇家守卫]";
		mes "找到公主了吗？！她安全吗？";
		close;
	case 2:
		mes "[令人不快的皇家守卫]";
		mes "密道周围的怪物已经消失了。现在是我们拯救公主的机会！";
		erasequest 1261;// Cursed Swordman
		close;
	}
	mes "[令人不快的皇家守卫]";
	mes "我需要帮助，即使是像你这样的人。";
	next;
	mes "[令人不快的皇家守卫]";
	mes "在战斗中，当我守卫宫殿时，公主消失了。";
	next;
	mes "[令人不快的皇家守卫]";
	mes "你跑在前面，我会追上的。我们必须救她。";
	next;
	if (select( "我会帮忙的。", "现在不是一个好时机。" ) == 2) {
		mes "[令人不快的皇家守卫]";
		mes "现在所有的皇家卫队都在与怪物战斗并死去。";
		next;
		mes "[令人不快的皇家守卫]";
		mes "无论心中有多么恐惧，我们都必须拯救公主！";
		close;
	}
	instance_create("Ghost Palace");
	mes "[令人不快的皇家守卫]";
	mes "谢谢..";
	mes "我将打开通往^0000ff鬼宫^000000的秘密通道。";
	mes "请做好准备！";
	close;
}

dali02,40,134,5	script	Interdimensional Device#dk	PORTAL,{
	if (BaseLevel < 120) {
		mes "[令人不快的皇家守卫]";
		mes "虽然我有任务需要帮忙，但是你的实力还不够，菜鸟~！";
		next;
		mes "[令人不快的皇家守卫]";
		mes "等你完成一些训练并至少达到 120 级后再回来！";
		close;
	}
	mes "[令人不快的皇家守卫]";
	mes "我可以看到进入宫殿的秘密通道。你准备好拯救公主了吗？！";
	next;
	if (select( "输入", "停止" ) == 2) {
		mes "[令人不快的皇家守卫]";
		mes "现在所有的皇家卫队都在与怪物战斗并死去。";
		next;
		mes "[令人不快的皇家守卫]";
		mes "无论心中有多么恐惧，我们都必须拯救公主！";
		close;
	}
	switch( checkquest(1261,PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		mes "[令人不快的皇家守卫]";
		mes "袭击宫殿的怪物正在秘密入口附近徘徊。如果他们发现我们的话那就太糟糕了……请稍等。";
		next;
		mes "入场时间已到时";
		mes "让我们尝试重新输入。";
		close;
	case 2:
		mes "^0000ff进入剑客诅咒的时间限制已经结束。请再次尝试与令人不快的皇家卫队交谈..^000000";
		erasequest 1261;// Cursed Swordman
		close;
	}
	.@party_id = getcharid(1);
	if (is_party_leader() == false || .@party_id < 1) {
		mes "[令人不快的皇家守卫]";
		mes "我只能为党的领导人打开秘密通道。请让你们的党魁跟我谈谈。";
		close;
	}

	.@md_name$ = "Ghost Palace";
	switch( instance_enter(.@md_name$) ) {
	case IE_OTHER:
		mes "发生未知错误。";
		close;
	case IE_NOINSTANCE:
		mes "^ff0000在设备内部你会看到一个黑洞。如果你想进入，你必须先与令人讨厌的皇家卫兵交谈。^000000";
		close;
	case IE_NOMEMBER:
		mes "只有注册会员才能进入" + .@md_name$ + "实例。";
		close;
	case IE_OK:
		mapannounce "dali", "" + strcharinfo(0) + " of the party, " + getpartyname(.@party_id) + ", is entering the Ghost Palace.", bc_map, 0x00ff99;
		// warp "1@spa",42,196;
		setquest 1261;// Cursed Swordman
		end;
	}
}

// 1st floor
1@spa,41,204,5	script	King#dk	4_M_RUSKING,{
	mes "[国王]";
	mes "您特此被任命为蒂亚拉公主的私人侍卫。我相信你的忠诚高于一切。";
	npctalk "国王：特此任命您为蒂亚拉公主的私人侍卫。我相信你的忠诚高于一切。";
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "我会用我的生命来保护公主。";
	npctalk "鲁瑞德皇家卫队：我会用我的生命来保护公主。", instance_npcname("鲁瑞德皇家卫队#dk");
	next;
	mes "[国王]";
	mes "婚姻安排正在按计划进行。王子来这里是要见公主的。";
	npctalk "King：婚姻安排正在按计划进行。王子来这里是要见公主的。";
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "……是的，陛下。";
	npctalk "鲁瑞德皇家卫队：……是的，陛下。", instance_npcname("鲁瑞德皇家卫队#dk");
	next;
	mes "[国王]";
	mes "天哪，城堡里有怪物！现在就摆脱他们吧！";
	npctalk "国王：天哪，城堡里有怪物！现在就摆脱他们吧！";
	close2;
	donpcevent instance_npcname("#gp_control_1") + "::OnStart";
	end;

OnInstanceInit:
	// 1: enabled potential double spawn (official), 0: disabled
	'bool_double_spawn = 1;

	'map_spa$ = instance_mapname("1@spa");

	// warps
	disablenpc instance_npcname("The second floor of Palace");
	disablenpc instance_npcname("3rd floor in the palace");
	disablenpc instance_npcname("4th floor in the palace");
	disablenpc instance_npcname("5th floor of Palace");
	disablenpc instance_npcname("Way out of Palace");

	// first floor
	disablenpc instance_npcname("#gp_control_1");

	// second floor
	disablenpc instance_npcname("Lurid Royal Guard#dk1");
	disablenpc instance_npcname("Princess Tiara#dk");
	disablenpc instance_npcname("#gp_control_2");

	// 3rd floor
	disablenpc instance_npcname("King#dk1");
	disablenpc instance_npcname("Lurid Royal Guard#dk2");
	disablenpc instance_npcname("Captain of the Guard#dk");
	disablenpc instance_npcname("Soldier#dk1");
	disablenpc instance_npcname("Soldier#dk2");
	disablenpc instance_npcname("Soldier#dk3");
	disablenpc instance_npcname("Lurid Royal Guard#dk3");
	disablenpc instance_npcname("Magic Sword Tartanos#dk1");
	disablenpc instance_npcname("#gp_control_3");

	// 4th floor
	disablenpc instance_npcname("#gp_control_4");
	disablenpc instance_npcname("#gp_control_5");
	disablenpc instance_npcname("Magic Sword Tartanos#dk2");
	disablenpc instance_npcname("Visiting Prince#dk");
	disablenpc instance_npcname("Lurid Royal Guard#dk4");
	disablenpc instance_npcname("King's Corpse#dk");
	disablenpc instance_npcname("Captain's Corpse#dk");
	disablenpc instance_npcname("Soldier's Corpse#dk");
	disablenpc instance_npcname("Soldier's Corpse#dk1");
	disablenpc instance_npcname("Soldier's Corpse#dk2");
	disablenpc instance_npcname("Soldier's Corpse#dk3");
	disablenpc instance_npcname("Soldier's Corpse#dk4");

	// 5th floor
	disablenpc instance_npcname("#gp_control_6");
	disablenpc instance_npcname("Princess Tiara#dk1");
	disablenpc instance_npcname("Lurid Royal Guard#dk5");
	disablenpc instance_npcname("King#dk11");
	disablenpc instance_npcname("Sakray#dk");
	disablenpc instance_npcname("Magic Sword Tartanos#dk3");
	disablenpc instance_npcname("#tb");
	disablenpc instance_npcname("#sv");
	disablenpc instance_npcname("#tv");
	end;
}

1@spa,42,201,1	script	Lurid Royal Guard#dk	4_M_SAKRAYROYAL,{ end; }

1@spa,1,1,1	script	#gp_control_1	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#gp_control_1");
	disablenpc instance_npcname("King#dk");
	disablenpc instance_npcname("Lurid Royal Guard#dk");
	initnpctimer;
	end;
OnTimer2000:
	.@label$ = instance_npcname("#gp_control_1") + "::OnMyMobDead1";
	monster 'map_spa$,50,194,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
	monster 'map_spa$,34,208,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
	monster 'map_spa$,32,194,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	monster 'map_spa$,52,209,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	stopnpctimer;
	end;
OnMyMobDead1:
	if (mobcount('map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead1") < (1 + 'bool_double_spawn)) {
		deltimer instance_npcname("#gp_control_1") + "::OnSpawn1";	// stop double spawn if timer is running
		addtimer 2000, instance_npcname("#gp_control_1") + "::OnSpawn1";
	}
	end;
OnSpawn1:
	.@label$ = instance_npcname("#gp_control_1") + "::OnMyMobDead2";
	monster 'map_spa$,37,182,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
	monster 'map_spa$,46,182,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	end;
OnMyMobDead2:
	if (mobcount('map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead2") < 1) {
		deltimer instance_npcname("#gp_control_1") + "::OnSpawn1";	// stop double spawn if timer is running
		deltimer instance_npcname("#gp_control_1") + "::OnSpawn2";
		addtimer 2000, instance_npcname("#gp_control_1") + "::OnSpawn2";
	}
	end;
OnSpawn2:
	.@label$ = instance_npcname("#gp_control_1") + "::OnMyMobDead3";
	monster 'map_spa$,30,200,"Cursed Soldier",2948,1, .@label$;		// CURSED_SOLDIER
	monster 'map_spa$,51,200,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	monster 'map_spa$,53,199,"Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	end;
OnMyMobDead3:
	if (mobcount('map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead3") < 1) {
		mapannounce 'map_spa$, "The passage on the 2nd floor in the palace of ghost is open .", bc_map,0xFFFF00;
		enablenpc instance_npcname("The second floor of Palace");
		enablenpc instance_npcname("Lurid Royal Guard#dk1");
		enablenpc instance_npcname("Princess Tiara#dk");
	}
	end;
}

// 2nd floor
1@spa,132,122,7	script	Lurid Royal Guard#dk1	4_M_SAKRAYROYAL,{
	.@tiara$ = instance_npcname("Princess Tiara#dk");
	cutin "npc-tiara.bmp",3;
	mes "[鲁瑞德皇家卫队]";
	mes "殿下，国王已邀请一位邻国的王子作为您的未婚妻。准备好去见他吧……";
	npctalk "鲁里德皇家卫队：殿下，国王已邀请一位邻国的王子作为您的未婚妻。准备好去见他吧……";
	next;
	mes "[皇冠公主]";
	mes "如果……如果包办婚姻我该怎么办？";
	npctalk "蒂亚拉公主：如果……如果婚姻被安排了，我该怎么办？", .@tiara$;
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "我唯一能做的就是保护你免受身体伤害。";
	npctalk "鲁瑞德皇家卫队：我唯一能做的就是保护你免受身体伤害。";
	next;
	mes "[皇冠公主]";
	mes "那么，我能做什么呢？";
	npctalk "蒂亚拉公主：那我能做什么呢？", .@tiara$;
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "殿下，您必须自己决定……";
	npctalk "鲁瑞德皇家卫队：殿下，您必须自己决定……";
	next;
	mes "[皇冠公主]";
	mes "如果我结婚并被送到另一个国家，对你来说会更容易吗？";
	npctalk "蒂亚拉公主：如果我结婚并被送到另一个国家，对你来说会更容易吗？", .@tiara$;
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "我会永远站在你身边，永远保护你。";
	npctalk "鲁瑞德皇家卫队：我会永远站在你身边，永远保护你。";
	cutin "npc-tiara.bmp",255;
	next;
	mes "[皇冠公主]";
	mes "城堡里进了怪物？请保护我！";
	npctalk "蒂亚拉公主：怪物已经进入城堡了？请保护我！", .@tiara$;
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "留在我身后。我们走吧！";
	npctalk "鲁瑞德皇家卫队：留在我身后。我们走吧！";
	close2;
	donpcevent instance_npcname("#gp_control_2") + "::OnStart";
	end;
}

1@spa,135,125,3	duplicate(Lurid Royal Guard#dk)	Princess Tiara#dk	4_F_MAYSEL

1@spa,1,1,1	script	#gp_control_2	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#gp_control_2");
	disablenpc instance_npcname("Lurid Royal Guard#dk1");
	disablenpc instance_npcname("Princess Tiara#dk");
	disablenpc instance_npcname("The second floor of Palace");

	killmonster 'map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead1";
	killmonster 'map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead2";
	killmonster 'map_spa$, instance_npcname("#gp_control_1") + "::OnMyMobDead3";
	disablenpc instance_npcname("#gp_control_1");
	initnpctimer;
	end;
OnTimer2000:
	.@label$ = instance_npcname("#gp_control_2") + "::OnMyMobDead1";
	monster 'map_spa$,135,116,"Broken Promise",2950,1, .@label$;	// BROKEN_MIND
	monster 'map_spa$,110,120,"Broken Promise",2950,1, .@label$;	// BROKEN_MIND
	monster 'map_spa$,122,112,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
	monster 'map_spa$,121,129,"Unfulfilled Love",2952,1, .@label$;	// LIKE_LOVE
	stopnpctimer;
	end;
OnMyMobDead1:
	if (mobcount('map_spa$, instance_npcname("#gp_control_2") + "::OnMyMobDead1") < 1) {
		sleep 2000;
		.@label$ = instance_npcname("#gp_control_2") + "::OnMyMobDead2";
		monster 'map_spa$,120,103,"Broken Promise",2950,1, .@label$;	// BROKEN_MIND
		monster 'map_spa$,115,137,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
		monster 'map_spa$,103,130,"Unfulfilled Love",2952,1, .@label$;	// LIKE_LOVE
	}
	end;
OnMyMobDead2:
	if (mobcount('map_spa$, instance_npcname("#gp_control_2") + "::OnMyMobDead2") < 1) {
		sleep 2000;
		.@label$ = instance_npcname("#gp_control_2") + "::OnMyMobDead3";
		monster 'map_spa$,98,119,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
		monster 'map_spa$,98,120,"Floating Sorrow",2951,1, .@label$;	// FLOATING_WORD
		monster 'map_spa$,103,109,"Unfulfilled Love",2952,1, .@label$;	// LIKE_LOVE
	}
	end;
OnMyMobDead3:
	if (mobcount('map_spa$, instance_npcname("#gp_control_2") + "::OnMyMobDead3") < 1) {
		mapannounce 'map_spa$, "The passage on the 3rd floor of the palace is open.", bc_map,0xFFFF00;
		enablenpc instance_npcname("3rd floor in the palace");
		enablenpc instance_npcname("King#dk1");
		enablenpc instance_npcname("Lurid Royal Guard#dk2");
		enablenpc instance_npcname("Captain of the Guard#dk");
		enablenpc instance_npcname("Soldier#dk1");
		enablenpc instance_npcname("Soldier#dk2");
		enablenpc instance_npcname("Soldier#dk3");
		disablenpc instance_npcname("#gp_control_2");
	}
	end;
}

// 3rd floor
1@spa,32,54,1	script	King#dk1	4_M_RUSKING,{
	mes "[国王]";
	mes "是你把怪物带到这里来的！你想除掉我，独占公主吗？";
	npctalk "King：是你把怪物带到这里来的！你想除掉我，独占公主吗？";
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "这是陛下的陷阱！你一定要相信我！";
	npctalk "鲁瑞德皇家卫队：陛下，这是一个陷阱！你一定要相信我！", instance_npcname("鲁瑞德皇家卫队#dk2");
	next;
	mes "[国王]";
	mes "住口！你背叛了我！我相信你……你必须为你的耻辱付出代价。";
	npctalk "国王：闭嘴！你背叛了我！我相信你……你必须为你的耻辱付出代价。";
	next;
	mes "[国王]";
	mes "你将终生受苦。";
	npctalk "国王：你将终生受苦。";
	close2;
	disablenpc instance_npcname("3rd floor in the palace");
	disablenpc instance_npcname("King#dk1");
	disablenpc instance_npcname("Captain of the Guard#dk");
	disablenpc instance_npcname("Soldier#dk1");
	disablenpc instance_npcname("Soldier#dk2");
	disablenpc instance_npcname("Soldier#dk3");
	disablenpc instance_npcname("Lurid Royal Guard#dk2");
	donpcevent instance_npcname("Magic Sword Tartanos#dk1") + "::OnStart";
	end;
}

1@spa,35,56,5	script	Magic Sword Tartanos#dk1	4_TRACE,{
	end;
OnStart:
	enablenpc instance_npcname("Magic Sword Tartanos#dk1");
	enablenpc instance_npcname("Lurid Royal Guard#dk3");
	initnpctimer;
	end;
OnTimer5000:
	npctalk "不祥的声音：哦，忠实的皇家卫兵，你被困住了。";
	end;
OnTimer8000:
	npctalk "不祥的声音：皇宫里的阴谋让你的公主陷入了严重的危险……";
	end;
OnTimer12000:
	npctalk "鲁瑞德皇家卫队：你是谁？展示你自己！", instance_npcname("鲁瑞德皇家卫队#dk3");
	end;
OnTimer15000:
	npctalk "不祥的声音：啊……你听到我说话了吗？";
	end;
OnTimer20000:
	npctalk "鲁瑞德皇家卫队：……？", instance_npcname("鲁瑞德皇家卫队#dk3");
	end;
OnTimer23000:
	npctalk "不祥的声音：我可以给你无限的力量。如果你和我有一个约定。";
	end;
OnTimer28000:
	npctalk "鲁瑞德皇家卫队：我……我……", instance_npcname("鲁瑞德皇家卫队#dk3");
	end;
OnTimer33000:
	npctalk "魔剑塔塔诺斯：库库库……孩子，你想要什么？";
	end;
OnTimer34000:
	stopnpctimer;
	disablenpc instance_npcname("Magic Sword Tartanos#dk1");
	disablenpc instance_npcname("Lurid Royal Guard#dk3");
	donpcevent instance_npcname("#gp_control_3") + "::OnStart";
	end;
}

1@spa,28,52,7	duplicate(Lurid Royal Guard#dk)	Captain of the Guard#dk	4_M_KY_KNT
1@spa,25,53,7	duplicate(Lurid Royal Guard#dk)	Soldier#dk1	4_M_CRU_SOLD
1@spa,34,53,1	duplicate(Lurid Royal Guard#dk)	Soldier#dk2	4_M_CRU_SOLD
1@spa,30,52,7	duplicate(Lurid Royal Guard#dk)	Soldier#dk3	4_M_KY_SOLD
1@spa,30,58,5	duplicate(Lurid Royal Guard#dk)	Lurid Royal Guard#dk2	4_M_SAKRAY_TIED
1@spa,31,57,5	duplicate(Lurid Royal Guard#dk)	Lurid Royal Guard#dk3	4_M_SAKRAY_TIED

1@spa,1,1,1	script	#gp_control_3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#gp_control_3");
	initnpctimer;
	end;
OnTimer2000:
	.@label$ = instance_npcname("#gp_control_3") + "::OnMyMobDead1";
	monster 'map_spa$,31,57, "Regretful Memory",2953,1, .@label$; 	// CURSED_MEMORY
	monster 'map_spa$,59,24, "Regretful Memory",2953,1, .@label$; 	// CURSED_MEMORY
	monster 'map_spa$,23,40, "Wasted Friendship",2955,1, .@label$; 	// OLD_FRIENDSHIP
	monster 'map_spa$,54,45, "Tarnished Oath",2954,1, .@label$; 	// COLORLESS_VOW
	monster 'map_spa$,26,20, "Tarnished Oath",2954,1, .@label$; 	// COLORLESS_VOW
	stopnpctimer;
	end;
OnMyMobDead1:
	if (mobcount('map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead1") < 1)
		addtimer 2000, instance_npcname("#gp_control_3") + "::OnSpawn1";
	end;
OnSpawn1:
	.@label$ = instance_npcname("#gp_control_3") + "::OnMyMobDead2";
	monster 'map_spa$,30,52, "Tarnished Oath",2954,1, .@label$; 	// COLORLESS_VOW
	monster 'map_spa$,58,58, "Wasted Friendship",2955,1, .@label$; 	// OLD_FRIENDSHIP
	monster 'map_spa$,54,24, "Regretful Memory",2953,1, .@label$; 	// CURSED_MEMORY
	end;
OnMyMobDead2:
	if (mobcount('map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead2") < (1 + 'bool_double_spawn)) {
		deltimer instance_npcname("#gp_control_3") + "::OnAnnounce";
		addtimer 500, instance_npcname("#gp_control_3") + "::OnAnnounce";
	}
	end;
OnAnnounce:
	mapannounce 'map_spa$, "The passage on the 4th floor of the palace is open.", bc_map,0xFFFF00;
	enablenpc instance_npcname("4th floor in the palace");
	deltimer instance_npcname("#gp_control_3") + "::OnSpawn2";	// stop double spawn if timer is running
	addtimer 2000, instance_npcname("#gp_control_3") + "::OnSpawn2";
	end;
OnSpawn2:
	donpcevent instance_npcname("#gp_control_4") + "::OnStart";
	// 35 mobs
	.@label$ = instance_npcname("#gp_control_3") + "::OnMyMobDead3";
	monster 'map_spa$,67,46, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "You are trapped...";
	monster 'map_spa$,62,48, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,57,46, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,51,48, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "Faithful royal guard...";
	monster 'map_spa$,56,55, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,62,56, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "You are trapped...";
	monster 'map_spa$,66,58, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "You put your princess in grave danger...";
	monster 'map_spa$,66,64, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Faithful royal guard...";
	monster 'map_spa$,62,65, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,57,67, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "You are trapped...";
	monster 'map_spa$,49,66, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,44,66, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,45,20, "Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,38,20, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "You are trapped...";
	monster 'map_spa$,32,20, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,51,22, "Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,26,34,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,21,37,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Faithful royal guard...";
	monster 'map_spa$,18,39,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,21,45,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,23,49,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "You are trapped...";
	monster 'map_spa$,25,54,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,29,54,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "You put your princess in grave danger...";
	monster 'map_spa$,56,39,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,61,36,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Faithful royal guard...";
	monster 'map_spa$,66,38,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away quickly!";
	monster 'map_spa$,42,54,"Wasted Friendship",2955,1, .@label$;	// OLD_FRIENDSHIP
	unittalk $@mobid[0], "Run away!";
	monster 'map_spa$,35,54,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "You are trapped...";
	monster 'map_spa$,43,60,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "Faithful royal guard...";
	monster 'map_spa$,30,25,"Tarnished Oath",2954,1, .@label$;		// COLORLESS_VOW
	unittalk $@mobid[0], "You put your princess in grave danger...";
	monster 'map_spa$,30,31,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	unittalk $@mobid[0], "You are trapped...";
	monster 'map_spa$,65,64, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	monster 'map_spa$,65,67, "Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	monster 'map_spa$,18,43,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	monster 'map_spa$,21,43,"Regretful Memory",2953,1, .@label$;	// CURSED_MEMORY
	end;
OnMyMobDead3:
	end;
}

// 4th floor
1@spa,1,1,1	script	#gp_control_4	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#gp_control_4");
	.@label$ = instance_npcname("#gp_control_4") + "::OnMyMobDead1";
	monster 'map_spa$,215,194, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
	monster 'map_spa$,194,178, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
	monster 'map_spa$,191,211, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
	monster 'map_spa$,185,203, "Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	monster 'map_spa$,209,218, "Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	end;
OnMyMobDead1:
	if (mobcount('map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead1") < (1 + 'bool_double_spawn)) {
		deltimer instance_npcname("#gp_control_4") + "::OnSpawn1";
		addtimer 2000, instance_npcname("#gp_control_4") + "::OnSpawn1";
	}
	end;
OnSpawn1:
	.@label$ = instance_npcname("#gp_control_4") + "::OnMyMobDead2";
	monster 'map_spa$,189,192, "Cursed Gatekeeper",2949,1, .@label$;	// CURSED_SENTINEL
	monster 'map_spa$,211,209, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
	end;
OnMyMobDead2:
	if (mobcount('map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead2") < 1) {
		enablenpc instance_npcname("Magic Sword Tartanos#dk2");
		enablenpc instance_npcname("Visiting Prince#dk");
		enablenpc instance_npcname("Lurid Royal Guard#dk4");
		enablenpc instance_npcname("King's Corpse#dk");
		enablenpc instance_npcname("Captain's Corpse#dk");
		enablenpc instance_npcname("Soldier's Corpse#dk");
		enablenpc instance_npcname("Soldier's Corpse#dk1");
		enablenpc instance_npcname("Soldier's Corpse#dk2");
		enablenpc instance_npcname("Soldier's Corpse#dk3");
		enablenpc instance_npcname("Soldier's Corpse#dk4");
	}
	end;
}

1@spa,199,214,1	script	Lurid Royal Guard#dk4	4_M_SAKRAYROYAL,{
	.@prince$ = instance_npcname("Visiting Prince#dk");
	mes "[鲁瑞德皇家卫队]";
	mes "蒂亚拉公主在哪里？";
	npctalk "华丽的皇家卫队：蒂亚拉公主在哪里？";
	next;
	mes "[来访王子]";
	mes "你来得太晚了……但你带来了我要找的东西。感谢您的帮助。";
	npctalk "来访的王子：你来得太晚了……但你带来了我要找的东西。感谢您的帮助。", .@prince$;
	next;
	mes "[来访王子]";
	mes "诅咒魔剑塔尔塔诺斯！你准备好迎接新主人了吗？";
	npctalk "来访的王子：诅咒魔剑塔塔诺斯！你准备好迎接新主人了吗？", .@prince$;
	next;
	cutin "tartanos.bmp",3;
	mes "[魔剑塔塔诺斯]";
	mes "你怎么敢！";
	npctalk "魔剑塔塔诺斯：你怎么敢！", instance_npcname("魔剑塔塔诺斯#dk2");
	next;
	mes "[来访王子]";
	mes "啊啊啊！！！";
	npctalk "来访的王子：啊啊啊！！！", .@prince$;
	donpcevent instance_npcname("#gp_control_5") + "::OnStart";
	close2;
	cutin "",255;
	end;
}
1@spa,201,214,1	duplicate(Lurid Royal Guard#dk)	Magic Sword Tartanos#dk2	CLEAR_NPC
1@spa,197,218,5	duplicate(Lurid Royal Guard#dk)	Visiting Prince#dk	4_M_KNIGHT_SILVER
1@spa,194,214,5	duplicate(Lurid Royal Guard#dk)	King's Corpse#dk	4_M_TRISTAN
1@spa,211,194,3	duplicate(Lurid Royal Guard#dk)	Captain's Corpse#dk	4_M_LIEMAN
1@spa,201,198,1	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk	4_M_DIEMAN
1@spa,197,190,1	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk1	4_M_DIEMAN
1@spa,191,207,7	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk2	4_M_DIEMAN
1@spa,206,209,7	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk3	4_M_DIEMAN
1@spa,189,195,1	duplicate(Lurid Royal Guard#dk)	Soldier's Corpse#dk4	4_M_DIEMAN

1@spa,1,1,1	script	#gp_control_5	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#gp_control_5");
	disablenpc instance_npcname("Magic Sword Tartanos#dk2");
	disablenpc instance_npcname("Visiting Prince#dk");
	disablenpc instance_npcname("Lurid Royal Guard#dk4");
	disablenpc instance_npcname("King's Corpse#dk");
	disablenpc instance_npcname("Captain's Corpse#dk");
	disablenpc instance_npcname("Soldier's Corpse#dk");
	disablenpc instance_npcname("Soldier's Corpse#dk1");
	disablenpc instance_npcname("Soldier's Corpse#dk2");
	disablenpc instance_npcname("Soldier's Corpse#dk3");
	disablenpc instance_npcname("Soldier's Corpse#dk4");
	sleep 1000;
	.@label$ = instance_npcname("#gp_control_5") + "::OnMyMobDead";
	monster 'map_spa$,198,218, "Torturous Redeemer",2959,1, .@label$;	// TORTUROUS_REDEEMER
	monster 'map_spa$,198,182, "Sweet Slaughter",2956,1, .@label$;		// SWEET_SLAUGHTER
	end;
OnMyMobDead:
	if (mobcount('map_spa$, instance_npcname("#gp_control_5") + "::OnMyMobDead") < 1) {
		deltimer instance_npcname("#gp_control_4") + "::OnSpawn1";	// stop double spawn if timer is running
		mapannounce 'map_spa$, "The passage on the 5th floor of the palace is open.", bc_map,0xFFFF00;
		disablenpc instance_npcname("4th floor in the palace");
		enablenpc instance_npcname("5th floor of Palace");
		enablenpc instance_npcname("Princess Tiara#dk1");
		enablenpc instance_npcname("Lurid Royal Guard#dk5");

		killmonster 'map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead2";
		killmonster 'map_spa$, instance_npcname("#gp_control_3") + "::OnMyMobDead3";
		disablenpc instance_npcname("#gp_control_3");
	}
	end;
}

// 5th floor
1@spa,216,43,3	script	Princess Tiara#dk1	4_F_MAYSEL,{
	.@guard$ = instance_npcname("Lurid Royal Guard#dk5");
	mes "[皇冠公主]";
	mes "你遵守了你的诺言。";
	npctalk "蒂亚拉公主：你遵守了你的诺言。";
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "公主！";
	npctalk "鲁瑞德皇家卫队：公主！", .@guard$;
	next;
	cutin "npc-tiara.bmp",3;
	mes "[皇冠公主]";
	mes "谢谢你……现在我可以在你身边休息了……";
	npctalk "蒂亚拉公主：谢谢你……现在我可以在你身边休息了……";
	next;
	mes "[鲁瑞德皇家卫队]";
	mes "不不不！！！！";
	npctalk "鲁瑞德皇家卫队：不……不！！！！！！", .@guard$;
	next;
	mes "[蒂亚拉公主的声音]";
	mes "我想永远平静地和你在一起……永远……";
	npctalk "蒂亚拉公主：我想永远平静地和你在一起……永远……";
	next;
	cutin "b-tiara.BMP",3;
	mes "[鲁瑞德皇家卫队]";
	mes "不……啊啊啊啊！！！！！！";
	npctalk "鲁瑞德皇家卫队：不……啊啊啊啊！！！！！！~！", .@guard$;
	next;
	mes "[蒂亚拉公主的声音]";
	mes "我爱你...";
	npctalk "蒂亚拉公主：我爱你……";
	cutin "",255;
	close2;
	donpcevent instance_npcname("#gp_control_6") + "::OnStart";
	end;
}

1@spa,213,42,7	duplicate(Lurid Royal Guard#dk)	Lurid Royal Guard#dk5	4_M_SAKRAYROYAL

1@spa,1,1,1	script	#gp_control_6	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#gp_control_6");
	disablenpc instance_npcname("Princess Tiara#dk1");
	disablenpc instance_npcname("Lurid Royal Guard#dk5");

	killmonster 'map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead1";
	killmonster 'map_spa$, instance_npcname("#gp_control_4") + "::OnMyMobDead2";
	killmonster 'map_spa$, instance_npcname("#gp_control_5") + "::OnMyMobDead";
	disablenpc instance_npcname("#gp_control_4");
	disablenpc instance_npcname("#gp_control_5");
	disablenpc instance_npcname("5th floor of Palace");
	sleep 2000;
	.@label$ = instance_npcname("#gp_control_6") + "::OnMyMobDead1";
	monster 'map_spa$,217,52, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
	unittalk $@mobid[0], "Wake up! look at us... We are monsters?";
	monster 'map_spa$,190,21, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
	unittalk $@mobid[0], "Sakray killed the King and tried to kill us!";
	monster 'map_spa$,198,49, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
	unittalk $@mobid[0], "What is this..??";
	monster 'map_spa$,212,28, "Fatal Days" ,2958,1, .@label$;		// FATAL_DAYS
	unittalk $@mobid[0], "Sakray killed a princess!";
	end;
OnMyMobDead1:
	if (mobcount('map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead1") < 1) {
		sleep 2000;
		.@label$ = instance_npcname("#gp_control_6") + "::OnMyMobDead2";
		monster 'map_spa$,217,43, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
		unittalk $@mobid[0], "Wake up! look at us... We are monsters?";
		monster 'map_spa$,193,24, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
		unittalk $@mobid[0], "Sakray..?? why.....Sakray!";
		monster 'map_spa$,216,59, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
		unittalk $@mobid[0], "Sakray killed the King and tried to kill us!";
		monster 'map_spa$,197,57, "Fatal Days" ,2958,1, .@label$;		// FATAL_DAYS
		unittalk $@mobid[0], "Sakray killed a princess!";
	}
	end;
OnMyMobDead2:
	if (mobcount('map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead2") < 1) {
		sleep 2000;
		.@label$ = instance_npcname("#gp_control_6") + "::OnMyMobDead3";
		monster 'map_spa$,193,39, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
		unittalk $@mobid[0], "Wake up! look at us... We are monsters?";
		monster 'map_spa$,212,34, "Forgotten Name" ,2957,1, .@label$;	// FORGOTTEN_NAME
		unittalk $@mobid[0], "Sakray..?? why.....Sakray!";
		monster 'map_spa$,201,64, "Tarnished Oath" ,2954,1, .@label$;	// COLORLESS_VOW
		unittalk $@mobid[0], "Sakray killed the King and tried to kill us!";
		monster 'map_spa$,175,44, "Fatal Days" ,2958,1, .@label$;		// FATAL_DAYS
		unittalk $@mobid[0], "Sakray killed a princess!";
	}
	end;
OnMyMobDead3:
	if (mobcount('map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead3") < 1) {
		sleep 2000;
		monster 'map_spa$,200,45, "Torturous Redeemer" ,2961,1, instance_npcname("#gp_control_6") + "::OnMyMobDead4";	// E_TORTUROUS_REDEEMER
		unittalk $@mobid[0], "Magic Sword Tartanos! I will destroy you if I can't have you... Oh no! How could this be?";
		sleep 5000;
		killmonster 'map_spa$, instance_npcname("#gp_control_6") + "::OnMyMobDead4";
		enablenpc instance_npcname("Sakray#dk");
		enablenpc instance_npcname("Magic Sword Tartanos#dk3");
		disablenpc instance_npcname("#gp_control_6");
	}
	end;
OnMyMobDead4:
	end;
}

1@spa,196,44,5	script	Sakray#dk	4_M_SAKRAY,{
	mes "[鲁瑞德皇家卫队]";
	mes "你满意吗..魔剑塔塔诺斯？";
	npctalk "鲁里德皇家卫队：你满意吗..？魔剑塔塔诺斯？";
	next;
	mes "[魔剑塔塔诺斯]";
	mes "我需要更多的血……Sakray，给我他的血。";
	npctalk "魔剑塔塔诺斯：我需要更多的血……Sakray，给我他的血。", instance_npcname("魔剑塔塔诺斯#dk3");
	next;
	mes "[萨克雷]";
	mes "好吧，那很好。一个旅行者。你有没有看到？";
	npctalk "萨克雷：好的，那很好。一个旅行者。你有没有看到？";
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "萨克雷，你想要什么？同情？或者说这是一个陷阱？";
	unittalk getcharid(3), "" + strcharinfo(0) + " : Sakray, what do you want? Sympathy? Or is it a trap?";
	next;
	mes "[萨克雷]";
	mes "不……我不需要费什么功夫去抓新手。";
	npctalk "Sakray：不……我不需要付出任何努力来抓住新手。";
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "如果是这样的话……那为什么……？";
	unittalk getcharid(3), "" + strcharinfo(0) + " : If so... then why...?";
	next;
	cutin "cry-b.bmp",2;
	mes "[萨克雷]";
	mes "我想通过你摆脱我内心的人性。";
	npctalk "Sakray：我想通过你摆脱我内心的人性。";
	next;
	mes "[萨克雷]";
	mes "我对结果非常满意。";
	npctalk "Sakray：我对结果非常满意。";
	next;
	mes "[魔剑塔塔诺斯]";
	mes "萨克雷，现在就把鲜血献给我吧！";
	npctalk "魔剑塔塔诺斯：萨克雷，现在就把鲜血献给我吧！", instance_npcname("魔剑塔塔诺斯#dk3");
	next;
	mes "[萨克雷]";
	mes "如果你是我，你会怎么做？";
	npctalk "Sakray：如果你是我，你会怎么做？";
	next;
	mes "[萨克雷]";
	mes "你能让自己摆脱这个诅咒吗？";
	npctalk "Sakray：你能摆脱这个诅咒吗？";
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "...";
	unittalk getcharid(3), "" + strcharinfo(0) + " : ...";
	next;
	mes "[萨克雷]";
	mes "呵呵……我会一直看着你，直到你长大的那一天。";
	npctalk "Sakray：呵呵……我会一直看着你，直到你足够大的那一天到来。";
	next;
	mes "[萨克雷]";
	mes "我愿意稍后去找你寻找塔塔诺斯。";
	npctalk "萨克雷：我愿意稍后为塔塔诺斯追捕你。";
	cutin "",255;
	next;
	mes "[萨克雷]";
	mes "那么再见了。";
	npctalk "萨克雷：那就再见了。";
	close2;
	donpcevent instance_npcname("#tb") + "::OnStart";
	end;
}

1@spa,196,46,1	script	#tb	CLEAR_NPC,{
	end;
OnStart:
	disablenpc instance_npcname("Sakray#dk");
	disablenpc instance_npcname("Magic Sword Tartanos#dk3");
	enablenpc instance_npcname("#tb");
	enablenpc instance_npcname("#sv");
	enablenpc instance_npcname("#tv");
	initnpctimer;
	end;
OnTimer3000:
	npctalk "蒂亚拉公主的声音：有什么可以帮你的吗？";
	end;
OnTimer8000:
	npctalk "萨克雷之声：我会永远支持你。请在我身边休息吧。", instance_npcname("#sv");
	end;
OnTimer13000:
	npctalk "魔剑塔塔诺斯：萨克雷，不要忘记我们之间的血之契约。", instance_npcname("#tv");
	end;
OnTimer17000:
	npctalk "魔剑塔塔诺斯：你的灵魂是我的，直到你找到令我满意的鲜血……", instance_npcname("#tv");
	end;
OnTimer22000:
	npctalk "萨克雷之声：和我一起在阴影中休息吧。", instance_npcname("#sv");
	end;
OnTimer25000:
	npctalk "萨克雷之声：永远在黑暗中……", instance_npcname("#sv");
	end;
OnTimer28500:
	disablenpc instance_npcname("#tb");
	disablenpc instance_npcname("#sv");
	disablenpc instance_npcname("#tv");
	mapannounce 'map_spa$, "It is time to leave the Ghost Palace.", bc_map,0xFFFF00;
	enablenpc instance_npcname("Way out of Palace");
	enablenpc instance_npcname("King#dk11");
	end;
}

1@spa,195,41,1	duplicate(Lurid Royal Guard#dk)	Magic Sword Tartanos#dk3	CLEAR_NPC
1@spa,198,43,1	duplicate(Lurid Royal Guard#dk)	#sv	CLEAR_NPC
1@spa,196,41,1	duplicate(Lurid Royal Guard#dk)	#tv	CLEAR_NPC

1@spa,204,29,1	script	King#dk11	4_M_RUSKING,{
	disable_items;
	mes "[国王]";
	mes "嘿，你还活着吗？";
	mes "没想到会遇到一个还活着的人。";
	next;
	.@item_name_req$ = getitemname(6672);// Gray_Shard
	mes "[国王]";
	mes "您可能有^006400" + .@item_name_req$ + "^000000吗？";
	mes "你能把它给我吗？";
	mes "忠诚的皇家卫队的回忆";
	mes "以及他所爱的每个人的回忆";
	mes "包含在这些碎片内。";
	next;
	mes "[国王]";
	mes "你能把^006400" + .@item_name_req$ + "^000000给我吗？";
	mes "愧疚正在吞噬我的灵魂。";
	mes "我的灵魂被困在鬼宫里。";
	next;
	mes "[国王]";
	mes "我正在从这些碎片中收集萨克雷遗弃的记忆。";
	next;
	mes "[国王]";
	mes "如果你给我任何^006400" + .@item_name_req$ + "^000000给我，我可以为你制作装备。";
	next;
	mes "[国王]";
	mes "被诅咒的骑士...";
	mes "请帮助我拯救萨克雷的灵魂。";
	next;
	setarray .@item_name$[0],
		"Thanos Sword", "Thanos Broadsword", "Thanos Spear", "Thanos Two-Handed Spear", "Thanos Staff",
		"Thanos Two-handed Staff", "Thanos Bow", "Thanos Dagger", "Thanos Katar", "Thanos Knuckle",
		"Thanos Hammer", "Thanos Axe", "Thanos Violin", "Thanos Whip",
		"Gray Shield", "Gray Armor", "Gray Robe", "Gray Cloak", "Gray Shoes", "Gray Helmet";
	setarray .@items_list[0],13441,21009,1438,1496,1669,2023,18119,13093,28000,1836,16028,28100,1933,1988,2187,15090,15091,20721,22033,18820;

	for ( .@i = 0; .@i < 20; .@i++ )
		.@menu$ += .@item_name$[.@i] + ":";
	.@menu$ += "Cancel";

	while(true) {
		.@s = select(.@menu$) - 1;
		if (.@s == 20) {
			mes "[国王]";
			mes "总有一天，萨克雷会";
			mes "一定能摆脱这个诅咒。";
			mes "我相信";
			mes "^006400" + .@item_name_req$ + "^000000 是唯一的方法";
			mes "为了救他……";
			close;
		}
		.@cost = ( .@s > 13 ? 100 : 200 );
		mes "[国王]";
		mes "制作 ^FF0000" + .@item_name$[.@s] + "^000000";
		mes "我需要" + .@cost + " ^006400" + .@item_name_req$ + "^000000。";
		next;
		if (select( "取消", "移交" + .@cost + " " + .@item_name_req$ + "." ) == 1) {
			mes "[国王]";
			mes "总有一天，萨克雷会";
			mes "一定能摆脱这个诅咒。";
			next;
			continue;
		}
		if (countitem(6672) < .@cost) {
			mes "[国王]";
			mes "您没有足够的^006400" + .@item_name_req$ + "^000000";
			mes "制作^FF0000" + .@item_name$[.@s] + "^000000。";
			mes "我需要" + .@cost + " ^006400" + .@item_name_req$ + "^000000。";
			next;
			continue;
		}
		mes "[国王]";
		mes "我将制作一个^FF0000" + .@item_name$[.@s] + "^000000";
		mes "与 " + .@cost + " ^006400" + .@item_name_req$ + "^000000。";
		next;
		mes "你从国王合拢的双手中看到一道光，它逐渐成形。";
		next;
		mes "[国王]";
		mes "好吧，完成了。";
		mes "拿这个^FF0000" + .@item_name$[.@s] + "^000000";
		mes "对抗萨克雷和塔塔诺斯。";
		next;
		mes "[国王]";
		mes "权力需要牺牲。";
		mes "请小心...";
		delitem 6672,.@cost;// Gray Shard
		getitem .@items_list[.@s],1;
		close;
	}
}

1@spa,210,28,0	script	Way out of Palace	WARPNPC,2,2,{
	end;
OnTouch_:
	mes "是时候离开鬼宫了。";
	mes "现在一切都完成了";
	mes "并且会消失..";
	close2;
	if (isbegin_quest(1263) == 2)
		getitem 6672,1;// Gray Shard
	else {
		setquest 1263;// Cursed Swordman
		completequest 1263;
		getexp 900000,500000;
		getitem 6672,2;// Gray Shard
	}
	warp "dali02",46,129;
	end;
}

1@spa,41,217,0	warp	The second floor of Palace	2,2,1@spa,114,120
1@spa,117,137,0	warp	3rd floor in the palace	2,2,1@spa,60,43
1@spa,54,28,0	warp	4th floor in the palace	2,2,1@spa,218,186
1@spa,178,186,0	warp	5th floor of Palace	2,2,1@spa,186,57
