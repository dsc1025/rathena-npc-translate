//===== rAthena Script ======================================= 
//= Cool Event Corp. Staff
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//============================================================ 

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
	cutin "zonda_01",2;
	mes "[酷事件公司员工]";
	mes "欢迎来到酷活动公司。";
	mes "我们的员工始终在工作";
	mes "超越你的期望";
	mes "为了优质的服务。那么如何";
	mes "今天我可以帮你吗？";
	next;
	switch(select(getarg(0))) {
	case 1:
		mes "[酷事件公司员工]";
		mes "你的重生点";
		mes "已保存在这里";
		mes getarg(1)+".";
		mes "感谢您使用";
		mes "Cool Event Corp. 服务~";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close2;
		break;
	case 2:
		mes "[酷事件公司员工]";
		if(!callfunc("F_CanOpenStorage")){
			mes "对不起，但是你";
			mes "需要新手的";
			mes "基本技能等级 6 至";
			mes "使用存储服务。";
			close2;
			break;
		}
		if(Zeny < 40){
			mes "对不起，但你不";
			mes "有足够的 Zeny 可供使用";
			mes "存储服务。我们的";
			mes "存储访问费为 40 Zeny。";
			close2;
			break;
		}
		set Zeny, Zeny - 40;
		mes "让我打开你的个人";
		mes "立即为您存储。";
		mes "感谢您对酷派的支持";
		mes "Event Corp. 通过使用我们的";
		mes "服务。祝你有美好的一天~";
		callfunc("F_CheckKafCode");	//check your storage password, if set
		close2;
		openstorage;
		break;
	case 3:
		mes "请选择";
		mes "你的目的地。";
		next;
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE))
			set .@cost,4400;
		else
			set .@cost,2200;
		.@destination$ = getarg(5);
		if (select(.@destination$ +" -> "+ .@cost +"z：取消") == 1) {
			if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
				if (countitem(7060) != 0) {
					delitem 7060,1; //Warp_Free_Ticket
				}
				else {
					set Zeny,Zeny-.@cost;
				}
				if (.@destination$ == "Veins") warp "veins",205,101;
				else if (.@destination$ == "Rachel") warp "rachel",115,125;
				cutin "",255;
				end;
			}
			else {
				mes "[酷事件公司员工]";
				mes "抱歉，但你没有";
				mes "足够的 zeny 进行传送";
				mes "服务。传送费用";
				mes "到"+ .@destination$ +"是"+.@cost+" zeny。";
			}
		}
		close2;
		break;
	case 4:
		mes "[酷事件公司员工]";
		if(BaseClass != Job_Merchant){
			mes "抱歉，但是";
			mes "手推车租赁服务";
			if (checkre(0))
				mes "仅适用于商户类别。";
			else {
				mes "仅适用于商家，";
				mes "铁匠、怀特史密斯、";
				mes "炼金术士和创造者。";
			}
			close2;
			break;
		}
		if(getskilllv("MC_PUSHCART") == 0){
			mes "您只能在之后租用一辆手推车";
			mes "学习手推车技能。";
			close2;
			break;
		}
		if(checkcart() == 1){
			mes "你已经有了";
			mes "配备手推车。";
			mes "不幸的是，我们不能";
			mes "租多于一间";
			mes "每个客户一次。";
			close2;
			break;
		}
		if(countitem(7061)){
			delitem 7061,1; //Cart_Free_Ticket
			setcart;
			close2;
			break;
		}
		mes "手推车租赁";
		mes "费用为800 Zeny。会";
		mes "您喜欢租一辆手推车吗？";
		next;
		if(select("租用手推车：取消") == 1){
			if(Zeny < 800){
				mes "[酷事件公司员工]";
				mes "对不起，但是你";
				mes "没有足够的";
				mes "Zeny 支付手推车费用";
				mes "租金800 Zeny。";
			} else {
				set Zeny, Zeny - 800;
				setcart;
			}
		}
		close2;
		break;
	case 5:
		callfunc("F_SetKafCode","[Cool Event Corp. Staff]","Cool Event Corp.");
		//the function closes msg and removes cutins
	case 6:
	default:
		mes "[酷事件公司员工]";
		mes "Cool Event Corp. 始终如一";
		mes "努力提供最好的";
		mes "为我们的客户提供服务。";
		mes "帮助我们成为最好的";
		mes "向我们提供您的意见";
		mes "和诚实的反馈。谢谢。";
		close2;
	}
	cutin "", 255;
	end;
}

lighthalzen,94,248,3	script	Cool Event Corp Staff::CoolEventCorpStaffLighthalzen	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage::Rent a Pushcart:Storage Password Service:Cancel",
		"in the town of Lighthalzen","lighthalzen",95,240;
	end;
}

hugel,88,168,4	script	Cool Event Corp. Staff::CoolEventCorpStaffHugel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage::Rent a Pushcart:Storage Password Service:Cancel",
		"in the village of Hugel","hugel",96,155;
	end;
}

rachel,109,138,5	script	Cool Event Corp. Staff::CoolEventCorpStaffRachel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage:Teleport Service:Rent a Pushcart:Storage Password Service:Cancel",
		"in the town of Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	Cool Event Corp. Staff::CoolEventCorpStaffVeins	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage:Teleport Service:Rent a Pushcart:Storage Password Service:Cancel",
		"in the town of Veins","veins",204,103,"Rachel";
	end;
}
