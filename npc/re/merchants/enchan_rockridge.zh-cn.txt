//===== rAthena Script =======================================
//= Rock Ridge npcs
//===== Description: =========================================
//= [Official Conversion]
//= Enchant npcs on Rockridge.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//===== Translated By: =======================================
//= dsc
//============================================================

// enchant npc
har_in01,17,74,7	script	Contraband Processor#pa	4_DR_SOLDIER,{
	if (checkweight(1201,1) == 0) {
		mes "我携带的物品种类太多。我应该先减轻我的包。";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "我超重了。我最好先减轻一下我的包。";
		close;
	}
	disable_items;
	mes "[违禁品处理器]";
	mes "您是否尝试过用 Rock Ridge Coins 购买一些违禁品？我可以让你的违禁品变得更好。";
	next;
	.@reset = select( "让他们变得更好？如何？", "为违禁物品附魔。", "重置违禁物品附魔等级。" ) - 2;
	if (.@reset == -1) {
		mes "[违禁品处理器]";
		mes "啊，我们以前见过吗？我每天都会遇到很多人，以至于我记不住他们所有的面孔。让我跟上你的进度。";
		next;
		mes "[违禁品处理器]";
		mes "我可以为您可以使用 Rock Ridge Coins 购买的装备附魔，最多可用于 3 个用于盔甲和服装的插槽以及最多 2 个用于配件的插槽。";
		next;
		mes "[违禁品处理器]";
		mes "我为每个插槽收取 2 枚 Rock Ridge Coins，用于附魔和重置。";
		next;
		mes "[违禁品处理器]";
		mes "哦，请注意，这些违禁品很容易被破坏，尤其是当它们被重置时。";
		close;
	}
	switch( select( "取消", "盔甲", "服装", "配饰（右栏）" ) ) {
	case 1:
		mes "[违禁品处理器]";
		mes "你真傻。";
		close;
	case 2:
		.@part = EQI_ARMOR;
		break;
	case 3:
		.@part = EQI_GARMENT;
		break;
	case 4:
		.@part = EQI_ACC_R;
		break;
	}
	.@equip_id = getequipid(.@part);
	switch( .@equip_id ) {
	case 15176:	// Vigilante_Suit
	case 15177:	// Elemental_Robe
	case 15178:	// Golden_Ninja_Suit
	case 15179:	// Mine_Worker's_Vest
	case 15180:	// Hippie Clothes

	case 20820:	// Elemental_Cape
	case 20821:	// Golden_Scarf
	case 20822:	// Mine_Worker's_Backpack

	case 28441:	// Vigilante_Badge
	case 28442:	// Hippie_Feather
		break;
	case -1:
		mes "[违禁品处理器]";
		mes "你为什么要浪费我的时间？先去穿你的装备吧。";
		close;
	default:
		mes "[违禁品处理器]";
		mes "我不处理这样的物品。";
		close;
	}
	.@refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	copyarray .@tmp_card[0], .@card[0], 4;
	if ((.@card[1] && getiteminfo(.@card[1], ITEMINFO_SUBTYPE) == CARD_NORMAL) || (.@card[2] && getiteminfo(.@card[2], ITEMINFO_SUBTYPE) == CARD_NORMAL) || (.@card[3] && getiteminfo(.@card[3], ITEMINFO_SUBTYPE) == CARD_NORMAL)) {// armor enchant system custom check
		mes "[违禁品处理器]";
		mes "发生了一些错误。";
		close;
	}
	switch( .@reset ) {
	case TRUE:
		if (.@card[3] == 0) {
			mes "[违禁品处理器]";
			mes "它没有着魔。你希望我重置什么？";
			close;
		}
		mes "[违禁品处理器]";
		mes "哦，你真的要重置它吗？正如我之前告诉过你的，违禁治安警察徽章很容易被毁坏。";
		if (select( "我稍后回来。", "我们开始吧。" ) == 1) {
			mes "[违禁品处理器]";
			mes "好吧，如果你改变主意就回来吧。";
			close;
		}
		if (.@part == EQI_ACC_R)
			.@chance_of_breaking_on_reset = 45;
		else
			.@chance_of_breaking_on_reset = 30;
		.@card[1] = .@card[2] = .@card[3] = 0;
		break;
	case FALSE:
		switch( .@part ) {
		case EQI_ARMOR:
		case EQI_GARMENT:
			if (.@card[1] > 0) {
				mes "[违禁品处理器]";
				mes "这已经是附魔到了最大的极限了。也许你应该重置它。";
				close;
			}
			if (.@card[3] == 0)
				.@enchant_slot = 3;
			else if (.@card[2] == 0)
				.@enchant_slot = 2;
			else
				.@enchant_slot = 1;
			if (.@refine >= 9) {
				.@r = rand(1,215);
				if (.@r < 16) .@card[.@enchant_slot] = getiteminfo("Agility2", ITEMINFO_ID);
				else if (.@r < 31) .@card[.@enchant_slot] = getiteminfo("Dexterity2", ITEMINFO_ID);
				else if (.@r < 46) .@card[.@enchant_slot] = getiteminfo("Strength2", ITEMINFO_ID);
				else if (.@r < 61) .@card[.@enchant_slot] = getiteminfo("Inteligence2", ITEMINFO_ID);
				else if (.@r < 76) .@card[.@enchant_slot] = getiteminfo("Def20", ITEMINFO_ID);
				else if (.@r < 91) .@card[.@enchant_slot] = getiteminfo("Mdef5", ITEMINFO_ID);
				else if (.@r < 106) .@card[.@enchant_slot] = getiteminfo("Critical3", ITEMINFO_ID);
				else if (.@r < 121) .@card[.@enchant_slot] = getiteminfo("Evasion6", ITEMINFO_ID);
				else if (.@r < 135) .@card[.@enchant_slot] = getiteminfo("Expert_Archer1", ITEMINFO_ID);
				else if (.@r < 149) .@card[.@enchant_slot] = getiteminfo("Fatal1", ITEMINFO_ID);
				else if (.@r < 163) .@card[.@enchant_slot] = getiteminfo("Spell1", ITEMINFO_ID);
				else if (.@r < 173) .@card[.@enchant_slot] = getiteminfo("Expert_Archer2", ITEMINFO_ID);
				else if (.@r < 183) .@card[.@enchant_slot] = getiteminfo("Fatal2", ITEMINFO_ID);
				else if (.@r < 193) .@card[.@enchant_slot] = getiteminfo("Spell2", ITEMINFO_ID);
				else if (.@r < 197) .@card[.@enchant_slot] = getiteminfo("Expert_Archer3", ITEMINFO_ID);
				else if (.@r < 201) .@card[.@enchant_slot] = getiteminfo("Fatal3", ITEMINFO_ID);
				else if (.@r < 205) .@card[.@enchant_slot] = getiteminfo("Spell3", ITEMINFO_ID);
				else if (.@r < 209) .@card[.@enchant_slot] = getiteminfo("Attack_Delay_1", ITEMINFO_ID);
				else if (.@r < 210) .@card[.@enchant_slot] = getiteminfo("Expert_Archer4", ITEMINFO_ID);
				else if (.@r < 211) .@card[.@enchant_slot] = getiteminfo("Fatal4", ITEMINFO_ID);
				else if (.@r < 212) .@card[.@enchant_slot] = getiteminfo("Spell4", ITEMINFO_ID);
				else .@card[.@enchant_slot] = getiteminfo("Attack_Delay_2", ITEMINFO_ID);
			}
			else {
				.@r = rand(1,206);
				if (.@r < 14) .@card[.@enchant_slot] = getiteminfo("Agility1", ITEMINFO_ID);
				else if (.@r < 27) .@card[.@enchant_slot] = getiteminfo("Dexterity1", ITEMINFO_ID);
				else if (.@r < 40) .@card[.@enchant_slot] = getiteminfo("Strength1", ITEMINFO_ID);
				else if (.@r < 53) .@card[.@enchant_slot] = getiteminfo("Inteligence1", ITEMINFO_ID);
				else if (.@r < 66) .@card[.@enchant_slot] = getiteminfo("Def15", ITEMINFO_ID);
				else if (.@r < 79) .@card[.@enchant_slot] = getiteminfo("Mdef2", ITEMINFO_ID);
				else if (.@r < 92) .@card[.@enchant_slot] = getiteminfo("Critical1", ITEMINFO_ID);
				else if (.@r < 105) .@card[.@enchant_slot] = getiteminfo("Evasion3", ITEMINFO_ID);
				else if (.@r < 114) .@card[.@enchant_slot] = getiteminfo("Agility2", ITEMINFO_ID);
				else if (.@r < 123) .@card[.@enchant_slot] = getiteminfo("Dexterity2", ITEMINFO_ID);
				else if (.@r < 132) .@card[.@enchant_slot] = getiteminfo("Strength2", ITEMINFO_ID);
				else if (.@r < 141) .@card[.@enchant_slot] = getiteminfo("Inteligence2", ITEMINFO_ID);
				else if (.@r < 150) .@card[.@enchant_slot] = getiteminfo("Def20", ITEMINFO_ID);
				else if (.@r < 159) .@card[.@enchant_slot] = getiteminfo("Mdef5", ITEMINFO_ID);
				else if (.@r < 168) .@card[.@enchant_slot] = getiteminfo("Critical3", ITEMINFO_ID);
				else if (.@r < 177) .@card[.@enchant_slot] = getiteminfo("Evasion6", ITEMINFO_ID);
				else if (.@r < 184) .@card[.@enchant_slot] = getiteminfo("Expert_Archer1", ITEMINFO_ID);
				else if (.@r < 191) .@card[.@enchant_slot] = getiteminfo("Fatal1", ITEMINFO_ID);
				else if (.@r < 198) .@card[.@enchant_slot] = getiteminfo("Spell1", ITEMINFO_ID);
				else if (.@r < 201) .@card[.@enchant_slot] = getiteminfo("Expert_Archer2", ITEMINFO_ID);
				else if (.@r < 204) .@card[.@enchant_slot] = getiteminfo("Fatal2", ITEMINFO_ID);
				else .@card[.@enchant_slot] = getiteminfo("Spell2", ITEMINFO_ID);
			}
			break;
		case EQI_ACC_R:
			if (.@card[2] > 0) {
				mes "[违禁品处理器]";
				mes "这已经是附魔到了最大的极限了。也许你应该重置它。";
				close;
			}
			if (.@card[3] == 0)
				.@enchant_slot = 3;
			else
				.@enchant_slot = 2;
			.@r = rand(1,236);
			if (.@r < 16) .@card[.@enchant_slot] = getiteminfo("Agility1", ITEMINFO_ID);
			else if (.@r < 31) .@card[.@enchant_slot] = getiteminfo("Dexterity1", ITEMINFO_ID);
			else if (.@r < 46) .@card[.@enchant_slot] = getiteminfo("Strength1", ITEMINFO_ID);
			else if (.@r < 61) .@card[.@enchant_slot] = getiteminfo("Inteligence1", ITEMINFO_ID);
			else if (.@r < 76) .@card[.@enchant_slot] = getiteminfo("Vitality1", ITEMINFO_ID);
			else if (.@r < 91) .@card[.@enchant_slot] = getiteminfo("Luck1", ITEMINFO_ID);
			else if (.@r < 106) .@card[.@enchant_slot] = getiteminfo("HP100", ITEMINFO_ID);
			else if (.@r < 121) .@card[.@enchant_slot] = getiteminfo("SP50", ITEMINFO_ID);
			else if (.@r < 131) .@card[.@enchant_slot] = getiteminfo("Agility2", ITEMINFO_ID);
			else if (.@r < 141) .@card[.@enchant_slot] = getiteminfo("Dexterity2", ITEMINFO_ID);
			else if (.@r < 151) .@card[.@enchant_slot] = getiteminfo("Strength2", ITEMINFO_ID);
			else if (.@r < 161) .@card[.@enchant_slot] = getiteminfo("Inteligence2", ITEMINFO_ID);
			else if (.@r < 171) .@card[.@enchant_slot] = getiteminfo("Vitality2", ITEMINFO_ID);
			else if (.@r < 181) .@card[.@enchant_slot] = getiteminfo("Luck2", ITEMINFO_ID);
			else if (.@r < 191) .@card[.@enchant_slot] = getiteminfo("HP200", ITEMINFO_ID);
			else if (.@r < 201) .@card[.@enchant_slot] = getiteminfo("SP100", ITEMINFO_ID);
			else if (.@r < 208) .@card[.@enchant_slot] = getiteminfo("Expert_Archer1", ITEMINFO_ID);
			else if (.@r < 215) .@card[.@enchant_slot] = getiteminfo("Spell1", ITEMINFO_ID);
			else if (.@r < 222) .@card[.@enchant_slot] = getiteminfo("Fighting_Spirit1", ITEMINFO_ID);
			else if (.@r < 229) .@card[.@enchant_slot] = getiteminfo("Attack_Delay_1", ITEMINFO_ID);
			else if (.@r < 236) .@card[.@enchant_slot] = getiteminfo("Fatal1", ITEMINFO_ID);
			else .@card[.@enchant_slot] = getiteminfo("Atk_Speed1", ITEMINFO_ID);
			break;
		default:
			mes "[违禁品处理器]";
			mes "您刚刚尝试切换设备吗？";
			close;
		}
		break;
	}
	if (countitem(25250) < 2) {
		mes "[违禁品处理器]";
		mes "我为此服务收取 2 个 Rock Ridge 硬币。把它们带给我。";
		close;
	}
	delitem 25250,2;// Rock_Ridge_Coin

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@tmp_card[0], .@tmp_card[1], .@tmp_card[2], .@tmp_card[3]))
		close;

	delequip .@part;
	if (rand(100) < .@chance_of_breaking_on_reset) {
		specialeffect2 EF_LORD;
		mes "[违禁品处理器]";
		mes "啊，结果不太好。";
		mes "好吧，只是我的运气。";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	mes "[违禁品处理器]";
	mes "完毕。一探究竟。";
	close;
}


har_in01,24,69,3	script	Affable Lady#rockridge0	1_F_MERCHANT_02,{
	if (checkweight(1301,3) == 0) {
		mes "[和蔼可亲的女士]";
		mes "你一定很坚强才能随身携带这么多东西。";
		next;
		mes "[和蔼可亲的女士]";
		mes "如果您想减轻行李重量，警长办公室方向有一名 Kafra 员工。";
		close;
	}
	mes "[和蔼可亲的女士]";
	mes "你是一个冒险家。我喜欢像你这样有激情的人。";
	mes "那么，你来这里是因为^0000CD它^000000吗？";
	next;
	if (select( "因为什么？", "我是。" ) == 1) {
		mes "[和蔼可亲的女士]";
		mes "哼，别装作不知道的样子。我说的是在 Rock Ridge 可以买到的特殊装备。";
		next;
		mes "[和蔼可亲的女士]";
		mes "盔甲经销商每周五都会进货，而且每次几乎一进货就卖光了。";
		close;
	}
	mes "[和蔼可亲的女士]";
	mes "*咯咯*你想要哪一个：一个适合你的^0000CD肩膀^000000，^0000CD颈部，还是胸部^000000？";
	next;
	switch( select( "嗯，任何东西。", "对于我的肩膀", "对于我的脖子或胸部" ) ) {
	case 1:
		mes "[和蔼可亲的女士]";
		mes "*咯咯*当然，年轻人无论穿什么总是看起来很好。";
		mes "但如果你想要一些特别的东西，就去看看盔甲店。";
		close;
	case 2:
		mes "[和蔼可亲的女士]";
		mes "选择一个你喜欢的，我会告诉你。";
		mes "所有装备无论其类型如何都是^0000CD3,000,000 Zeny^000000。";
		next;
		.@cost = 3000000;
		setarray .@item_id[0],20820,20821,20822;
		setarray .@item_name$[0], "Elemental Cape", "Golden Scarf", "Mine Worker Backpack";
		.@s = select( "元素斗篷", "金围巾", "矿工背包", "我不知道你在说什么。" ) - 1;
		break;
	case 3:
		mes "[和蔼可亲的女士]";
		mes "选择一个你喜欢的，我会告诉你。";
		mes "所有装备无论其类型如何，都是^0000CD1,500,000 Zeny^000000。";
		next;
		.@cost = 1500000;
		setarray .@item_id[0],28441,28442;
		setarray .@item_name$[0], "Vigilante Badge", "Hippie Feather";
		.@s = select( "义务警员徽章", "嬉皮羽毛", "我不知道你在说什么。" ) - 1;
		break;
	}
	if (.@item_id[.@s] == 0) {
		mes "[和蔼可亲的女士]";
		mes "哎呀，我猜你太天真了，没听清我刚才说的话。";
		mes "为了你好，你应该忘记它。";
		close;
	}
	mes "[和蔼可亲的女士]";
	mes "啊，你感兴趣的是";
	mes "[" + mesitemlink( .@item_id[.@s], false, .@item_name$[.@s] ) + "]。";
	mes "单击名称可查看详细说明。";
	mes "正如我告诉你的，它是 ^0000CD"+callfunc("F_InsertComma",.@cost)+" Zeny^000000。你想买吗？";
	next;
	if (select( "是的。", "不。" ) == 2) {
		mes "[和蔼可亲的女士]";
		mes "呵呵。所以我在一个不想买的人身上浪费了我的精力。";
		mes "每一件装备都有一个故事要讲，如果你不随身携带它，它可能会记恨。";
		next;
		mes "[和蔼可亲的女士]";
		mes "我建议你不要告诉其他人";
		mes "并向我保证你会回来的。我会为你保留装备。";
		close;
	}
	if (Zeny < .@cost) {
		mes "[和蔼可亲的女士]";
		mes "呃哦，你没有足够的钱买这个。";
		mes "你为什么不晚点回来？我会永远在这里。";
		close;
	}
	mes "[和蔼可亲的女士]";
	mes "当然。这是您的" + .@item_name$[.@s] + ".";
	Zeny = Zeny - .@cost;
	getitem .@item_id[.@s],1;
	next;
	mes "[和蔼可亲的女士]";
	mes "人们常常被先入为主的观念所欺骗。大多数人认为像我这样的老妇人不会卖这种特殊的物品。";
	mes "如果你想要更多装备就回来吧。我会永远在这里。 *咯咯笑*";
	close;
}

har_in01,34,81,5	script	Howard#rr	4_M_TATIO,{
	if (checkweight(1301,1) == 0) {
		mes "[霍华德]";
		mes "呃，你带了很多东西。";
		mes "你为什么不走";
		mes "先减轻你的包？";
		close;
	}
	mes "[霍华德]";
	mes "你好。今天我能为您提供什么帮助？";
	next;
	if (countitem(25250) < 1) {
		mes "[霍华德]";
		mes "嗯，你没有金币。";
		mes "你为什么不先把它们收集起来呢？";
		close;
	}
	mes "[霍华德]";
	mes "啊，你有硬币。";
	mes "感谢您对岩岭的帮助。";
	mes "环顾四周，看看是否有你想要的东西。";
	next;
	mes "[霍华德]";
	mes "你想要点什么？";
	next;
	setarray .@data$[0],
		 24, 15176, "Vigilante Suit",	// <cost>, <item ID>, <item name>
		 24, 15177, "Elemental Robe",
		 24, 15178, "Golden Ninja Suit",
		 24, 15179, "Mine Worker Vest",
		 24, 15180, "Hippie Clothes",
		150, 20834, "Drifter's Cape",
		150, 22131, "Spurred Boots",
		180, 16060, "Liquor Bottle",
		300, 26107, "Elder Staff",
		100, 31140, "Costume Black Cowboy Hat",
		100, 31141, "Costume Cactus Flower Corsage";

	.@size = getarraysize(.@data$);
	for ( .@i = 0; .@i < .@size; .@i += 3 )
		.@menu$ += .@data$[.@i+2] + ":";
	.@s = ( select(.@menu$) - 1 ) * 3;

	mes "[霍华德]";
	mes mesitemlink( atoi( .@data$[.@s+1] ), false, .@data$[.@s+2] ) + "， 知道了。";
	mes "那将是" + .@data$[.@s] + " Rock Ridge 硬币。";
	mes "您确定想要该商品吗？";
	next;
	if (select( "是的。", "不。" ) == 2) {
		mes "[霍华德]";
		mes "如果你已经决定了，那么请再跟我谈谈。";
		close;
	}
	if (countitem(25250) < atoi(.@data$[.@s])) {
		mes "[霍华德]";
		mes "你没有足够的金币。";
		mes "请多带一些。";
		close;
	}
	mes "[霍华德]";
	mes "就这样吧。享受！";
	delitem 25250, atoi(.@data$[.@s]);// Rock Ridge Coin
	getitem atoi(.@data$[.@s+1]),1;
	close;
}

-	shop	shotguns_rockridge	-1,13154:-1,13155:-1,13192:-1,13193:-1,13194:-1
-	shop	revolvers_rockridge	-1,13102:-1,13120:-1,13122:-1
-	shop	bullets_rockridge	-1,13200:-1,13221:-1,13222:-1,13228:-1,13229:-1,13230:-1,13231:-1,13232:-1

har_in01,19,86,4	script	Weapon Dealer#rockridge01	1_F_02,{
	if (checkweight(1301,3) == 0) {
		mes "[武器商人]";
		mes "你带的东西太多了，买不了武器。";
		mes "请先去减轻你的行李。";
		close;
	}
	mes "[武器商人]";
	mes "我携带猎枪、左轮手枪和子弹。";
	mes "告诉我你想要什么类型的物品，我会给你一份清单。";
	next;
	switch( select( "霰弹枪", "左轮手枪", "子弹", "出售物品。", "取消。" ) ) {
	case 1:
		mes "[武器商人]";
		mes "您想浏览霰弹枪吗？我懂了。";
		mes "让我给你展示一些好的模型。";
		close2;
		callshop "shotguns_rockridge", 1;
		end;
	case 2:
		mes "[武器商人]";
		mes "您想浏览左轮手枪吗？我懂了。";
		mes "我刚刚引进了一些好的模型。";
		close2;
		callshop "revolvers_rockridge", 1;
		end;
	case 3:
		mes "[武器商人]";
		mes "我明白了，你想要子弹。";
		mes "我所有的子弹都是最好的品质。";
		close2;
		callshop "bullets_rockridge", 1;
		end;
	case 4:
		mes "[武器商人]";
		mes "当然，如果你有东西我可以卖掉。";
		mes "告诉我你有什么。";
		close2;
		callshop "bullets_rockridge", 2;
		end;
	case 5:
		mes "[武器商人]";
		mes "再来吧。";
		close;
	}
}

-	marketshop	armor_rockridge_1	-1,2247:500000:10,2248:500000:10,5018:700000:10
-	marketshop	armor_rockridge_2	-1,5067:700000:10,5071:700000:10,5075:700000:10
-	marketshop	armor_rockridge_3	-1,2267:100000:10,2268:500000:10,2241:5000:10
-	marketshop	armor_rockridge_4	-1,5067:700000:10,5071:700000:10,5075:700000:10,31144:1500000:3

har_in01,16,86,4	script	Armor Dealer#rockridge02	1_M_04,{
	if (checkweight(1301,3) == 0) {
		mes "[盔甲商人]";
		mes "你带的东西太多了，买不了东西。";
		mes "请先去减轻你的行李。";
		close;
	}
	.@day = gettime(DT_DAYOFWEEK);
	if (.@day == SUNDAY) {
		mes "[盔甲商人]";
		mes "我今天关门了。";
		mes "每周一次，我都会关门歇业，花一天的时间来感谢我所拥有的一切。";
		next;
		mes "[盔甲商人]";
		mes "......同一天没有送货。";
		close;
	}
	if (.@day == MONDAY)
		.@string$ = "Romantic Gents, Western Graces, and Feather Bonnet";
	else if (.@day == TUESDAY)
		.@string$ = "Sombreros, Indian Headbands, and Cowboy Hats";
	else if (.@day == WEDNESDAY)
		.@string$ = "Cigarettes, Pipes, and Grampa Beards";
	else if (.@day == THURSDAY)
		.@string$ = "Romantic Gents, Western Graces, and Feather Bonnet";
	else if (.@day == FRIDAY)
		.@string$ = "Sombreros, Indian Headbands, Cowboy Hats, and Indian Feather Headbands, the last of the rare costumes that are available in limited quantities every week,";
	else
		.@string$ = "Cigarettes, Pipes, and Grampa Beards";
	mes "[盔甲商人]";
	mes "你好。欢迎来到岩岭盔甲店。";
	mes "今天，" +.@string$ + "有货。";
	next;
	mes "[盔甲商人]";
	mes "盔甲比武器笨重，所以我每周只能交付一定数量的产品。";
	mes "我将向您展示可用项目的列表。 ^0000CD如果为空，则所有商品均已售完^000000。";
	next;
	mes "[盔甲商人]";
	mes "出于同样的原因，我无法给予^0000CD特别折扣^000000。";
	mes "但是^0000CD我的所有产品都很难在其他地方找到^000000。你为什么不看看他们呢？";
	close2;
	if (.@day == MONDAY || .@day == THURSDAY)
		callshop "armor_rockridge_1", 1;
	else if (.@day == TUESDAY)
		callshop "armor_rockridge_2", 1;
	else if (.@day == WEDNESDAY || .@day == SATURDAY)
		callshop "armor_rockridge_3", 1;
	else if (.@day == FRIDAY)
		callshop "armor_rockridge_4", 1;
	end;
	
OnSun0000:	// custom restock, no official info
	npcshopupdate "armor_rockridge_1",2247,0,10;
	npcshopupdate "armor_rockridge_1",2248,0,10;
	npcshopupdate "armor_rockridge_1",5018,0,10;
	npcshopupdate "armor_rockridge_2",5067,0,10;
	npcshopupdate "armor_rockridge_2",5071,0,10;
	npcshopupdate "armor_rockridge_2",5075,0,10;
	npcshopupdate "armor_rockridge_3",2267,0,10;
	npcshopupdate "armor_rockridge_3",2268,0,10;
	npcshopupdate "armor_rockridge_3",2241,0,10;
	npcshopupdate "armor_rockridge_4",5067,0,10;
	npcshopupdate "armor_rockridge_4",5071,0,10;
	npcshopupdate "armor_rockridge_4",5075,0,10;
	npcshopupdate "armor_rockridge_4",31144,0,3;
	end;
}

har_in01,34,84,3	shop	Tool Dealer#rockridge00	1_F_03,611:-1,645:-1,656:-1,601:-1,602:-1,1065:-1,610:-1
