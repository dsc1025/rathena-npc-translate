//===== rAthena Script =======================================
//= Mission WoE TE
//===== Description: =========================================
//= [Official Conversion]
//= Template for mission WoE TE
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

// This npc call the duplicate of pop_mission_TE npc in order to spawn the mobs mission.
-	script	#popswitch	-1,{
OnInit:
	if (strnpcinfo(4) == "") end;
	if (agitcheck3() == 1)
		donpcevent strnpcinfo(0) + "::OnCommandOn";
	else
		donpcevent strnpcinfo(0) + "::OnReset";
	end;

OnCommandOn:
	.@map$ = strnpcinfo(4);
	if (compare( .@map$,"te_alde" ))
		callsub S_Event, "alde0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnCommandOn";// #popa_alde0[1-5]::OnCommandOn
	else
		callsub S_Event, "prt0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnCommandOn";
	end;

OnReset:
	.@map$ = strnpcinfo(4);
	if (compare( .@map$,"te_alde" ))
		callsub S_Event, "alde0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnReset";
	else
		callsub S_Event, "prt0"+ charat( .@map$,getstrlen(.@map$)-1 ) + "::OnReset";
	end;

S_Event:
	.@string$ = getarg(0);
	donpcevent "#popa_"+ .@string$;
	donpcevent "#popb_"+ .@string$;
	donpcevent "#popc_"+ .@string$;
	donpcevent "#popd_"+ .@string$;
	end;
}

-	script	pop_mission_TE	-1,{
OnMyMobDead:
	.@map$ = strnpcinfo(4);
	if (mobcount( .@map$,strnpcinfo(0) +"::OnMyMobDead" ) < 1 && playerattached()) {// playerattached stop the script if killmonster / maprespawnguildid
		callsub S_Coord, .@item_id, .@mob_id, .@coord;
		for ( .@mtem = 0; .@mtem < 20; .@mtem++ )
			makeitem .@item_id,1, .@map$, rand( .@coord[0],.@coord[1] ), rand( .@coord[2],.@coord[3] );
		initnpctimer;
	}
	end;

OnTimer12000:
	stopnpctimer;
OnCommandOn:
	callsub S_Coord, .@item_id, .@mob_id, .@coord;
	monster strnpcinfo(4), rand( .@coord[0],.@coord[1] ), rand( .@coord[2],.@coord[3] ),"--ja--",.@mob_id,1,strnpcinfo(0) + "::OnMyMobDead";
	end;

OnReset:
	killmonster strnpcinfo(4),strnpcinfo(0) +"::OnMyMobDead";
	stopnpctimer;
	end;

S_Coord:
	if (compare( strnpcinfo(4),"te_alde" )) {
		set getarg(0), 7056;// Payroll_Of_Kafra
		set getarg(1), 2450;// THIEF_PORING
		.@hidden_name$ = strnpcinfo(2);

		if (.@hidden_name$ == "popa_alde01") setarray .@coord[0],178,234, 128,175;// area x1,x2, y1,y2
		else if (.@hidden_name$ == "popb_alde01") setarray .@coord[0],14,65, 186,222;
		else if (.@hidden_name$ == "popc_alde01") setarray .@coord[0],46,114, 37,105;
		else if (.@hidden_name$ == "popd_alde01") setarray .@coord[0],198,233, 5,41;
		else if (.@hidden_name$ == "popa_alde02") setarray .@coord[0],29,73, 169,211;
		else if (.@hidden_name$ == "popb_alde02") setarray .@coord[0],175,220, 136,184;
		else if (.@hidden_name$ == "popc_alde02") setarray .@coord[0],196,231, 7,40;
		else if (.@hidden_name$ == "popd_alde02") setarray .@coord[0],53,125, 11,80;
		else if (.@hidden_name$ == "popa_alde03") setarray .@coord[0],43,107, 212,264;
		else if (.@hidden_name$ == "popb_alde03") setarray .@coord[0],181,215, 150,187;
		else if (.@hidden_name$ == "popc_alde03") setarray .@coord[0],59,121, 63,123;
		else if (.@hidden_name$ == "popd_alde03") setarray .@coord[0],192,215, 24,51;
		else if (.@hidden_name$ == "popa_alde04") setarray .@coord[0],154,189, 34,101;
		else if (.@hidden_name$ == "popb_alde04") setarray .@coord[0],21,76, 64,125;
		else if (.@hidden_name$ == "popc_alde04") setarray .@coord[0],120,143, 200,219;
		else if (.@hidden_name$ == "popd_alde04") setarray .@coord[0],13,37, 199,222;
		else if (.@hidden_name$ == "popa_alde05") setarray .@coord[0],145,207, 47,78;
		else if (.@hidden_name$ == "popb_alde05") setarray .@coord[0],13,43, 87,116;
		else if (.@hidden_name$ == "popc_alde05") setarray .@coord[0],11,63, 177,230;
		else if (.@hidden_name$ == "popd_alde05") setarray .@coord[0],130,167, 191,225;
	}
	else {
		set getarg(0), 6592;// Small_Wooden_Chest
		set getarg(1), 2451;// BABY_STRANGE
		.@hidden_name$ = strnpcinfo(2);

		if (.@hidden_name$ == "popa_prt01") setarray .@coord[0],10,90, 10,80;
		else if (.@hidden_name$ == "popb_prt01") setarray .@coord[0],146,190, 64,118;
		else if (.@hidden_name$ == "popc_prt01") setarray .@coord[0],92,128, 170,204;
		else if (.@hidden_name$ == "popd_prt01") setarray .@coord[0],188,204, 180,204;
		else if (.@hidden_name$ == "popa_prt02") setarray .@coord[0],15,95, 162,225;
		else if (.@hidden_name$ == "popb_prt02") setarray .@coord[0],144,172, 152,175;
		else if (.@hidden_name$ == "popc_prt02") setarray .@coord[0],43,99, 23,62;
		else if (.@hidden_name$ == "popd_prt02") setarray .@coord[0],168,204, 24,58;
		else if (.@hidden_name$ == "popa_prt03") setarray .@coord[0],7,76, 48,110;
		else if (.@hidden_name$ == "popb_prt03") setarray .@coord[0],160,204, 32,81;
		else if (.@hidden_name$ == "popc_prt03") setarray .@coord[0],138,190, 183,232;
		else if (.@hidden_name$ == "popd_prt03") setarray .@coord[0],8,36, 202,227;
		else if (.@hidden_name$ == "popa_prt04") setarray .@coord[0],246,270, 233,275;
		else if (.@hidden_name$ == "popb_prt04") setarray .@coord[0],14,53,236,273;
		else if (.@hidden_name$ == "popc_prt04") setarray .@coord[0],13,96, 12,63;
		else if (.@hidden_name$ == "popd_prt04") setarray .@coord[0],259,283, 14,35;
		else if (.@hidden_name$ == "popa_prt05") setarray .@coord[0],17,95, 226,281;
		else if (.@hidden_name$ == "popb_prt05") setarray .@coord[0],243,287, 243,288;
		else if (.@hidden_name$ == "popc_prt05") setarray .@coord[0],16,66, 17,60;
		else if (.@hidden_name$ == "popd_prt05") setarray .@coord[0],205,282, 12,93;
	}
	for ( .@i = 0; .@i < 4; .@i++ )
		set getelementofarray( getarg(2),.@i ), .@coord[.@i];
	return;
}


-	script	Training_Instructor_TE	-1,{
	if (checkweight(1201,5) == 0) {
		mes "您的物品太多，无法继续任务。";
		mes "请减轻负担并重试。";
		close;
	}
	.@map$ = strnpcinfo(4);
	if (.@map$ == "te_alde_gld") {
		.@npc_name$ = "Mana";
		.@fortress$ = "Kafragarten";
		.@menu$ = callsub( S_Menu, .@quest_id,
			7510,"1 Suspicious Poring",
			7511,"10 Suspicious Poring",
			7512,"30 Suspicious Poring",
			7504,"Destroy 1 Emperium",
			7505,"Destroy 3 Emperiums",
			7506,"Destroy 5 Emperiums",
			7513,"10 Kafra's Paychecks",
			7514,"20 Kafra's Paychecks",
			7515,"30 Kafra's Paychecks" );
	}
	else {
		.@npc_name$ = "Rune";
		.@fortress$ = "Gloria";
		.@menu$ = callsub( S_Menu, .@quest_id,
			7501,"1 Suspicious Babee",
			7502,"10 Suspicious Babee",
			7503,"30 Suspicious Babee",
			7504,"Destroy 1 Emperium",
			7505,"Destroy 3 Emperiums",
			7506,"Destroy 5 Emperiums",
			7507,"10 Small Wooden Boxes",
			7508,"20 Small Wooden Boxes",
			7509,"30 Small Wooden Boxes" );
	}
	mes "["+ .@npc_name$ +"]";
	if (jobcanentermap("te_prtcas01") == 0) {
		mes "啊……你……";
		mes "根据 TE 规则，不幸的是，我不会命令你或要求你帮忙。";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "哎呀，既然这里只是初级职级和次级职级以及没有拓展等级的用户，那你没事的话就可以离开了吗？";
		close;
	}
	mes "您可以在TE地区完成一些任务。";
	mes "让我们看看，你想要什么？";
	next;
	switch( select( "接受/遵守任务", "任务是什么意思？", "我没有。" ) ) {
	case 1:
		mes "["+ .@npc_name$ +"]";
		if (BaseLevel < 70) {
			mes "嗯，很抱歉，任务是为^4d4dff70级^000000或更高等级的用户准备的。";
			mes "老实说，等级太低的话，任务是很难完成的。";
			close;
		}
		.@time_check = WoeTETimeStart(3600);
		.@siege_check = agitcheck3();
		mes "你已经知道你必须在一段时间内接受任务了吧？";
		if (.@time_check == 1 || .@siege_check == 1)
			mes "而且，你要记住，如果高级别和低级别的任务内容相同，你不能同时接受它们。";
		else {
			mes "不幸的是，您现在无法接受任务。";
			next;
			mes "["+ .@npc_name$ +"]";
			.@playtime_check2 = checkquest(7516,PLAYTIME);// A Break for Commercial Development
			if (.@playtime_check2 == 2) {
				erasequest 7516;
				mes "嗯，你休息了吗？";
				mes "好吧，这已经不重要了。";
			}
			mes "您只能查看已接受的任务的结果。";
		}
		next;
		.@s = select( .@menu$ + "取消" ) -1;
		mes "["+ .@npc_name$ +"]";
		if (.@s == 9) {
			mes "到时候见。";
			close;
		}

		.@main_quest = .@quest_id[.@s];
		deletearray .@quest_id[.@s],1;
		.@mission01 = isbegin_quest(.@main_quest);
		.@playtime_check = checkquest(.@main_quest,PLAYTIME);
		.@hunting_check = checkquest(.@main_quest,HUNTING);

		.@index = (.@s/3) *3;
		.@mission02 = isbegin_quest(.@quest_id[.@index]);
		.@mission03 = isbegin_quest(.@quest_id[.@index+1]);

		if (.@s == 0 || .@s == 1 || .@s == 2) {// Hunting suspicious quest
			if (.@map$ == "te_alde_gld")
				setarray .@mob_name$[0],"Porings","suspicious Porings";
			else
				setarray .@mob_name$[0],"Orc Babies","suspicious creatures";

			if (.@s == 0)
				.@num_kill = 1;
			else if (.@s == 1) {
				.@num_tool_box = 2;
				.@num_kill = 10;
				setarray .@reward[0],12873,2;
				setarray .@exp[0],20000,10000;
			}
			else if (.@s == 2) {
				.@num_tool_box = 3;
				.@num_kill = 30;
				setarray .@reward[0],12873,3;
				setarray .@exp[0],30000,15000;
			}

			if (.@mission01 == 1) {
				if (.@playtime_check == 0) {
					if (.@hunting_check == 2) {
						mes "你做的很及时。";
						mes "我不明白为什么"+ .@mob_name$[0] +"会出现在一天中的那个时间。";
						mes "不管怎样，干得好。";
						if (.@num_tool_box == 0) {
							erasequest .@main_quest;
							getitem 11558,5;// TE_White_Slim_Potion
							getexp 5000,2500;
							close;
						}
						next;
						mes "["+ .@npc_name$ +"]";
						mes "既然你做得很好，我就让你" + .@num_tool_box + "次打开我的工具箱。";
						mes "虽然你大多不会发现任何东西......";
						mes "这是一个奖励，所以即使你没有找到任何东西也不要抱怨。";
						next;
						mes "就像"+ .@npc_name$ +"说的，你穿过前面的盒子，然后……";
						erasequest .@main_quest;
						getitem .@reward[0],.@reward[1];
						getexp .@exp[0],.@exp[1];
						callsub( S_Rentbox, .@num_tool_box );
					}
					callsub( S_Cancelquest, .@main_quest, .@npc_name$, "This mission is to eliminate "+ .@mob_name$[1] +" that secretly appear at the fortress in "+ .@fortress$ +"." );
				}
				mes "时间到了。";
				mes "不幸的是，任务失败了！";
				mes "下一次吧！";
				erasequest .@main_quest;
				close;
			}
			if (.@time_check != 1 && .@siege_check != 1) {
				mes "您目前没有执行此任务。";
				mes "你能再检查一下吗？";
				close;
			}
			if (.@mission02 == 1 || .@mission03 == 1) {
				if (.@num_tool_box == 0) {
					mes "你已经在执行高级任务了！";
					mes "如果您已经在执行相同内容的高级任务，则无法接受低级任务！";
				}
				else {
					mes "你已经在执行消灭任务了！";
					mes "我希望你照顾好你已经拥有的东西，除非你想取消它。";
				}
				next;
				mes "["+ .@npc_name$ +"]";
				mes "只有这样才有意义，不是吗？";
				mes "你不这么认为吗？";
				close;
			}
			mes "我被告知"+ .@mob_name$[1] +"会在"+ .@fortress$ +".";
			mes "我希望您^4d4dff消除"+ .@num_kill +"或更多"+ .@mob_name$[1] +"^000000。";
			callsub( S_Acceptquest, .@main_quest, .@npc_name$ );
		}
		else if (.@s == 3 || .@s == 4 || .@s == 5) {// Breaking Emperiums
			if (.@s == 3) {
				.@num_tool_box = 1;
				.@num_kill = 1;
				setarray .@reward[0],12873,3;
				setarray .@exp[0],30000,10000;
			}
			else if (.@s == 4) {
				.@num_tool_box = 3;
				.@num_kill = 3;
				setarray .@reward[0],12873,9;
				setarray .@exp[0],90000,30000;
			}
			else if (.@s == 5) {
				.@num_tool_box = 5;
				.@num_kill = 5;
				setarray .@reward[0],12873,15;
				setarray .@exp[0],150000,50000;
			}
			if (.@mission01 == 1) {
				if (.@playtime_check == 0) {
					if (.@hunting_check == 2) {
						mes "极好的！";
						mes "打破帝国绝对不是一件容易的事。";
						mes "你一定为你的公会做出了很多贡献。好工作！";
						next;
						mes "["+ .@npc_name$ +"]";
						mes "既然你做得很好，我就让你"+ .@num_tool_box +"次打开我的工具箱。";
						mes "虽然你大多不会发现任何东西......";
						mes "这是一个奖励，所以即使你没有找到任何东西也不要抱怨。";
						next;
						mes "就像"+ .@npc_name$ +"说的，你穿过前面的盒子，然后……";
						erasequest .@main_quest;
						setquest 7516;
						getitem .@reward[0],.@reward[1];
						getexp .@exp[0],.@exp[1];
						callsub( S_Rentbox, .@num_tool_box );
					}
					callsub( S_Cancelquest, .@main_quest, .@npc_name$, "This mission is to ^4d4dff destroy Emperiums that exist all over Gloria and Kafragarten regions.^000000" );
				}
				mes "时间到了。";
				mes "不幸的是，任务失败了！";
				mes "下一次吧！";
				erasequest .@main_quest;
				close;
			}
			if (.@time_check != 1 && .@siege_check != 1) {
				mes "您目前没有执行此任务。";
				mes "你能再检查一下吗？";
				close;
			}
			if (.@mission02 == 1 || .@mission03 == 1) {
				mes "你已经在执行相同内容的任务，";
				mes "虽然水平不一样";
				mes "我希望你照顾好你已经拥有的东西，除非你想取消它。";
				next;
				mes "["+ .@npc_name$ +"]";
				mes "只有这样才有意义，不是吗？";
				mes "你不这么认为吗？";
				close;
			}
			.@playtime_check2 = checkquest(7516,PLAYTIME);
			if (.@playtime_check2 == 0) {
				mes "每个要塞里都有一个叫做‘商业发展水平’的东西。";
				mes "每个公会投资多少，它就会增加多少。";
				next;
				mes "["+ .@npc_name$ +"]";
				mes "然而，这与Emperiums密切相关。";
				mes "不断摧毁帝国会扰乱商业发展水平。";
				next;
				mes "["+ .@npc_name$ +"]";
				mes "所以，现在我们休息一下，防止商业发展水平过度下降。";
				mes "一会儿见。";
				close;
			}
			else if (.@playtime_check2 == 2) {
				erasequest 7516;
				mes "要塞的商业开发间歇期现已结束。";
				mes "好的。想想你想要什么样的任务，然后再回来。";
				close;
			}
			mes "Error 500 (Server Error)!!1500.That’s an error.There was an error. Please try again later.That’s all we know.";
			mes "它正在摧毁帝国！让我给你摧毁帝国的确切任务！";
			next;
			mes "["+ .@npc_name$ +"]";
			mes "Gloria 或 Kafragarten，无论它在哪里！现在，就在这个时刻，^4d4dff摧毁帝国！^000000";
			mes "^4d4dff目标编号为"+ .@num_kill +"或更多！^000000";
			callsub( S_Acceptquest, .@main_quest, .@npc_name$ );
		}
		else if (.@s == 6 || .@s == 7 || .@s == 8) {
			if (.@map$ == "te_alde_gld") {
				.@item_quest = 7056;// Payroll_Of_Kafra
				setarray .@string$[0], "paychecks", "I don't understand why Porings would steal Kafra's Paychecks", "Kafra's stolen paychecks";
			}
			else {
				.@item_quest = 6592;// Small_Wooden_Chest
				setarray .@string$[0], "wooden boxes", "So, those Orc Babies were after this, huh?", "wooden boxes";
			}
			if (.@s == 6) {
				.@num_tool_box = 1;
				.@num_items = 10;
				setarray .@reward[0],12873,1;
				setarray .@exp[0],10000,5000;
			}
			else if (.@s == 7) {
				.@num_tool_box = 2;
				.@num_items = 20;
				setarray .@reward[0],12873,2;
				setarray .@exp[0],20000,10000;
			}
			else if (.@s == 8) {
				.@num_tool_box = 3;
				.@num_items = 30;
				setarray .@reward[0],12873,3;
				setarray .@exp[0],30000,15000;
			}

			if (.@mission01 == 1) {
				if (.@playtime_check == 0) {
					if (countitem(.@item_quest) > 9) {
						mes .@num_items +"或更多"+ .@string$[0] +" 让我们看看您是否做对了。";
						mes .@string$[1];
						mes "不管怎样，干得好。";
						next;
						mes "["+ .@npc_name$ +"]";
						mes "让我来谈谈"+ .@string$[0] +".";
						mes "我会把它还给它原来的主人。";
						next;
						mes "["+ .@npc_name$ +"]";
						mes "既然你做得很好，我就让你"+ .@num_tool_box +"次打开我的工具箱。";
						mes "虽然你大多不会发现任何东西......";
						mes "这是一个奖励，所以即使你没有找到任何东西也不要抱怨。";
						next;
						mes "就像"+ .@npc_name$ +"说的，你穿过前面的盒子，然后……";
						delitem .@item_quest, countitem(.@item_quest);
						erasequest .@main_quest;
						getitem .@reward[0],.@reward[1];
						getexp .@exp[0],.@exp[1];
						callsub( S_Rentbox, .@num_tool_box );
					}
					callsub( S_Cancelquest, .@main_quest, .@npc_name$, "This mission is to ^4d4dffcollect "+ .@string$[2] +"^000000 at the fortress in "+ .@fortress$ +"." );
				}
				mes "时间到了。";
				mes "不幸的是，任务失败了！";
				mes "下一次吧！";
				erasequest .@main_quest;
				close;
			}
			if (.@time_check != 1 && .@siege_check != 1) {
				mes "您目前没有执行此任务。";
				mes "你能再检查一下吗？";
				close;
			}
			if (.@mission02 == 1 || .@mission03 == 1) {
				mes "你已经在执行相同内容的任务，";
				mes "虽然水平不一样";
				mes "我希望你照顾好你已经拥有的东西，除非你想取消它。";
				next;
				mes "["+ .@npc_name$ +"]";
				mes "只有这样才有意义，不是吗？";
				mes "你不这么认为吗？";
				close;
			}
			mes "嗯，你可能不会相信，但这个任务与盗窃有关。";
			mes "他们告诉我"+ .@string$[2] +"的堡垒里缺少一堆"+ .@fortress$ +".";
			next;
			mes "["+ .@npc_name$ +"]";
			mes "窃贼很可能利用了围攻时间。";
			mes "这是查找 ^4d4dff"+ .@num_items +" 或更多 "+ .@string$[2] +"^000000 的请求。";
			callsub( S_Acceptquest, .@main_quest, .@npc_name$ );
		}
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "由于TE地区的氛围比较休闲，";
		mes "仅仅攻城战并不好玩，对吧？";
		mes "所以，我带来了^4d4dff攻城期间可以做的一些作品^000000>~";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "你可以接受任务^4d4dff从攻城开始前1小时开始，直到攻城战TE^000000结束，";
		mes "每个任务都有^4d4dff 4 小时的时间限制^000000 完成。";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "简单来说，你在攻城期间接受我的任务，";
		mes "^4d4dff限时回来给我报告^000000。";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "尝试一下吧。";
		close;
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "我懂了。";
		mes "如果您改变主意，请随时回来。";
		mes "不过，我可能没空。呵呵。";
		close;
	}

S_Cancelquest:
	.@npc_name$ = getarg(1);
	if (WoeTETimeStart(3600) == 1 || agitcheck3()) {
		mes "看来你的任务还没有完成？";
		mes getarg(2);
		next;
		mes "["+ .@npc_name$ +"]";
		mes "您想取消吗？";
		next;
		switch( select( "不。","是的。" ) ) {
		case 1:
			mes "["+ .@npc_name$ +"]";
			mes "祝你好运！";
			close;
		case 2:
			mes "["+ .@npc_name$ +"]";
			mes "如你所愿，这个任务被取消了。";
			erasequest getarg(0);
			close;
		}
	}
	else {
		mes "嗯，你还有一些时间，但现在还不是攻城时刻";
		mes "我认为你不应该继续下去。";
		mes "你应该取消这个任务。我会说它失败了。";
		next;
		switch( select( "我想继续。","是的，我会取消它。" ) ) {
		case 1:
			mes "["+ .@npc_name$ +"]";
			mes "我认为这不值得。好吧，祝你好运！";
			close;
		case 2:
			mes "["+ .@npc_name$ +"]";
			mes "如你所愿，这个任务被取消了。";
			erasequest getarg(0);
			close;
		}
	}

S_Acceptquest:
	.@npc_name$ = getarg(1);
	next;
	switch( select( "接受它。","不接受它。" ) ) {
	case 1:
		mes "["+ .@npc_name$ +"]";
		mes "^4d4dff时间限制为4小时。^000000";
		mes "到时候完成任务并向我汇报。";
		setquest getarg(0);
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "然后尝试另一个任务。";
		close;
	}

S_Rentbox:
	setarray .@item_id[0], 2950,15067,20709,22012;// Rune_Ring, Rune_Suit, Mana_Manteau, Mana_Boots
	for ( .@count = 0; .@count < getarg(0); .@count++ ) {
		.@miss_item01 = rand(100);
		if (.@miss_item01 > 3)
			mes "你什么也没找到。";
		else {
			rentitem .@item_id[ .@miss_item01 ],86400;
			mes "您找到了“"+ getitemname(.@item_id[ .@miss_item01 ]) +"！”";
		}
	}
	close;

S_Menu:
	for ( .@i = 1; .@i < getargcount(); .@i += 2 ) {
		if ( isbegin_quest(getarg(.@i)) )
			.@menu$ = .@menu$ + getarg(.@i+1) + " ^4d4dffCheck the result^000000:";
		else
			.@menu$ = .@menu$ + getarg(.@i+1) + ":";
		set getelementofarray( getarg(0),.@index ), getarg(.@i);
		.@index++;
	}
	return .@menu$;
}
