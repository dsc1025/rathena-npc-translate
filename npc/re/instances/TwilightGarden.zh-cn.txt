//===== rAthena Script =======================================
//= Twilight Garden
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.2 Twilight Garden
//= Episode 17.2 Hey Sweety
//= Note:
// - NPC_LOCKON_LASER skill is currently no implemented.
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//= 1.2 Cleanup [Capuche]
//===== Translated By: =======================================
//= dsc
//============================================================

1@bamn,1,1,0	script	#twilight_garden_control	-1,{
	end;
OnInstanceInit:
	'twilight_story = 0;
	'sweety = 0;
	'map_bamn$ = instance_mapname("1@bamn");
	'map_bamq$ = instance_mapname("1@bamq");

	// npcs from story
	//----------------------------
	// npcs on 1@bamn

	// disablenpc instance_npcname("Repeater#wifi03");	// (not disabled)
	// disablenpc instance_npcname("Repeater#wifi02");	// (not disabled)
	// disablenpc instance_npcname("Repeater#wifi01");	// (not disabled)

	//----------------------------
	// Daily: Hey! Sweety
	if (instance_live_info(ILI_NAME) == "Hey! Sweety")
		disablenpc instance_npcname("#bamn_evt01");	// story starter npc
	else
		disablenpc instance_npcname("#sweety_evt01");	// daily starter npc
	end;
}

// Story
1@bamn,100,320,0	script	#bamn_evt01	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	if (!is_party_leader())
		end;
	disablenpc();
	enablenpc instance_npcname("Est#est01");
	end;
}

1@bamn,96,318,5	script(DISABLED)	Est#est01	4_F_ESTLOVELOY,{
	if (!is_party_leader())
		end;
	if ('twilight_story == 0) {
		'twilight_story = 1;
		npctalk "Est：天又黑又安静。如果你想执行计划，现在就是最佳时机。正确的？";
		sleep 2000;
		npctalk "Est ：埃琳娜说她稍后会加入我们，但她很可能无法加入。";
		sleep 2000;
		npctalk "Est：即便如此，也不必担心。其他人都做好了准备，等待着埋伏的信号。";
		sleep 2000;
		npctalk "Est：我们所要做的就是像巡逻队一样四处走动，让他们措手不及。";
		sleep 2000;
		npctalk "东部：沿着道路前往宅邸的主楼。我会潜伏在你身后。";
		sleep 2000;
		npctalk "Est：别东张西望，假装不警惕，明白了吗？";
		sleep 2000;
		npctalk "艾斯特：现在，我们走吧。";
		for ( .@i = 1; .@i < 6; ++.@i )
			enablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
		end;
	}
	if ('twilight_story == 1)
		end;
	if ('twilight_story == 2) {
		cutin "ep162_est01",2;
		mes "[是]";
		mes "他们一定也在等待。";
		mes "让我们更安静地做这件事。";
		close3;
	}
	end;
}

1@bamn,119,299,3	script(DISABLED)	Heart Hunter#md_hh01	G_EP17_2_HEART_HUNTER,3,3,{
	end;
OnTouch_:
	if (!is_party_leader())
		end;
	if ('twilight_story == 1) {
		'twilight_story = 2;
		npctalk "默默投降或者回到你来的地方。";
		sleep 2000;
		npctalk "嗯，这并不意味着我们会让你回去。";
		sleep 1000;
		for ( .@i = 1; .@i < 6; ++.@i )
			disablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
		donpcevent instance_npcname("twilight_story_mob_1") + "::OnStart";
	}
	end;
}

1@bamn,119,305,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh02	G_EP17_2_HEART_HUNTER
1@bamn,119,302,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh03	G_EP17_2_HEART_HUNTER
1@bamn,119,296,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh04	G_EP17_2_HEART_HUNTER
1@bamn,119,293,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh05	G_EP17_2_HEART_HUNTER


1@bamn,1,1,0	script	twilight_story_mob_1	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("twilight_story_mob_1") + "::OnMobDead";
	monster 'map_bamn$,119,308,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,128,308,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,121,300,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,118,293,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,124,294,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,130,308,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	end;
OnMobDead:
	.@event$ = instance_npcname("twilight_story_mob_1") + "::OnMobDead";
	if (mobcount('map_bamn$, .@event$) < 1) {
		disablenpc instance_npcname("Est#est01");
		enablenpc instance_npcname("Est#est02");
		if ('twilight_story == 2)
			'twilight_story = 3;
	}
	end;
}

1@bamn,119,299,5	script(DISABLED)	Est#est02	4_F_ESTLOVELOY,{
	if (!is_party_leader())
		end;
	if ('twilight_story == 3) {
		cutin "ep162_est01",2;
		mes "[是]";
		mes "我不知道是该设个陷阱，还是给你一个机会。";
		mes "激活我们从 Elyumina 获得的追踪器。";
		next;
		cutin "",255;
		unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : According to the intuition of the great Elyumina... Too Doo Too Doo Too~";
		sleep2 2000;
		unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Over there!! Beep!";
		navigateto("1@bamn",206,273);
		sleep2 2000;
		cutin "ep162_est01",2;
		mes "[是]";
		mes "这是一件响亮的事情。";
		mes "你要先搬出去吗？";
		mes "我有一个地方要去一会。";
		next;
		mes "[是]";
		mes "我很快就会加入你们。";
		close2;
		if ('twilight_story == 3) {
			'twilight_story = 4;
			donpcevent instance_npcname("twilight_story_mob_2") + "::OnStart";
		}
		cutin "",255;
		disablenpc();
		end;
	}
	end;
}

1@bamn,1,1,0	script	twilight_story_mob_2	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("twilight_story_mob_2") + "::OnMobDead";
	monster 'map_bamn$,208,283,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,210,285,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,210,282,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,213,284,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,213,281,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	end;
OnMobDead:
	.@event$ = instance_npcname("twilight_story_mob_2") + "::OnMobDead";
	if (mobcount('map_bamn$, .@event$) < 1) {
		enablenpc instance_npcname("Est#est03");
		enablenpc instance_npcname("Almond#amond01");
		if ('twilight_story == 4)
			'twilight_story = 5;
	}
	end;
}

1@bamn,202,274,5	script(DISABLED)	Est#est03	4_F_ESTLOVELOY,{
	if ('twilight_story == 5) {
		cutin "ep162_est01",2;
		mes "[是]";
		mes "我想还是多扔些诱饵比较好，于是就去找自动娃娃帮忙。";
		mes "另外，阿尔蒙德说她必须出去检查一下。";
		next;
		cutin "ep172_beta",0;
		mes "[杏仁]";
		mes "中继器又死了。这就是为什么我必须解决它。";
		mes "中继器是入侵者最喜欢的目标，所以我们不能让它坏掉，对吧？";
		next;
		cutin "ep162_est01",2;
		mes "[是]";
		mes "因此，我想请你护送阿尔蒙来这里检查中继器。";
		mes "我会环顾四周并寻找其他路线。";
		next;
		cutin "ep172_beta",0;
		mes "[杏仁]";
		mes "只要你准备好了，我们就一起去吧。";
		close2;
		cutin "",255;
		if ('twilight_story == 5)
			'twilight_story = 6;
		end;
	}
	if ('twilight_story == 6) {
		cutin "ep162_est01",2;
		mes "[是]";
		mes "当你准备好移动时，与阿尔蒙德交谈。";
		close3;
	}
	end;
}

1@bamn,199,275,5	script(DISABLED)	Almond#amond01	EP17_2_BETA_BASIC,{
	if ('twilight_story < 6) {
		cutin "ep172_beta",2;
		mes "[杏仁]";
		mes "今天所有人都应该在宅邸里。";
		mes "有重要的事情发生了。这就是我和埃斯特一起来这里的原因。";
		close3;
	}
	if ('twilight_story == 6) {
		'guide = getnpcid(0);
		'twilight_story = 7;
		npcspeed 200;
		npctalk "那么，我们走吧。";
		unitwalk 'guide,199,248, instance_npcname("Almond#amond01") + "::OnEvent00";
		enablenpc instance_npcname("Intruder#sweety02");
		end;
	}
	if ('twilight_story == 7)
		end;
	if ('twilight_story == 8) {
		'twilight_story = 9;
		unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Pyo-o-o-o-o! There!!";
		setpcblock PCBLOCK_NPC, true;
		navigateto("1@bamn",242,206);
		sleep2 2000;
		npctalk "啊哈哈哈，太吵了";
		sleep2 2000;
		npctalk "我很好奇，我们要不要快点走？";
		sleep2 2000;
		npctalk "客人，有入侵者！";
		donpcevent instance_npcname("twilight_story_mob_3") + "::OnStart";
		setpcblock PCBLOCK_NPC, false;
		sleep 200;
		npcspeed 80;
		unitwalk 'guide,245,229, instance_npcname("Almond#amond01") + "::OnEvent02";
		end;
	}
	if ('twilight_story == 10 || 'twilight_story == 11 || 'twilight_story == 12) {
		npctalk "当心！";
		end;
	}
	if ('twilight_story == 13) {
		'twilight_story = 14;
		npcspeed 200;
		setpcblock PCBLOCK_NPC, true;
		npctalk "我们去检查一下中继器吧。";
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		sleep2 1000;
		unitwalk 'guide,244,208, instance_npcname("Almond#amond01") + "::OnEvent04";
		end;
	}
	if ('twilight_story == 14)
		end;
	if ('twilight_story == 15) {
		'twilight_story = 16;
		setpcblock PCBLOCK_NPC, true;
		for ( .@i = 1; .@i < 5; ++.@i )
			enablenpc instance_npcname("Rebellion#md_rb0" + .@i);
		for ( .@i = 6; .@i < 9; ++.@i )
			enablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
		unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Kakakakakakakil! The culprit is around! They're on my radar! It's that way!";
		navigateto("1@bamn",332,143);
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		sleep2 1000;
		npcspeed 80;
		npctalk "还有一个入侵者！难怪我开始兴奋了~！";
		unitwalk 'guide,332,169;
		end;
	}
	if ('twilight_story < 18)
		end;
	if ('twilight_story == 18) {
		npcspeed 200;
		'twilight_story = 19;
		npctalk "你还好吗，冒险家？然后，我们就可以继续前进了。";
		unitwalk 'guide,332,143, instance_npcname("Almond#amond01") + "::OnEvent11";
		end;
	}
	if ('twilight_story == 19)
		end;
	if ('twilight_story == 20) {
		npctalk "你不应该和艾斯特谈谈吗？看来她已经等了很久了。";
		end;
	}
	end;

OnEvent00:
	npctalk "这边走。";
	sleep 1000;
	unitwalk 'guide,225,248, instance_npcname("Almond#amond01") + "::OnFollow00";
	end;
OnFollow00:
	unitwalk 'guide,245,248, instance_npcname("Almond#amond01") + "::OnEvent01";
	end;
OnEvent01:
	npctalk "我们去看看经常被黑客攻击的Repeater。";
	sleep 2000;
	npctalk "啊？你现在带的是什么？把它拿出来给我看。";
	'twilight_story = 8;
	end;

OnEvent02:
	npctalk "冒险家，有入侵者！";
	unitwalk 'guide,245,215, instance_npcname("Almond#amond01") + "::OnEvent03";
	end;
OnEvent03:
	'twilight_story = 10;
	end;

OnEvent04:
	npctalk "...检查信号。";
	sleep 2000;
	npctalk "...黑客通道已检查。";
	sleep 2000;
	npctalk "...正在恢复...";
	sleep 2000;
	npctalk "...正在恢复...84%";
	sleep 2000;
	npctalk "...恢复完成。";
	sleep 2000;
	npctalk "我们去复读机那里好吗？";
	sleep 2000;
	unitwalk 'guide,245,204, instance_npcname("Almond#amond01") + "::OnEvent05";
	end;
OnEvent05:
	npctalk "冒险家。你喜欢杏仁吗？一位喜欢杏仁的顾客给我起了名字。适合我吗？";
	sleep 3000;
	unitwalk 'guide,265,204, instance_npcname("Almond#amond01") + "::OnEvent06";
	end;
OnEvent06:
	npctalk "那个小子不久前，我见过他。";
	unitwalk 'guide,285,204, instance_npcname("Almond#amond01") + "::OnEvent07";
	end;
OnEvent07:
	npctalk "但他总是跑得那么好，我还是第一次见到他正常的样子~";
	unitwalk 'guide,300,204, instance_npcname("Almond#amond01") + "::OnEvent08";
	end;
OnEvent08:
	npctalk "这里的火龙果很可爱~他们说杏仁很好吃？你以前吃过杏仁吗？";
	sleep 2000;
	npctalk "这边走。";
	unitwalk 'guide,300,190, instance_npcname("Almond#amond01") + "::OnEvent09";
	end;
OnEvent09:
	npctalk "好久没有这么舒服了~";
	unitwalk 'guide,327,189, instance_npcname("Almond#amond01") + "::OnEvent10";
	end;
OnEvent10:
	npctalk "冒险家。那个吵闹的东西又闪烁了吗？让我们检查一下。";
	'twilight_story = 15;
	end;

OnEvent11:
	npctalk "...检查信号。";
	sleep 2000;
	npctalk "...黑客通道已检查。";
	sleep 2000;
	npctalk "...正在恢复...";
	sleep 2000;
	npctalk "...正在恢复...72%";
	sleep 2000;
	npctalk "...恢复完成。";
	sleep 2000;
	npctalk "我们去下一个复读机吧~";
	sleep 2000;
	unitwalk 'guide,320,139, instance_npcname("Almond#amond01") + "::OnFollow12";
	end;
OnFollow12:
	unitwalk 'guide,300,139, instance_npcname("Almond#amond01") + "::OnEvent12";
	end;
OnEvent12:
	npctalk "是之前那个孩子吧？他是一个入侵者。很奇怪，有点像杏仁……";
	unitwalk 'guide,285,139, instance_npcname("Almond#amond01") + "::OnEvent13";
	end;
OnEvent13:
	npctalk "我们要走快一点吗？有声音从那边传来……";
	unitwalk 'guide,285,118, instance_npcname("Almond#amond01") + "::OnEvent14";
	end;
OnEvent14:
	npctalk "比我想象的还要安静。入侵者去哪儿了？";
	unitwalk 'guide,285,98, instance_npcname("Almond#amond01") + "::OnEvent15";
	end;
OnEvent15:
	npctalk "这边走。";
	unitwalk 'guide,261,98, instance_npcname("Almond#amond01") + "::OnEvent16";
	end;
OnEvent16:
	npcspeed 80;
	npctalk "Error 500 (Server Error)!!1500.That’s an error.There was an error. Please try again later.That’s all we know.";
	'twilight_story = 20;
	unitwalk 'guide,261,87;
	end;
}

1@bamn,1,1,0	script	twilight_story_mob_3	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("twilight_story_mob_3") + "::OnMobDead";
	monster 'map_bamn$,246,208,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,248,208,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,246,206,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	monster 'map_bamn$,248,206,"Heart Hunter","G_EP17_2_HEART_HUNTER",1, .@event$;
	enablenpc instance_npcname("Intruder#sweety02");
	end;
OnMobDead:
	.@event$ = instance_npcname("twilight_story_mob_3") + "::OnMobDead";
	if (mobcount('map_bamn$, .@event$) < 1) {
		if ('twilight_story == 10)
			'twilight_story = 11;
	}
	end;
}

1@bamn,243,208,5	script	Repeater#wifi03	CLEAR_NPC,{
	mes "没有办法知道中继器的状态是什么。通讯芯片似乎已正确安装。";
	close;
}

1@bamn,242,206,7	script(DISABLED)	Intruder#sweety02	4_EP17_SWEETY,{
	if ('twilight_story < 11) {
		npctalk "你就落后一步了！你们这群白痴！";
		end;
	}
	if ('twilight_story == 11) {
		'twilight_story = 12;
		npctalk "哈！你觉得你能抓住我吗？";
		sleep 2000;
		npctalk "去受苦吧！";
		sleep 500;
		disablenpc();
		donpcevent instance_npcname("twilight_story_mob_4") + "::OnStart";
		end;
	}
	end;
}

1@bamn,1,1,0	script	twilight_story_mob_4	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("twilight_story_mob_4") + "::OnMobDead";
	monster 'map_bamn$,248,206,"Heart Hunter Commander","G_BELLARE3",1, .@event$;
	monster 'map_bamn$,248,208,"Heart Hunter Commander","G_BELLARE3",1, .@event$;
	end;
OnMobDead:
	.@event$ = instance_npcname("twilight_story_mob_4") + "::OnMobDead";
	if (mobcount('map_bamn$, .@event$) < 1) {
		if ('twilight_story == 12)
			'twilight_story = 13;
	}
	end;
}


1@bamn,331,161,3	script(DISABLED)	Rebellion#md_rb02	4_F_REBELLION,7,7,{
	end;
OnTouch:
	if ('twilight_story == 16) {
		'twilight_story = 17;
		npctalk "我们会接管这个地方，等着吧！";
		sleep 1000;
		specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh06");
		sleep 1000;
		npctalk "卡卡卡，让我们用爆炸来击败他们吧！", instance_npcname("叛乱#md_rb04");
		specialeffect EF_TRIPLEACTION, AREA, instance_npcname("Heart Hunter#md_hh07");
		sleep 1000;
		specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh07");
		sleep 2000;
		npctalk "感谢你们展现自己！你们这些混蛋！", instance_npcname("叛乱#md_rb03");
		sleep 2000;
		specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh08");
		sleep 2000;
		npctalk "...", instance_npcname("心脏猎手#md_hh07");
		sleep 2000;
		npctalk "...（点头）", instance_npcname("心脏猎手#md_hh06");
		sleep 1000;
		npctalk "...", instance_npcname("心脏猎手#md_hh08");
		sleep 1000;
		npctalk "你们向对方发出什么信号？";
		sleep 1000;
		specialeffect EF_DESPERADO, AREA, instance_npcname("Heart Hunter#md_hh07");
		for ( .@i = 1; .@i < 5; ++.@i )
			specialeffect EF_SPREADATTACK, AREA, instance_npcname("Rebellion#md_rb0" + .@i);
		sleep 1000;
		for ( .@i = 6; .@i < 9; ++.@i )
			disablenpc instance_npcname("Heart Hunter#md_hh0" + .@i);
		sleep 1000;
		npctalk "什么？他们跑了？！搬出去吧！";
		sleep 2000;
		for ( .@i = 1; .@i < 5; ++.@i )
			disablenpc instance_npcname("Rebellion#md_rb0" + .@i);
		enablenpc instance_npcname("Intruder#sweety03");
		npctalk "什么...？", instance_npcname("入侵者#sweety03");
	}
	end;
}
1@bamn,321,161,5	duplicate(dummy_disabled_npc)	Rebellion#md_rb01	4_M_REBELLION
1@bamn,331,155,1	duplicate(dummy_disabled_npc)	Rebellion#md_rb03	4_M_REBELLION
1@bamn,321,155,7	duplicate(dummy_disabled_npc)	Rebellion#md_rb04	4_F_REBELLION
1@bamn,324,158,7	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh06	G_EP17_2_HEART_HUNTER
1@bamn,326,159,3	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh07	G_EP17_2_HEART_HUNTER
1@bamn,328,158,1	duplicate(dummy_disabled_npc)	Heart Hunter#md_hh08	G_EP17_2_HEART_HUNTER


1@bamn,333,141,7	script(DISABLED)	Intruder#sweety03	4_EP17_SWEETY,{
	if ('twilight_story == 17) {
		setpcblock PCBLOCK_NPC, true;
		'twilight_story = 18;
		npctalk "什么，其他人去哪儿了？";
		sleep2 2000;
		npctalk "你做到了吗？";
		sleep2 2000;
		npctalk "你很不错，不是吗？";
		sleep2 2000;
		npctalk "复读机：（.......）", instance_npcname("中继器#wifi02");
		npctalk "哦，有消息。在那里呆一会儿。是的，你好。";
		sleep2 2000;
		npctalk "复读机：（... ??... ??）", instance_npcname("中继器#wifi02");
		sleep2 2000;
		npctalk "现在？更多时间...是的，不...啊...";
		sleep2 2000;
		npctalk "... ...到目前为止...没有？！...好吧...";
		sleep2 2000;
		emotion ET_THINK;
		sleep2 2000;
		npctalk "你！你就是我现在没时间的原因！下次见面我就杀了你！";
		sleep2 2000;
		disablenpc();
		enablenpc instance_npcname("Est#est04");
		for ( .@i = 5; .@i < 9; ++.@i )
			enablenpc instance_npcname("Rebellion#md_rb0" + .@i);
		for ( .@i = 0; .@i < 3; ++.@i )
			enablenpc instance_npcname("Heart Hunter#md_hh1" + .@i);
		setpcblock PCBLOCK_NPC, false;
	}
	end;
}
1@bamn,334,143,5	duplicate(dummy_npc)	Repeater#wifi02	2_POSTBOX


1@bamn,257,84,3	script(DISABLED)	Est#est04	4_F_ESTLOVELOY,{
	if ('twilight_story == 20) {
		cutin "ep162_est01",2;
		mes "[是]";
		mes "我听到了报道。你找到了一个看起来像领导的人吗？";
		mes "我觉得其他成员也都不错。";
		next;
		mes "[是]";
		mes "复读机怎么了？";
		next;
		cutin "ep172_beta",2;
		mes "[杏仁]";
		mes "我们正在前往另一处的路上。";
		mes "但距离这里还蛮远的。";
		next;
		cutin "ep162_est01",2;
		mes "[是]";
		mes "嗯...我们要这样做吗？";
		mes "你错过的那个人是一个紫色头发的男孩，对吗？";
		mes "我们要引诱他出去。";
		next;
		mes "[是]";
		mes "我会处理掉躲在花园里的其他人。";
		mes "啊。我也来护送阿蒙德。";
		next;
		mes "[是]";
		mes "他应该去了宅子的西边，所以就往那边走吧。";
		mes "你有 Elyumina 的追踪器。如果你靠近他，它就会激活。";
		if ('twilight_story == 20) {
			'twilight_story = 21;
			enablenpc instance_npcname("#to_bamq");
			enablenpc instance_npcname("Intruder#sweety");
			enablenpc instance_npcname("#to_swty01");
			enablenpc instance_npcname("#to_swty02");
		}
		close3;
	}
	if ('twilight_story == 21) {
		mes "[是]";
		mes "他应该去了宅子的西边，所以就往那边走吧。";
		mes "你有Elyumina的追踪器，如果你靠近他，它就会激活。";
		next;
		mes "[是]";
		mes "别担心，我会在这里照顾阿蒙德的。";
		close3;
	}
	end;
}

1@bamn,250,80,7	script(DISABLED)	Rebellion#md_rb05	4_M_REBELLION,{
	npctalk "剩下的就交给我们吧。";
	end;
}

// 1@bamn,254,80,7	script	Rebellion#md_rb06	4_F_REBELLION,7,7,{	// unknown effect
1@bamn,254,80,7	script(DISABLED)	Rebellion#md_rb06	4_F_REBELLION,{
	npctalk "嘿嘿嘿……终于到了报仇的时候了……嘿嘿嘿……";
	end;
}

1@bamn,258,80,1	script(DISABLED)	Rebellion#md_rb07	4_M_REBELLION,{
	npctalk "如果有敌人闹事，我们就应该用更多的兵力攻击他们。";
	end;
}

1@bamn,202,84,3	script(DISABLED)	Rebellion#md_rb08	4_M_REBELLION2,{
	npctalk "你正在寻找一个紫色头发的男孩，对吧？他朝实验室走去。";
	end;
}


1@bamn,67,173,0	script(DISABLED)	#to_swty01	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	if ('twilight_story == 21) {
		unittalk getcharid(3), "" + strcharinfo(0) + " : Tracker : Pyo-o-o-o-o! That way! Pyo-pyo~!";
		navigateto("1@bamn",33,224);
	}
	end;
}

1@bamn,121,204,0	duplicate(#to_swty01)	#to_swty02	HIDDEN_WARP_NPC,7,7

1@bamn,67,195,3	script(DISABLED)	Heart Hunter#md_hh10	G_BELLARE3,5,5,{
	end;
OnTouch:
	monster 'map_bamn$,67,195,"Heart Hunter Commander","G_BELLARE3",1;
	monster 'map_bamn$,65,199,"Heart Hunter Commander","G_BELLARE3",1;
	monster 'map_bamn$,71,197,"Heart Hunter Commander","G_BELLARE3",1;
	disablenpc();
	end;
}

1@bamn,78,204,5	script(DISABLED)	Heart Hunter#md_hh11	G_BELLARE3,5,5,{
	end;
OnTouch:
	emotion ET_GO;
	monster 'map_bamn$,78,204,"Heart Hunter Commander","G_BELLARE3",1;
	monster 'map_bamn$,83,204,"Heart Hunter Commander","G_BELLARE3",1;
	monster 'map_bamn$,76,200,"Heart Hunter Commander","G_BELLARE3",1;
	disablenpc();
	end;
}

1@bamn,61,214,7	script(DISABLED)	Heart Hunter#md_hh12	G_BELLARE3,5,5,{
	end;
OnTouch:
	emotion ET_GO;
	monster 'map_bamn$,61,214,"Heart Hunter Commander","G_BELLARE3",1;
	monster 'map_bamn$,58,212,"Heart Hunter Commander","G_BELLARE3",1;
	monster 'map_bamn$,64,214,"Heart Hunter Commander","G_BELLARE3",1;
	disablenpc();
	end;
}

1@bamn,145,106,5	duplicate(dummy_npc)	Repeater#wifi01	CLEAR_NPC


1@bamq,125,39,3	script(DISABLED)	Intruder#sweety	4_EP17_SWEETY,5,5,{
	end;
OnTouch:
	if ('twilight_story == 21) {
		setpcblock PCBLOCK_NPC, true;
		'twilight_story = 22;
		npctalk "你太晚了。你们这些白痴！";
		sleep2 2000;
		npctalk "他们已经利用飞空艇逃走了！";
		sleep2 2000;
		npctalk "现在，我甜甜来对付刚刚进来的白痴们！";
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		disablenpc();
		donpcevent instance_npcname("twilight_story_mob_5") + "::OnStart";
		end;
	}
	end;
}

1@bamq,1,1,7	script	twilight_story_mob_5	-1,{
	end;
OnStart:
	monster 'map_bamq$,125,39,"Sweety",20642,1, instance_npcname("twilight_story_mob_5") + "::OnMobDead";
	setunitdata $@mobid[0],UMOB_HP,500000;
	end;
OnMobDead:
	killmonster 'map_bamq$, instance_npcname("twilight_story_mob_5") + "::OnMobDead";
	if ('twilight_story == 22)
		'twilight_story = 23;
	mapannounce 'map_bamq$, "Est: Did you hear a loud noise from here?", bc_map, 0xFF00;
	enablenpc instance_npcname("Broken Sweety#sweety04");
	enablenpc instance_npcname("Est#est05");
	enablenpc instance_npcname("#tgd_bamq_exit");
	end;
}

1@bamq,123,39,3	script(DISABLED)	Broken Sweety#sweety04	4_EP17_SWEETY,{
	specialeffect EF_NPC_STOP;
	npctalk "...是的...去...去...不...两个...";
	mes "电弹起来不动，就像机器坏了一样。";
	mes "看到他移开了视线，他看起来并没有完全崩溃。";
	close;
}

1@bamq,108,41,5	script(DISABLED)	Est#est05	4_F_ESTLOVELOY,{
	cutin "ep162_est01",2;
	mes "[是]";
	mes "虽然有点晚了，但已经结束了。";
	mes "这就是你。那个在花园里拖着脚步的家伙。";
	next;
	mes "[是]";
	mes "这家伙以为控制自动人偶就能帮自己争取时间吗？";
	next;
	cutin "ep162_est02",2;
	mes "[是]";
	mes "毕竟，它们只是自动娃娃，对吧？";
	mes "没办法，只好把豪宅的自动人偶炸了……";
	next;
	cutin "ep162_est01",2;
	mes "[是]";
	mes "我们离开这里吧。";
	mes "我会在宾馆等你。";
	close3;
}

// warps story
1@bamn,33,224,0	script(DISABLED)	#to_bamq	WARPNPC,1,1,{
	end;
OnTouch_:
	if ('twilight_story == 21)
		warp 'map_bamq$,103,39;
	end;
}

1@bamq,101,39,0	warp2(DISABLED)	#tgd_bamq_exit	1,1,ba_maison,33,220


// Daily
1@bamq,16,39,0	script	#sweety_evt01	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	if (!is_party_leader())
		end;
	disablenpc();
	enablenpc instance_npcname("Almond#amond02");
	enablenpc instance_npcname("Sweety#sweety_boss");
	enablenpc instance_npcname("#to_bamq2");
	enablenpc instance_npcname("#to_bamn");

	monster 'map_bamn$,120,257,"--ja--",20681,1;	// G_EP17_2_HEART_HUNTER
	monster 'map_bamn$,130,255,"--ja--",20681,1;
	monster 'map_bamn$,130,244,"--ja--",20681,1;
	monster 'map_bamn$,127,184,"--ja--",20681,1;
	monster 'map_bamn$,188,240,"--ja--",20699,1;	// G_BELLARE3
	monster 'map_bamn$,203,248,"--ja--",20699,1;
	monster 'map_bamn$,203,252,"--ja--",20699,1;
	monster 'map_bamn$,301,234,"--ja--",20681,1;
	monster 'map_bamn$,302,239,"--ja--",20681,1;
	monster 'map_bamn$,297,244,"--ja--",20681,1;
	monster 'map_bamn$,197,52,"--ja--",20699,1;
	monster 'map_bamn$,120,83,"--ja--",20681,1;
	monster 'map_bamn$,117,84,"--ja--",20681,1;
	monster 'map_bamn$,137,176,"--ja--",20681,1;
	monster 'map_bamn$,143,181,"--ja--",20681,1;
	monster 'map_bamn$,62,165,"--ja--",20699,1;
	end;
}

1@bamq,32,49,3	script(DISABLED)	Sweety#sweety_boss	4_EP17_Sweety,{
	if ('sweety == 0) {
		cutin "ep172_Sweety01",2;
		mes "[甜心]";
		mes "嘿，你准备好了吗？";
		mes "随时来找我吧。";
		mes "我相信，一旦打败你，我就会感觉好一些。";
		next;
		if (select( "稍等一下。", "我们走吧！" ) == 1) {
			mes "[甜心]";
			mes "怎么，你来这里是为了探索花园吗？";
			mes "猎心者不知道我发生了什么事，但是……";
			next;
			cutin "ep172_Sweety02",2;
			mes "[甜心]";
			mes "什么，为什么？";
			mes "我根本不像他们，我是一个优秀的生物！";
			mes "老师们也是如此。";
			mes "但是，那些人只不过是失败者。";
			close3;

		}
		cutin "",255;
		npctalk "这次我不会再输给你了！";
		disablenpc();
		donpcevent instance_npcname("twilight_daily") + "::OnStart";
		'sweety = 1;
		close;
	}
	specialeffect EF_NPC_STOP;
	mes "他没有回应任何事情。";
	mes "我想他是因为我的攻击而晕倒的。";
	npctalk "……";
	close;
}

1@bamq,1,1,7	script	twilight_daily	-1,{
	end;
OnStart:
	monster 'map_bamq$,32,49,"Sweety",20642,1, instance_npcname("twilight_daily") + "::OnMobDead";
	end;
OnMobDead:
	killmonster 'map_bamq$, instance_npcname("twilight_daily") + "::OnMobDead";
	mapannounce 'map_bamq$, "Almond: Sweety~ Do it in moderation~", bc_map, 0xFF00;
	enablenpc instance_npcname("Sweety#sweety_boss");
	'sweety = 2;
	end;
}

1@bamq,32,43,3	script(DISABLED)	Stunned Sweety#sweety_bo	4_EP17_SWEETY,{
	setpcblock PCBLOCK_NPC, true;
	specialeffect EF_NPC_STOP;
	sleep2 500;
	setpcblock PCBLOCK_NPC, false;
	npctalk "……";
	mes "我震惊地昏过去了。";
	mes "看起来他已经昏过去了。";
	close;
}
	
1@bamq,37,59,3	script(DISABLED)	Almond#amond02	EP17_2_BETA_BASIC,{
	if ('sweety < 2) {
		cutin "ep172_beta",2;
		mes "[杏仁]";
		mes "我无法理解。";
		mes "为什么Sweety喜欢挑战自己的身体极限……";
		next;
		mes "[杏仁]";
		mes "我的工作就是修复他被毁坏的身体。";
		mes "请好好对待他，让他满意。";
		close3;
	}
	cutin "ep172_beta",2;
	mes "[杏仁]";
	mes "我会照顾甜甜的搬家事宜。";
	mes "你现在想结束吗？";
	next;
	if (select( "让我们结束吧。", "我会先探索花园。" ) == 2) {
		mes "[杏仁]";
		mes "那里仍然有一些入侵者。";
		mes "你知道吗？";
		close3;
	}
	mes "[杏仁]";
	mes "那么，我们可以吗？";
	close2;
	warp "ba_in01",18,255;
	end;
}


// warps daily
1@bamn,33,224,0	warp2(DISABLED)	#to_bamq2	1,1,1@bamq,13,39
1@bamq,8,39,0	warp2(DISABLED)	#to_bamn	1,1,1@bamn,33,220


// Daily quest
1@bamn,150,47,3	script	#bam_body01	4_EP17_BROKENBETA,{
	if (isbegin_quest(18024) == 1) {
		if (checkweight(1000226,1) == 0) {	// (custom)
			mes "^008800等一下！！";
			mes "您无法接收更多物品，因为您携带的物品太多。请等灯亮后再试。^000000";
			close;
		}
		.@id = atoi(replacestr(strnpcinfo(2), "bam_body0", ""));

		if ('broken_beta[.@id] == 0) {
			mes "贝塔经理的尸体隐藏在风景之下。";
			next;
			if (rand(1,10) < 5)
				mes "核心槽是空的。";
			else {
				mes "我已经恢复了核心。";
				getitem 1000226,1;
			}
			mes "我稍后会让阿尔法知道，这样她就可以取回娃娃的尸体。";
			'broken_beta[.@id] = 1;
		}
	}
	mes "核心部分是空的。";
	close;

OnInstanceInit:
	questinfo( QTYPE_CLICKME, QMARK_YELLOW, "isbegin_quest(18024) == 1" );
	end;
}
1@bamn,221,193,3	duplicate(#bam_body01)	#bam_body02	4_EP17_BROKENBETA
1@bamn,208,87,3	duplicate(#bam_body01)	#bam_body03	4_EP17_BROKENBETA
1@bamn,314,146,3	duplicate(#bam_body01)	#bam_body04	4_EP17_BROKENBETA
1@bamn,207,276,3	duplicate(#bam_body01)	#bam_body05	4_EP17_BROKENBETA
1@bamn,70,256,3	duplicate(#bam_body01)	#bam_body06	4_EP17_BROKENBETA
1@bamn,64,187,3	duplicate(#bam_body01)	#bam_body07	4_EP17_BROKENBETA
1@bamn,275,313,3	duplicate(#bam_body01)	#bam_body08	4_EP17_BROKENBETA
1@bamn,338,267,3	duplicate(#bam_body01)	#bam_body09	4_EP17_BROKENBETA
