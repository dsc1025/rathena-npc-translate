//===== rAthena Script ======================================= 
//= Card Separation System
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Separates cards from equipment.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//= 1.1 Added "Richard" NPC. [Euphy]
//============================================================ 

-	script	::CardSeparation_mal	-1,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "你有太多种类的物体。让我们在减少这些对象后尝试继续。";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "无法继续，因为您有太多重物。减肥后我们继续努力吧。";
		close;
	}
	if (strnpcinfo(1) == "Jeremy") {
		set .@Jeremy,1;
		set .@n$, "[Jeremy]";
		setarray .@equip_name$[0],  "Armor",  "Shoes",  "Garment", "Upper Hat";
		setarray .@equip_slot[0], EQI_ARMOR,EQI_SHOES,EQI_GARMENT,EQI_HEAD_TOP;

		mes .@n$;
		mes "好久不见~";
		mes "我学会了一项新技能，可以将卡牌与盔甲、鞋子、服装和头饰分开。你想尝试一下吗？";
	} else {
		set .@Jeremy,0;
		set .@n$, "[Richard]";
		setarray .@equip_name$[0], "Left hand", "Right hand";
		setarray .@equip_slot[0],   EQI_HAND_L,   EQI_HAND_R;

		mes .@n$;
		mes "愚蠢的杰里米不想碰武器和盾牌，因为他挑选了几张可能被损坏的卡牌。所以我准备了武器和盾牌的卡牌分离技能……";
	}
	next;
	mes .@n$;
	mes "一般费用为 1,000,000 Zeny。在卡片分离期间，您可以使用^990000特殊物品来降低设备或卡片的破坏率^000000。我们不会为此收取额外的 zeny 费用。";
	next;
	mes .@n$;
	mes "即使使用特殊物品也有可能将其破坏。此外，^ff0000精炼级别可能会丢失^000000。你有什么设备可以分离吗？";
	next;

	for(set .@i,0; .@i<getarraysize(.@equip_slot); set .@i,.@i+1) {
		if (getequipisequiped(.@equip_slot[.@i]))
			set .@menu$, .@menu$+getequipname(.@equip_slot[.@i])+":";
		else
			set .@menu$, .@menu$+"^999999"+.@equip_name$[.@i]+" (empty)^000000:";
	}

	set .@i, select("停止工作："+((.@Jeremy)?"怎么可能？":"")+":"+.@menu$);
	switch(.@i) {
	case 1:
		mes .@n$;
		mes "每当你需要工作的时候，就来这里找我。";
		close;
	case 2:
		mes .@n$;
		mes "您想知道什么如此特别。好吧，我不想只提供指导，所以让我告诉你这个故事......";
		next;
		mes "^000099杰里米正在伸展他的肩膀和双手。他可能正在等待有人与他交谈。^000000";
		next;
		mes .@n$;
		mes "您知道马兰多的特产是罐头食品吗？";
		next;
		select("我很清楚这一点。有那么有限吗？");
		mes .@n$;
		mes "呵呵……大家都喜欢。但之前也出现过一些问题。";
		next;
		select("有问题吗？是不是有什么坏鱼啊？");
		mes .@n$;
		mes "不，鱼没有任何问题。问题是加工后会产生大量鱼油。那就是这样的工业废物。";
		next;
		mes .@n$;
		mes "然而，经过复兴的精炼过程后，这种鱼油变得有价值，可用于旧设备护理和工业润滑剂。";
		next;
		mes .@n$;
		mes "此外，这种油对于将文物与装备的武器分离非常有用，我们无法用 Zeny 购买高级润滑剂和普通润滑剂。";
		next;
		mes .@n$;
		mes "嗯，不用担心钱的问题。当然，高级润滑油很贵。如果你付一些钱，我可以给你更便宜的润滑剂。";
		next;
		mes .@n$;
		mes "我不确定成功的质量。无论如何，这太便宜了，对吧？";
		close;
	default:
		set .@equip_num, .@equip_slot[.@i-3];
		if (!getequipisequiped(.@equip_num)) {
			mes .@n$;
			if (.@Jeremy)
				mes "这部分，什么都没有？";
			else
				mes "那部分什么都没有？";
			close;
		}
		break;
	}

	setarray .@check_equip_card[0], getequipcardid(.@equip_num,0),getequipcardid(.@equip_num,1),getequipcardid(.@equip_num,2),getequipcardid(.@equip_num,3);
	setarray .@equip_card[0], .@check_equip_card[0], .@check_equip_card[1], .@check_equip_card[2], .@check_equip_card[3];
	set .@mvp_list$,
		"|4408|4128|4456|4168|4142"+  //Gloom_Under_Night_Card, Golden_Bug_Card, Nidhogg_Shadow_Card, Dark_Lord_Card, Doppelganger_Card
		"|4134|4137|4386|4407|4357"+  //Dracula_Card, Drake_Card, Detale_Card, Randgris_Card, B_Seyren_Card
		"|4146|4132|4147|4372|4145"+  //Maya_Card, Mistress_Card, Baphomet_Card, Bacsojin_Card, Berzebub_Card
		"|4374|4352|4367|4236|4425"+  //Apocalips_H_Card, B_Ygnizem_Card, B_Shecil_Card, Amon_Ra_Card, Atroce_Card
		"|4359|4123|4144|4135|4143"+  //B_Eremes_Card, Eddga_Card, Osiris_Card, Orc_Load_Card, Orc_Hero_Card
		"|4263|4131|4430|4276|4419"+  //Incant_Samurai_Card, Moonlight_Flower_Card, Ifrit_Card, Lord_Of_Death_Card, Ktullanux_Card
		"|4403|4399|4376|4441|4302"+  //Kiel_Card, Thanatos_Card, Lady_Tanee_Card, Fallen_Bishop_Card, Tao_Gunka_Card
		"|4305|4148|4318|4121|4365"+  //Turtle_General_Card, Pharaoh_Card, Knight_Windstorm_Card, Phreeoni_Card, B_Katrinn_Card
		"|4363|4324|4361|4330|4342|"; //B_Magaleta_Card, Garm_Card, B_Harword_Card, Dark_Snake_Lord_Card, Rsx_0806_Card

	if (.@Jeremy) {
		for ( .@i = 0; .@i < MAX_SLOTS; .@i++ ) {
			if (getiteminfo(.@equip_card[.@i], ITEMINFO_SUBTYPE) == CARD_ENCHANT)
				.@equip_card[.@i] = 0;// Armor Enchant System
		}
		if (!getarraysize(.@equip_card)) {
			mes .@n$;
			mes "该卡未配备。您想再检查一次吗？";
			close;
		}
		if ((.@equip_card[0] && compare(.@mvp_list$,"|"+.@equip_card[0]+"|")) ||
			(.@equip_card[1] && compare(.@mvp_list$,"|"+.@equip_card[1]+"|")) ||
			(.@equip_card[2] && compare(.@mvp_list$,"|"+.@equip_card[2]+"|")) ||
			(.@equip_card[3] && compare(.@mvp_list$,"|"+.@equip_card[3]+"|")))
			set .@boss_chk,1;
	} else {
		// Official "Richard" script uses a hardcoded list including every possible item.
		//if (!getequipisequiped(.@equip_num)) {
		//	mes "[Richard]";
		//	mes "I'm sorry. We don't provide that equipment yet.";
		//	close;
		//}

		mes "[理查德]";
		mes "您想将卡分离到哪个号码的插槽？从左边的插座开始，它们被排序为1、2、3、4。";
		next;
		set .@menu$,"";
		for ( .@i = 0; .@i < MAX_SLOTS; .@i++ ) {
			if (.@equip_card[.@i] && getiteminfo(.@equip_card[.@i], ITEMINFO_SUBTYPE) == CARD_NORMAL) // Armor Enchant System
				.@menu$ = .@menu$ + "Socket " + (.@i+1) + " - " + getitemname(.@equip_card[.@i])+":";
			else {
				.@menu$ = .@menu$ + "^777777Socket " + (.@i+1) + " - No card^000000:";
			}
		}
		set .@i, select("停止工作："+.@menu$);
		switch(.@i) {
		case 1:
			mes .@n$;
			mes "当你需要工作的时候，就来找我吧。";
			close;
		default:
			set .@slot, .@i-2;
			if (.@equip_card[.@slot] == 0 || getiteminfo(.@equip_card[.@slot], ITEMINFO_SUBTYPE) == CARD_ENCHANT) {
				mes .@n$;
				mes "该插座未配备任何卡。你为什么不再检查一下？";
				close;
			}
			break;
		}
		if (compare(.@mvp_list$,"|"+.@equip_card[.@slot]+"|"))
			set .@boss_chk,1;
	}
	if (.@boss_chk == 0) {
		mes .@n$;
		if (.@Jeremy)
			mes "除卡牌外，^ff0000所有附魔效果都会消失。^000000 如果您同意，请选择作品类型：";
		else
			mes "请选择工作费用。";
		next;
		set .@menu$,
			"Next time...:"+
			((Zeny >= 1000000)?"Use 1,000,000z (Do not use special item):":"^999999Use 1,000,000z (Insufficient)^000000:")+
			((countitem(6441))?"Use Premium Lubricant:":"^999999Premium Lubricant (Insufficient)^000000:")+
			((countitem(6440))?"Use Ordinary Lubricant":"^999999Ordinary Lubricant (Insufficient)^000000");
		switch(select(.@menu$)) {
		case 1:
			mes .@n$;
			mes "每当你需要工作的时候，就来这里找我。";
			close;
		case 2:
			if (Zeny < 1000000) {
				mes .@n$;
				mes "你的禅意还不够。请带上足够的费用回来。";
				close;
			}
			mes .@n$;
			mes "这是相当旧的设备。工作过程中卡牌或装备的破坏率很高。您确定要继续吗？";
			next;
			if(select("下次...:继续") == 1) {
				mes .@n$;
				mes "每当你需要工作的时候，就来这里找我。";
				close;
			}
			set .@sf_c_num,150;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			set Zeny, Zeny - 1000000;
			break;
		case 3:
			if (countitem(6441) == 0) {
				mes .@n$;
				mes "您没有高级润滑油。";
				close;
			}
			mes .@n$;
			mes "如果你使用高级润滑剂，破坏率会大大降低，但我不能给你100%的保证。您确定要继续吗？";
			next;
			if(select("下次...:继续") == 1) {
				mes .@n$;
				mes "每当你需要工作的时候，就来这里找我。";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,75;
			set .@sf_w_num,75;
			delitem 6441,1; //High_RankLubricant
			break;
		case 4:
			if (countitem(6440) == 0) {
				mes .@n$;
				mes "你没有普通润滑油。";
				close;
			}
			mes .@n$;
			mes "如果你使用普通润滑剂，破坏率会大大降低，但我不能给你100%的保证。您确定要继续吗？";
			next;
			if(select("下次...:继续") == 1) {
				mes .@n$;
				mes "每当你需要工作的时候，就来这里找我。";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			delitem 6440,1; //General_Lubricant
			break;
		}
	} else if (.@boss_chk == 1) {
		mes .@n$;
		mes "该装备包含一张珍贵的MVP卡。此卡不能用润滑剂分离。如果你带上超级表面活性剂^0000ffSillit Pong^000000，我就能工作了。";
		next;
		switch(select("下次...：我有一个 Sillit Pong。")) {
		case 1:
			mes .@n$;
			mes "每当你需要工作的时候，就来这里找我。";
			close;
		case 2:
			if (countitem(6443) == 0) {
				mes .@n$;
				mes "你没有 Sillit Pong。";
				close;
			}
			break;
		}
		mes .@n$;
		if (.@Jeremy) {
			mes "除卡牌外，^ff0000所有附魔效果都会消失。^000000 如果您同意，请选择作品类型：";
			set .@menu$,"Alright, let's do it!";
		} else {
			mes "我可以继续吗？";
			set .@menu$,"I got it. Just do it quickly!";
		}
		next;
		switch(select("下次...："+.@menu$)) {
		case 1:
			mes .@n$;
			mes "每当你需要工作的时候，就来这里找我。";
			close;
		case 2:
			set .@sf_c_num,60;
			set .@sf_r_num,60;
			set .@sf_w_num,60;
			delitem 6443,1; //Sillit_Pong_Bottle
			break;
		}
	}

	set .@equip_id, getequipid(.@equip_num);
	set .@equip_refine, getequiprefinerycnt(.@equip_num);

	// anti-hack
	if (callfunc("F_IsEquipCardHack", .@equip_num, .@check_equip_card[0], .@check_equip_card[1], .@check_equip_card[2], .@check_equip_card[3]))
		close;

	delequip .@equip_num;

	// Chance of retaining refine level.
	if (rand(1,.@sf_r_num) >= 61)
		set .@equip_refine,0;

	if (.@Jeremy) {
		// Chance of retaining equipment.
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,0,0;
		}

		// Chance of retaining cards.
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i]) {
				if (rand(1,.@sf_c_num) < 61)
					getitem .@equip_card[.@i],1;
				else
					set .@card_break,1;
			}
		}
	} else {
		set .@card, .@equip_card[.@slot];
		set .@check_equip_card[.@slot],0;

		// Chance of retaining equipment (all enchantments are preserved).
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@check_equip_card[0],.@check_equip_card[1],.@check_equip_card[2],.@check_equip_card[3];
		}

		// Chance of retaining card.
		if (rand(1,.@sf_c_num) < 61)
			getitem .@card,1;
		else
			set .@card_break,1;
	}

	// Display corresponding effect.
	if (!.@equip_safe && .@card_break)
		specialeffect2 EF_LORD;
	else if (.@equip_safe && .@card_break)
		specialeffect2 EF_SUI_EXPLOSION;
	else if (!.@equip_safe && !.@card_break)
		specialeffect2 EF_FIREPILLAR;
	else
		specialeffect2 EF_MAXPOWER;

	// Output results.
	mes "-- 分卡结果 --";
	if (.@equip_safe) {
		mes "卡片分离过程中未出现裂纹。";
		mes "^0000FF设备分离正常。^000000";
	} else {
		mes "卡片分离过程中出现裂纹。";
		mes "设备已损坏。 ^ff0000不可恢复。^000000";
	}
	mes "-------------------";
	if (!.@card_break) {
		mes "在卡片分离过程中没有发生表面侵蚀。";
		mes "^0000ff卡片分离成功。^000000";
	} else {
		mes "在卡片分离过程中发生表面腐蚀。";
		mes "卡已损坏。 ^ff0000不可恢复。^000000";
	}
	next;
	mes .@n$;
	mes "这就是卡分离的结果。请再来吧。";
	close;
}
malangdo,215,166,4	duplicate(CardSeparation_mal)	Jeremy#pa0829	553	// Armors :: mal_yong
malangdo,208,166,6	duplicate(CardSeparation_mal)	Richard#pa0829	559	// Weapons :: soc_weapon
