//===== rAthena Script =======================================
//= Insurance Exchanger
//===== Description: =========================================
//= Exchange items for New_Insurance item.
//===== Changelogs: ==========================================
//= 1.0 Initial release. [Balfear]
//===== Translated By: =======================================
//= dsc
//============================================================

prontera,82,108,5	script	Heart Merchant#life01	4_M_NFDEADMAN2,5,5,{
	.@old_item = 12209;
	.@new_item = 6413;
	.@old_item_name$ = getitemname(.@old_item);
	.@new_item_name$ = getitemname(.@new_item);

	if (!checkweight(1201,1) || (MaxWeight - Weight) < 500) {
		mes "您无法继续对话，因为您有大量物品。";
		mes "请整理您的物品并重试。";
		close;
	}
	mes "[红心商人]";
	mes "你好！我看你是一个热心旅行的冒险家，乘船穿越大陆，享受危险的快感！";
	next;
	if(!countitem(.@old_item)){
		mes "[红心商人]";
		mes "您的存储空间里有" + mesitemlink( .@old_item ) + "吗？";
		next;
		mes "[红心商人]";
		mes "如果你给我一张" + .@old_item_name$ + "，我就把它换成" + mesitemlink( .@new_item ) + "。你怎么认为？你知道这个物品是什么吗？";
	} else {
		mes "[红心商人]";
		mes "你有没有想过更换你的" + mesitemlink( .@old_item ) + "为了更好的事情？例如..." + mesitemlink( .@new_item ) + "。你怎么认为？";
	}
	next;
	switch(select("这是什么？",(countitem(.@old_item)?"请兑换！":""),"我不感兴趣")) {
	case 1:
		mes "[红心商人]";
		mes mesitemlink( .@old_item ) + "是一个可以防止你在使用后 30 分钟内因死亡而失去经验值的物品。";
		next;
		mes "[红心商人]";
		mes "但很多人都对时间限制感到难过，对吧？所以我研究了一下并准备了" + mesitemlink( .@new_item ) + ".";
		next;
		mes "[红心商人]";
		mes mesitemlink( .@new_item ) + "是一个物品，只要拥有它^EE0000，无论持续时间如何^000000，都可以防止死亡时经验值的损失。当然，死亡后，"+.@new_item_name$+"就会被消耗掉。";
		next;
		select("你为什么要交换它？");
		mes "[红心商人]";
		mes "你对此感到好奇吗？我是一名商人。买卖东西是我的命运，关系到生死。你觉得我会错过这个赚钱的机会吗？我赚钱了，活着的人就可以摆脱死亡的痛苦。这不是很划算吗？";
		next;
		mes "[红心商人]";
		mes "汇率为 5 " + mesitemlink( .@old_item ) + " 兑换 1 " + mesitemlink( .@new_item ) + "。您还有什么疑问吗？";
		next;
		mes "[红心商人]";
		mes "嗯~你觉得怎么样？这不是公平交易吗？你想总是担心时间吗？这是享受惊心动魄的冒险的冒险家的必备品。";
		close;
		
	case 2:	
		mes "[红心商人]";
		mes "是啊是啊。好决定。最好还是换一个更好的不是吗？";
		next;
		mes "[红心商人]";
		mes "我可以将您的 ^0000cd5 "+.@old_item_name$+" 兑换为 1 "+.@new_item_name$+".";
		next;
		mes "[红心商人]";
		mes "不过《<<CLR0>>>>[非卖品]" + .@old_item_name$ + "不符合兑换条件^000000，所以拿来要求兑换是没有用的。";
		next;
		mes "[红心商人]";
		mes "我们现在交换吗？";
		next;
		if (select("请换货！：我不想换货。") == 2)
			break;
		mes "[红心商人]";
		mes "你想兑换多少？ ^0000cd一次最多可以兑换10个"+.@new_item_name$+"^000000。";
		next;
		input .@amount;
		if (.@amount == 0) {
			mes "[红心商人]";
			mes "嗯？你不感兴趣吗？这是一款很棒的产品，对于像您这样的人来说真的很有好处。但如果你不想保自己的性命，我也不会勉强你。";
			close;
		}
		if (.@amount > 10) {
			mes "[红心商人]";
			mes "就像我说的，^0000cd一次最多只能兑换 10 个^000000。再次检查数量。";
			close;
		}
		if (countitem(.@old_item) < (.@amount*5)) {
			mes "[红心商人]";
			mes "嗯，对不起，但是...你的" +.@old_item_name$+ " 还不够，不是吗？ 1 " + mesitemlink( .@old_item ) + " 为 5 " + mesitemlink( .@new_item ) + "。请检查数量。";
			close;
		}
		mes "[红心商人]";
		mes "现在，我已经交换了你的^0000cd" + .@old_item_name$ + "^000000 为 ^EE0000" + .@new_item_name$ + "^000000!!这是一笔很好的交易，不是吗？下次当你拿到手时"+.@old_item_name$+"，再来看我吧！";
		delitem .@old_item,.@amount*5;
		getitem .@new_item,.@amount;
		close;
	case 3:
		break;
	}
	mes "[红心商人]";
	mes "这是一件大事！冒险家，对于像你这样去危险地方冒险的人来说，这是必备物品！";
	next;
	mes "[红心商人]";
	mes "如果您对此感兴趣，请务必回来交流。好的？";
	close;
	
OnTouch:
	if(countitem(12209))
		npctalk "你有好东西。您想更换您的" + getitemname(12209) + "有更好的吗？", "", bc_self;
	end;
}
