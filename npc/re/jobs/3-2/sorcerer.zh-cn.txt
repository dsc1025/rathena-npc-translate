//===== rAthena Script ======================================= 
//= Sorcerer Job Quest
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Sage / Professor -> Sorcerer.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Cleaning. [Euphy]
//= 1.2 Updated script, optimized. [Euphy]
//= 1.3 Added VIP features. [Euphy]
//===== Translated By: =======================================
//= dsc
//============================================================

gef_tower,102,34,5	script	Merito	742,{
	mes "[优点]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "你的包里装了这么多东西。清空袋子后重试。";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "请和精灵们好好相处吧~应该~！";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "哇~你是一位年轻的冒险家。我羡慕你。";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "啊~我觉得你没必要跟我说话。";
		close;
	}
	if (job_soc == 0) {
		mes "欢迎。我能为您提供什么帮助？";
		next;
		select("你了解魔法师吗？");
		mes "[优点]";
		mes "作为一个学者生活不是更舒服吗？法师们忙于与神灵沟通，没有太多时间看书……";
		next;
		mes "[优点]";
		mes "是什么让你想成为一名魔法师？";
		next;
		if(select("火之灵带领我来到这里。:水之灵带领我来到这里。:土之灵带领我来到这里:肉之灵带领我来到这里。") == 4) {
			mes "[优点]";
			mes "这种精神是不存在的！";
			close;
		}
		mes "[优点]";
		mes "我懂了。";
		mes "你必须生来就有能力看到你已经与神灵沟通，否则你一定是一个出色的骗子。";
		next;
		mes "[优点]";
		mes "嗯……那么首先，你能在这张名单上写下你的名字吗？";
		next;
		if(select("写名字：我不会。") == 2) {
			mes "[优点]";
			mes "好吧，好吧。当你有机会时再次见到你。";
			close;
		}
		mes "[优点]";
		mes "所以，你是[" + strcharinfo(0) + "]。";
		next;
		mes "[优点]";
		mes "请给我一点时间来注册您的名字。";
		mes "过一会再跟我说话。";
		set job_soc,1;
		close;
	} else if (job_soc == 1) {
		mes "好吧，那我就给大家简单介绍一下魔法师吧。";
		next;
		mes "[优点]";
		mes "[" + strcharinfo(0) + "]，你能熟练运用魔法吗？";
		next;
		if(select("是的，我可以。:不，我不太擅长。") == 2) {
			mes "[优点]";
			mes "什么！？你不知道如何使用魔法吗？";
			mes "那你来这里做什么？离开这里。现在！";
			close;
		}
		mes "[优点]";
		mes "好吧，这应该是显而易见的。";
		mes "当然，魔法师也使用魔法。但是，你知道魔法师与其他魔法职业的主要区别吗？";
		next;
		switch(select("他们会唱歌。：他们都是老顽固。：他们能对付鬼魂。")) {
		case 1:
			mes "[优点]";
			mes "也许有一些，但这不是主要区别。";
			close;
		case 2:
			mes "[优点]";
			mes "什么！你怎么敢！！";
			close;
		case 3:
			break;
		}
		mes "[优点]";
		mes "这是正确的。这是魔法师最显着的特征。";
		next;
		mes "[优点]";
		mes "当想要成为魔法师的人来的时候，我们首先要做的就是检查他们是否可以与灵魂沟通。";
		next;
		mes "[优点]";
		mes "这就是基本的介绍，所以你只需要决定是否参加资格考试即可。";
		next;
		mes "[优点]";
		mes "好吧，这一切都取决于你。你会怎样做？";
		next;
		switch(select("我要参加考试：我稍后再做。")) {
		case 1:
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_soc,2;
				setquest 12096;
				next;
			}
			mes "[优点]";
			mes "好的。";
			mes "然后我会把它交给四楼的卡拉卡斯。他将指导您完成测试过程。";
			set job_soc,2;
			setquest 12096;
			close;
		case 2:
			mes "[优点]";
			mes "什么？我确信你想成为一名魔法师。";
			close;
		}
	} else {
		mes "你的测试怎么样？好吧，卡拉卡斯有点懒，所以这可能会打扰你一些...... kkk";
		close;
	}
}

gef_tower,113,161,5	script	Karacas	754,{
	mes "[加拉加斯]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "哦，看来你的体重太重了。清空你的包然后再来。";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "你看起来很高兴。祝你旅途愉快。";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "有什么我可以为您做的吗？";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "我属于魔法师协会。我不认为你跟我有什么关系。";
		close;
	}
	if (job_soc < 2) {
		mes "这些天来参观格芬塔的人越来越多。";
		close;
	} else if (job_soc == 2) {
		mes "....";
		next;
		select("啊……不好意思啊~");
		mes "[加拉加斯]";
		mes "……（Zzz~）";
		next;
		select("嗯嗯！！！~");
		mes "[加拉加斯]";
		mes "奥克？";
		mes "哦，我的...";
		next;
		mes "[加拉加斯]";
		mes "呃……你从哪里来？";
		next;
		select("梅里托叫我来拜访你。");
		mes "[卡拉卡斯]";
		mes "我明白了，请给我一点时间。";
		mes "名单……已经到了。";
		next;
		mes "[加拉加斯]";
		mes "那么，你是[" + strcharinfo(0) + "]...对吗？";
		next;
		if(select("是的，我是。：不，我不是！") == 2) {
			mes "[加拉加斯]";
			mes "哦，我想列表中一定有什么错误。稍后你会再来吗？";
			close;
		}
		mes "[加拉加斯]";
		mes "在你参加魔法师测试之前，让我先给你介绍一下基本信息。";
		next;
		mes "[加拉加斯]";
		mes "获得魔法师报名券的方法非常简单。你只需要去寻找散布在世界各地的每种灵魂，然后将它们的力量转化为某种物质的形状即可。";
		next;
		mes "[加拉加斯]";
		mes "听起来很容易，对吧？";
		next;
		select("啊……先生。这真的可能吗？");
		mes "[卡拉卡斯]";
		mes "嗯，一个人不可能遇见每一个灵魂。你需要非常努力地训练自己，才能给灵魂塑造一个形状，并通过你的眼睛清楚地看到它们。";
		next;
		mes "[卡拉卡斯]";
		mes "所以，通常我们都会向能够感受到神灵的地方进行某种仪式游行。";
		next;
		mes "[加拉加斯]";
		mes "我会告诉你如何做到这一点。所以要仔细听。";
		next;
		mes "[加拉加斯]";
		mes "通常我们无法用肉眼看到灵魂，但有一种方法可以通过特殊的媒介来认识灵魂的部分。";
		next;
		mes "[卡拉卡斯]";
		mes "例如，如果你带着一定数量的翠风来到风之精灵停留的地方，精灵的反应就会被放大。";
		next;
		mes "[加拉加斯]";
		mes "同样的，如果你在拥有一定数量的红血的情况下接近火之精灵似乎停留的地方，精灵就会出现不良反应。";
		next;
		mes "[加拉加斯]";
		mes "法师就是利用这个基本原理，这是法师达到灵界所需要具备的必备知识。";
		next;
		select("所以与灵魂交流...");
		mes "[加拉加斯]";
		mes "对吧~";
		mes "这项工作是基于精神力量的。";
		next;
		mes "[加拉加斯]";
		mes "嗯，不过对于像你这样只专注于读书的人来说可能很难……";
		next;
		mes "[加拉加斯]";
		mes "首先，您需要携带考试所需的以下材料。";
		next;
		mes "[加拉加斯]";
		mes "3 翠绿之风。";
		mes "3 水晶蓝。";
		mes "3 红血。";
		mes "3 绿色生活。";
		next;
		mes "[加拉加斯]";
		mes "当你把这些材料带来后，我会告诉你下一步的步骤。";
		set job_soc,3;
		changequest 12096,12097;
		close;
	} else if (job_soc == 3) {
		if (countitem(992) > 2 && countitem(991) > 2 && countitem(990) > 2 && countitem(993) > 2) {
			mes "哦，材料都齐全了。";
			next;
			select("接下来我应该做什么？");
			mes "[加拉加斯]";
			mes "我现在就告诉你这件事。首先我给大家介绍一下考试地点。你最好把它们写下来。";
			next;
			mes "[卡拉卡斯]";
			mes "1. 冰洞一层";
			mes "2. 雷神火山地下城第一层";
			mes "3.雷神之锤死坑第三层";
			mes "4、昆仑地下城第二层";
			next;
			mes "[加拉加斯]";
			mes "我们在这4个地方设立了探测杆，让普通人能够与神灵沟通。";
			next;
			mes "[加拉加斯]";
			mes "请注意，只有当你拥有超过一定数量的属性石时，这些探测杆才会被激活。";
			next;
			mes "[加拉加斯]";
			mes "每个属性至少需要准备3块小石头。例如，您需要翠绿之风，而不是狂风。";
			next;
			select("所以我需要属性石...");
			mes "[卡拉卡斯]";
			mes "是的，你的思维敏捷！";
			next;
			mes "[加拉加斯]";
			mes "将石头带到柱子上后，将它们放入附在柱子上的属性分解机中。探测极将被激活，你可以与灵魂进行交流。";
			next;
			mes "[加拉加斯]";
			mes "如果成功的话，就可以提取一部分灵气作为试剂。如果失败的话，属性石就会碎裂。";
			next;
			mes "[卡拉卡斯]";
			mes "当然，如果运气好的话，如果失败了，翠风可以变成狂风……嗯，这种情况不常见，暂时不讨论。";
			next;
			select("如果我的石头用完了怎么办？");
			mes "[加拉加斯]";
			mes "啊哈哈哈……属性石用完了，就得再弄点。";
			next;
			mes "[加拉加斯]";
			mes "所以参加这个测试的人一般都是一开始就带着尽可能多的属性石。我告诉你的金额只是最低限度。";
			next;
			mes "[卡拉卡斯]";
			mes "如果你是高级魔法师，你将能够与灵魂交谈，而无需再次经历这个过程。但初学者需要使用这种媒介来与他们交谈。";
			next;
			select("那么试剂是……？");
			mes "[卡拉卡斯]";
			mes "我们不知道哪种试剂将由烈酒制成。它因精神状态而异。";
			next;
			mes "[加拉加斯]";
			mes "当你成功获得3种不同的试剂后，你就可以回来了。";
			next;
			mes "[加拉加斯]";
			mes "一种烈酒制作的试剂不会超过2种，所以如果你要制作3种试剂，我建议你多去几个地方。";
			next;
			mes "[卡拉卡斯]";
			mes "有任何疑问吗？";
			next;
			if(select("否：请再解释一下。") == 2) {
				mes "[卡拉卡斯]";
				mes "哈哈，那我可以喝杯水吗？请稍后再来找我。";
				next;
			}
			mes "[卡拉卡斯]";
			mes "然后我会很快写下我总结的进展。等待...";
			next;
			mes "^000099你得到了一张纸，上面写着与灵魂交流的方式。欲了解更多信息，请打开您的任务窗口^000000。";
			set job_soc,4;
			changequest 12097,12098;
			close;
		}
		mes "在我告诉您进展方式之前，您需要携带一些必需的材料。";
		next;
		mes "[加拉加斯]";
		mes "3 翠绿之风。";
		mes "3 水晶蓝。";
		mes "3 红血。";
		mes "3 绿色生活。";
		next;
		mes "[加拉加斯]";
		mes "当你带上这些基本的东西后，我将引导你进行下一步。";
		close;
	} else if (job_soc == 4) {
		if (countitem(6276) && countitem(6278) && countitem(6277)) {
			mes "哇~你的手艺真好。";
			mes "我以为你会被鬼魂压倒，没想到你竟然得到了有效的药物！";
			next;
			select("这就是我需要做的全部吗？");
			mes "[加拉加斯]";
			mes "干得好。首先我会整理灵药，请稍等。";
			delitem 6276,1; //Clear_Reagent
			delitem 6278,1; //Black_Reagent
			delitem 6277,1; //Red_Reagent
			set job_soc,5;
			completequest 12098;
			close;
		}
		mes "与神灵沟通想要达到预期的效果并不是一件容易的事情。";
		next;
		mes "[加拉加斯]";
		mes "不要放弃，继续努力~";
		close;
	} else if (job_soc == 5) {
		mes "原本我的老师需要检查魔法师转职的情况，但是他出去寻找鱼灵后，我就看不到他了。所以我自己检查一下。";
		next;
		mes "[卡拉卡斯]";
		mes "亲爱的[" + strcharinfo(0) + "]，";
		mes "你穿越了崎岖的荒野，打开了魔法师的大门，并通过了我们学院给出的所有要求。";
		next;
		mes "[加拉加斯]";
		mes "当然，这只是一个开始，我们不知道你将与哪个灵魂交流，也不知道等待你的将是怎样的磨难。";
		next;
		mes "[加拉加斯]";
		mes "但以你迄今为止所表现出的能力，从现在开始你将有能力做任何事情。亲爱的[" + strcharinfo(0) + "]";
		next;
		mes "[力量]";
		if (BaseLevel < 99 || JobLevel < 50 || SkillPoint) {
			mes "在我给你指定需要的工作之前，请先检查一下你自己的身体状况。";
			next;
			mes "[力量]";
			mes "在接受新工作之前，您现在必须掌握工作礼仪！";
			close;
		}
		if (ismounting()) {
			mes "好，在我们开始之前...";
			mes "请解除你的骑乘宠物的装备~";
			close;
		}
		mes "而且，我正式承认你已经完成了魔法学院的所有转职测试。";
		next;
		mes "[卡拉卡斯]";
		mes "恭喜，[" + strcharinfo(0) + "]";
		jobchange roclass(eaclass()|EAJL_THIRD);
		getitem 5756,1; //Wind_Whisper
		getitem 2795,1; //Green_Apple_Ring
		next;
		mes "[加拉加斯]";
		mes "我希望你从现在开始与灵魂有更多的交流。";
		close;
	}
}
-	script	Spirit Detecting Staff#0	-1,{

	if (strnpcinfo(4) == "thor_v01") setarray .@id[0],990,994,6276,6278;
	else if (strnpcinfo(4) == "ice_dun01") setarray .@id[0],991,995,6278,6276;
	else if (strnpcinfo(4) == "mjo_dun03") setarray .@id[0],993,997,6277,6276;
	else if (strnpcinfo(4) == "gon_dun02") setarray .@id[0],992,996,6277,6278;

	if (job_soc == 4 && countitem(.@id[0]) > 2) {
		progressbar "ffff00",5;
		delitem .@id[0],3; //Boody_Red,Crystal_Blue,Yellow_Live,Wind_Of_Verdure
		set .@soc_ran, rand(1,70);
		if (.@soc_ran < 10) {
			if (countitem(.@id[2]) == 0) {
				mes "你通过与看不见的灵魂交流而获得了试剂。";
				getitem .@id[2],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "看起来你并没有从精灵身上得到什么特别的东西。";
			close;
		} else if (.@soc_ran > 9 && .@soc_ran < 16) {
			if (countitem(.@id[3]) == 0) {
				mes "你通过与看不见的灵魂交流而获得了试剂。";
				getitem .@id[3],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "看起来你并没有从精灵身上得到什么特别的东西。";
			close;
		} else if (.@soc_ran == 56) {
			mes "神灵将属性石组合成一块巨大的属性石。";
			getitem .@id[1],1; //Flame_Heart,Mistic_Frozen,Great_Nature,Rough_Wind
			close;
		} else {
			mes "你使用了属性石，但看起来你无法与神灵沟通。";
			close;
		}
	}
	mes "我可以看到魔法师联盟设置的用于与精灵沟通的探测人员。";
	if (job_soc == 4 && countitem(.@id[0]) < 3) {
		next;
		mes "如果你有合适的属性石，应该可以与神灵沟通。";
	}
	close;
}
thor_v01,64,252,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#1	836
ice_dun01,274,274,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#2	836
mjo_dun03,200,141,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#3	836
gon_dun02,195,190,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#4	836
