//===== rAthena Script ======================================= 
//= Event King Items
//===== By: ================================================== 
//= $ephiroth
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Grand Circlet Quest combined with Excalibur Quest.
//===== Additional Comments: =================================
//= 2006/09/27: 1.0 Release and fully working. [$ephiroth]
//= 1.1 Optimized. [Euphy]
//===== Translated By: =======================================
//= dsc
//============================================================

prt_castle,80,170,4	script	King of Prontera	108,{
	mes "[弗龙特拉之王]";
	mes "你好，你能帮我吗？";
	next;
	if(select("当然，我能做什么？：抱歉，我很忙。")==2) {
		mes "[弗龙特拉之王]";
		mes "好的，没关系，我相信其他人会提供帮助。";
		close; }
	mes "[弗龙特拉之王]";
	mes "谢谢。呃，我旁边的女儿有点吵闹……";
	mes "如果你给她这些东西，我相信她会平静下来。";
	mes "我会给你我的王冠或者我的剑作为奖励。";
	next;
	setarray .@Items1[0],5007,742,30,741,50,753,20,732,1,710,20,629,20;
	setarray .@Items2[0],1137,2254,1,2282,1,714,1;
	switch(select("你需要什么？：我有东西！：算了。")) {
		case 1:
			for(set .@i,1; .@i<3; set .@i,.@i+1) {
				mes "[弗龙特拉之王]";
				mes "如果你能找到我：";
				for(set .@j,1; .@j<getarraysize(getd(".@Items"+.@i)); set .@j,.@j+2)
					mes " ~ ^0055FF"+getd(".@Items"+.@i+"["+(.@j+1)+"]")+"^000000x "+getitemname(getd(".@Items"+.@i+"["+.@j+"]"));
				mes "我将献出我的"+((.@i == 2 )?"剑。":"王冠。");
				if (.@i==2) close;
				next; }
		case 2:
			mes "[弗龙特拉之王]";
			mes "您想要什么商品？";
			next;
			set .@i, select("皇冠：剑");
			for(set .@j,1; .@j<getarraysize(getd(".@Items"+.@i)); set .@j,.@j+2)
				if (countitem(getd(".@Items"+.@i+"["+.@j+"]")) < getd(".@Items"+.@i+"["+(.@j+1)+"]")) set .@nr,1;
			mes "[弗龙特拉之王]";
			if (.@nr) { 
				mes "你错过了一些东西。";
				mes "拿到所有物品后回来。";
				close; }
			for(set .@j,1; .@j<getarraysize(getd(".@Items"+.@i)); set .@j,.@j+2)
				delitem getd(".@Items"+.@i+"["+.@j+"]"), getd(".@Items"+.@i+"["+(.@j+1)+"]");
			getitem getd(".@Items"+.@i+"[0]"),1;
			mes "非常感谢！";
			mes "这肯定能让她平静下来。";
			mes "正如所承诺的，这是我的"+((.@i==1)?"王冠。":"剑。");
			close;
		case 3:
			mes "[弗龙特拉之王]";
			mes "好的，没关系，我相信其他人会提供帮助。";
			close;
	}
}

prt_castle,84,170,4	script	King's Daughter	62,{
	mes "[国王的女儿]";
	mes "哼，我现在讨厌我爸爸！！";
	next;
	if(select("为什么？：不管怎样...")==2) {
		mes "[国王的女儿]";
		mes "那么好吧！哼！！";
		close; }
	mes "[国王的女儿]";
	mes "好吧，他不会给我我想要的！！";
	mes "他是普隆德拉的国王。他应该能给我任何东西！";
	close;
}
