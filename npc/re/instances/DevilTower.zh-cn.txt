//===== rAthena Script =======================================
//= Devil Tower
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Devil Tower Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

dali02,134,119,5	script	Historian Shep#tnm01	4_F_NOVICE,{
	if (BaseLevel < 130) {
		mes "[谢普]";
		mes "你自愿去探索魔鬼塔吗？";
		mes "这是非常危险的，所以你至少需要^4d4dff等级130^000000。";
		next;
		mes "[谢普]";
		mes "当你变得更强之后你会回来吗？";
		mes "你知道我的意思吗？";
		close;
	}
	switch( checkquest(7577,PLAYTIME) ) {// daily timer
	case -1:
		break;
	case 0:
	case 1:
		mes "[谢普]";
		mes "这么快就想再次探索吗？";
		mes "你的身体需要从时间旅行中恢复过来。";
		next;
		mes "[谢普]";
		mes "休息一天，明天再来。！";
		mes "你明白吗？";
		close;
	case 2:
		mes "[谢普]";
		mes "你休息够久了吗？";
		mes "我今天预定去探索！";
		mes "我一直在等待探险队的成员！";
		erasequest 7577;// Space distortion
		devil_tower_can_reenter = 0;
		devil_tower_enter = 0;
		next;
		mes "！- 注意 -！";
		mes "现在你可以重新进入恶魔塔了。";
		close;
	}
	if (isbegin_quest(7576) > 0) {// instance completed
		mes "[谢普]";
		mes "那么时间和空间似乎在那里交汇了？";
		mes "我很惊讶时间旅行是可能的。";
		next;
		mes "[谢普]";
		mes "这就是你所拥有的一切。";
		mes "你做得很好。";
		mes "我期待更多的发现！";
		setquest 7577;// Space distortion
		erasequest 7568;// Explore the tower
		erasequest 7571;// Tower Expedition
		erasequest 7574;// Thanatos Tower
		erasequest 7576;// Morocc castle seal
		getexp 450000,450000;
		devil_tower_can_reenter = 0;
		devil_tower_enter = 0;
		mes "请明天再来吧！";
		close;
	}
	if (isbegin_quest(7574) > 0) {// failed after seal
		mes "[谢普]";
		mes "啊，你这么快就回来了……";
		mes "好的，我明白发生了什么。";
		next;
		mes "[谢普]";
		mes "这就是你所拥有的一切。";
		mes "你做得很好。";
		mes "我期待更多的发现！";
		setquest 7577;// Space distortion
		erasequest 7568;// Explore the tower
		erasequest 7571;// Tower Expedition
		erasequest 7574;// Thanatos Tower
		getexp 300000,300000;
		devil_tower_can_reenter = 0;
		devil_tower_enter = 0;
		mes "请明天再来吧！";
		close;
	}
	if (isbegin_quest(7571) > 0) {// failed at stair 2 tnm1
		mes "[谢普]";
		mes "哦，你回来了。";
		mes "我想想……你是说这座塔有一条通往魔界的通道。";
		next;
		mes "[谢普]";
		mes "这就是你所拥有的一切。";
		mes "你做得很好。";
		mes "我期待更多的发现！";
		setquest 7577;// Space distortion
		erasequest 7568;// Explore the tower
		erasequest 7571;// Tower Expedition
		if (isbegin_quest(7572)) erasequest 7572;// Lucile...?
		if (isbegin_quest(7573)) erasequest 7573;// Magic Swordman Thanatos
		getexp 200000,200000;
		devil_tower_can_reenter = 0;
		devil_tower_enter = 0;
		mes "请明天再来吧！";
		close;
	}
	switch( checkquest(7568,PLAYTIME) ) {// entrance timer - 1h30 to enter
	case -1:
		mes "[谢普]";
		mes "你自愿去探索魔鬼塔吗？我一直在等待像你这样的人！";
		next;
		mes "[谢普]";
		mes "终于有人来给我分享这座塔的奥秘了。";
		mes "没有人自愿提出这一点，所以我很担心。";
		next;
		mes "[谢普]";
		mes "请向我报告你在塔中发现的任何东西。";
		mes "信息越好~我付给你的钱就越多！";
		next;
		mes "[谢普]";
		mes "那边的拉芬学者会用这个时间装置打开空间缝隙。";
		mes "它仍然不稳定，因此您的探索时间将受到限制。";
		next;
		mes "[谢普]";
		mes "如果你意外地再次被弹回这里，你应该尽快返回。";
		mes "如果您这样做，您可能会或可能无法重新进入，具体取决于您的速度。";
		next;
		mes "[谢普]";
		mes "但如果你没时间，通往塔楼的通道就会消失。";
		mes "那么你想当志愿者吗？";
		next;
		if (select( "志愿者", "不，谢谢" ) == 2) {
			mes "[谢普]";
			mes "哦，那太糟糕了";
			mes "我想我无能为力。";
			mes "我还是去找别人吧...";
			close;
		}
		mes "[谢普]";
		mes "谢谢。";
		mes "好吧，召集你的队伍，记得向我报告你的发现。";
		next;
		mes "[谢普]";
		mes "^4d4dff探索时间限制是为了您的安全^000000！";
		setquest 7568;// Explore the tower
		close;
	case 0:
	case 1:
		if (devil_tower_can_reenter == 0)// player can re-enter
			break;
		// fall through
	case 2:
		mes "[谢普]";
		mes "你好像来晚了。";
		mes "你花了非常长的时间。";
		mes ".....";
		mes "我无法使用你的任何信息。";
		next;
		mes "[谢普]";
		mes "我们无能为力。";
		mes "总有其他探险的机会。";
		setquest 7577;// Space distortion
		erasequest 7568;
		if (isbegin_quest(7569)) erasequest 7569;// Treat the injured
		if (isbegin_quest(7570)) erasequest 7570;// Destroy the demons
		devil_tower_can_reenter = 0;
		devil_tower_enter = 0;
		close;
	}
	mes "[谢普]";
	mes "首先，你需要在门口预约排队。";
	mes "Laphine Scholar Artie 将为您预留队列。";
	next;
	mes "[谢普]";
	mes "时间有限，因为次元差距还不稳定。";
	mes "让您的探索变得简短而甜蜜。";
	next;
	mes "[谢普]";
	mes "根据您的时间不同，奖励也会有所不同。";
	mes "祝你好运！";
	next;
	if (select( "相信我！", "取消我的预订" ) == 1) {
		mes "[谢普]";
		mes "我相信你可以在这些限制下进行管理。";
		mes "我相信你！";
		close;
	}
	if (devil_tower_enter == 1) {
		mes "[谢普]";
		mes "你似乎厌倦了探索。";
		mes "^4d4dff如果您现在取消。一天后你就可以挑战了。^000000";
		mes "可以吗？";
		next;
		if (select( "是的，取消。", "等等，不要取消！" ) == 2) {
			mes "[谢普]";
			mes "好吧，我不会取消。";
			close;
		}
		mes "[谢普]";
		mes "真的。";
		mes "那么，明天再来吧。";
		setquest 7577;// Space distortion
		erasequest 7568;
		if (isbegin_quest(7569)) erasequest 7569;// Treat the injured
		if (isbegin_quest(7570)) erasequest 7570;// Destroy the demons
		devil_tower_enter = 0;
		devil_tower_can_reenter = 0;
		close;
	}
	mes "[谢普]";
	mes "真的吗？";
	mes "听完这些解释后？";
	erasequest 7568;// Explore the tower
	close;
}

dali02,137,121,3	script	Magic Scholar Artie#tnm	4_M_FAIRYKID6,{
	.@party_id = getcharid(1);
	if (.@party_id == 0) {
		mes "[阿蒂]";
		mes "我们刚刚离开bifrost，让我们玩得开心吧。";
		mes "不？";
		mes "您正在申请成为志愿者吗？";
		mes "你最好为这次探险召集一个队伍。";
		next;
		mes "[阿蒂]";
		mes "不要冒任何不必要的风险，好吗？";
		close;
	}
	if (is_party_leader() == false) {
		mes "[阿蒂]";
		mes "带上远征队队长。";
		mes "魔鬼塔不是一个可以单独参观的地方。";
		mes "带经理来。";
		close;
	}
	if (devil_tower_enter == 1) {
		mes "[阿蒂]";
		mes "您已经通过连接进入了。";
		mes "再次连接空间？我不能那样做。";
		close;
	}
	switch( checkquest(7568,PLAYTIME) ) {
	case 0:
	case 1:
		break;
	case -1:
	case 2:
		mes "[阿蒂]";
		mes "嗯……除非是指定的探索时间，否则很难连接。";
		mes "更不用说还有危险了。";
		close;
	}
	mes "[阿蒂]";
	mes "您是探索魔鬼塔的志愿者吗？";
	mes "要我激活次元装置吗？";
	next;
	mes "[阿蒂]";
	mes "迅速做出决定，因为你在那里的时间有限。";
	next;
	if (select( "是的，激活设备。", "稍等片刻。" ) == 2) {
		mes "[阿蒂]";
		mes "你探索的时间越来越少。";
		mes "时间非常重要。";
		mes "你还在等什么？";
		close;
	}
	mes "[阿蒂]";
	mes "维度装置被激活。";
	mes "如果你幸运的话，你可以很快地度过这个难关。";
	mes "我期待听到好消息。";
	instance_create("Devil's Tower");
	close;
}

dali02,141,120,3	script	Dimensional Device#tnm0	PORTAL,{
	if (devil_tower_can_reenter == 1) {// can't re-enter
		mes "[阿蒂]";
		mes "嘿嘿";
		mes "现在很难通过设备发送给您。";
		mes "我们今天应该停止探索，改天再去吧。";
		close;
	}
	switch( checkquest(7568,PLAYTIME) ) {
	case 0:
	case 1:
		break;
	case -1:
	case 2:
		mes "[阿蒂]";
		mes "听着，抱歉，除了允许的时间内不允许进入，魔鬼塔很危险。";
		close;
	}
	mes "[阿蒂]";
	mes "是的，这就是次元装置。";
	mes "它用于旅行时间。";
	mes "如果离开了就很难再进去了。";
	next;
	mes "[阿蒂]";
	mes "不过放心，我是天才学者~";
	mes "无论如何，准备好进入了吗？";
	next;
	if (select( "输入设备。", "停止。" ) == 2) {
		mes "[阿蒂]";
		mes "正确的。";
		mes "自从您激活设备以来似乎已经过去了很长一段时间。我说得对吗？";
		close;
	}
	.@md_name$ = "Devil's Tower";
	switch( instance_enter(.@md_name$) ) {
	case IE_OTHER:
		mes "发生未知错误。";
		close;
	case IE_NOINSTANCE:
		mes "[谢普]";
		mes "进入之前需要先激活次元装置。";
		mes "好像还没有激活。";
		next;
		mes "[谢普]";
		mes "请与魔法学者阿蒂交谈以获得激活许可。";
		close;
	case IE_NOMEMBER:
		mes "[阿蒂]";
		mes "只有组队探险才允许探索魔鬼塔。";
		mes "确保您已准备好。";
		close;
	case IE_OK:
		mapannounce "dali02", "Party member " + strcharinfo(0) + " of party's " + getpartyname( getcharid(1) ) + " enters Room " + .@md_name$ + "", bc_map,0xFF99;
		// warp "1@tnm1",50,104;
		devil_tower_enter = 1;
		end;
	}
}

dali02,134,112,3	script	#tnmtks01	HIDDEN_WARP_NPC,3,3,{
	end;
OnTouch_:
	disablenpc "#tnmtks01";
	initnpctimer;
	end;
OnTimer1000:
	npctalk "阿蒂：嘿，你！你的研究进行得怎么样了？", "魔法学者阿蒂#tnm";
	end;
OnTimer3000:
	npctalk "Shep：嘿，你在叫谁？你只是一个尘封的小仙女！", "历史学家谢普#tnm01";
	end;
OnTimer6000:
	npctalk "阿蒂：一个满是灰尘的小东西什么？我会让你知道，我的基因比你们这些猿类优越！", "魔法学者阿蒂#tnm";
	end;
OnTimer9000:
	npctalk "谢普：我管你说什么？连我们的孩子都比你们这些小仙女大！", "历史学家谢普#tnm01";
	end;
OnTimer12000:
	npctalk "Shep：以为你是魔法学者就这么伟大吧？", "历史学家谢普#tnm01";
	end;
OnTimer14000:
	npctalk "阿蒂：你对人类一无所知。再次提醒我是谁找到了这座塔的入口？", "魔法学者阿蒂#tnm";
	end;
OnTimer18000:
	npctalk "Shep：*叹气*...是...你做到了。", "历史学家谢普#tnm01";
	end;
OnTimer21000:
	npctalk "阿蒂：是……你？我结巴了吗？谁解出了计算与塔相连的空间间隙的公式？", "魔法学者阿蒂#tnm";
	end;
OnTimer24000:
	npctalk "谢普：是的，是的，是你！", "历史学家谢普#tnm01";
	end;
OnTimer27000:
	npctalk "阿蒂：所以我想你不再质疑我有多聪明了，对吗？", "魔法学者阿蒂#tnm";
	end;
OnTimer30000:
	npctalk "谢普：好吧，好吧。我需要一个冒险家来帮我们探索这座塔。", "历史学家谢普#tnm01";
	end;
OnTimer33000:
	npctalk "Shep：没有人自愿，因为大家都说这太危险了！", "历史学家谢普#tnm01";
	end;
OnTimer36000:
	npctalk "阿蒂：没有志愿者吗？快点。利用你的激励技巧并找到合适的人。", "魔法学者阿蒂#tnm";
	end;
OnTimer37000:
	emotion ET_KIK, getnpcid(0, "Magic Scholar Artie#tnm");
	end;
OnTimer39000:
	npctalk "谢普：您好！！！现在正在招募新成员来探索恶魔之塔！", "历史学家谢普#tnm01";
	end;
OnTimer42000:
	npctalk "谢普：请来吧！现在你有机会与撒旦摩洛哥臭名昭著的爪牙面对面！", "历史学家谢普#tnm01";
	end;
OnTimer45000:
	npctalk "阿蒂：你确定这样说是正确的吗？也许你的招聘策略需要改进......", "魔法学者阿蒂#tnm";
	stopnpctimer;
	enablenpc "#tnmtks01";
	end;
}

// Entrance
1@tnm1,50,104,0	script	#event01	HIDDEN_WARP_NPC,1,1,{
	end;
OnTouch:
	disablenpc instance_npcname("#event01");
	initnpctimer;
	end;
OnTimer2000:
	npctalk "海姆警官：这是紧急情况……情况怎么样？", instance_npcname("海姆警官#heim0");
	end;
OnTimer5000:
	npctalk "提拉勒尔：下一层是最高的……但是恶魔太多了……", instance_npcname("一名散兵#tnm01");
	end;
OnTimer6000:
	emotion ET_THINK, getnpcid(0, instance_npcname("Officer Heim#heim0"));
	end;
OnTimer8000:
	npctalk "提拉勒尔：以我们的人数是不可能突破的。", instance_npcname("一名散兵#tnm01");
	emotion ET_PROFUSELY_SWEAT, getnpcid(0, instance_npcname("A tirailleur#tnm01"));
	end;
OnTimer11000:
	npctalk "海姆军官：现在有多少士兵幸存？支持团队什么时候到达？", instance_npcname("海姆警官#heim0");
	end;
OnTimer12000:
	emotion ET_THINK, getnpcid(0, instance_npcname("An aide#tnm02"));
	end;
OnTimer16000:
	npctalk "助手：没有足够的钱来打电话……", instance_npcname("一名助手#tnm02");
	end;
OnTimer17000:
	npctalk "助手：好消息是恶魔已经停止攻击了。", instance_npcname("一名助手#tnm02");
	end;
OnTimer21000:
	npctalk "海姆警官：我们不能就这样被困在这里，还可以走得更远。为什么恶魔停止了攻击？", instance_npcname("海姆警官#heim0");
	end;
OnTimer24000:
	npctalk "A tirailleur： 上面好像有什么事情发生......", instance_npcname("一名散兵#tnm01");
	end;
OnTimer26000:
	disablenpc instance_npcname("Officer Heim#heim0");
	enablenpc instance_npcname("Officer Heim#heim");
	npctalk "海姆警官：你是谁？", instance_npcname("海姆警官#heim");
	stopnpctimer;
	end;
}

1@tnm1,57,112,5	script	Officer Heim#heim0	4_TOWER_05,{ end; }

1@tnm1,56,110,3	script	Officer Heim#heim	4_TOWER_05,{
	if (is_party_leader() == false) {
		mes "[海姆警官]";
		mes "谁把你们带到这里来的？";
		mes "我将与一位代表交谈。";
		close;
	}
	switch('devil_tower) {
	case 0:
		mes "[海姆警官]";
		mes "支援士兵？那么你为什么只是站在那里？";
		npctalk "海姆警官：支援士兵？那么你为什么只是站在那里？";
		next;
		select("现在？");
		if (is_party_leader() == true && 'devil_tower == 0) {
			enablenpc instance_npcname("Healer Fama#feima");
			'devil_tower = 1;
		}
		mes "[治疗师法玛]";
		mes "指挥官请帮助我。我一个人做不到啊~！";
		npctalk "法玛法师：指挥官请帮助我。我一个人做不到啊~！", instance_npcname("治疗师法玛#feima");
		next;
		mes "[海姆警官]";
		mes "你听到了吗？";
		mes "帮助法玛照顾受伤的士兵。";
		mes "Fama会给你急救箱，用它来帮助受伤的人。";
		close2;
		setpcblock PCBLOCK_NPC, true;
		npctalk "海姆警官：你听到了吗？";
		sleep2 2000;
		npctalk "海姆警官：帮助法玛照顾受伤的士兵。";
		sleep2 2000;
		npctalk "法师法师：好吧~你要帮我吗？快来领取急救箱吧~！";
		setpcblock PCBLOCK_NPC, false;
		end;
	case 1:
		mes "[海姆警官]";
		mes "协助法玛优先救治受伤士兵。";
		mes "即使环境再恶劣，也请尽力而为。";
		close;
	default:
		mes "[海姆警官]";
		mes "幸运的是，目前还没有人死亡。";
		mes "我不想看到我的士兵死去。";
		close;
	}
}

1@tnm1,47,109,3	script	Healer Fama#feima	4_TOWER_10,{
	if ('devil_tower == 1) {
		if (countitem(7641) < 1) {
			mes "[治疗师法玛]";
			mes "请用这个急救箱治疗伤者。";
			mes "这并不难。只需按照说明操作即可。";
			next;
			mes "[治疗师法玛]";
			mes "我们的供应不足。所以我会一次给他们一次。";
			mes "抱歉，如果用完请再次请求。";
			if ('devil_tower == 1)
				getitem 7641,1;// Medical_Cure_Box
			close;
		}
		mes "[治疗师法玛]";
		mes "抱歉……现在采购不太好，所以我一次只能提供一个急救箱。";
		mes "如果用完请再次请求。";
		close;
	}
	mes "[治疗师法玛]";
	mes "呃……好了，我们可以休息一下了。";
	mes "非常感谢！";
	close;
}

1@tnm1,39,114,3	script	Injured Soldier#1_1	4_TOWER_01,3,3,{
	if ('devil_tower != 1)
		end;
	if (countitem(7641) < 1) {
		mes "我需要 1 个急救箱来治疗受伤的士兵。";
		close;
	}
	mes "这名士兵已经失去了知觉，发出呻吟声。";
	mes "我应该使用急救箱吗？";
	next;
	if (select( "使用急救箱。", "观察情况。" ) == 2) {
		mes "你决定进一步观察受伤士兵的情况。";
		close;
	}
	mes "您为士兵提供急救箱。";
	next;
	specialeffect EF_SPHERE;
	progressbar "000000",10;
	if ('devil_tower == 1) {
		delitem 7641,1;
		specialeffect EF_ENTRY;
		disablenpc();
		enablenpc instance_npcname( strnpcinfo(1) + "#" + replacestr( strnpcinfo(2), "_1", "" ) );
		specialeffect EF_ENTRY,AREA, instance_npcname( strnpcinfo(1) + "#" + replacestr( strnpcinfo(2), "_1", "" ) );
		mes "它起作用了。";
		'heal_count++;
		if ('heal_count == 7) {
			'devil_tower = 2;
			npctalk "治愈者法玛：刺客？！", instance_npcname("治疗师法玛#feima");
			enablenpc instance_npcname("Lucile#tnm01");
			enablenpc instance_npcname("Magic Swordman#tnm01");
			enablenpc instance_npcname("Assassin Huey#tnm01");
			enablenpc instance_npcname("Assassin Louie#tnm01");
		}
	}
	close;

OnTouch:
	.@num = atoi( replacestr( strnpcinfo(2), "_1", "" ) );
	if (.@num == 1)
		npctalk "受伤士兵：呻吟……";
	else if (.@num == 2)
		npctalk "受伤的士兵：呜哇……";
	else if (.@num == 3)
		npctalk "受伤的士兵：……啊……啊……";
	else if (.@num == 4)
		npctalk "受伤的士兵：呜哇……";
	else if (.@num == 5)
		npctalk "受伤士兵：我不想死……";
	else if (.@num == 6)
		npctalk "受伤的士兵：哎呀~";
	else
		npctalk "受伤士兵：呻吟……";
	end;
}

1@tnm1,30,120,3	duplicate(Injured Soldier#1_1)	Injured Soldier#2_1	4_TOWER_03,3,3
1@tnm1,25,117,5	duplicate(Injured Soldier#1_1)	Injured Soldier#3_1	4_TOWER_06,3,3
1@tnm1,19,118,4	duplicate(Injured Soldier#1_1)	Injured Soldier#4_1	4_TOWER_08,3,3
1@tnm1,18,110,4	duplicate(Injured Soldier#1_1)	Injured Soldier#5_1	4_TOWER_11,3,3
1@tnm1,24,104,7	duplicate(Injured Soldier#1_1)	Injured Soldier#6_1	4_TOWER_13,3,3
1@tnm1,31,110,3	duplicate(Injured Soldier#1_1)	Injured Soldier#7_1	4_TOWER_03,3,3

1@tnm1,39,114,3	script	Injured Soldier#1	4_TOWER_02,{
	mes "[受伤的士兵]";
	mes "呃..现在好像没问题了。";
	mes "谢谢。";
	close;
}

1@tnm1,30,120,3	script	Injured Soldier#2	4_TOWER_04,{
	mes "[受伤的士兵]";
	mes "呻吟……就让我死吧。";
	mes "不，太痛了。";
	close;
}

1@tnm1,25,117,5	script	Injured Soldier#3	4_TOWER_07,{
	mes "[受伤的士兵]";
	mes "他的意识似乎依然模糊。";
	mes "让他好好休息一下。";
	close;
}

1@tnm1,19,118,4	script	Injured Soldier#4	4_TOWER_09,{
	mes "[受伤的士兵]";
	mes "我以为我要死了。";
	mes "但我还活着！";
	close;
}

1@tnm1,18,110,4	script	Injured Soldier#5	4_TOWER_12,{
	mes "[受伤的士兵]";
	mes "我不能死在这里。回家后我必须向我心爱的人求婚。";
	mes "我们一定会幸福吗？";
	close;
}

1@tnm1,24,104,7	script	Injured Soldier#6	4_TOWER_14,{
	mes "[受伤的士兵]";
	mes "我感觉好多了，但我还是不能动。";
	mes "不过我很幸运还活着。";
	close;
}

1@tnm1,31,110,3	script	Injured Soldier#7	4_TOWER_04,{
	mes "[受伤的士兵]";
	mes "谢谢..谢谢..";
	close;
}

1@tnm1,61,110,3	script	An aide#tnm02	4_M_MOCASS2,{
	mes "[一名助手]";
	mes "几百年前的灾难，不会再重演了吧？";
	mes "恶魔怎么突然又出现了？";
	close;
}

1@tnm1,61,113,3	script	A tirailleur#tnm01	4_M_MOCASS1,{
	mes "[散兵]";
	mes "您是后备团队的成员吗？";
	mes "好的。这里的情况正在变得更糟。";
	mes "很高兴大家都来帮忙。";
	close;
}

1@tnm1,46,105,5	script	Lucile#tnm01	4_F_LUCILE,{
	if (is_party_leader() == false) {
		cutin "tnm_lucile01.bmp",2;
		mes "[露西尔]";
		mes "后备团队由谁负责？";
		close3;
	}
	if ('devil_tower == 2) {
		.@lucile$ = instance_npcname("Lucile#tnm01");
		.@heim$ = instance_npcname("Officer Heim#heim");
		'devil_tower = 3;
		setpcblock PCBLOCK_NPC, true;
		cutin "tnm_lucile01.bmp",2;
		sleep2 1500;
		npctalk "露西尔：我是刺客公会的露西尔。", .@lucile$;
		sleep2 2000;
		npctalk "海姆警官：我们的后援来自刺客公会？感谢您及时赶到。", .@heim$;
		sleep2 3000;
		npctalk "露西尔：情况似乎不太好。", .@lucile$;
		sleep2 3000;
		npctalk "露西尔：普隆德拉勇敢的骑士们怎么了？", .@lucile$;
		sleep2 3000;
		npctalk "海姆警官：你在嘲笑我吗？", .@heim$;
		sleep2 3000;
		npctalk "露西尔：一点也不。我真的很想知道。楼上这是怎么回事？", .@lucile$;
		sleep2 2500;
		emotion ET_OHNO, getnpcid(0, .@heim$);
		sleep2 1000;
		npctalk "海姆警官：你怎么知道的？是的，恶魔们已经停止了来自楼上的攻击。", .@heim$;
		sleep2 3000;
		emotion ET_SMILE, getnpcid(0, .@lucile$);
		npctalk "露西尔：你对《魔剑士》的情况有什么看法？", .@lucile$;
		cutin "tnm_lucile01.bmp",0;
		sleep2 3000;
		npctalk "魔剑士：嗯，我去调查一下。", instance_npcname("魔法剑士#tnm01");
		cutin "tnm_loki.bmp",2;
		sleep2 2000;
		cutin "",255;
		disablenpc instance_npcname("Magic Swordman#tnm01");
		sleep2 1000;
		npctalk "露西尔：你怎么这么快？", .@lucile$;
		cutin "tnm_lucile01.bmp",2;
		sleep2 2000;
		npctalk "海姆警官：你在做什么？如果你一个人去，你就会被杀！", .@heim$;
		sleep2 3000;
		npctalk "露西尔：我不会孤单。还有一些没有受伤的士兵！", .@lucile$;
		sleep2 2500;
		npctalk "露西尔：好吧，我会请你做我的后盾。哈哈哈对杜威吗？", .@lucile$;
		sleep2 500;
		enablenpc instance_npcname("Assassin Dewey#tnm01");
		enablenpc instance_npcname("#tnm1stepmob");
		npctalk "杜威：好的，我会的。", instance_npcname("刺客杜威#tnm01");
		'devil_tower = 4;
		sleep2 500;
		cutin "",255;
		disablenpc .@lucile$;
		disablenpc instance_npcname("Assassin Huey#tnm01");
		disablenpc instance_npcname("Assassin Louie#tnm01");
		sleep2 2000;
		npctalk "海姆警官：他们太不可预测了。你知道发生了什么事吗？支持他们...", .@heim$;
		sleep2 2500;
		npctalk "海姆警官：帮我们对付从楼上下来的恶魔。", .@heim$;
		setpcblock PCBLOCK_NPC, false;
		end;
	}
}

1@tnm1,52,104,2	duplicate(Officer Heim#heim0)	Magic Swordman#tnm01	4_M_ROKI
1@tnm1,42,104,4	duplicate(Officer Heim#heim0)	Assassin Huey#tnm01	4_TOWER_15
1@tnm1,44,101,5	duplicate(Officer Heim#heim0)	Assassin Louie#tnm01	4_M_ACROSS

1@tnm1,70,108,3	script	Assassin Dewey#tnm01	4_M_ACROSS,{
	if ('devil_tower < 4)
		end;
	if (isbegin_quest(7569) == 0)
		setquest 7569;// Treat the injured
	if (isbegin_quest(7570) == 0)
		setquest 7570;// Destroy the demons
	mes "[杜威]";
	mes "我会带你到楼上。";
	mes "不要忘记消灭路上遇到的任何恶魔。";
	next;
	if (select( "立即搬家", "我需要更多的时间来准备。" ) == 2) {
		mes "[杜威]";
		mes "为任何事情做好准备。";
		close;
	}
	mes "[杜威]";
	mes "小心旅行。";
	close2;
	warp 'map_tnm1$,91,23;
	devil_tower_can_reenter = 1;// nb. different from official (condition overcomplicated)
	end;
}

// Stairs tnm1
// spawn inaccurate
1@tnm1,91,23,0	script	#tnm1stepmob	HIDDEN_WARP_NPC,1,1,{
	end;
OnTouch:
	if ('devil_tower != 4)
		end;
	disablenpc instance_npcname("#tnm1stepmob");
	enablenpc instance_npcname("Assassin Louie#tnm02");
	enablenpc instance_npcname("Lucile#tnm02");
	initnpctimer;
	monster 'map_tnm1$,99,24,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	'mob_id = $@mobid[0];
	end;
OnTimer2000:
	if (unitexists('mob_id) == true)
		unittalk 'mob_id, "What is this? A human? How dare they must have lost their minds!";
	end;
OnTimer5000:
	monster 'map_tnm1$,103,21,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	unittalk $@mobid[0], "Did you make a resolution?";
	monster 'map_tnm1$,103,26,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	unittalk $@mobid[0], "Ha ha ha ha....Let's fight..!!!";
	end;
OnTimer11000:
	monster 'map_tnm1$,106,25,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,106,27,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,110,20,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,112,24,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,107,22,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	monster 'map_tnm1$,114,24,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	end;
OnTimer17000:
	monster 'map_tnm1$,120,20,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,122,23,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	end;
OnTimer23000:
	monster 'map_tnm1$,116,25,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,116,21,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	end;
OnTimer29000:
	monster 'map_tnm1$,134,21,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,130,25,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,129,27,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,120,27,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	monster 'map_tnm1$,118,22,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	end;
OnTimer35000:
	monster 'map_tnm1$,138,27,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,144,23,"Evil Shadow",2939,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,129,20,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	end;
OnTimer41000:
	monster 'map_tnm1$,140,24,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,144,20,"Evil Shadow",2940,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,130,27,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	monster 'map_tnm1$,127,24,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	monster 'map_tnm1$,142,27,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	monster 'map_tnm1$,140,22,"Evil Shadow",2941,1, instance_npcname("#tnm1stepmob") + "::OnMobDead";	// MM_EVIL_SHADOW3
	stopnpctimer;
	end;
OnMobDead:
	end;
}

1@tnm1,158,24,3	script	Assassin Louie#tnm02	4_M_ACROSS,{
	if ('devil_tower < 4)
		end;
	if (checkquest(7570,HUNTING) != 2) {
		mes "[路易]";
		mes "队长说我们需要消灭所有的恶魔......";
		mes "最好开始做。";
		close;
	}
	mes "[路易]";
	mes "许多士兵在袭击中仍然幸存。";
	mes "他们除掉了所有的恶魔。";
	next;
	mes "[路易]";
	mes "我确保没有人上楼，我担心又会发生什么事情。";
	next;
	mes "[路易]";
	mes "我希望你能帮助我。";
	mes "我会引导你。";
	if (isbegin_quest(7571) == 0) {
		erasequest 7569;// Treat the injured
		erasequest 7570;// Destroy the demons
		setquest 7571;// Tower Expedition
	}
	next;
	mes "我已经探索这座塔的通道有一段时间了。";
	close2;
	warp 'map_tnm1$,124,86;
	end;
}

// Stairs 2 tnm1
1@tnm1,138,96,3	script	Lucile#tnm02	4_F_LUCILE,{
	if (isbegin_quest(7571) == 0)
		end;
	if (is_party_leader() == false) {
		mes "你在她面前挥了挥手，但她没有反应。";
		close;
	}
	if ('devil_tower < 4)
		end;
	if ('devil_tower < 6) {
		if ('devil_tower == 4) {
			mes "[露西尔]";
			mes "如果我给你任何态度，我很抱歉，但我不能动。";
			npctalk "露西尔：如果我给你任何态度，我很抱歉，但我不能动。";
			cutin "tnm_lucile02.bmp",2;
			next;
			select("我可以帮忙吗？");
			mes "[露西尔]";
			mes "我可以使用自我恢复，但我没有时间这样做。";
			mes "你愿意与我分享你的力量吗？";
			npctalk "露西尔：我可以使用自我恢复，但我没有时间。你愿意与我分享你的力量吗？";
			next;
			mes "[露西尔]";
			mes "我需要你的帮助来激活我的恢复系统。";
			mes "请抓紧。";
			npctalk "露西尔：我需要你的帮助来激活我的恢复系统。";
			next;
			sleep2 750;
			npctalk "露西尔：请抓紧。";
			if (is_party_leader() == true && 'devil_tower == 4) {
				donpcevent instance_npcname("Huey#hui02") + "::OnEnable";
				'devil_tower = 5;
			}
			percentheal -30,0;
		}
		cutin "tnm_lucile03.bmp",2;
		mes "露西尔似乎是一台机器。";
		mes "我突然听到机械声。";
		specialeffect EF_LEVEL99_3;
		progressbar "000000",40;
		if (is_party_leader() == true && 'devil_tower == 5) {
			if (isbegin_quest(7572) == 0)
				setquest 7572;// Lucile...?
			'devil_tower = 6;
		}
		mes "系统已恢复。";
		next;
		mes "[露西尔]";
		mes "哇！你修复它的速度比我想象的要快。";
		mes "这让你感到惊讶吗？";
		mes "因为我的身体不正常，所以偶尔需要修复一下";
		cutin "tnm_lucile02.bmp",2;
		next;
		mes "[露西尔]";
		mes "这个身体是上天赐予的礼物。";
		mes "看到中间的门了吗？";
		cutin "tnm_lucile01.bmp",2;
		next;
		mes "[露西尔]";
		mes "快点，否则这个世界就会有危险。";
		close2;
		cutin "",255;
		// continue
	}
	if ('devil_tower == 6) {
		sleep 1500;
		npctalk "露西尔：请原谅，如果你想拯救世界，就跟我来吧。";
		sleep 2000;
		npctalk "休伊：姐姐……", instance_npcname("休伊#hui02");
		if ('devil_tower == 6) {
			disablenpc instance_npcname("Lucile#tnm02");
			enablenpc instance_npcname("Lucile#tnm03");
			enablenpc instance_npcname("Magic Swordman#tnm02");
			enablenpc instance_npcname("Demon Morocc#tnm01");
			'devil_tower = 7;
		}
	}
	end;
}
	
1@tnm1,135,99,5	script	Huey#hui02	4_TOWER_15,{
	if ('devil_tower < 5)
		end;
	if ('devil_tower == 5 || 'devil_tower == 6) {
		mes "[休伊]";
		mes "我很担心她。";
		mes "每当她受伤时就会发生这种情况。";
		mes "因为她基本上是机器...";
		close;
	}
	if ('mob_count > 0) {
		mes "[休伊]";
		mes "我知道楼上的路...";
		mes "首先，我们必须除掉这些恶魔！";
		close;
	}
	mes "[休伊]";
	mes "我想知道其他人怎么样了...";
	mes "只有塔纳托斯和露西尔曾进入过上层……";
	next;
	mes "[休伊]";
	mes "如果你想帮助他们，就必须抓紧时间。";
	mes "我会为你引路。";
	next;
	if (select( "立即移动。", "不要跟随。" ) == 2) {
		mes "[休伊]";
		mes "...我懂了。";
		close;
	}
	mes "[休伊]";
	mes "这边走。";
	mes "当心。";
	close2;
	warp 'map_tnm2$,152,130;
	end;

OnEnable:
	enablenpc instance_npcname("Huey#hui02");
	initnpctimer;
	end;
OnTimer3000:
	npctalk "休伊：哦不，姐姐小心！恶魔突然出现了！";
	'mob_count = 16;
	monster 'map_tnm1$,142,90,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,139,91,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,130,86,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
	monster 'map_tnm1$,143,95,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW3
	monster 'map_tnm1$,134,90,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";
	end;
OnTimer6000:
	npctalk "休伊：他们从哪里来？恶魔又来了……";
	end;
OnTimer8000:
	monster 'map_tnm1$,137,103,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,140,102,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";
	monster 'map_tnm1$,132,103,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW2
	monster 'map_tnm1$,146,100,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
	monster 'map_tnm1$,142,105,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
	monster 'map_tnm1$,135,107,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW3
	end;
OnTimer11000:
	npctalk "休伊：噢，不……";
	end;
OnTimer12000:
	monster 'map_tnm1$,135,107,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";	// MM_EVIL_SHADOW1
	monster 'map_tnm1$,146,100,"Evil Shadow",2939,1, instance_npcname("Huey#hui02") + "::OnMobDead";
	monster 'map_tnm1$,130,86,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";		// MM_EVIL_SHADOW2
	monster 'map_tnm1$,137,103,"Evil Shadow",2940,1, instance_npcname("Huey#hui02") + "::OnMobDead";
	monster 'map_tnm1$,143,95,"Evil Shadow",2941,1, instance_npcname("Huey#hui02") + "::OnMobDead";		// MM_EVIL_SHADOW3
	stopnpctimer;
	end;
OnMobDead:
	'mob_count--;
	if ('mob_count == 0)
		npctalk "休伊：这边走。这样……我们就可以利用恶魔过来的缝隙上楼了！";
	end;
}

// Morocc - Thanatos Battle
1@tnm2,136,139,3	script	Magic Swordman#tnm02	THANATOS_KEEP,{
	mes "[魔剑士塔纳托斯]";
	mes "对不起，但我已经度过了一段艰难的时光......";
	close;
}

1@tnm2,133,139,5	duplicate(Officer Heim#heim0)	Demon Morocc#tnm01	1916

1@tnm2,144,137,3	script	Lucile#tnm03	4_F_LUCILE,{
	if (is_party_leader() == false) {
		mes "[露西尔]";
		mes "我现在正在和你们的党魁谈话，所以请退后一步。";
		cutin "tnm_lucile01.bmp",2;
		close3;
	}
	if ('devil_tower == 7) {
		mes "[露西尔]";
		mes "哦。你来得正是时候。";
		mes "你想去那边看看吗？";
		cutin "tnm_lucile01.bmp",2;
		next;
		mes "[露西尔]";
		mes "我们需要阻止恶魔......";
		mes "你能做到吗？";
		npctalk "露西尔：我们需要阻止恶魔……你能做到吗？";
		if (is_party_leader() == true && 'devil_tower == 7) {
			if (isbegin_quest(7573) == 0)
				setquest 7573;// Magic Swordman Thanatos
			'demon$ = instance_npcname("Demon Morocc#tnm01");
			'thanatos$ = instance_npcname("Magic Swordman#tnm02");
			initnpctimer;
			'devil_tower = 8;
		}
		close3;
	}
	mes "[露西尔]";
	mes "我们必须守住这个位置，直到魔剑士设下陷阱。";
	cutin "tnm_lucile01.bmp",2;
	close3;

OnTimer2000:
	npctalk "恶魔摩洛哥：你这个愚蠢的人类。你以为你能绑我多久！", 'demon$;
	end;
OnTimer5000:
	npctalk "恶魔摩洛哥：阴影！出来消灭一切黑暗之敌吧！", 'demon$;
	end;
OnTimer6000:
	donpcevent instance_npcname("#devil_seal_timer") + "::OnStart";
	end;
OnTimer8000:
	npctalk "塔纳托斯：你如此绝望，求我救你。", 'thanatos$;
	end;
OnTimer11500:
	npctalk "露西尔：塔纳托斯。先生，你应该这么虚张声势吗？";
	end;
OnTimer12000:
	npctalk "恶魔摩洛哥：害怕我被释放后带来的毁灭！", 'demon$;
	end;
OnTimer15000:
	npctalk "塔纳托斯：闭嘴！除非我杀了你，否则你永远不会离开这里！", 'thanatos$;
	end;
OnTimer16500:
	npctalk "塔纳托斯：露西尔！当我召唤出盾牌来挡住他时，请注意我的背后！", 'thanatos$;
	end;
OnTimer19500:
	npctalk "露西尔：是的，先生。";
	end;
OnTimer22500:
	npctalk "露西尔：好的大家，现在我们知道该怎么做了。我们必须阻止魔法封印到达这里！";
	stopnpctimer;
	end;
}

1@tnm2,1,1,0	script	#devil_flamecross	HIDDEN_WARP_NPC,{
	end;
OnStart:
	npctalk "恶魔摩洛哥：不可能！！麻烦制造者！", 'demon$;
	mapannounce 'map_tnm2$, "Demon Morocc: No way!! Troublemakers!", bc_map,0xFF0000;
	initnpctimer;
	end;
OnTimer1000:
	setarray .@north[0], 138,151, 138,160, 140,165, 140,176, 140,170, 140,181, 140,186, 140,191;
	setarray .@north_west[0], 126,149, 122,151, 118,153, 113,154, 107,156, 101,158, 96,160, 91,161, 86,162, 81,163;
	setarray .@south_west[0], 128,130, 125,127, 122,124, 118,121, 115,118, 112,115, 109,114, 106,106, 102,102, 99,98;
	setarray .@south_east[0], 145,130, 148,126, 151,122, 156,120, 160,117, 164,113, 168,109, 172,105, 176,101, 180,97;
	setarray .@north_east[0], 146,149, 148,149, 150,151, 155,152, 162,153, 167,154, 173,156, 179,158, 185,160, 191,161;
	.@label$ = instance_npcname("#devil_flamecross") + "::OnFlameDead";
	for ( .@i = 0; .@i < 20; .@i += 2 ) {
		if (.@north[.@i] > 0)	// 8 mobs for north
			monster 'map_tnm2$,.@north[.@i],.@north[.@i+1], "",2960,1, .@label$;
		monster 'map_tnm2$,.@north_west[.@i],.@north_west[.@i+1], "",2960,1, .@label$;
		monster 'map_tnm2$,.@south_west[.@i],.@south_west[.@i+1], "",2960,1, .@label$;	
		monster 'map_tnm2$,.@south_east[.@i],.@south_east[.@i+1], "",2960,1, .@label$;	
		monster 'map_tnm2$,.@north_east[.@i],.@north_east[.@i+1], "",2960,1, .@label$;	
		sleep 200;
	}
	end;
OnTimer6000:
	killmonster 'map_tnm2$, instance_npcname("#devil_flamecross") + "::OnFlameDead";
	end;
OnTimer20000:
	stopnpctimer;
	donpcevent instance_npcname("#devil_flamecross") + "::OnStart";
	end;
OnStop:
	stopnpctimer;
	killmonster 'map_tnm2$, instance_npcname("#devil_flamecross") + "::OnFlameDead";
	end;
OnFlameDead:
	end;
}

1@tnm2,1,1,0	script	#devil_seal_timer	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#devil_seal_timer");
	enablenpc instance_npcname("#devil_flamecross");
	setarray .@direction$[0], "DIR_NORTHWEST", "DIR_SOUTHWEST", "DIR_SOUTHEAST", "DIR_NORTHEAST", "DIR_NORTH";
	for ( .@i = 0; .@i < 5; .@i++ ) {
		enablenpc instance_npcname("devil_seal_spawn#" + .@direction$[.@i]);
		enablenpc instance_npcname("seal_reach_center#" + .@direction$[.@i]);
	}
	enablenpc instance_npcname("#devil_seal_dead");
	donpcevent instance_npcname("#devil_seal_spawn_global") + "::OnCoord";
	initnpctimer;
	end;
OnTimer5000:
	mapannounce 'map_tnm2$, "A Magic Seal has just appeared in southeast direction of the tower.", bc_map,0xFFFFFF;
	donpcevent instance_npcname("#devil_seal_spawn_global") + "::OnFirstSpawn";
	end;
OnTimer10000:
	donpcevent instance_npcname("#devil_flamecross") + "::OnStart";
	end;
OnTimer25000:
	mapannounce 'map_tnm2$, "Lucile: We should stop before Magic Seal has just started to move again.", bc_map,0xFFFFFF;
	end;
OnTimer65000:
	mapannounce 'map_tnm2$, "Lucile: We should stop before Magic Seal reaches the center.", bc_map,0xFFFFFF;
	end;
OnTimer95000:
	mapannounce 'map_tnm2$, "Lucile: The Magic Seal has just started to move again.", bc_map,0xFFFFFF;
	end;
OnTimer125000:
	mapannounce 'map_tnm2$, "Lucile: It is coming closer... No... It's too dangerous.", bc_map,0xFFFFFF;
	end;
OnTimer155000:
	mapannounce 'map_tnm2$, "Lucile: The Magic Seal is almost approaching the center!!", bc_map,0xFFFFFF;
	end;
OnTimer180000:
	donpcevent instance_npcname("#devil_flamecross") + "::OnStop";
	end;
OnTimer185000:
	mapannounce 'map_tnm2$, "Lucile: We should stop it now!", bc_map,0xFFFFFF;
	end;
OnTimer191000:
	npctalk "塔纳托斯：你在做什么！！！你现在在玩游戏吗？！", 'thanatos$;
	mapannounce 'map_tnm2$, "Thanatos: What are you doing!!! Are you playing a game now?!", bc_map,0xFF00;
	end;
OnTimer193000:
	specialeffect EF_MAGNUMBREAK,AREA, 'thanatos$;
	specialeffect EF_LORD,AREA, 'thanatos$;
	setarray .@direction$[0], "DIR_NORTHWEST", "DIR_SOUTHWEST", "DIR_SOUTHEAST", "DIR_NORTHEAST", "DIR_NORTH";
	for ( .@i = 0; .@i < 5; .@i++ )
		donpcevent instance_npcname("devil_seal_spawn#" + .@direction$[.@i]) + "::OnStop";
	end;
OnTimer195000:
	npctalk "恶魔摩洛哥：你还想继续骚扰我吗！！！", 'demon$;
	mapannounce 'map_tnm2$, "Demon Morocc: Are you going to keep disturbing me!!!", bc_map,0xFF0000;
	end;
OnTimer198000:
	npctalk "恶魔摩洛哥：好吧，让我们看看谁会赢！出来！", 'demon$;
	mapannounce 'map_tnm2$, "Demon Morocc: Okay, Let's see who is going to win!!! Come out!", bc_map,0xFF0000;
	end;
OnTimer201000:
	npctalk "塔纳托斯：不管尝试多少次都是一样的！放弃吧，滚回魔界吧！！", 'thanatos$;
	mapannounce 'map_tnm2$, "Thanatos: It will be the same no matter how many times you try! Give up and go back to demon world!!", bc_map,0xFF00;
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	donpcevent instance_npcname("#devil_flamecross") + "::OnStop";
	disablenpc instance_npcname("#devil_flamecross");
	disablenpc instance_npcname("#devil_seal_timer");
	end;
}

1@tnm2,1,1,0	script	#devil_seal_spawn_global	HIDDEN_WARP_NPC,{
	end;
OnCoord:
	enablenpc instance_npcname("#devil_seal_spawn_global");
	// setarray 'coord_seal_DIR_NORTHWEST[0],  68,167,	 78,165,	88,163,		98,160,		108,156,	118,152,	127,147;
	setarray 'coord_seal_DIR_NORTHWEST[0],  68,167,	 78,165,	88,163,		98,160,		108,156,	118,152,	118,152;// officially
	setarray 'coord_seal_DIR_SOUTHWEST[0],  90, 86,	 96, 95,	103,103,	110,110,	116,116,	122,122,	129,130;
	setarray 'coord_seal_DIR_SOUTHEAST[0], 191,86,	183, 94,	174,103,	165,112,	156,120,	147,128,	143,131;
	setarray 'coord_seal_DIR_NORTHEAST[0], 197,162,	188,161,	179,158,	170,155,	161,152,	152,149,	144,148;
	setarray 'coord_seal_DIR_NORTH[0], 142,214,	140,205,	140,195,	140,185,	140,173,	139,161,	138,150;
	end;
OnFirstSpawn:
	'round[DIR_NORTHWEST] = 'round[DIR_SOUTHWEST] = 'round[DIR_SOUTHEAST] = 'round[DIR_NORTHEAST] = 'round[DIR_NORTH] = 0;
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnFirstSpawn";
	donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnFirstSpawn";
	donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnFirstSpawn";
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnFirstSpawn";
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnFirstSpawn";
	initnpctimer;
	end;
OnTimer2000:
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnSlaves";
	donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnSlaves";
	donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnSlaves";
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnSlaves";
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnSlaves";
	end;
OnTimer20000:
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnStart";
	donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnStart";
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnStart";
	end;
OnTimer30000:
	donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnStart";
	donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnStart";
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#devil_seal_spawn_global");
	end;
}

1@tnm2,1,1,0	script	devil_seal_spawn#DIR_NORTHWEST	HIDDEN_WARP_NPC,{
	end;
OnStart:
	.@num = getd( strnpcinfo(2) );
	if ('seal_id[.@num] == 0 || unitexists('seal_id[.@num]) == 0)
		stopnpctimer;
	else {
		getunitdata 'seal_id[.@num], .@data;
		'seal_hp[.@num] = .@data[UMOB_HP];
		killmonster 'map_tnm2$, instance_npcname("#devil_seal_dead") + "::OnSeal_" + strnpcinfo(2);
		initnpctimer;
	}
	end;
OnFirstSpawn:
OnTimer5000:
	.@num = getd( strnpcinfo(2) );
	.@index = 'round[.@num] * 2;
	if (.@index == 0 || 'seal_id[.@num]) {
		.@direction$ = strnpcinfo(2);
		.@x = getd( "'coord_seal_" + .@direction$ + "[" + .@index + "]" );
		.@y = getd( "'coord_seal_" + .@direction$ + "[" + (.@index+1) + "]" );

		monster 'map_tnm2$,.@x,.@y,"Magic Seal",2938,1, instance_npcname("#devil_seal_dead") + "::OnSeal_" + .@direction$;	// MM_MAGIC_SEAL
		'seal_id[.@num] = $@mobid[0];
		if (.@index == 0)
			setunitdata 'seal_id[.@num], UMOB_HP, 3000000;
		else if ('seal_hp[.@num] > 0)
			setunitdata 'seal_id[.@num], UMOB_HP, 'seal_hp[.@num];
		else
			stopnpctimer;
		'round[.@num]++;
		if ('round[.@num] == 7)
			donpcevent instance_npcname("seal_reach_center#" + .@direction$) + "::OnStart";
	}
	end;
OnSlaves:
OnTimer7000:
	killmonster 'map_tnm2$, instance_npcname(strnpcinfo(0)) + "::OnMobDead";
	.@num = getd( strnpcinfo(2) );
	if ('seal_id[.@num] && unitexists('seal_id[.@num])) {
		.@direction$ = strnpcinfo(2);
		.@index = ('round[.@num] - 1) * 2;
		.@x = getd( "'coord_seal_" + .@direction$ + "[" + .@index + "]" );
		.@y = getd( "'coord_seal_" + .@direction$ + "[" + (.@index+1) + "]" );
		areamonster 'map_tnm2$,(.@x-1),(.@y-1),(.@x+1),(.@y+1),"Evil Shadow",2939,2, instance_npcname(strnpcinfo(0)) + "::OnMobDead";	// MM_EVIL_SHADOW1
		areamonster 'map_tnm2$,(.@x-1),(.@y-1),(.@x+1),(.@y+1),"Evil Shadow",2940,2, instance_npcname(strnpcinfo(0)) + "::OnMobDead";	// MM_EVIL_SHADOW2
		if ('round[.@num] == 7)
			stopnpctimer;
	}
	end;
OnTimer30000:
	donpcevent instance_npcname("devil_seal_spawn#" + strnpcinfo(2)) + "::OnStart";
	end;

OnDisable:
	stopnpctimer;
	killmonster 'map_tnm2$, instance_npcname(strnpcinfo(0)) + "::OnMobDead";	// note : officially still alive
	killmonster 'map_tnm2$, instance_npcname("#devil_seal_dead") + "::OnSeal_" + strnpcinfo(2);	// note : officially still alive
	disablenpc();
	end;
OnStop:
	stopnpctimer;
	killmonster 'map_tnm2$, instance_npcname(strnpcinfo(0)) + "::OnMobDead";
	killmonster 'map_tnm2$, instance_npcname("#devil_seal_dead") + "::OnSeal_" + strnpcinfo(2);
	end;
OnMobDead:
	end;
}
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_SOUTHWEST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_SOUTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_NORTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(devil_seal_spawn#DIR_NORTHWEST)	devil_seal_spawn#DIR_NORTH	HIDDEN_WARP_NPC

1@tnm2,1,1,0	script	seal_reach_center#DIR_NORTHWEST	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc();
	initnpctimer;
	end;
OnTimer2000:
	npctalk "恶魔摩洛哥：库哈哈哈哈我的力量碎片！再次相聚于此！！！", 'demon$;
	mapannounce 'map_tnm2$, "Demon Morocc: Kuh ha ha ha ha Pieces of my power! Gather here again!!!", bc_map,0xFF0000;
	end;
OnTimer4000:
	npctalk "塔纳托斯：阻止他！阻止他靠近！！！", 'thanatos$;
	mapannounce 'map_tnm2$, "Thanatos: Stop him! Prevent him from coming closer!!!", bc_map,0xFF00;
	stopnpctimer;
	disablenpc();
	end;
}
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_SOUTHWEST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_SOUTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_NORTHEAST	HIDDEN_WARP_NPC
1@tnm2,1,1,0	duplicate(seal_reach_center#DIR_NORTHWEST)	seal_reach_center#DIR_NORTH	HIDDEN_WARP_NPC

1@tnm2,1,1,0	script	#devil_seal_dead	HIDDEN_WARP_NPC,{
	end;
OnSeal_DIR_NORTHWEST: callsub( S_Sealdead, DIR_NORTHWEST );
OnSeal_DIR_SOUTHWEST: callsub( S_Sealdead, DIR_SOUTHWEST );
OnSeal_DIR_SOUTHEAST: callsub( S_Sealdead, DIR_SOUTHEAST );
OnSeal_DIR_NORTHEAST: callsub( S_Sealdead, DIR_NORTHEAST );
OnSeal_DIR_NORTH: callsub( S_Sealdead, DIR_NORTH );

S_Sealdead:
	'seal_id[ getarg(0) ] = 0;
	'seal_dead[ getarg(0) ] = 1;
	.@total_seal = 'seal_dead[DIR_NORTHWEST] + 'seal_dead[DIR_SOUTHWEST] + 'seal_dead[DIR_SOUTHEAST] + 'seal_dead[DIR_NORTHEAST] + 'seal_dead[DIR_NORTH];
	if (.@total_seal != 5) {
		if (getarg(0) == DIR_NORTHWEST)
			mapannounce 'map_tnm2$, "Lucile: It is too early to feel easy! There are Magic Seals that haven't been destroyed yet.", bc_map,0xFF00;
		else
			mapannounce 'map_tnm2$, "There are still Magic Seals to destroy.", bc_map,0xFF00;
	}
	else {
		donpcevent instance_npcname("#devil_seal_spawn_global") + "::OnStop";
		donpcevent instance_npcname("#devil_seal_timer") + "::OnStop";	// stop timer of npc #devil_flamecross as well
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHWEST") + "::OnDisable";// note: on official seals and slaves are alive
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHWEST") + "::OnDisable";
		donpcevent instance_npcname("devil_seal_spawn#DIR_SOUTHEAST") + "::OnDisable";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTHEAST") + "::OnDisable";
		donpcevent instance_npcname("devil_seal_spawn#DIR_NORTH") + "::OnDisable";
		mapannounce 'map_tnm2$, "Demon Morocc: No.. This can't be.. Who the hell.. are you!", bc_map,0xFF0000;
		npctalk "恶魔摩洛哥：不..这不可能..你到底是谁..你！", 'demon$;
		initnpctimer;
	}
	end;
OnTimer3000:
	mapannounce 'map_tnm2$, "Thanatos: It will be the last time to show off!! Good bye!!!", bc_map,0xFF0000;
	npctalk "塔纳托斯：这是最后一次炫耀了！！再见！！！", 'thanatos$;
	end;
OnTimer5000:
	specialeffect EF_LORD,AREA, 'thanatos$;
	end;
OnTimer7000:
	stopnpctimer;
	disablenpc 'demon$;
	disablenpc 'thanatos$;
	disablenpc instance_npcname("Lucile#tnm03");
	enablenpc instance_npcname("Lucile#tnm04");
	enablenpc instance_npcname("#tnm3event01");
	enablenpc instance_npcname("Young Girl#tnm01");
	'devil_tower = 9;

	'demon$ = 'thanatos$ = "";
	'round[DIR_NORTHWEST] = 'round[DIR_SOUTHWEST] = 'round[DIR_SOUTHEAST] = 'round[DIR_NORTHEAST] = 'round[DIR_NORTH] = 0;
	'seal_id[DIR_NORTHWEST] = 'seal_id[DIR_SOUTHWEST] = 'seal_id[DIR_SOUTHEAST] = 'seal_id[DIR_NORTHEAST] = 'seal_id[DIR_NORTH] = 0;
	'seal_hp[DIR_NORTHWEST] = 'seal_hp[DIR_SOUTHWEST] = 'seal_hp[DIR_SOUTHEAST] = 'seal_hp[DIR_NORTHEAST] = 'seal_hp[DIR_NORTH] = 0;
	'seal_dead[DIR_NORTHWEST] = 'seal_dead[DIR_SOUTHWEST] = 'seal_dead[DIR_SOUTHEAST] = 'seal_dead[DIR_NORTHEAST] = 'seal_dead[DIR_NORTH] = 0;
	deletearray 'coord_seal_DIR_NORTHWEST[0],14;
	deletearray 'coord_seal_DIR_SOUTHWEST[0],14;
	deletearray 'coord_seal_DIR_SOUTHEAST[0],14;
	deletearray 'coord_seal_DIR_NORTHEAST[0],14;
	deletearray 'coord_seal_DIR_NORTH[0],14;
	end;
}

1@tnm2,144,137,3	script	Lucile#tnm04	4_F_LUCILE,{
	if ('devil_tower < 9)
		end;
	if (isbegin_quest(7574) == 0) {
		if (isbegin_quest(7572) == 1)
			erasequest 7572;// Lucile...?
		if (isbegin_quest(7573) == 1)	// leader
			erasequest 7573;// Magic Swordman Thanatos
		setquest 7574;// Thanatos Tower
		mes "[露西尔]";
		mes "我猜恶魔摩洛哥人打破封印之盾的力量已经和塔纳托斯碰撞了。";
		cutin "tnm_lucile01.bmp",2;
		next;
		mes "[露西尔]";
		mes "南方某个地方已经走得太远了";
		mes "你会去那里吗？";
	}
	else {
		mes "[露西尔]";
		mes "这两种力量的碰撞，实在是太难以承受了。";
		mes "它可能影响了符文米德加特南部。";
	}
	next;
	if (select( "跟随。", "停止。" ) == 2) {
		mes "[露西尔]";
		mes "……我想这就是你能做到的吧？";
		cutin "tnm_lucile02.bmp",2;
		close3;
	}
	mes "[露西尔]";
	mes "祝你好运。";
	close2;
	warp 'map_tnm3$,21,171;
	end;
}

// tnm3 Stairs 1
1@tnm3,21,171,0	script	#tnm3event01	HIDDEN_NPC,1,1,{
	end;
OnTouch:
	if ('devil_tower != 9)
		end;
	setpcblock PCBLOCK_NPC, true;
	disablenpc instance_npcname("#tnm3event01");
	enablenpc instance_npcname("Young Girl#tnm01");
	sleep2 4000;
	mapannounce 'map_tnm3$, "Young Girl: Odin Preserve me! A demon!! Help me!", bc_map,0xFF00;
	'mob_count = 12;
	initnpctimer;
	setpcblock PCBLOCK_NPC, false;
	end;
OnTimer1000:
	monster 'map_tnm3$,33,171,"Evil Shadow",2939,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW1
	end;
OnTimer2000:
	monster 'map_tnm3$,39,172,"Evil Shadow",2939,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW1
	end;
OnTimer3000:
	monster 'map_tnm3$,34,177,"Evil Shadow",2940,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW2
	end;
OnTimer4000:
	monster 'map_tnm3$,36,176,"Evil Shadow",2940,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW2
	end;
OnTimer5000:
	monster 'map_tnm3$,37,175,"Evil Shadow",2940,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW2
	end;
OnTimer6000:
	monster 'map_tnm3$,37,173,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
	monster 'map_tnm3$,36,172,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
	end;
OnTimer7000:
	monster 'map_tnm3$,29,178,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
	monster 'map_tnm3$,35,172,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
	end;
OnTimer8000:
	monster 'map_tnm3$,34,173,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
	monster 'map_tnm3$,35,172,"Evil Shadow",2939,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW1
	end;
OnTimer9000:
	monster 'map_tnm3$,34,173,"Evil Shadow",2941,1, instance_npcname("Young Girl#tnm01") + "::OnMobDead";// MM_EVIL_SHADOW3
	stopnpctimer;
	end;
}

1@tnm3,43,175,3	script	Young Girl#tnm01	4_F_DST_CHILD,{
	if ('devil_tower < 9)
		end;
	if ('devil_tower == 9) {
		mes "[年轻女孩]";
		mes "城堡里怎么会有恶魔？！";
		mes "帮我！";
		close;
	}
	if ('devil_tower == 10) {
		if (is_party_leader() == false) {
			mes "[年轻女孩]";
			mes "*哭泣*恶魔是如此可怕。";
			close;
		}
		mes "[年轻女孩]";
		mes "为什么恶魔会突然降临，";
		mes "很多守卫都受伤了。";
		npctalk "少女：为什么魔族突然来了，很多守卫都受伤了。";
		next;
		select("他们在哪里？");
		unittalk getcharid(3), "" + strcharinfo(0) + " : Where are they?";
		mes "[年轻女孩]";
		mes "他们在摩洛哥城堡里。我的父亲是一名骑士。";
		npctalk "他们在摩洛哥城堡里。我的父亲是一名骑士。";
		next;
		mes "[年轻女孩]";
		mes "好像发生了什么事情，我听到楼下有奇怪的声音。";
		npctalk "少女：好像发生了什么事情，我听到楼下有奇怪的声音。";
		next;
		mes "[年轻女孩]";
		mes "我看到几个黑衣人进来了。城堡里发生了什么？";
		npctalk "少女：我看到一些黑衣人进来了。城堡里发生了什么？";
		next;
		mes "[年轻女孩]";
		mes "爸爸..爸爸请注意安全。 *哭泣*";
		mes "请跟我一起穿过那个架子后面的通道！";
		npctalk "少女：请跟我一起穿过那个架子后面的通道吧！";
		close2;
		if (is_party_leader() == true && 'devil_tower == 10) {
			enablenpc instance_npcname("#tnm3gate01");
			enablenpc instance_npcname("#tnm3event02ect");
			'devil_tower = 11;
		}
		end;
	}
	if ('devil_tower == 11) {
		mes "[年轻女孩]";
		mes "那个架子后面有​​一条通往地下室的秘密通道！";
		mes "请跟我来，确保我爸爸的安全。";
		close;
	}
	end;

OnMobDead:
	'mob_count--;
	if ('mob_count == 0) {
		'devil_tower = 10;
		npctalk "少女：谢谢你救了我……但是为什么会有恶魔呢？";
	}
	end;
}

1@tnm3,52,176,0	warp	#tnm3gate01	2,2,1@tnm3,85,178

// tnm3 Stairs 2
1@tnm3,85,178,0	script	#tnm3event02ect	HIDDEN_NPC,1,1,{
	end;
OnTouch:
	if ('devil_tower != 11)
		end;
	disablenpc instance_npcname("#tnm3event02ect");
	enablenpc instance_npcname("Young Girl#tnm02");
	npctalk "年轻女孩：看！更多的恶魔，很快就会淹没城堡！", instance_npcname("年轻女孩#tnm02");
	'mob_count = 18;
	initnpctimer;
	end;
OnTimer1000:
	.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
	monster 'map_tnm3$,97,174,"Evil Shadow",2939,1, .@label$;
	monster 'map_tnm3$,98,175,"Evil Shadow",2939,1, .@label$;
	monster 'map_tnm3$,100,166,"Evil Shadow",2939,1, .@label$;
	monster 'map_tnm3$,108,171,"Evil Shadow",2940,1, .@label$;
	monster 'map_tnm3$,104,172,"Evil Shadow",2940,1, .@label$;
	end;
OnTimer3000:
	.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
	monster 'map_tnm3$,109,169,"Evil Shadow",2941,1, .@label$;
	monster 'map_tnm3$,111,173,"Evil Shadow",2941,1, .@label$;
	monster 'map_tnm3$,113,167,"Evil Shadow",2941,1, .@label$;
	monster 'map_tnm3$,101,176,"Evil Shadow",2939,1, .@label$;
	monster 'map_tnm3$,116,170,"Evil Shadow",2941,1, .@label$;
	end;
OnTimer5000:
	.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
	monster 'map_tnm3$,107,175,"Evil Shadow",2940,1, .@label$;
	monster 'map_tnm3$,106,174,"Evil Shadow",2940,1, .@label$;
	monster 'map_tnm3$,106,169,"Evil Shadow",2939,1, .@label$;
	monster 'map_tnm3$,103,177,"Evil Shadow",2940,1, .@label$;
	end;
OnTimer7000:
	stopnpctimer;
	.@label$ = instance_npcname("Young Girl#tnm02") + "::OnMobDead";
	monster 'map_tnm3$,121,171,"Evil Shadow",2941,1, .@label$;
	monster 'map_tnm3$,119,173,"Evil Shadow",2941,1, .@label$;
	monster 'map_tnm3$,127,166,"Evil Shadow",2941,1, .@label$;
	monster 'map_tnm3$,125,166,"Evil Shadow",2941,1, .@label$;
	end;
}

1@tnm3,89,179,4	script	Young Girl#tnm02	4_F_DST_CHILD,{
	end;
OnMobDead:
	'mob_count--;
	if ('mob_count == 0) {
		mapannounce 'map_tnm3$, "Young Girl: The way underground is along this aisle!", bc_map,0xFF00;
		disablenpc instance_npcname("Young Girl#tnm02");
		enablenpc instance_npcname("Young Girl#tnm03");
		npctalk "少女：地下的路就是沿着这条过道！", instance_npcname("年轻女孩#tnm03");
		enablenpc instance_npcname("#tnm3gate02");
		enablenpc instance_npcname("Evil Shadow#tnm3mob01");
	}
	end;
}

1@tnm3,128,164,3	script	Young Girl#tnm03	4_F_DST_CHILD,{
	mes "[年轻女孩]";
	mes "从这里开始..我不太了解...";
	mes "我看到这么多楼梯...";
	mes "你看，这里有这么多的恶魔。我的父亲将会有危险！";
	mes "请救救他。";
	close;
}

1@tnm3,134,156,0	warp	#tnm3gate02	1,1,1@tnm3,169,165

// tnm3 Stairs 3
1@tnm3,179,172,3	script	Evil Shadow#tnm3mob01	2941,4,4,{
	end;
OnTouch_:
	if ('devil_tower == 11) {
		'devil_tower = 12;
		setpcblock PCBLOCK_NPC, true;
		sleep2 1000;
		npctalk "邪影：我以为我闻到了老鼠偷偷溜进来的味道。我是对的！";
		sleep2 3000;
		npctalk "邪影：你会后悔自己的好奇心和干涉。喂食时间到了！";
		sleep2 3000;
		npctalk "邪影：我召唤黑暗！！";
		disablenpc instance_npcname("Evil Shadow#tnm3mob01");
		donpcevent instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnSummon";
		setpcblock PCBLOCK_NPC, false;
	}
	end;
}

1@tnm3,1,1,0	script	Evil Shadow kill#tnm3mob01	HIDDEN_WARP_NPC,{
	end;
OnSummon:
	enablenpc instance_npcname("Evil Shadow kill#tnm3mob01");
	'mob_count = 5;
	initnpctimer;
	monster 'map_tnm3$,179,172, "Evil Shadow",2941,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";// MM_EVIL_SHADOW3
	end;
OnTimer1000:
	monster 'map_tnm3$,181,172, "Evil Shadow",2940,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";// MM_EVIL_SHADOW2
	monster 'map_tnm3$,178,172, "Evil Shadow",2940,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";
	end;
OnTimer2000:
	monster 'map_tnm3$,179,172, "Evil Shadow",2939,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";// MM_EVIL_SHADOW1
	monster 'map_tnm3$,176,173, "Evil Shadow",2939,1, instance_npcname("Evil Shadow kill#tnm3mob01") + "::OnMobDead";
	stopnpctimer;
	end;
OnMobDead:
	'mob_count--;
	if ('mob_count == 0) {
		enablenpc instance_npcname("#tnm3gate03");
		enablenpc instance_npcname("Demonic Shade#mobmaster");
		enablenpc instance_npcname("Morocc Lord Knight#mocl");
		enablenpc instance_npcname("Loki#tnmloki01");
	}
	end;
}

1@tnm3,183,177,0	warp	#tnm3gate03	1,1,1@tnm3,97,6

// Demonic Shade (Boss room)
1@tnm3,97,18,3	script	Demonic Shade#mobmaster	2939,5,5,{
	end;
OnTouch_:// possibility to skip this step
	if ('devil_tower < 12)
		end;
	if ('shade == 0) {
		'shade = 1;
		setpcblock PCBLOCK_NPC, true;
		sleep2 1000;
		npctalk "恶魔之影：所以你们才是麻烦制造者！";
		sleep2 2000;
		npctalk "恶魔之影：切，你连保护都保护不了……？无论如何……你们都会死在这里。";
		sleep2 3000;
		npctalk "恶魔之影：永恒的真理是我会用你们的心血将你们的死亡变成恶魔的生命！！！";
		disablenpc instance_npcname("Demonic Shade#mobmaster");
		donpcevent instance_npcname("#mobmaster_kill") + "::OnSummon";
		setpcblock PCBLOCK_NPC, false;
	}
	end;
}

1@tnm3,1,1,0	script	#mobmaster_kill	HIDDEN_WARP_NPC,{
	end;
OnSummon:
	enablenpc instance_npcname("#mobmaster_kill");
	initnpctimer;
	monster 'map_tnm3$,97,18, "Demonic Shade",2939,1, instance_npcname("#mobmaster_kill") + "::OnMobDead";
	end;
OnTimer1000:
	.@label$ = instance_npcname("#mobmaster_kill") + "::OnMobDead";
	stopnpctimer;
	monster 'map_tnm3$,94,18, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,96,18, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,113,18, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,119,22, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,116,18, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,116,18, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,114,12, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,116,21, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,74,18, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,75,12, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,76,21, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,69,16, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,57,43, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,58,44, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,59,46, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,60,48, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,56,45, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,57,44, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,80,71, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,59,77, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,77,73, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,77,72, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,59,70, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,81,66, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,60,69, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,57,73, "Demonic Shade",2940,1, .@label$;
	monster 'map_tnm3$,62,74, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,78,67, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,75,22, "Demonic Shade",2941,1, .@label$;
	monster 'map_tnm3$,81,69, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,77,11, "Demonic Shade",2939,1, .@label$;
	monster 'map_tnm3$,58,74, "Demonic Shade",2941,1, .@label$;
	end;

OnMobDead:
	end;
}

1@tnm3,93,52,4	script	Morocc Lord Knight#mocl	1_M_MOC_LORD,{
	mes "[摩洛哥领主骑士]";
	mes "你们就是偷偷溜到这里的老鼠！";
	close;
}

1@tnm3,97,48,3	script	Loki#tnmloki01	4_M_ROKI2,{
	if ('devil_tower == 12) {
		'devil_tower = 13;
		donpcevent instance_npcname("#morocc_lord_talk") + "::OnStart";
		end;
	}
	mes "[洛基]";
	mes "你在这里没有权力。";
	close;
}

1@tnm3,94,51,4	duplicate(Officer Heim#heim0)	Young Girl#tnm04	4_F_DST_CHILD

1@tnm2,1,1,0	script	#morocc_lord_talk	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#morocc_lord_talk");
	'Morocc$ = instance_npcname("Morocc Lord Knight#mocl");
	'loki$ = instance_npcname("Loki#tnmloki01");
	npctalk "摩洛哥骑士勋爵：那么……那只老鼠是刺客？你来这里是为了送死吗？", 'Morocc$;
	initnpctimer;
	end;
OnTimer3000:
	npctalk "洛基：唤醒墙中的恶魔你会做什么？", 'loki$;
	end;
OnTimer6000:
	npctalk "洛基：我的任务把我带到这里来再次埋葬它。", 'loki$;
	end;
OnTimer9000:
	npctalk "摩洛哥领主骑士：真的……我认为刺客公会的使命是平衡世界权力。", 'Morocc$;
	end;
OnTimer12000:
	npctalk "摩洛哥领主骑士：复活恶魔就能带来平衡！即使是最强大的人也会成为神的奴隶。", 'Morocc$;
	end;
OnTimer15000:
	npctalk "摩洛哥领主骑士：诸神有他们自己的计划。你知道诸神对你我有何计划吗？", 'Morocc$;
	end;
OnTimer18000:
	npctalk "摩洛哥骑士勋爵：他们希望这个世界只按照他们的想象。我们在哪里适合这个？", 'Morocc$;
	end;
OnTimer20000:
	npctalk "洛基：闭嘴！", 'loki$;
	end;
OnTimer22000:
	npctalk "洛基：我不关心神或恶魔，也不关心他们制定的计划。唯一重要的就是杀了你。", 'loki$;
	end;
OnTimer25000:
	npctalk "洛基：这就是我的使命！", 'loki$;
	end;
OnTimer28000:
	npctalk "摩洛哥领主骑士：所以你只想完成你该死的任务？", 'Morocc$;
	end;
OnTimer31000:
	npctalk "洛基：*笑*当然！！", 'loki$;
	specialeffect EF_BEGINSPELL3,AREA, 'loki$;
	end;
OnTimer33000:
	npctalk "洛基：心灵冲击波——！！！", 'loki$;
	end;
OnTimer34000:
	enablenpc instance_npcname("Young Girl#tnm04");
	npctalk "少女：不……！！别伤害我爸爸！！！", instance_npcname("年轻女孩#tnm04");
	specialeffect EF_HIT3,AREA, instance_npcname("Young Girl#tnm04");
	end;
OnTimer34500:
	specialeffect EF_HIT2,AREA, instance_npcname("Young Girl#tnm04");
	end;
OnTimer35500:
	specialeffect EF_ICECRASH,AREA, instance_npcname("Young Girl#tnm04");
	end;
OnTimer36500:
	npctalk "！！！", 'loki$;
	end;
OnTimer38500:
	npctalk "摩洛哥领主骑士：哦不……他们应该派出比你更好的人来执行这个任务，刺客先生。", 'Morocc$;
	end;
OnTimer41500:
	npctalk "少女：出事了……", instance_npcname("年轻女孩#tnm04");
	end;
OnTimer44500:
	npctalk "年轻女孩：我爸爸永远不会这样做..呃..", instance_npcname("年轻女孩#tnm04");
	end;
OnTimer47500:
	npctalk "摩洛哥领主骑士：人类……做出如此愚蠢的事情……牺牲。", 'Morocc$;
	end;
OnTimer50500:
	npctalk "摩洛哥领主骑士：你知道，事实是，它毫无用处。", 'Morocc$;
	end;
OnTimer53500:
	npctalk "摩洛哥骑士勋爵：咕咕哈哈哈哈。你们这些愚蠢的人类，我要把你们全部杀掉，作为恶魔的祭品！！", 'Morocc$;
	specialeffect EF_COUPLECASTING,AREA, 'Morocc$;
	specialeffect EF_VOLCANO,AREA, 'Morocc$;
	end;
OnTimer54500:
	npctalk "洛基：……啧！无用....！", 'loki$;
	end;
OnTimer55500:
	enablenpc instance_npcname("Loki#tnmloki02");
	mapwarp 'map_tnm3$, 'map_tnm3$,136,62;// todo area 1x1
	'devil_tower = 13;
	stopnpctimer;
	disablenpc instance_npcname("#morocc_lord_talk");
	disablenpc instance_npcname("Morocc Lord Knight#mocl");
	disablenpc instance_npcname("Loki#tnmloki01");
	disablenpc instance_npcname("Young Girl#tnm04");
	end;
}

1@tnm3,137,65,3	script	Loki#tnmloki02	4_M_ROKI2,{
	if ('devil_tower != 13)
		end;
	if (is_party_leader() == false) {
		mes "[洛基]";
		mes "我正在和你的老板说话。";
		close;
	}
	if (getmercinfo(0) > 0) {
		mes "[洛基]";
		mes "……尴尬。";
		mes "如果你想帮助我完成这个任务，你应该^4d4dff终止雇佣兵合同^000000。";
		close;
	}
	mes "[洛基]";
	mes "你从哪里来？";
	mes "我这里可能需要帮助。";
	npctalk "洛基：你从哪里来？我这里可能需要帮助。", instance_npcname("洛基#tnmloki02");
	next;
	mes "[洛基]";
	mes "骑士领主不再是人类。由于他的恶魔追随者，我必须把他送入永恒。";
	npctalk "洛基：骑士大人不再是人类了。我必须送他到永恒。", instance_npcname("洛基#tnmloki02");
	next;
	mes "[洛基]";
	mes "^4d4dff受到恶魔的地狱祝福的人是无敌的。我们可以用心灵冲击波切断祝福。^000000";
	npctalk "洛基：受到恶魔的地狱祝福的人是无敌的。我们可以用心灵冲击波切断祝福。", instance_npcname("洛基#tnmloki02");
	next;
	mes "[洛基]";
	mes "您将负责激活 Mind Blaster。";
	npctalk "洛基：你将负责启动心灵冲击波。", instance_npcname("洛基#tnmloki02");
	next;
	if (is_party_leader() == true && 'devil_tower == 13) {
		disablenpc instance_npcname("Loki#tnmloki02");
		mes "^4d4dff刺客公会断头台十字洛基已加入你的队伍。";
		mes "我们必须用洛基的技能心灵爆破来除掉恶魔骑士领主。^000000";
		'devil_tower = 14;
		mercenary_create 2937,1800000;// M_LOKI
		mapannounce 'map_tnm3$, "Demon Worshipper's Voice: Such rubbish! Where did you hide yourself?", bc_map,0xFF00;
		initnpctimer;
	}
	close;
OnTimer500:
	enablenpc instance_npcname("#boss_skills_timer");
	monster 'map_tnm3$,93,52,"Evil Believer",2942,1, instance_npcname("#boss_skills_timer") + "::OnBossDead";// MM_EVIL_FANATICS
	'boss_id = $@mobid[0];
	end;
OnTimer2000:
	mapannounce 'map_tnm3$, "Demon Worshipper's Voice: You will all be sacrificed to awaken the Demon!!!", bc_map,0xFF00;
	end;
OnTimer12000:
	donpcevent instance_npcname("#boss_skills_timer") + "::OnStart";
	stopnpctimer;
	end;
}

1@tnm3,1,1,0	script	#boss_skills_timer	HIDDEN_WARP_NPC,{
	end;
OnStart:
	'label$ = instance_npcname("#boss_skills_timer") + "::OnMobDead";
	initnpctimer;
	end;
OnTimer1000:// + form
	getunitdata 'boss_id, .@data;
	'x_boss = .@data[UMOB_X];
	'y_boss = .@data[UMOB_Y];
	monster 'map_tnm3$, 'x_boss, 'y_boss, "",2943,1, 'label$;// MM_ICE_MINE
	monster 'map_tnm3$, ('x_boss-5), 'y_boss, "",2943,1, 'label$;
	monster 'map_tnm3$, ('x_boss+5), 'y_boss, "",2943,1, 'label$;
	monster 'map_tnm3$, 'x_boss, ('y_boss-5), "",2943,1, 'label$;
	monster 'map_tnm3$, 'x_boss, ('y_boss+5), "",2943,1, 'label$;
	end;
OnTimer3000:
	monster 'map_tnm3$, ('x_boss-10), 'y_boss, "",2943,1, 'label$;// MM_ICE_MINE
	monster 'map_tnm3$, ('x_boss+10), 'y_boss, "",2943,1, 'label$;
	monster 'map_tnm3$, 'x_boss, ('y_boss-10), "",2943,1, 'label$;
	monster 'map_tnm3$, 'x_boss, ('y_boss+10), "",2943,1, 'label$;
	end;
OnTimer6000:
	killmonster 'map_tnm3$, 'label$;
	end;
OnTimer21000:// X form
	getunitdata 'boss_id, .@data;
	'x_boss = .@data[UMOB_X];
	'y_boss = .@data[UMOB_Y];
	monster 'map_tnm3$, ('x_boss+3), ('y_boss+3), "",2960,1, 'label$;// MM_FLAMECROSS
	monster 'map_tnm3$, ('x_boss+3), ('y_boss-3), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-3), ('y_boss+3), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-3), ('y_boss-3), "",2960,1, 'label$;
	end;
OnTimer21350:
	monster 'map_tnm3$, ('x_boss+6), ('y_boss+6), "",2960,1, 'label$;// MM_FLAMECROSS
	monster 'map_tnm3$, ('x_boss+6), ('y_boss-6), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-6), ('y_boss+6), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-6), ('y_boss-6), "",2960,1, 'label$;
	end;
OnTimer21700:
	monster 'map_tnm3$, ('x_boss+9), ('y_boss+9), "",2960,1, 'label$;// MM_FLAMECROSS
	monster 'map_tnm3$, ('x_boss+9), ('y_boss-9), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-9), ('y_boss+9), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-9), ('y_boss-9), "",2960,1, 'label$;
	end;
OnTimer22050:
	monster 'map_tnm3$, ('x_boss+13), ('y_boss+13), "",2960,1, 'label$;// MM_FLAMECROSS
	monster 'map_tnm3$, ('x_boss+13), ('y_boss-13), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-13), ('y_boss+13), "",2960,1, 'label$;
	monster 'map_tnm3$, ('x_boss-13), ('y_boss-13), "",2960,1, 'label$;
	end;
OnTimer30000:
	killmonster 'map_tnm3$, 'label$;
	end;
OnTimer51000:// X form
	getunitdata 'boss_id, .@data;
	'x_boss = .@data[UMOB_X];
	'y_boss = .@data[UMOB_Y];
	monster 'map_tnm3$, 'x_boss, 'y_boss, "",2943,1, 'label$;// MM_ICE_MINE
	monster 'map_tnm3$, ('x_boss+5), ('y_boss+5), "",2943,1, 'label$;
	monster 'map_tnm3$, ('x_boss-5), ('y_boss-5), "",2943,1, 'label$;
	monster 'map_tnm3$, ('x_boss+10), ('y_boss+10), "",2943,1, 'label$;
	monster 'map_tnm3$, ('x_boss-10), ('y_boss-10), "",2943,1, 'label$;
	end;
OnTimer53000:
	monster 'map_tnm3$, ('x_boss-5), ('y_boss+5), "",2943,1, 'label$;// MM_ICE_MINE
	monster 'map_tnm3$, ('x_boss+5), ('y_boss-5), "",2943,1, 'label$;
	monster 'map_tnm3$, ('x_boss-10), ('y_boss+10), "",2943,1, 'label$;
	monster 'map_tnm3$, ('x_boss+10), ('y_boss-10), "",2943,1, 'label$;
	end;
OnTimer56000:
	killmonster 'map_tnm3$, 'label$;
	end;
OnTimer81000:// S form
	getunitdata 'boss_id, .@data;
	'x_boss = .@data[UMOB_X];
	'y_boss = .@data[UMOB_Y];
	monster 'map_tnm3$, ('x_boss-2), ('y_boss+5), "",2960,1, 'label$;// MM_FLAMECROSS
	end;
OnTimer81300:
	monster 'map_tnm3$, ('x_boss+4), ('y_boss+8), "",2960,1, 'label$;
	end;
OnTimer81600:
	monster 'map_tnm3$, ('x_boss+10), ('y_boss+2), "",2960,1, 'label$;
	end;
OnTimer82000:
	monster 'map_tnm3$, ('x_boss+10), ('y_boss-1), "",2960,1, 'label$;
	end;
OnTimer82300:
	monster 'map_tnm3$, ('x_boss+7), ('y_boss-7), "",2960,1, 'label$;
	end;
OnTimer82600:
	monster 'map_tnm3$, ('x_boss+1), ('y_boss-10), "",2960,1, 'label$;
	end;
OnTimer83000:
	monster 'map_tnm3$, ('x_boss-11), ('y_boss-1), "",2960,1, 'label$;
	end;
OnTimer83300:
	monster 'map_tnm3$, ('x_boss-11), ('y_boss+5), "",2960,1, 'label$;
	end;
OnTimer83600:
	monster 'map_tnm3$, ('x_boss-8), ('y_boss+11), "",2960,1, 'label$;
	end;
OnTimer84000:
	monster 'map_tnm3$, ('x_boss-2), ('y_boss+17), "",2960,1, 'label$;
	end;
OnTimer84300:
	monster 'map_tnm3$, ('x_boss+7), ('y_boss+20), "",2960,1, 'label$;
	end;
OnTimer90000:
	killmonster 'map_tnm3$, 'label$;
	initnpctimer;
	end;
OnMobDead:
	end;
OnBossDead:
	stopnpctimer;
	killmonster 'map_tnm3$, 'label$;
	'x_boss = 'y_boss = 'boss_id = 0;
	'label$ = "";
	mapannounce 'map_tnm3$, "Demon Worshipper's Voice: NO it is impossible. It cannot be.. This force.. power.. you.. may..be you are..", bc_map,0xFF0000;
	sleep2 1000;
	warpparty 'map_tnm3$,71,70, getcharid(1), 'map_tnm3$,1,1;
	enablenpc instance_npcname("Loki#tnmloki03");
	enablenpc instance_npcname("box#tnmbosang");
	disablenpc instance_npcname("#boss_skills_timer");
	'devil_tower = 15;
	end;
}

1@tnm3,69,70,3	script	box#tnmbosang	4_TREASURE_BOX,{
	if ('devil_tower != 15)
		end;
	specialeffect EF_COIN;
	disablenpc instance_npcname("box#tnmbosang");
	makeitem 7293,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Rose Quartz
	makeitem 749,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Frozen Rose
	makeitem 7511,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Rune of Darkness
	if (rand(100) < 75)
		makeitem 616,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Old Card Album
	if (rand(100) < 50)
		makeitem 748,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5));	// Witherless_Rose

	setarray .@item_id_list[0],
		1671,	// Devil_Won_Staff
		13094,	// Devil_Pierced_Dagger
		16027,	// Hammer_Of_Evil_Slayer
		18120, 	// Bow_Of_Evil_Slayer
		21010, 	// Tw_Sword_Of_Evil_Slayer
		28001; 	// Katar_Of_Evil_Slayer
	setarray .@bonus_list1[0],
		4800,	// SP50
		4811,	// Fighting_Spirit1
		4810,	// Fighting_Spirit2
		4809,	// Fighting_Spirit3
		4808,	// Fighting_Spirit4
		4820,	// Fighting_Spirit5
		4821,	// Fighting_Spirit6
		4815,	// Spell1
		4814,	// Spell2
		4813,	// Spell3
		4812,	// Spell4
		4786;	// Mdef2
	setarray .@bonus_list2[0],
		4815,	// Spell1
		4814,	// Spell2
		4813,	// Spell3
		4812,	// Spell4
		4826,	// Spell5
		4811,	// Fighting_Spirit1
		4810,	// Fighting_Spirit2
		4809,	// Fighting_Spirit3
		4808,	// Fighting_Spirit4
		4820,	// Fighting_Spirit5
		4786;	// Mdef2
	setarray .@bonus_list3[0],
		4815,	// Spell1
		4814,	// Spell2
		4813,	// Spell3
		4812,	// Spell4
		4811,	// Fighting_Spirit1
		4810,	// Fighting_Spirit2
		4809,	// Fighting_Spirit3
		4808,	// Fighting_Spirit4
		4820,	// Fighting_Spirit5
		4821,	// Fighting_Spirit6
		4791,	// DEF+3
		4786;	// Mdef2
	.@item_id = .@item_id_list[ rand( getarraysize(.@item_id_list) ) ];
	.@refine = rand(1,6);
	.@bonus1 = .@bonus_list1[ rand( getarraysize(.@bonus_list1) ) ];
	.@bonus2 = .@bonus_list1[ rand( getarraysize(.@bonus_list2) ) ];
	.@bonus3 = .@bonus_list1[ rand( getarraysize(.@bonus_list3) ) ];

	makeitem2 .@item_id,1, 'map_tnm3$, (69 + rand(-5,5)), (70 + rand(-5,5)), 0, .@refine,0, 0,.@bonus1,.@bonus2,.@bonus3;
	end;
}

//1@tnm3,69,72,3	script	Loki#tnmloki03	4_M_ROKI2,5,5,{
1@tnm3,69,72,3	script	Loki#tnmloki03	4_M_ROKI2,{
	if ('devil_tower != 15)
		end;
	if (isbegin_quest(7576) == 0) {
		mes "[洛基]";
		mes ".. 恼人的";
		mes "复活仪式还没完成，恶魔就被消灭了。";
		mes "事情会暂时好起来。";
		setquest 7576;// Morocc castle seal
		next;
		mes "在探索塔的过程中，我获得了意想不到的信息。";
		next;
		mes "[洛基]";
		mes "我会远离我的良心一段时间";
		mes "有一天我会让自己再次感受到，今天将占据我的思绪..";
		close;
	}
	mes "[洛基]";
	mes "有趣..有一个女儿..";
	mes "对此应该做什么？";
	mes "我不确定我的下一步行动是什么..";
	next;
	mes "[洛基]";
	mes "但没关系，你现在显然不应该在这里。";
	mes "还会留在这里吗？";
	mes "我可以送你回到你所属的地方和时间。";
	next;
	if (select( "离开这里。", "四处看看。" ) == 2) {
		mes "[洛基]";
		mes "在时间扭曲的地方徘徊是不明智的。";
		close;
	}
	mes "[洛基]";
	mes "也许，我们的使命会在未来再次让我们结盟……";
	close2;
	warp "dali02",134,112;
	end;

OnInstanceInit:
	'heal_count = 0;
	'devil_tower = 0;
	'mob_count = 0;
	'shade = 0;

	'map_tnm1$ = instance_mapname("1@tnm1");
	'map_tnm2$ = instance_mapname("1@tnm2");
	'map_tnm3$ = instance_mapname("1@tnm3");

	// Entrance
	disablenpc instance_npcname("Officer Heim#heim");
	disablenpc instance_npcname("Healer Fama#feima");
	for ( .@i = 1; .@i < 8; .@i++ )
		disablenpc instance_npcname( "Injured Soldier#" + .@i );
	disablenpc instance_npcname("Lucile#tnm01");
	disablenpc instance_npcname("Magic Swordman#tnm01");
	disablenpc instance_npcname("Assassin Huey#tnm01");
	disablenpc instance_npcname("Assassin Louie#tnm01");
	disablenpc instance_npcname("Assassin Dewey#tnm01");

	// Stairs tnm1
	disablenpc instance_npcname("#tnm1stepmob");
	disablenpc instance_npcname("Assassin Louie#tnm02");

	// Stairs 2 tnm1
	disablenpc instance_npcname("Lucile#tnm02");
	disablenpc instance_npcname("Huey#hui02");

	// Morocc - Thanatos Battle
	disablenpc instance_npcname("Magic Swordman#tnm02");
	disablenpc instance_npcname("Demon Morocc#tnm01");
	disablenpc instance_npcname("Lucile#tnm03");
	disablenpc instance_npcname("Lucile#tnm04");
	disablenpc instance_npcname("#devil_flamecross");
	disablenpc instance_npcname("#devil_seal_timer");
	disablenpc instance_npcname("#devil_seal_spawn_global");
	setarray .@direction$[0], "DIR_NORTHWEST", "DIR_SOUTHWEST", "DIR_SOUTHEAST", "DIR_NORTHEAST", "DIR_NORTH";
	for ( .@i = 0; .@i < 5; .@i++ ) {
		disablenpc instance_npcname("devil_seal_spawn#" + .@direction$[.@i]);
		disablenpc instance_npcname("seal_reach_center#" + .@direction$[.@i]);
	}
	disablenpc instance_npcname("#devil_seal_dead");

	'demon$ = 'thanatos$ = "";
	'round[DIR_NORTHWEST] = 'round[DIR_SOUTHWEST] = 'round[DIR_SOUTHEAST] = 'round[DIR_NORTHEAST] = 'round[DIR_NORTH] = 0;
	'seal_id[DIR_NORTHWEST] = 'seal_id[DIR_SOUTHWEST] = 'seal_id[DIR_SOUTHEAST] = 'seal_id[DIR_NORTHEAST] = 'seal_id[DIR_NORTH] = 0;
	'seal_hp[DIR_NORTHWEST] = 'seal_hp[DIR_SOUTHWEST] = 'seal_hp[DIR_SOUTHEAST] = 'seal_hp[DIR_NORTHEAST] = 'seal_hp[DIR_NORTH] = 0;
	'seal_dead[DIR_NORTHWEST] = 'seal_dead[DIR_SOUTHWEST] = 'seal_dead[DIR_SOUTHEAST] = 'seal_dead[DIR_NORTHEAST] = 'seal_dead[DIR_NORTH] = 0;
	deletearray 'coord_seal_DIR_NORTHWEST[0],14;
	deletearray 'coord_seal_DIR_SOUTHWEST[0],14;
	deletearray 'coord_seal_DIR_SOUTHEAST[0],14;
	deletearray 'coord_seal_DIR_NORTHEAST[0],14;
	deletearray 'coord_seal_DIR_NORTH[0],14;

	// tnm3 Stairs 1
	disablenpc instance_npcname("#tnm3event01");
	disablenpc instance_npcname("Young Girl#tnm01");
	disablenpc instance_npcname("#tnm3gate01");

	// tnm3 Stairs 2
	disablenpc instance_npcname("#tnm3event02ect");
	disablenpc instance_npcname("Young Girl#tnm02");
	disablenpc instance_npcname("Young Girl#tnm03");
	disablenpc instance_npcname("#tnm3gate02");

	// tnm3 Stairs 3
	disablenpc instance_npcname("Evil Shadow#tnm3mob01");
	disablenpc instance_npcname("Evil Shadow kill#tnm3mob01");
	disablenpc instance_npcname("#tnm3gate03");

	// Demonic Shade (Boss room)
	disablenpc instance_npcname("Demonic Shade#mobmaster");
	disablenpc instance_npcname("#mobmaster_kill");

	// Boss
	disablenpc instance_npcname("Morocc Lord Knight#mocl");
	disablenpc instance_npcname("#morocc_lord_talk");
	disablenpc instance_npcname("Loki#tnmloki01");
	disablenpc instance_npcname("Young Girl#tnm04");
	disablenpc instance_npcname("Loki#tnmloki02");
	disablenpc instance_npcname("#boss_skills_timer");
	'x_boss = 'y_boss = 'boss_id = 0;
	'label$ = "";

	// Final
	disablenpc instance_npcname("Loki#tnmloki03");
	disablenpc instance_npcname("box#tnmbosang");
	end;
}
