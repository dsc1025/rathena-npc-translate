//===== rAthena Script =======================================
//= Dynamic NPC: Kachua's Secret Box
//===== Description: =========================================
//- [Official conversion]
//= Kachua's Secret Box is a Gachapon NPC.
//= It lets the player exchange a Kachua's Secret Key for a random item.
//= It also gives a Kachua's Mileage Coupon for each pull.
//===== Changelogs: ==========================================
//= 1.0 First version. [secretdataz]
//===== Translated By: =======================================
//= dsc
//============================================================

sec_in02,126,178,3	script	Kachua's Secret Box#bm	4_TREASURE_BOX,{
	.@key$ = "K_Secret_Key";
	.@keyname$ = getitemname( .@key$ );
	.@box$ = "Main_Lucky_Box";
	.@boxname$ = getitemname( .@box$ );
	mes "一个没人知道里面装着什么的秘密盒子。";
	mes "^4d4dff您可以通过消耗" + mesitemlink( .@key$ ) + "来打开此盒子。^000000";
	next;
	switch(select("^4d4dff打开盒子 1 次（1 " + .@keyname$ + ")^000000","^4d4dff打开盒子 10 次（10 " + .@keyname$ + " 秒）^000000")) {
		case 1:
			mes "^FF0000[通知]^000000";
			mes "^FF0000用一个" + .@keyname$ + "交换一件随机物品。^000000";
			mes "^FF0000上面兑换的物品不能提现，也不能兑换到" + .@keyname$ + "。^000000";
			next;
			if(select("继续。","停止谈话。") == 2) {
				mes "您已决定不打开盒子。";
				close;
			} else if (countitem(.@key$) < 1) {
				mes "不够" + mesitemlink( .@key$ ) + ".";
				close;
			} else {
				if (checkweight(1201,1) == 0 || ((MaxWeight - Weight) * 100 / MaxWeight) < 10) {
					mes "^4d4dff请确保您的库存中有足够的空间。^000000";
					close;
				}
				delitem(.@key$, 1);
				consumeitem(.@box$);
				mes .@boxname$ + "被打开了！";
				mes "你幸运吗？";
				specialeffect2 EF_VALLENTINE;
				close;
			}
		case 2:
			mes "^FF0000[通知]^000000";
			mes "^FF0000用10个" + .@keyname$ + "交换10个随机物品。^000000";
			mes "^FF0000以上兑换的物品不能提现，也不能兑换到" + .@keyname$ + "。^000000";
			next;
			if(select("继续。","停止谈话。") == 2) {
				mes "您已决定不打开盒子。";
				close;
			} else if (countitem(.@key$) < 10) {
				mes "您没有足够的" + mesitemlink( .@key$ ) + ".";
				close;
			} else {
				for (.@i = 1; .@i <= 10; ++.@i) {
					progressbar "4d4dff",2;
					if (checkweight(1201,1) == 0 || ((MaxWeight - Weight) * 100 / MaxWeight) < 10) {
						mes "^4d4dff请确保您的库存中有足够的空间。^000000";
						close;
					}
					if (countitem(.@key$) < 1) { // Custom check, just in case
						close;
					}
					delitem(.@key$, 1);
					consumeitem(.@box$);
					dispbottom .@boxname$ + " was opened " + .@i + " times. Another one is being opened.",0xFFFFFF;
					specialeffect2 EF_VALLENTINE;
				}
				mes .@boxname$ + "被打开了10次！";
				mes "你幸运吗？";
				close;
			}
	}
}
