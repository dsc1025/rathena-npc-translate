//===== rAthena Script =======================================
//= Instance: Thor Gunsu Base.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Part of the Episode 18 main quest.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//===== Translated By: =======================================
//= dsc
//============================================================

// Main Quest: Step 47.
que_thr,133,53,5	script	Maram#Armybase1	4_EP18_MARAM,{
	if (ep18_main < 45 || ep18_main > 46)
		end;
	if (ep18_main == 45) {
		cutin "ep18_maram_01.png",2;
		mes "[马拉姆]";
		mes "你来了，冒险家！";
		mes "谢谢你千里迢迢来到这里。";
		next;
		cutin "ep18_maram_03.png",2;
		mes "[马拉姆]";
		mes "看来这毕竟是真正的交易。这里的治安与以往不同......";
		mes "我们要小心不要被守卫抓住并探索里面。";
		next;
		cutin "ep18_maram_01.png",2;
		mes "[马拉姆]";
		mes "尽量远离保安的视线。";
		mes "如果你被抓住的话，你可能会受到攻击，所以如果你无法突破，我们会在这里再次见面。";
		erasequest 16577;
		setquest 16578;
		ep18_main = 46;
		next;
	}
	else if (ep18_main == 46) {
		cutin "ep18_maram_01.png",2;
		mes "[马拉姆]";
		mes "我们尽量避开守卫塔，尝试进去吧。";
		next;
	}

	.@md_name$ = "Thor Military Base";
	cutin "",255;
	mes "^FF0000注意：如果您在地下城内进行任意处理，例如驯服怪物，则不会被视为正常进度。^000000";
	next;
	// player has party
	if (is_party_leader())
		// player is leader of the party
		.@create$ = "Apply for entry to " + .@md_name$ + "";

	switch( select( .@create$, "进入" + .@md_name$ + "" ) ) {
	case 1:
		cutin "ep18_maram_01.png",2;
		mes "[马拉姆]";
		mes "申请完成后，再次与我交谈并选择^0000CD" + .@md_name$ + "条目^000000";
		if (instance_create(.@md_name$) < 0) {
			mes "派对：" + getpartyname(getcharid(1)) + "";
			mes "领导者：" + strcharinfo(0) + "";
			mes "^0000ff" + .@md_name$ + " ^000000- 未知错误";
			close3;
		}
		close3;
	case 2:
		// Note: instance not forced solo
		switch( instance_enter(.@md_name$) ) {
		case IE_OTHER:
			mes "[马拉姆]";
			mes "^ff0000发生未知错误。^000000";
			close;
		case IE_NOINSTANCE:
			cutin "ep18_maram_01.png",2;
			mes "[马拉姆]";
			mes "看来你还没有创建地牢。请稍后再检查。";
			close3;
		case IE_NOMEMBER:
			mes "[马拉姆]";
			mes "^ff0000只有队伍成员才能进入纪念地下城。^000000";
			close;
		case IE_OK:
			mapannounce "que_thr", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering " + .@md_name$ + ".", bc_map, 0xFF99;
			// warp 1@tcamp,103,237;
			end;
		}
	}
	end;

OnInit:
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(16577) == 1" );
	end;
}

1@tcamp,106,237,3	script	Maram#Base1	4_EP18_MARAM,{
	mes "[马拉姆]";
	mes "小心并探索里面。";
	mes "之前听蒲公英说，人不多，但到处都有t望塔，监视着相当广阔的区域。";
	next;
	mes "[马拉姆]";
	mes "如果你被t望塔抓住，就会出现士兵，所以要小心不要被抓住。";
	mes "好吧，如果你发现什么，请联系我！";
	close;

OnInstanceInit:
	'step = 0;
	'map_tcamp$ = instance_mapname("1@tcamp");

	setcell 'map_tcamp$,137,216,142,216,cell_walkable,0;
	setcell 'map_tcamp$,135,145,144,145,cell_walkable,0;
	setcell 'map_tcamp$,223,105,223,110,cell_walkable,0;
	setcell 'map_tcamp$,80,94,80,100,cell_walkable,0;

	// some monsters spawn randomly
	monster 'map_tcamp$,0,0,"Base Soldier",21310,18;	// EP18_MD_GUARD_A
	monster 'map_tcamp$,0,0,"Base Soldier",21309,18;	// EP18_MD_THOR_GUARD
	end;
}

// unknown npc effect
// 1@tcamp,244,152,3	duplicate(dummy_npc)	#Remote 1	CLEAR_NPC


//-------------------------------------------------------------------
// Traps
//-------------------------------------------------------------------
1@tcamp,120,228,3	script	Watchtower#Watch1	4_SYSTEM_BOX,7,7,{
	end;
OnTouch:	// note: hidden player trigger the event
	npctalk "发现入侵者！！";
	mapannounce 'map_tcamp$, "Intruder found, intruder found. Ruled out immediately.", bc_map, 0xFF0000;
	getmapxy .@map$,.@x,.@y, BL_NPC;

	areamonster 'map_tcamp$,.@x-2,.@y-2,.@x+2,.@y+2,"Base Soldier",21309,3;	// EP18_MD_THOR_GUARD
	areamonster 'map_tcamp$,.@x-2,.@y-2,.@x+2,.@y+2,"Base Soldier",21310,3;	// EP18_MD_GUARD_A
	end;
OnTimer2000:
	specialeffect EF_WARP;
	initnpctimer;
	end;
OnInstanceInit:
	initnpctimer;
	end;
}

1@tcamp,135,228,3	duplicate(Watchtower#Watch1)	Watchtower#Watch2	4_SYSTEM_BOX,7,7
1@tcamp,152,133,3	duplicate(Watchtower#Watch1)	Watchtower#Watch3	4_SYSTEM_BOX,7,7
1@tcamp,167,133,3	duplicate(Watchtower#Watch1)	Watchtower#Watch4	4_SYSTEM_BOX,7,7
1@tcamp,182,133,3	duplicate(Watchtower#Watch1)	Watchtower#Watch5	4_SYSTEM_BOX,7,7
1@tcamp,161,96,3	duplicate(Watchtower#Watch1)	Watchtower#Watch6	4_SYSTEM_BOX,7,7
1@tcamp,146,96,3	duplicate(Watchtower#Watch1)	Watchtower#Watch7	4_SYSTEM_BOX,7,7
1@tcamp,116,102,3	duplicate(Watchtower#Watch1)	Watchtower#Watch8	4_SYSTEM_BOX,7,7
1@tcamp,88,92,3	duplicate(Watchtower#Watch1)	Watchtower#Watch9	4_SYSTEM_BOX,7,7
1@tcamp,240,148,3	duplicate(Watchtower#Watch1)	Watchtower#Watch10	4_SYSTEM_BOX,7,7
1@tcamp,156,71,3	duplicate(Watchtower#Watch1)	Watchtower#Watch11	4_SYSTEM_BOX,7,7
1@tcamp,176,194,3	duplicate(Watchtower#Watch1)	Watchtower#Watch12	4_SYSTEM_BOX,7,7
1@tcamp,192,194,3	duplicate(Watchtower#Watch1)	Watchtower#Watch13	4_SYSTEM_BOX,7,7
1@tcamp,142,153,3	duplicate(Watchtower#Watch1)	Watchtower#Watch14	4_SYSTEM_BOX,7,7
1@tcamp,142,168,3	duplicate(Watchtower#Watch1)	Watchtower#Watch15	4_SYSTEM_BOX,7,7
1@tcamp,192,179,3	duplicate(Watchtower#Watch1)	Watchtower#Watch16	4_SYSTEM_BOX,7,7
1@tcamp,237,90,3	duplicate(Watchtower#Watch1)	Watchtower#Watch17	4_SYSTEM_BOX,7,7


//-------------------------------------------------------------------
// Walls
//-------------------------------------------------------------------
1@tcamp,138,216,3	script	Lock Device#1-1	4_ROPEPILE,{
	.@maram$ = instance_npcname("Maram#Lock Device1");
	enablenpc .@maram$;
	npctalk "马拉姆：请稍等。我知道怎么打开它。", .@maram$;
	progressbar_npc "3131FF",10;
	npctalk "Maram：好吧，我们现在可以过去了！", .@maram$;
	disablenpc instance_npcname("Lock Device#1-1");
	disablenpc instance_npcname("Lock Device#1-2");
	disablenpc .@maram$;
	setcell 'map_tcamp$,137,216,142,216,cell_walkable,1;
	end;
}
1@tcamp,141,216,3	duplicate(Lock Device#1-1)	Lock Device#1-2	4_ROPEPILE
1@tcamp,140,218,3	duplicate(dummy_disabled_npc)	Maram#Lock Device1	4_EP18_MARAM

1@tcamp,136,145,3	script	Lock Device#2-1	4_ROPEPILE,{
	.@maram$ = instance_npcname("Maram#Lock Device2");
	enablenpc .@maram$;
	progressbar_npc "3131FF",10;
	npctalk "最好谨慎行事。", .@maram$;
	disablenpc instance_npcname("Lock Device#2-1");
	disablenpc instance_npcname("Lock Device#2-2");
	disablenpc instance_npcname("Lock Device#2-3");
	disablenpc instance_npcname("Lock Device#2-4");
	disablenpc .@maram$;
	setcell 'map_tcamp$,135,145,144,145,cell_walkable,1;
	end;
}
1@tcamp,138,145,3	duplicate(Lock Device#2-1)	Lock Device#2-2	4_ROPEPILE
1@tcamp,141,145,3	duplicate(Lock Device#2-1)	Lock Device#2-3	4_ROPEPILE
1@tcamp,143,145,3	duplicate(Lock Device#2-1)	Lock Device#2-4	4_ROPEPILE
1@tcamp,142,147,3	duplicate(dummy_disabled_npc)	Maram#Lock Device2	4_EP18_MARAM

1@tcamp,223,109,3	script	Lock Device#3-1	4_ROPEPILE,{
	.@maram$ = instance_npcname("Maram#Lock Device3");
	enablenpc .@maram$;
	npctalk "马拉姆：这座建筑看起来很可疑，对吧？这里肯定也有监控系统。", .@maram$;
	progressbar_npc "3131FF",10;
	npctalk "马拉姆：这次我们仔细探索一下，小心别被抓住。", .@maram$;
	disablenpc instance_npcname("Lock Device#3-1");
	disablenpc instance_npcname("Lock Device#3-2");
	disablenpc .@maram$;
	setcell 'map_tcamp$,223,105,223,110,cell_walkable,1;
	end;
}
1@tcamp,223,106,3	duplicate(Lock Device#3-1)	Lock Device#3-2	4_ROPEPILE
1@tcamp,226,114,3	duplicate(dummy_disabled_npc)	Maram#Lock Device3	4_EP18_MARAM

1@tcamp,80,99,3	script	Lock Device#4-1	4_ROPEPILE,{
	.@maram$ = instance_npcname("Maram#Lock Device4");
	enablenpc .@maram$;
	npctalk "Maram：看来我们已经陷得很深了。我们应该能够安全地找到我们需要的东西。", .@maram$;
	progressbar_npc "3131FF",10;
	npctalk "马拉姆：我们祝你好运吧？", .@maram$;
	disablenpc instance_npcname("Lock Device#4-1");
	disablenpc instance_npcname("Lock Device#4-2");
	disablenpc instance_npcname("Lock Device#4-3");
	disablenpc .@maram$;
	setcell 'map_tcamp$,80,94,80,100,cell_walkable,1;

	enablenpc instance_npcname("Pile of documents#Base1");
	enablenpc instance_npcname("Pile of documents#Base2");
	enablenpc instance_npcname("Pile of documents#Base3");
	enablenpc instance_npcname("Pile of documents#Base4");
	enablenpc instance_npcname("Pile of documents#Base5");
	enablenpc instance_npcname("Miriam#Base1");
	enablenpc instance_npcname("Maram#Base5");
	if ('step == 0)
		'step = 1;
	end;
}
1@tcamp,80,97,3	duplicate(Lock Device#4-1)	Lock Device#4-2	4_ROPEPILE
1@tcamp,80,95,3	duplicate(Lock Device#4-1)	Lock Device#4-3	4_ROPEPILE
1@tcamp,82,100,3	duplicate(dummy_disabled_npc)	Maram#Lock Device4	4_EP18_MARAM


//-------------------------------------------------------------------
// Final
//-------------------------------------------------------------------
1@tcamp,32,100,3	script(DISABLED)	Pile of documents#Base1	4_EP18_PAPERS,{
	if ('step != 1)
		end;
	mes "[一堆文件]";
	mes "各种文件都混在一起了。";
	mes "我从中间拿出几页，发现是武器的有效期和销售记录。";
	next;
	mes "[一堆文件]";
	mes "甚至日期也是最近的。还有向蒲公英先生移交大量武器的记录。";
	mes "看来我找到了我要找的东西。我把文件放在怀里。";
	close2;
	getitem 1000409,1;	// Ep18_Docu_File
	cloakonnpcself();
	end;
}

1@tcamp,49,123,3	script(DISABLED)	Pile of documents#Base2	4_EP18_PAPERS,{
	if ('step != 1)
		end;
	mes "[一堆文件]";
	mes "各种文件都混在一起了。";
	mes "在首页……决定午餐的菜单是一场战斗吗？";
	next;
	mes "[一堆文件]";
	mes "看起来菜单上有酱汁炸鱼......";
	mes "看来他只留下了不重要的文件。现在让我们照顾他们吧。";
	close2;
	getitem 1000409,1;	// Ep18_Docu_File
	cloakonnpcself();
	end;
}

1@tcamp,29,86,3	script(DISABLED)	Pile of documents#Base3	4_EP18_PAPERS,{
	if ('step != 1)
		end;
	mes "[一堆文件]";
	mes "各种文件都混在一起了。";
	mes "我从中间拿出一张，看到……军事基地保安人员的地图？";
	next;
	mes "[一堆文件]";
	mes "看日期，是很久以前的文档了。";
	mes "看来他只留下了不重要的文件。现在让我们照顾他们吧。";
	close2;
	getitem 1000409,1;	// Ep18_Docu_File
	cloakonnpcself();
	end;
}

1@tcamp,60,122,3	script(DISABLED)	Pile of documents#Base4	4_EP18_PAPERS,{
	if ('step != 1)
		end;
	mes "[一堆文件]";
	mes "各种文件都混在一起了。";
	mes "最上面的章节是……“如果你喂一只岩浆马铃薯，然后在一段时间后猎杀它，它会掉落一个烤马铃薯吗？”";
	next;
	mes "[一堆文件]";
	mes "……为什么会在这里？但我很好奇结果！";
	mes "看来他只留下了不重要的文件。现在让我们照顾他们吧。";
	close2;
	getitem 1000409,1;	// Ep18_Docu_File
	cloakonnpcself();
	end;
}

1@tcamp,31,84,3	script(DISABLED)	Pile of documents#Base5	4_EP18_PAPERS,{
	if ('step != 1)
		end;
	mes "[一堆文件]";
	mes "各种文件都混在一起了。";
	mes "当我从中间拉出一个时......我发现了一张用钢笔用教皇饼干糖霜绘制的教皇插图。";
	next;
	mes "[一堆文件]";
	mes "这是一幅什么样的图画？而且画得真的很好......";
	mes "看来他只留下了不重要的文件。现在让我们照顾他们吧。";
	close2;
	getitem 1000409,1;	// Ep18_Docu_File
	cloakonnpcself();
	end;
}


1@tcamp,34,100,3	script(DISABLED)	Miriam#Base1	4_EP18_MIRIAM,3,3,{
	if ('step == 1) {
		if (countitem(1000409) < 5) {	// Ep18_Docu_File
			cutin "ep18_miriam_02.png",2;
			mes "[米丽亚姆]";
			mes "果然，你是一位天才，冒险家。我动用了所有的人脉才找到了这个位置。";
			mes "看来你马上就找到了冒险者和玛拉姆。";
			next;
			mes "[米丽亚姆]";
			mes "请阅读这些文件。它们包含我们正在寻找的内容。";
			mes "我认为与其让我向你解释一切，不如你亲自看看会更好。";
			close3;
		}
		if (getequipid(EQI_HEAD_TOP) != 400127) {	// Ep18_Mini_Elly
			mes "[迷你艾莉]";
			mes "把它从袋子里拿出来。把它戴在头上。";
			close;
		}
		mes "[米丽亚姆]";
		mes ".......";
		next;
		cutin "ep18_maram_01.png",2;
		mes "[马拉姆]";
		mes "不，米丽亚姆！你怎么在这里？";
		mes "他们表示将进行单独调查！";
		next;
		cutin "ep18_miriam_01.png",2;
		mes "[米丽亚姆]";
		mes "你和冒险家是怎么挖到这么远的？";
		mes "正如所料，你的技能很好。我动用了所有的人脉来寻找位置，但你却立刻就找到了。";
		next;
		cutin "ep18_maram_02.png",2;
		mes "[马拉姆]";
		mes "让我们做点什么吧，冒险家？";
		mes "无论如何，你得到了什么信息？";
		next;
		cutin "ep18_miriam_02.png",2;
		mes "[米丽亚姆]";
		mes "在这里查看这个文档。";
		next;
		cutin "",255;
		mes "[迷你艾莉]";
		mes "该文件是武器销售记录。请给我看看。";
		mes "这些人处理掉的所有武器枪械，距离保质期都只剩不到一年了。";
		next;
		cutin "ep18_miriam_03.png",2;
		mes "[米丽亚姆]";
		mes "你们就这么肆无忌惮地卖武器。";
		mes "如果武器没有得到适当的维护，它可能无法正常工作。";
		next;
		cutin "ep18_maram_03.png",2;
		mes "[马拉姆]";
		mes "所有从后洞出来的武器品质都是一样的。";
		mes "……但我想现在就是这个时候了。";
		next;
		mes "[马拉姆]";
		mes "一年的有效期不算长，但也不算太紧。";
		mes "为什么在我们准备抗议的时候却倾盆而出呢？";
		next;
		mes "[马拉姆]";
		mes "没有必要立即处理掉它。";
		next;
		cutin "ep18_miriam_02.png",2;
		mes "[米丽亚姆]";
		mes "这是我养父管理的军事基地之一。";
		mes "我想知道它是否真的会在这种时候突然出现。";
		next;
		cutin "",255;
		select( "如果是你的养父，是阿迈勒神父吗？", "那个强硬派、亲战的养父？" );
		cutin "ep18_miriam_02.png",2;
		mes "[米丽亚姆]";
		mes "没错，冒险者。当我查看账本时，我的养父似乎并没有参与其中。";
		mes "我认为这是一起孤立的最低层腐败事件，即武器泄漏事件。";
		next;
		mes "[米丽亚姆]";
		mes "我必须更深入地挖掘，但这只是我现在的猜测。";
		mes "哦，请把文件给我。我会把它们作为证据。";
		delitem 1000409,5;	// Ep18_Docu_File
		if ('step == 1)
			'step = 2;
		if (isbegin_quest(16578) == 1) {
			erasequest 16578;
			setquest 16579;
		}
		close2;
		cutin "",255;
		npctalk "马拉姆：哈，这真的很有趣。", instance_npcname("我喜欢它#Base5");
		end;
	}
	if ('step == 2) {
		cutin "ep18_miriam_02.png",2;
		mes "[米丽亚姆]";
		mes "...请稍等，我正在阅读该文件。";
		close;
	}
	end;

OnTouch:
	if (countitem(1000409) < 5)
		npctalk "米丽亚姆：噢，冒险家？玛拉姆，你也是吗！？";
	end;

OnInstanceInit:
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(16578) == 1 && countitem(1000409) > 4" );	// Ep18_Docu_File
	end;
}

1@tcamp,32,102,3	script(DISABLED)	Maram#Base5	4_EP18_MARAM,{
	if ('step == 1) {
		cutin "ep18_maram_01.png",2;
		mes "[马拉姆]";
		mes "我不知道米丽亚姆会在这里。对吧，冒险家？";
		close3;
	}
	if ('step == 2) {
		mes "[马拉姆]";
		mes "他们分开搬家，但最后还是到了同一个地方。";
		mes "不过，我从这个过程中得到的收获还是不一样的，所以我想它还是有一定意义的。";
		next;
		cutin "ep18_miriam_02.png",2;
		mes "[米丽亚姆]";
		mes "那我想我得离开一段时间了。";
		mes "我得把我的发现转告给我的养父。";
		next;
		mes "[米丽亚姆]";
		mes "如果是个别腐败，说不定连我的养父都不知道。";
		next;
		cutin "ep18_maram_03.png",2;
		mes "[马拉姆]";
		mes "这真的是一起孤立的腐败案件吗？这是令我困扰的事情。";
		mes "我还有东西要送给瑞秋，所以米里亚姆，我们一起去吧。";
		next;
		cutin "ep18_maram_01.png",2;
		mes "[马拉姆]";
		mes "我会告诉你一条出去的捷径，那么冒险者你能告诉苏阿德你在这里发现了什么吗？";
		mes "我们很快就会跟进，所以请先去告诉我们。";
		if (ep18_main == 46) {
			ep18_main = 47;
			erasequest 16579;
			setquest 16580;
			getitem 1000405,50;	// Ep18_Amethyst_Fragment
		}
		close2;
		warp "wolfvill",162,154;
		end;
	}
	// debug
	warp "wolfvill",162,154;
	end;

OnInstanceInit:
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(16579) == 1" );
	end;
}
