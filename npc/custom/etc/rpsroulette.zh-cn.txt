//===== rAthena Script =======================================
//= Rock Scissors Roulette
//===== By: ==================================================
//= acky
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Plays a hybrid Russian Roulette Rock Scissors Paper game.
//===== Additional Comments: =================================
//= Prizes customizable, Added emotions.
//= 1.2 Fixes by Blackthunder and me [Poki#3]
//===== Translated By: =======================================
//= dsc
//============================================================

cmd_in02,182,126,2	script	Crazy Boris	85,{
	set .@counter,1;
	mes "嘿，你！准备玩剪刀轮盘赌吗？";
	next;
	menu "Let me play.",PLAY,"Explain the rules.",RULES,"Leave",LEAVE;
SAME:
	mes "画！再次！";
	next;
PLAY:
	mes "石头...纸...";
	set .@opp, rand(1,3);
	menu "^0000FFROCK!",-,"^FF0000SCISSORS!",SCISSORS,"^00FF00PAPER!^000000",PAPER;

  	if (.@lastchoice == 1) set .@opp, rand(1,3);
  	if (.@opp == 1) emotion ET_ROCK;
  	else if (.@opp == 2) emotion ET_SCISSOR;
  	else emotion ET_WRAP;
  	set .@lastchoice,1;
	if (.@opp == 1) goto SAME;
	if (.@opp == 2) goto WIN;
	goto LOSE;

SCISSORS:
	if (.@lastchoice == 2) set .@opp,rand(1,2);
  	if (.@opp == 1) emotion ET_ROCK;
  	else if (.@opp == 2) emotion ET_SCISSOR;
  	else emotion ET_WRAP;
  	set .@lastchoice,2;
	if (.@opp == 1) goto LOSE;
	if (.@opp == 2) goto SAME;
	goto WIN;

PAPER:
	if (.@lastchoice == 3) set .@opp,rand(2,3);
  	if (.@opp == 1) emotion ET_ROCK;
  	else if (.@opp == 2) emotion ET_SCISSOR;
  	else emotion ET_WRAP;
  	set .@lastchoice,3;
	if (.@opp == 1) goto WIN;
	if (.@opp == 2) goto LOSE;
	goto SAME;

WIN:
	mes "妈的，你赢了！";
	emotion ET_PROFUSELY_SWEAT;
	next;
	set .@win, 1;
	goto YOUPULL;

LOSE:
	emotion ET_SMILE;
	mes "布拉！你输了！";
	next;
	set .@win, 0;

YOUPULL:
	mes .@counter +"共 6 个";
	if ( .@counter == 6 )
		mes "说出你的祈祷";
	set .@pull, rand( 1,( 7 -.@counter ) );
	set .@counter, .@counter +1;
	next;
	if ( .@pull == 1 ) {
		if ( .@win ) goto KILL;
		specialeffect2 EF_SUI_EXPLOSION;
		emotion ET_KIK;
		percentheal -100,-100;
		mes "*^0000FF点击^000000* *^FF0000砰^000000*";
		mes "你死定了！";
		close;
	}
	emotion ET_HNG;
	mes "*^0000FF点击^000000*呼...";
	goto PLAY;

RULES:
	mes "好的，规则如下：";
	mes "我随身携带了一把^FF00006^000000手枪，配有^FF00001^000000子弹。首先我们玩^FF0000剪刀^00FF00布^0000FF石头^000000。失败者扣动扳机。谁表现最好，谁就是胜利者。";
	mes "打败我就可以获奖。";
	next;
	menu "Let me play.",-,"No thanks.",LEAVE;
	mes "好的，我们开始...";
	next;
	goto PLAY;

KILL:
	specialeffect EF_SUI_EXPLOSION;
	emotion ET_HUK;
	mes "*^0000FF点击^000000* *^FF0000砰^000000*";
	mes "哇哇@#$%^！！太痛了！";
	next;
	mes "恭喜！你赢了...";
	switch( rand( 1,10 ) ) {
		case 1: setarray .@reward[0], 10,984; break;
		case 3: setarray .@reward[0],100,601; break;	// 100x Fly Wings
		case 4: setarray .@reward[0],  8,603; break;	// 8x Old Blue Box
		case 5: setarray .@reward[0],  4,617; break;	// 4x Old Violet Box
		case 6: setarray .@reward[0],  1,616; break;	// 1x Old Card Album
		case 7: setarray .@reward[0], 10,604; break;	// 10x Dead Branch
		case 8: setarray .@reward[0],  3,969; break;	// 3x Gold
		case 10: setarray .@reward[0],20,505; break;	// 20x Blue Potion
		case 2:
		case 9:
			setarray .@reward[0], 10,985;
	}
	mes .@reward[0] +"x "+ getitemname( .@reward[1] ) +"！";
	getitem .@reward[1], .@reward[0];
	close;

LEAVE:
	mes "三色堇。";
	close;
}
