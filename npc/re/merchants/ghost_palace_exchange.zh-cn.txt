//===== rAthena Script =======================================
//= Ghost Palace Exchange NPC.
//===== Description: =========================================
//= Walkthrough Conversion.
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

dali02,51,130,4	script	Dimension Traveler	4_F_SHABBY,{
	if (checkweight(28100,1) == 0) {// Thanos Axe
		mes "- 您无法继续，因为您携带的物品太多。 -";
		close;
	}
	.@item_name_req$ = getitemname(6672);// Gray_Shard
	mes "[次元旅行者]";
	mes "冒险家朋友，" + mesitemlink( 6672 ) + "是我需要的。用我有的东西交换怎么样？";
	next;
	.@type = select( "给我看看你的武器。", "给我看看你的盔甲。", "有什么特别的吗？", "我不需要。" ) - 1;
	mes "[次元旅行者]";
	switch(.@type) {
	case 0:
		mes "哦！你对武器感兴趣吗？从鬼宫能得到的武器我们都有。为什么不选一个呢？";
		break;
	case 1:
		mes "盔甲？从鬼宫能得到的铠甲我们都有了。为什么不选一个呢？";
		break;
	case 2:
		mes "呵呵！似乎他们中的许多人已经听说我拥有最好的东西。欢迎查看我的收藏。";
		break;
	case 3:
		mes "再见朋友~再来吧！";
		close;
	}
	next;
	mes "[次元旅行者]";
	mes "啊!需要写在 " + mesitemlink( 6672 ) + " 旁边的数字。";
	mes "请记住这一点。";

	switch(.@type) {
	case 0:
		setarray .@items[0],
			13441, 200,		// Thanos Sword
			21009, 200, 	// Thanos Great Sword
			1438,  200,		// Thanos Spear
			1496,  200,		// Thanos Long Spear
			1669,  200,		// Thanos Staff
			2023,  200,		// Thanos Rod
			18119, 200,		// Thanos Bow
			13093, 200,		// Thanos Dagger
			28000, 200,		// Thanos Katar
			1836,  200,		// Thanos Knuckle
			16028, 200,		// Thanos Hammer
			28100, 200,		// Thanos Axe
			1933,  200,		// Thanos Violin
			1988,  200;		// Thanos Whip
		break;
	case 1:
		setarray .@items[0],
			2187,  100,		// Shield Of Gray
			15090, 100, 	// Armor Of Gray
			15091, 100,		// Gray Robe
			20721, 100,		// Cloak Of Gray
			22033, 100,		// Boots Of Gray
			18820, 100;		// Gray Helmet
		break;
	case 2:
		setarray .@items[0],
			27184, 10000,		// Knight Sakray Card
			23676, 50, 			// Gray Charcoal Melee (Melee)
			23677, 50,			// Gray Charcoal Magic (Magic)
			23678, 50;			// Gray Charcoal Range (Range)
		break;
	}
	.@size = getarraysize(.@items);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		.@menu$ += getitemname(.@items[.@i]) + " - " + .@items[.@i+1] + ":";

	while(true) {
		next;
		.@s = (select(.@menu$) - 1) * 2;
		mes "[次元旅行者]";
		if (.@type != 1)	// armor type doesn't display item info
			mes "你是说" + mesitemlink( .@items[.@s] ) + "吗？";
		mes "交换时需要 " + .@items[.@s+1] + " ^006400" + .@item_name_req$ + "^000000。";
		next;
		switch( select( "我们交流一下。", "让我看看其他东西。", "我稍后再回来。" ) ) {
		case 1:
			break;
		case 2:
			mes "[次元旅行者]";
			switch(.@type) {
			case 0:
				mes "我的武器已经足够多了。";
				break;
			case 1:
				mes "我们为你准备了很多盔甲。你为什么不选一个呢？";
				break;
			case 2:
				mes "如果你带我来" + .@item_name_req$ + "，我可以用你想要的任何东西来交换。";
				break;
			}
			continue;
		case 3:
			mes "[次元旅行者]";
			mes "保重朋友~下次再见！";
			close;
		}
		if (countitem(6672) < .@items[.@s+1]) {
			mes "[次元旅行者]";
			mes "你让我陷入了困境，朋友。";
			mes "我需要" + .@item_name_req$ + "。我会把它换成^006400" + .@items[.@s+1] + " " + .@item_name_req$ + "^000000。";
			close;
		}
		mes "[次元旅行者]";
		mes "谢谢。如果你还需要什么就回来吧。";
		delitem 6672, .@items[.@s+1];
		getitem .@items[.@s], 1;
		close;
	}
}
