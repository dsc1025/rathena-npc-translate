//===== rAthena Script =======================================
//= 2012 Headgear Quests
//===== By: ==================================================
//= Euphy, -SkittleNugget-
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Enhance gears by synthesizing them with Energy Crystals.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//=     Script is a little messy, could use some cleaning.
//===== Translated By: =======================================
//= dsc
//============================================================

// Main NPC :: 2012_hat_quest
//============================================================
moc_para01,41,169,3	script	Reno#2012hat	64,{

	// iRO has (very) minor differences in dialogue from the original script.
	// To use iRO's version, uncomment the line below.
	//set .@features_iRO,1;

	//custom translation -  all checks below
	if (checkweight(1301,1) == 0 || MaxWeight - Weight < 800) {
		mes "- 携带太多物品，无法继续。 -";
		close;
	}

	setarray .@type$[0],"lower","intermediate","advanced","highest";
	setarray .@crystal$[0],"Rough","Purified","High";

	// playtime checks
	setarray .@quests_playtime[0],5161,5169,5174,5225,5226,5227;
	for (set .@i,0; .@i<6; set .@i,.@i+1) {
		if (checkquest(.@quests_playtime[.@i],PLAYTIME) == 2) {
			erasequest .@quests_playtime[.@i];
			mes "[里诺]";
			if (.@i < 3)	// hunting quest wait time
				mes ""+.@type$[.@i]+" 收集任务的等待时间已过。您现在可以接受另一个请求。";
			else        	// buff wait time
				mes "储存"+.@crystal$[.@i]+"能量水晶的等待时间已过。您现在可以再次存储它们。";
			close;
		}
	}

	// hunting quest checks
	callsub L_CheckHunting,5161,5162,7,.@type$[0],6623,10; //Rough_Energy_Crystal
	callsub L_CheckHunting,5169,5170,4,.@type$[1],6624,10; //Purified_Energy_Crystal
	callsub L_CheckHunting,5174,5175,4,.@type$[2],6625,5;  //High_Purity_Energy_Xtal
	callsub L_CheckHunting,5174,5179,4,.@type$[3],6625,10; //High_Purity_Energy_Xtal

	// buff checks
	set .@energy_buf00, getstatus(SC_QUEST_BUFF1) + getstatus(SC_QUEST_BUFF2) + getstatus(SC_QUEST_BUFF3);
	if (!.@energy_buf00) {
		for (set .@i,0; .@i<3; set .@i,.@i+1) {
			if (isbegin_quest(.@i + 5222)) {
				set .@item, 6623 + .@i;
				set .@amount, 2 + rand(3);
				mes "[里诺]";
				mes getitemname(.@item)+"现已完成存储。";
				next;
				setquest 5225 + .@i;
				erasequest 5222 + .@i;
				getitem .@item,.@amount;
				mes "[里诺]";
				mes "^0000FF"+.@amount+" "+getitemname(.@item)+" 已提取^000000。";
				close;
			}
		}
	}

	mes "[里诺]";
	mes "你好？我是能源水晶研究协会（ECRA）的雷诺。";
	next;
	switch(select("ECRA?:寻求获得能量水晶:能量水晶增益:压缩能量水晶:用水晶交换头饰:水晶的随机装备" + ((.@features_iRO)?":参见装备清单":""))) {
	case 1:
		mes "[里诺]";
		mes "ECRA是在能源短缺的符文米德加尔特王国中研究作为替代能源使用的“^0000FF能量结晶^000000”的研究所。";
		next;
		select("这样做的原因是什么？");
		mes "[里诺]";
		mes "我想解释一下，在我解释原因之前，我们并不是想找借口逃避面临的问题。绝不！那么请让我说完。";
		next;
		mes "[里诺]";
		mes "研究小组已将他们发现的结果发送给我。";
		next;
		mes "[里诺]";
		mes "能量结晶原来是在一些怪物^000000的体内生成的^0000FF，并且根据研究，更强的怪物体内会生成更高等级的能量结晶。";
		next;
		mes "[里诺]";
		mes "研究小组决定通过向冒险者提出请求来收集能量水晶，并给予适当的奖励。这使我们能够更好地收集并且更加^FF0000高效^000000。";
		next;
		mes "[里诺]";
		mes "按照我的要求，通过猎杀怪物所积累的能量水晶将用于我的研究。 ^0000FF其中一些计划返回给冒险者。^000000";
		next;
		mes "[里诺]";
		mes "冒险者可以通过我使用收集到的能量水晶来^FF0000升级装备^000000。";
		next;
		mes "[里诺]";
		mes "如果运作良好，似乎对 ECRA 和冒险家都有好处。";
		close;
	case 2:
		mes "[里诺]";
		mes "狩猎任务按照怪物等级和难度进行排名。任务范围分为低级、中级、高级，每天只能完成一次。";
		next;

		setarray .@quest_status[0],
			isbegin_quest(5162) + isbegin_quest(5163) + isbegin_quest(5164) + isbegin_quest(5165) + isbegin_quest(5166) + isbegin_quest(5167) + isbegin_quest(5168),
			isbegin_quest(5170) + isbegin_quest(5171) + isbegin_quest(5172) + isbegin_quest(5173),
			isbegin_quest(5175) + isbegin_quest(5176) + isbegin_quest(5177) + isbegin_quest(5178) + isbegin_quest(5179) + isbegin_quest(5180) + isbegin_quest(5181) + isbegin_quest(5182);
		setarray .@quest_index[0],5161,5169,5174;

		set .@select, select("低级狩猎[61-80]：中级狩猎[80-99]：高级狩猎[90及以上]：取消我当前的任务。") - 1;
		if (.@select < 3) {
			setarray .@min_level[0],60,80,90,100;
			if (BaseLevel < .@min_level[.@select]) {
				mes "[里诺]";
				mes "抱歉，"+.@type$[.@select]+" 收藏的请求仅适用于 "+.@min_level[.@select]+" 及更高级别。您似乎没有能力执行此请求。";
				close;
			}
			if (.@quest_status[.@select]) {
				mes "[里诺]";
				mes "您似乎正在接受"+.@type$[.@select]+"收藏的其他请求。如果任务看起来太难了...你^FF0000放弃请求^000000怎么样？";
				close;
			}
			set .@playtime, checkquest(.@quest_index[.@select],PLAYTIME);
			if (.@playtime == 0 || .@playtime == 1) {
				mes "[里诺]";
				mes "目前正在等待"+.@type$[.@select]+"征收委员会。每个集合中的任务每天只能完成一次。"; //custom translation
				close;
			}
			mes "[里诺]";
			mes "您选择从"+.@type$[.@select]+"系列中狩猎怪物。猎杀这些怪物后，当你回到我身边时，你将收到"+((.@features_iRO)?"10 ":"")+"^0000FF"+.@crystal$[.@select]+"能量水晶^000000。";
			next;
			mes "[里诺]";
			mes "请从列表中选择您认为可以轻松猎杀的怪物。";
			next;
			switch (.@select) {
			case 0:
				set .@quest, select(
					"[Lower class] Requiem",
					"[Lower class] Bathory",
					"[Lower class] Spring Rabbit",
					"[Lower class] Sleeper",
					"[Lower class] Evil Druid",
					"[Lower class] Ground Petite",
					"[Lower class] Clock"
				);
				break;
			case 1:
				set .@quest, select(
					"[Intermediate class] Siroma",
					"[Intermediate class] Dark Priest",
					"[Intermediate class] Stapo",
					"[Intermediate class] Solider"
				);
				break;
			case 2:
				set .@quest, select(
					"[Advanced] Desert Wolf",
					"[Advanced] Medusa",
					"[Advanced] Pinguicula",
					"[Advanced] Majoruros",
					"[Highest] Raydric",
					"[Highest] Naga",
					"[Highest] Ancient Mummy",
					"[Highest] Ancient Mimic"
				);
				if (.@quest > 4 && BaseLevel < .@min_level[3]) {
					mes "[里诺]";
					mes "抱歉，最高级收藏的请求仅适用于 "+.@min_level[3]+" 及更高级别。您似乎没有能力执行此请求。";
					close;
				}
				break;
			}
			setquest .@quest_index[.@select] + .@quest;
			mes "[里诺]";
			mes "请求"+.@type$[.@select]+"收集怪物已发布。请检查已发行文件的详细信息。";
			close;
		} else {
			setarray .@quest_count[0],7,4,8;
			for (set .@i,0; .@i<3; set .@i,.@i+1) {
				if (.@quest_status[.@i]) {
					mes "[里诺]";
					mes "您目前正在接受"+.@type$[.@i]+" 收藏的请求。如果你现在放弃，你所有的历史都将丢失。你真的想放弃吗？";
					next;
					set .@erase_quest, select("是：否");
					switch (.@erase_quest) {
					case 1:
						for (set .@quest,.@quest_index[.@i]+1; .@quest<=.@quest_index[.@i]+.@quest_count[.@i]; set.@quest,.@quest+1) {
							if (isbegin_quest(.@quest))
								erasequest .@quest;
						}
						break;
					case 2:
						break;
					}
				}
			}
			switch (.@erase_quest) {
			case 0:
				mes "[里诺]";
				mes "您没有收到任何请求。";
				close;
			case 1:
				mes "[里诺]";
				mes "给你的书面请求已被撤回。";
				close;
			case 2:
				mes "[里诺]";
				mes "请继续关注。";
				close;
			}
		}
	case 3:
		mes "[里诺]";
		mes "你想在体内积累能量水晶来获得增益吗？";
		next;
		set .@select, select("积累能量结晶：积累粗能量结晶：积累精炼能量结晶：积累高能量结晶");
		switch (.@select) {
		case 1:
			mes "[里诺]";
			mes "当你猎杀我们要求你去的怪物时，能量水晶会积聚在你的体内。";
			next;
			mes "[里诺]";
			mes "我们的研究表明，某些方法可以使人类以增益的形式在体内积累能量晶体。";
			next;
			mes "[里诺]";
			mes "如果花费3小时左右，就能获得少量的能量水晶。那么你想获得buff吗？";
			close;
		case 2:
			set .@playtime, checkquest(5225,PLAYTIME);
			set .@quest,5222;
			set .@min_level,60;
			set .@rate,1;
			set .@buff, SC_QUEST_BUFF1;
			break;
		case 3:
			set .@playtime, checkquest(5226,PLAYTIME);
			set .@quest,5223;
			set .@min_level,80;
			set .@rate,2;
			set .@buff, SC_QUEST_BUFF2;
			break;
		case 4:
			set .@playtime, checkquest(5227,PLAYTIME);
			set .@quest,5224;
			set .@min_level,90;
			set .@rate,3;
			set .@buff, SC_QUEST_BUFF3;
			break;
		}
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[里诺]";
			mes "你成功了吗？虽然储存其他能量水晶不会对你的身体产生太大影响，但储存相同的能量水晶可能会对你造成伤害。"; //custom translation
			close;
		}
		if (BaseLevel < .@min_level) {
			mes "[里诺]";
			mes "有了这样的身体，你就无法积累"+.@crystal$[.@select-2]+"Error 500 (Server Error)!!1500.That’s an error.There was an error. Please try again later.That’s all we know."+.@min_level+"否则您的身体会在此过程中受到伤害。"; //custom translation
			close;
		}
		if (.@energy_buf00) {
			mes "[里诺]";
			mes "积蓄能量水晶已经开始了。抱歉，两种不同类型的能量结晶是不可能同时积累的。";
			close;
		}
		if (isbegin_quest(.@quest) > 0) {
			erasequest .@quest;
			mes "[里诺]";
			mes "发生错误。给您带来的不便，敬请原谅。"; //custom translation
			close;
		}
		mes "[里诺]";
		mes "您的身体需要 3 小时才能积累 2 至 4 个"+.@crystal$[.@select-2]+" 能量水晶。你想开始吗？";
		next;
		if(select("取消：开始") == 1) {
			mes "[里诺]";
			mes "我建议大家有时间的时候尝试积累能量水晶。";
			close;
		}
		mes "[里诺]";
		mes "您可能会感到轻微的刺痛感。";
		next;
		specialeffect2 EF_BASH3D;
		percentheal .@rate * -5,0;
		//consumeitem ??; //Keep_Connection_[.@rate]
		sc_start .@buff,10800000,.@rate;	// Atk/Matk + 5*rate (+5, +10, +15)
		setquest .@quest;
		mes "[里诺]";
		mes "- 冰球 -";
		next;
		mes "[里诺]";
		mes "一切似乎都井然有序。让我们开始积累"+.@crystal$[.@select-2]+"能量水晶吧。记得3小时后回来找我领取"+.@crystal$[.@select-2]+"能量水晶。";
		close;
	case 4:
		mes "[里诺]";
		mes "你想压缩能量晶体吗？";
		next;
		switch(select("压缩的作用是什么？：将粗晶压缩为精晶：将精晶压缩为高晶")) {
		case 1:
			mes "[里诺]";
			mes "您对能量水晶压缩感到好奇吗？我可以将低级能量晶体压缩成高级能量晶体。";
			next;
			mes "[里诺]";
			mes "这是我能做的。";
			mes "^FF0000 10 颗能量结晶原石^000000 = ^FF00001 颗精炼能量结晶^000000";
			mes "^FF00005个精炼能量结晶^000000 = ^FF00001个高能量结晶^000000";
			next;
			mes "[里诺]";
			mes "如果您需要更高等级的能量水晶，这是获得它们的好方法，因为较低等级的能量水晶更容易获得。";
			close;
		case 2:
			set .@crystal,6623;  //Rough_Energy_Crystal
			set .@crystal_,6624; //Purified_Energy_Crystal
			set .@rate,10;
			break;
		case 3:
			set .@crystal,6624;  //Purified_Energy_Crystal
			set .@crystal_,6625; //High_Purity_Energy_Xtal
			set .@rate,5;
			break;
		}
		set .@crystal_count, countitem(.@crystal);
		set .@crystal_get, .@crystal_count / .@rate;
		mes "[里诺]";
		mes sprintf("^FF0000%d %s^000000 = ^FF00001 %s^000000",.@rate,getitemname(.@crystal),getitemname(.@crystal_));
		next;
		mes "[里诺]";
		mes sprintf("%d %ss 将被压缩为 %d %ss。",.@crystal_count,getitemname(.@crystal),.@crystal_get,getitemname(.@crystal_));
		mes "你想压缩吗？";
		next;
		if(select("压缩：停止") == 2) {
			mes "[里诺]";
			mes "好的，以后有需要的话请联系我。";
			close;
		}
		while(1) {
			if (.@crystal_count >= .@rate) {  //custom translation
				delitem .@crystal,.@rate;
				getitem .@crystal_,1;
				set .@crystal_count, countitem(.@crystal);
				set .@crystal_get, .@crystal_count / .@rate;
				mes "[里诺]";
				mes "压缩非常成功。";
				mes "还剩下"+.@crystal_count+"能量晶体，可以压缩成"+.@crystal_get+"能量晶体。您想继续吗？";
				next;
				if(select("压缩：停止") == 2)
					break;
			} else {
				mes "[里诺]";
				mes "您没有足够的"+getitemname(.@crystal)+"。";
				next;
				break;
			}
		}
		mes "[里诺]";
		mes "压缩完成。";
		close;
	case 5:
		disable_items;
		mes "[里诺]";
		mes "你想用能量水晶换装备吗？请选择您要兑换的能量水晶类型。";
		next;
		setarray .@crystals[0], countitem(6623), countitem(6624), countitem(6625);
		switch(select("查看装备列表：["+.@crystals[0]+"] 粗能量结晶：["+.@crystals[1]+"] 精炼能量结晶：["+.@crystals[2]+"] 高能量结晶")) {
		case 1:
			mes "[里诺]";
			mes "这是我们能量晶研究会提供的关于装备升级的小册子。请四处看看。";
			close2;
			readbook 11060,1; //Energy_Xtal_Combi_Book
			end;

		// Variable descriptions
		// .@crystal     : Energy Crystal used
		// .@materials[] : equipment to upgrade
		// .@showslot[]  : display slots with equipment name (-1 to disable)
		// .@costs[]     : amount of Energy Crystals needed
		// .@rewards[]   : upgraded equipment
		case 2:
			set .@crystal,6623; //Rough_Energy_Crystal
			setarray .@materials[0], 5027, 5045, 5069, 5003, 2214, 5167, 5168, 5043;
			setarray .@showslot[0],    -1,   -1,   -1,   -1,   -1,    1,    1,   -1;
			setarray .@costs[0],      100,  100,  100,  100,  100,  100,  100,  100;
			setarray .@rewards[0],  18760,18761,18762,18763,18764,18769,18770,18771;
			break;
		case 3:
			set .@crystal,6624; //Purified_Energy_Crystal
			setarray .@materials[0], 2296, 5014, 5096, 2292,2615, 2355,2116, 2420, 2521, 5125;
			setarray .@showslot[0],    -1,   -1,   -1,   -1,  -1,    1,   1,    1,    1,    1;
			setarray .@costs[0],      200,  200,  200,  200, 200,  100, 100,  100,  100,  100;
			setarray .@rewards[0],  18772,18773,18774,18775,2956,15068,2183,22015,20710,18776;
			break;
		case 4:
			set .@crystal,6625; //High_Purity_Energy_Xtal
			setarray .@materials[0], 5019, 5162, 5025, 5022, 5353, 2423,2678,2679, 2355,2116, 2420, 2521, 5125;
			setarray .@showslot[0],    -1,    1,   -1,    0,    1,   -1,  -1,  -1,    1,   1,    1,    1,    1;
			setarray .@costs[0],      300,  300,  300, 1000,  100,  300, 300, 300,   20,  20,   20,   20,   20;
			setarray .@rewards[0],  18765,18768,18766,18767,18767,22014,2957,2958,15068,2183,22015,20710,18776;
			break;
		}
		mes "[里诺]";
		mes "请选择要兑换"+getitemname(.@crystal)+"的装备。";
		next;
		set .@crystal_count, countitem(.@crystal);
		set .@menu$,"";
		for (set .@i,0; .@i<getarraysize(.@materials); set .@i,.@i+1) {
			set .@item_name$, getitemname(.@materials[.@i]) + ((.@showslot[.@i] > -1)?"["+.@showslot[.@i]+"]":"");
			set .@menu$, .@menu$ + sprintf("%s (%d/%d):",.@item_name$,.@crystal_count,.@costs[.@i]);
		}
		set .@index, select(.@menu$) - 1;
		set .@material, .@materials[.@index];
		set .@item_name$, getitemname(.@material) + ((.@showslot[.@index] > -1)?"["+.@showslot[.@index]+"]":"");
		set .@cost, .@costs[.@index];
		set .@reward, .@rewards[.@index];

		//custom translations
		if (.@material == 2615) {  // Special case: Safety Ring
			if (countitem(2615) < 1 || countitem(2621) < 1 || countitem(2622) < 1 || countitem(2624) < 1 ||
			    countitem(2625) < 1 || countitem(2623) < 1 || countitem(2626) < 1 || countitem(.@crystal) < .@cost) {
				mes "[里诺]";
				mes "制作安全环[1]的材料不足。请检查您的库存。";
				close;
			}
			mes "[里诺]";
			mes "在升级您的安全环之前，请阅读这些^FF0000注意事项^000000。";
			next;
			mes "[里诺]";
			mes "^FF0000除了安全环之外，所需材料的所有精炼和卡牌也会消失。^000000";
			next;
		} else {
			if (countitem(.@material) < 1 || countitem(.@crystal) < .@cost) {
				mes "[里诺]";
				mes "材料不够。请检查您的材料。";
				close;
			}
			mes "[里诺]";
			mes "在升级您的设备之前，请先阅读这些^FF0000注意事项^000000。";
			next;
			mes "[里诺]";
			mes "^FF0000使用能量水晶升级装备时，之前所有的精炼、结界、卡牌都会丢失。^000000";
			next;
		}
		mes "[里诺]";
		mes "您熟悉^FF0000注意事项^000000吗？";
		next;
		if(select("我是：不。") == 2) {
			mes "[里诺]";
			mes "好的。那么下次吧。";
			close;
		}
		mes "[里诺]";
		mes "那么我们就开始升级"+.@item_name$+".";
		next;
		specialeffect2 EF_REPAIRWEAPON;
		progressbar "ffff00",2;
		delitem .@material,1;
		if (.@material == 2615) {
			delitem 2621,1; //Ring_
			delitem 2622,1; //Earring_
			delitem 2624,1; //Glove_
			delitem 2625,1; //Brooch_
			delitem 2623,1; //Necklace_
			delitem 2626,1; //Rosary_
		}
		delitem .@crystal,.@cost;
		getitem .@reward,1;
		mes "[里诺]";
		mes "升级成功。你们的能量水晶将对我们的研究有很大帮助。";
		close;
	case 6:
		mes "[里诺]";
		mes "您想捐赠能量水晶来获得随机的新头饰吗？";
		next;
		switch(select("赠送能量水晶？：赠送原石能量水晶300颗。：赠送精炼能量水晶600颗。：赠送高能水晶900颗。")) {
		case 1:
			mes "[里诺]";
			mes "我们正在收集能量水晶，以便为冒险者研究更多的头饰组合。";
			next;
			mes "[里诺]";
			mes "感谢冒险家的支持，我们可以使用这些能量水晶来进一步研究。";
			next;
			mes "[里诺]";
			mes "因此，我们至少会收到 300 颗能量原石、600 颗精炼能量水晶和 900 颗高能水晶。";
			if (!.@features_iRO) {
				next;
				mes "[里诺]";
				mes "你可能会觉得这是一种负担，但为了我们的研究，请继续。"; //custom translation
				close;
			}
			mes "作为捐赠，我将随机赠送你一个新头饰。";
			next;
			mes "[里诺]";
			mes "300 颗能量原石";
			mes "- 新法师帽子";
			mes "- 新魔术师帽子";
			mes "- 新狐面具";
			mes "- 新小丑小丑";
			mes "- 新兔子乐队";
			mes "- 新穆纳克帽子";
			mes "- 新凤枪帽";
			mes "- 新幻影面具";
			next;
			mes "[里诺]";
			mes "600 纯净能量水晶";
			mes "- 好的双筒望远镜";
			mes "- 好鳍头盔";
			mes "- 好刺客面具";
			mes "- 良好的焊接面罩";
			mes "- 良好的安全环";
			mes "- 良好的天使保护";
			mes "- 善良的天使守卫";
			mes "- 好天使开衫";
			mes "- 善良天使的转世";
			next;
			mes "[里诺]";
			mes "900颗高能水晶";
			mes "- 增强的海盗船";
			mes "- 强化骨质头盔";
			mes "- 强化天使头盔";
			mes "- 增强型变异鞋";
			mes "- 强化火焰领主之戒";
			mes "- 增强的共振环";
			close;
		case 2:
			if (countitem(6623) < 300) {
				mes "[里诺]";
				mes "感谢您的支持，但我们需要 300 颗能量原晶。";
				close;
			}
			delitem 6623,300; //Rough_Energy_Crystal
			set .@r, rand(1,17);
			     if (.@r <= 2)  getitem 18760,1; //Remodel_Wizardry_Hat
			else if (.@r <= 4)  getitem 18761,1; //Remodel_Magician_Hat
			else if (.@r <= 6)  getitem 18762,1; //Remodel_Mask_Of_Fox
			else if (.@r <= 8)  getitem 18763,1; //Remodel_Joker_Jester
			else if (.@r <= 10) getitem 18764,1; //Remodel_Bunny_Band
			else if (.@r <= 13) getitem 18769,1; //Remodel_Munak_Turban
			else if (.@r <= 16) getitem 18770,1; //Remodel_Bongun_Hat
			else                getitem 18771,1; //Remodel_Opera_Mask
			break;
		case 3:
			if (countitem(6624) < 600) {
				mes "[里诺]";
				mes "感谢您的支持，但我们需要 600 颗精炼能量晶。";
				close;
			}
			delitem 6624,600; //Purified_Energy_Crystal
			set .@r, rand(1,18);
			     if (.@r <= 3)  getitem 18772,1; //Improved_Binoculars
			else if (.@r <= 6)  getitem 18773,1; //Improved_Fin_Helm
			else if (.@r <= 9)  getitem 18774,1; //Improved_Assassin_Mask
			else if (.@r <= 12) getitem 18775,1; //Improved_Welding_Mask
			else if (.@r <= 13) getitem 2956,1;  //Safety_Ring_
			else if (.@r <= 14) getitem 15068,1; //Im_Angel's_Protection
			else if (.@r <= 15) getitem 2183,1;  //Impr_Angel's_Safeguard
			else if (.@r <= 16) getitem 22015,1; //Impr_Angel's_Arrival
			else if (.@r <= 17) getitem 20710,1; //Impr_Angel's_Warmth
			else                getitem 18776,1; //Improved_Kiss_Of_Angel
			break;
		case 4:
			if (countitem(6625) < 900) {
				mes "[里诺]";
				mes "感谢您的支持，但我们需要 900 个高能水晶。";
				close;
			}
			delitem 6625,900; //High_Purity_Energy_Xtal
			set .@r, rand(1,33);
			     if (.@r <= 10) getitem 18765,1; //Enhanced_Corsair
			else if (.@r <= 20) getitem 18768,1; //Enhanced_Bone_Helm
			else if (.@r <= 25) getitem 18766,1; //Enhanced_Helm_Of_Angel
			else if (.@r <= 30) getitem 22014,1; //Enhanced_Variant_Shoes
			else if (.@r <= 31) getitem 2957,1;  //Good_Ring_Of_Flame_Lord
			else                getitem 2958,1;  //Good_Ring_Of_Resonance
			break;
		}
		mes "[里诺]";
		mes "非常感谢。您捐赠的能量晶体将对我们的研究有很大帮助。这是对您捐款的奖励。"; //custom translation
		close;
	case 7:  // iRO only
		mes "[里诺]";
		mes "这是我们能量晶研究会提供的关于装备升级的小册子。请四处看看。";
		close2;
		readbook 11060,1; //Energy_Xtal_Combi_Book
		end;
	}

//callsub L_CheckHunting,<quest index>,<start quest>,<number of quests>,"<type>",<reward id>,<reward amount>;
L_CheckHunting:
	for (set .@quest,getarg(0)+1; .@quest<getarg(1)+getarg(2); set .@quest,.@quest+1) {
		if (checkquest(.@quest,HUNTING) == 2) {
			mes "[里诺]";
			mes "完成"+getarg(3)+"收集狩猎任务。";
			next;
			setquest getarg(0);
			erasequest .@quest;
			getitem getarg(4),getarg(5);
			mes "您将收到 "+getarg(5)+" "+getitemname(getarg(4))+" 作为 "+getarg(3)+" 收集奖励。";
			close;
		}
	}
	return;
}
