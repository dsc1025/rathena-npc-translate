//===== rAthena Script =======================================
//= Paradise Group Market
//===== By: ==================================================
//= Anity99
//===== Current Version: =====================================
//= 1.0.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= The Para Market is an NPC vending system in which certain monster-drop
//= items are available from NPCs at a high price. Items are only available
//= from designated Para Market NPCs. The Para Market is open all day and
//= all week, however there are certain NPCs that are only available on
//= the weekends. Items sold in the Para Market are sold in limited
//= quantities for the entire server. It is unknown how often they restock
//= their wares.
//===== Additional Comments: =================================
//= 1.0 First Version. [Nyalin]
//= 1.0.1 Convert to Hercules [Ridley8819]
//= 1.0.2 Convert to rAthena [Anity99]
//= 1.0.3 Fix typos and update marketshop [Anity99]
//= 1.0.4 Fixed item id typo [Everade]
//============================================================

// $event_paramk, 0 // Holiday Wednesday
// $event_paramk, 1 // Monday, Tuesday, Thursday, Friday, Saturday, Sunday
// $event_paramk, 2 // Friday, Saturday, Sunday
// $event_paramk, 3 // Saturday, Sunday
// $event_paramk, 4 // Late Saturday, Sunday

function	script	F_CoolDown	{
	if (@cd_marketshop + getarg(0) > gettimetick(2))
		end;
	@cd_marketshop = gettimetick(2);
	return;
}

// Shop NPC
// Limited
-	marketshop	para_coin10	FAKE_NPC,6304:500000:10,6080:100000:10,6081:300000:10,6380:1000000:1000,6004:100000:1

-	marketshop	para_hei10	FAKE_NPC,6608:150000:1,6607:20000:1,6755:200000:1,7642:10000:1,22687:200000:1,23016:1000000:1

-	marketshop	para_key10	FAKE_NPC,7026:100000:10,7027:100000:10 // todo

-	marketshop	para_wp00	FAKE_NPC,1181:10000000:1

-	marketshop	para_arm10	FAKE_NPC,5007:2000000:1

-	marketshop	para_ptn00	FAKE_NPC,678:80000:999,12016:50000:9999,525:100000:1000,12422:40000:100,12423:50000:100,12425:40000:100,12426:50000:100

-	marketshop	para_ptn10	FAKE_NPC,505:40000:100,12679:100000:500,12676:30000:500,12680:50000:100

-	marketshop	para_itm00	FAKE_NPC,12106:1500000:100,12107:500000:100,616:2500000:100,12194:200000:100

-	marketshop	para_itm10	FAKE_NPC,12008:5000:99999,12009:5000:99999,12012:5000:100,12010:5000:100,12011:5000:100,12013:20000:100,12014:80000:100,7931:5000:100

-	marketshop	para_seed10	FAKE_NPC,12290:5000:100,12376:5000:100,12291:10000:100,12377:10000:100,576:2000:100,587:2000:100,526:10000:100,607:70000:20,608:50000:20

-	marketshop	para_mora10	FAKE_NPC,11526:5000:100,11525:5000:1000,11520:50000:100,12574:20000:100

-	marketshop	para_ref10	FAKE_NPC,998:10000:500,1003:10000:500,1002:10000:500,999:10000:500

-	marketshop	para_ref20	FAKE_NPC,984:200000:10,985:200000:100,987:240000:1,988:600000:1,989:1200000:1

-	marketshop	para_jew10	FAKE_NPC,969:1000000:20,7289:500000:20,7290:500000:20,7291:500000:20,7292:500000:20,7293:500000:20,7294:500000:20,7295:500000:20,7296:500000:20,7297:500000:20

-	marketshop	para_alc10	FAKE_NPC,971:20000:20,972:12000:20,970:12000:20:7136:7000:20,7135:18000:20

-	marketshop	para_alc20	FAKE_NPC,1061:3000:1000,905:1000:1000,1059:1000:1000,7033:1000:1000,929:4000:1000

-	marketshop	para_alc30	FAKE_NPC,1000:10000:100,1001:10000:100,990:10000:100,991:10000:100,992:10000:100,993:10000:100

-	marketshop	para_fod10	FAKE_NPC,6252:40000:100,6253:8000:100,6254:30000:100,6256:15000:100,6257:15000:100,6259:15000:100,6260:10000:100,22658:80000:50,22659:100000:50

-	marketshop	para_mag10	FAKE_NPC,12108:20000:99999,717:1500:99999,715:4500:99999,716:4500:99999,14512:100000:100,14513:100000:100,14514:100000:100,12737:500:99999,12734:1500:99999,12738:2000:100,12735:3000:50,12736:10000:10,6360:200:99999,6363:200:99999,6361:200:99999,6362:200:99999

-	marketshop	para_mag11	FAKE_NPC,717:1500:99999,12737:500:99999,12734:1500:99999,6360:200:99999,6363:200:99999,6361:200:99999,6362:200:99999

-	marketshop	para_mag20	FAKE_NPC,12119:8000:100,12121:8000:100,12120:5000:100,12118:15000:100

-	marketshop	para_plt01	FAKE_NPC,709:10000:50,7932:4000:99999,7933:4000:99999,7934:4000:99999,7935:4000:99999,7936:4000:99999,7937:4000:99999

// Update Market Shop
-	script	Update_Shop	-1,{
OnInit:
OnClock0000:
	npcshopupdate "para_coin10",6304,0,1;
	npcshopupdate "para_coin10",6080,0,1;
	npcshopupdate "para_coin10",6081,0,1;
	npcshopupdate "para_coin10",6380,0,1;
	npcshopupdate "para_coin10",6004,0,1;
	npcshopupdate "para_hei10",6608,0,1;
	npcshopupdate "para_hei10",6607,0,1;
	npcshopupdate "para_hei10",6755,0,1;
	npcshopupdate "para_hei10",7642,0,1;
	npcshopupdate "para_hei10",22687,0,1;
	npcshopupdate "para_hei10",23016,0,1;
	npcshopupdate "para_key10",7026,0,10;
	npcshopupdate "para_key10",7027,0,10;
	npcshopupdate "para_wp00",1181,0,1;
	npcshopupdate "para_arm10",5007,0,1;
	npcshopupdate "para_ptn00",678,0,999;
	npcshopupdate "para_ptn00",12016,0,9999;
	npcshopupdate "para_ptn00",525,0,1000;
	npcshopupdate "para_ptn00",12422,0,100;
	npcshopupdate "para_ptn00",12423,0,100;
	npcshopupdate "para_ptn00",12425,0,100;
	npcshopupdate "para_ptn00",12426,0,100;
	npcshopupdate "para_ptn10",505,0,100;
	npcshopupdate "para_ptn10",12679,0,500;
	npcshopupdate "para_ptn10",12676,0,500;
	npcshopupdate "para_ptn10",12680,0,100;
	npcshopupdate "para_itm00",12106,0,100;
	npcshopupdate "para_itm00",12107,0,100;
	npcshopupdate "para_itm00",616,0,100;
	npcshopupdate "para_itm00",12194,0,100;
	npcshopupdate "para_itm10",12008,0,99999;
	npcshopupdate "para_itm10",12009,0,99999;
	npcshopupdate "para_itm10",12012,0,100;
	npcshopupdate "para_itm10",12010,0,100;
	npcshopupdate "para_itm10",12011,0,100;
	npcshopupdate "para_itm10",12013,0,100;
	npcshopupdate "para_itm10",12014,0,100;
	npcshopupdate "para_itm10",7931,0,100;
	npcshopupdate "para_seed10",12290,0,100;
	npcshopupdate "para_seed10",12376,0,100;
	npcshopupdate "para_seed10",12291,0,100;
	npcshopupdate "para_seed10",12377,0,100;
	npcshopupdate "para_seed10",576,0,100;
	npcshopupdate "para_seed10",587,0,100;
	npcshopupdate "para_seed10",526,0,100;
	npcshopupdate "para_seed10",607,0,20;
	npcshopupdate "para_seed10",608,0,20;
	npcshopupdate "para_mora10",11526,0,100;
	npcshopupdate "para_mora10",11525,0,1000;
	npcshopupdate "para_mora10",11520,0,100;
	npcshopupdate "para_mora10",12574,0,100;
	npcshopupdate "para_ref10",998,0,500;
	npcshopupdate "para_ref10",1003,0,500;
	npcshopupdate "para_ref10",1002,0,500;
	npcshopupdate "para_ref10",999,0,500;
	npcshopupdate "para_ref20",984,0,10;
	npcshopupdate "para_ref20",985,0,100;
	npcshopupdate "para_ref20",987,0,1;
	npcshopupdate "para_ref20",988,0,1;
	npcshopupdate "para_ref20",989,0,1;
	npcshopupdate "para_jew10",969,0,20;
	npcshopupdate "para_jew10",7289,0,20;
	npcshopupdate "para_jew10",7290,0,20;
	npcshopupdate "para_jew10",7291,0,20;
	npcshopupdate "para_jew10",7292,0,20;
	npcshopupdate "para_jew10",7293,0,20;
	npcshopupdate "para_jew10",7294,0,20;
	npcshopupdate "para_jew10",7295,0,20;
	npcshopupdate "para_jew10",7296,0,20;
	npcshopupdate "para_jew10",7297,0,20;
	npcshopupdate "para_alc10",971,0,20;
	npcshopupdate "para_alc10",972,0,20;
	npcshopupdate "para_alc10",970,0,20;
	npcshopupdate "para_alc10",7136,0,20;
	npcshopupdate "para_alc10",7135,0,20;
	npcshopupdate "para_alc20",1061,0,1000;
	npcshopupdate "para_alc20",905,0,1000;
	npcshopupdate "para_alc20",1059,0,1000;
	npcshopupdate "para_alc20",7033,0,1000;
	npcshopupdate "para_alc20",929,0,1000;
	npcshopupdate "para_alc30",1000,0,100;
	npcshopupdate "para_alc30",1001,0,100;
	npcshopupdate "para_alc30",990,0,100;
	npcshopupdate "para_alc30",991,0,100;
	npcshopupdate "para_alc30",992,0,100;
	npcshopupdate "para_alc30",993,0,100;
	npcshopupdate "para_fod10",6252,0,100;
	npcshopupdate "para_fod10",6253,0,100;
	npcshopupdate "para_fod10",6254,0,100;
	npcshopupdate "para_fod10",6256,0,100;
	npcshopupdate "para_fod10",6257,0,100;
	npcshopupdate "para_fod10",6259,0,100;
	npcshopupdate "para_fod10",6260,0,100;
	npcshopupdate "para_fod10",22658,0,50;
	npcshopupdate "para_fod10",22659,0,50;
	npcshopupdate "para_mag10",12108,0,99999;
	npcshopupdate "para_mag10",717,0,99999;
	npcshopupdate "para_mag10",715,0,99999;
	npcshopupdate "para_mag10",716,0,99999;
	npcshopupdate "para_mag10",14512,0,100;
	npcshopupdate "para_mag10",14513,0,100;
	npcshopupdate "para_mag10",14514,0,100;
	npcshopupdate "para_mag10",12737,0,99999;
	npcshopupdate "para_mag10",12734,0,99999;
	npcshopupdate "para_mag10",12738,0,100;
	npcshopupdate "para_mag10",12735,0,50;
	npcshopupdate "para_mag10",12736,0,10;
	npcshopupdate "para_mag10",6360,0,99999;
	npcshopupdate "para_mag10",6363,0,99999;
	npcshopupdate "para_mag10",6361,0,99999;
	npcshopupdate "para_mag10",6362,0,99999;
	npcshopupdate "para_mag11",717,0,99999;
	npcshopupdate "para_mag11",12737,0,99999;
	npcshopupdate "para_mag11",12734,0,99999;
	npcshopupdate "para_mag11",6360,0,99999;
	npcshopupdate "para_mag11",6363,0,99999;
	npcshopupdate "para_mag11",6361,0,99999;
	npcshopupdate "para_mag11",6362,0,99999;
	npcshopupdate "para_mag20",12119,0,100;
	npcshopupdate "para_mag20",12121,0,100;
	npcshopupdate "para_mag20",12120,0,100;
	npcshopupdate "para_mag20",12118,0,100;
	npcshopupdate "para_plt01",709,0,50;
	npcshopupdate "para_plt01",7932,0,99999;
	npcshopupdate "para_plt01",7933,0,99999;
	npcshopupdate "para_plt01",7934,0,99999;
	npcshopupdate "para_plt01",7935,0,99999;
	npcshopupdate "para_plt01",7936,0,99999;
	npcshopupdate "para_plt01",7937,0,99999;
	end;
}

// General Sales
-	shop	para_wp01	FAKE_NPC,1501:-1,1504:-1,1507:-1,1510:-1,1513:-1,1519:-1,1516:-1,1522:-1,1601:-1,1604:-1,1607:-1,1610:-1,1617:-1,1619:-1,1801:-1,1803:-1,1805:-1,1807:-1,1811:-1,1809:-1

-	shop	para_wp02	FAKE_NPC,1101:-1,1104:-1,1107:-1,1110:-1,1113:-1,1122:-1,1119:-1,1123:-1,1129:-1,1116:-1,1151:-1,1154:-1,1157:-1,1160:-1,1146:-1

-	shop	para_wp03	FAKE_NPC,1701:-1,1704:-1,1707:-1,1710:-1,1713:-1,1714:-1,1718:-1,1721:-1,1901:-1,1903:-1,1905:-1,1950:-1,1952:-1,1954:-1

-	shop	para_wp04	FAKE_NPC,1201:-1,1204:-1,1207:-1,1210:-1,1213:-1,1216:-1,1219:-1,1222:-1,13003:-1,1245:-1,1247:-1,1248:-1,1249:-1,13000:-1,1250:-1,1252:-1,1254:-1

-	shop	para_wp05	FAKE_NPC,1301:-1,1351:-1,1354:-1,1357:-1,1360:-1,1401:-1,1404:-1,1407:-1,1451:-1,1454:-1,1457:-1,1460:-1,1463:-1,1410:-1

-	shop	para_arm01	FAKE_NPC,2328:-1,2314:-1,2312:-1,2316:-1,2341:-1,2340:-1,2411:-1,2228:-1,2230:-1

-	shop	para_arm02	FAKE_NPC,2101:-1,2103:-1,2105:-1,2107:-1

-	shop	para_hat01	FAKE_NPC,2301:-1,2303:-1,2305:-1,2307:-1,2309:-1,2335:-1,2323:-1,2325:-1,2321:-1,2332:-1,2501:-1,2503:-1,2505:-1,2401:-1,2403:-1,2405:-1

-	shop	para_hat02	FAKE_NPC,2239:-1,2203:-1,2201:-1,2243:-1,2242:-1,2205:-1,2220:-1,2226:-1,2222:-1,5114:-1,2216:-1,5092:-1,2218:-1,2211:-1,2212:-1,2208:-1,2232:-1

-	shop	para_ptn01	FAKE_NPC,501:-1,502:-1,503:-1,504:-1,645:-1,656:-1,657:-1

-	shop	para_itm01	FAKE_NPC,7940:-1,1065:-1

-	shop	para_seed01	FAKE_NPC,522:-1

-	shop	para_mora01	FAKE_NPC,11522:-1,11523:-1,11524:-1,11519:-1

-	shop	para_fod01	FAKE_NPC,7482:-1,577:-1,580:-1,7457:-1,581:-1,7453:-1,7455:-1,7454:-1,7456:-1,7452:-1,579:-1

-	shop	para_unm01	FAKE_NPC,537:-1,643:-1,10013:-1,10014:-1,6113:-1,6114:-1,6115:-1,6110:-1,6100:-1,6098:-1,6112:-1,6104:-1,6108:-1,6111:-1,6095:-1,7824:-1,6097:-1,6107:-1,6105:-1,6099:-1,25231:1000,25232:1000,25233:1000


// setting map-variable
-	script	MV_Eden Market Group	FAKE_NPC,{
OnInit:
OnClock0000:
	switch( gettime(DT_DAYOFWEEK) ) {
	case WEDNESDAY:
		$event_paramk = 0;
		break;
	case FRIDAY:
		$event_paramk = 2;
		break;
	case SATURDAY:
		if (gettime(DT_HOUR) > 21)
			$event_paramk = 4;
		else
			$event_paramk = 3;
		break;
	case THURSDAY:
	default:
		$event_paramk = 1;
		break;
	}
	end;

OnSat2200:
	$event_paramk = 4;
	end;
}

// Eden Market Group Hustler
moc_para01,106,38,4	script	Market Group Hustler#ent	4_M_YOYOROGUE,{
	mes "[骗子]";
	mes "嘿，冒险家。";
	mes "我可以带你去一个好地方。你有兴趣吗？";
	next;
	switch (select("移至 Eden Market Group！：关于 Eden Market Group。：取消。")) {
	case 1:
		mes "[骗子]";
		mes "搬家啦~！";
		close2;
		warp "paramk",97,17;
		end;
	case 2:
		mes "[骗子]";
		mes "哈哈哈……伊甸市场群是商人聚集的地方，出售各种物品。";
		mes "我指的不是像果冻这样的常见东西，你可以在任何地方获得它们。在这里，我们只经营真货。";
		next;
		mes "[骗子]";
		mes "简单的原料、宝石、来自地下城和挖掘地点的稀有物品，以及来自另一个维度的奇异文物……";
		mes "您想要的一切！";
		next;
		select("你的意思是，这是黑市？！");
		mes "[骗子]";
		mes "啊……我不会这么说。";
		mes "这个市场在技术上是合法的......";
		mes "请记住，大多数特殊产品都是^4d4dff数量有限^000000。如果你迟到了，它们可能就卖完了。";
		next;
		mes "[骗子]";
		mes "大多数数量有限的商品不会有折扣。";
		mes "您必须支付全价。";
		next;
		mes "[骗子]";
		mes "冒险家有时会过来出售他们在探险中发现的东西。";
		mes "但你必须亲自看看！";
		next;
		mes "[骗子]";
		mes "请记住，大多数商店每周都会休息一天； ^4d4dff大多数商店周三不营业^000000。";
		mes "不过，并非所有商店都在周三关门。其中大多数仍然开放。";
		next;
		mes "[骗子]";
		mes "周末出售更多特别商品。";
		mes "我什至见过一个" + mesitemlink( 709, false ) + "正在出售~";
		mes "太棒了。";
		mes "当然，它非常昂贵，而且只有少数可以出售。";
		next;
		mes "[骗子]";
		mes "怎么样？感兴趣的？";
		mes "难道不值得一去吗？";
		mes "哈哈哈哈！";
		close;
	case 3:
		mes "[骗子]";
		mes "如果您想去伊甸园市场集团，请随时来！";
		close;
	}
}

// Eden Market Group Guide
paramk,101,22,3	script	Market Group Guide#info	4_F_ZONDAGIRL,{
	mes "[指南]";
	mes "你好。欢迎来到伊甸市场集团，这是由大陆商人联盟运营并得到伊甸集团支持的官方市场。";
	next;
	switch (select("关于我们的营业时间：关于特殊产品。：关于 Eden Market Group。：取消。")) {
	case 1:
		mes "[指南]";
		mes "Eden Market Group 是一个包含许多不同商店的市场。";
		next;
		mes "[指南]";
		mes "周三为市场指定法定节假日；大多数商店在这一天都关门。";
		next;
		mes "[指南]";
		mes "然而，根据业主的意愿以及所提供商品的类型，一些商店即使在假期也仍部分营业。";
		next;
		mes "[指南]";
		mes "而且，一些商店在一周的某些日子提供特殊商品。请记住这一点。";
		close;
	case 2:
		mes "[指南]";
		mes "有些商店会在周末、一周中的某些日子甚至在某些时间出售不同的商品；这些物品与平常的日常物品不同。";
		next;
		mes "[指南]";
		mes "“Wonderful Friends”和“Strong Friends”每个周末都会出售一件稀有物品。";
		mes "“矿工联盟”只会在周末出售用于锻造的铁砧。";
		next;
		mes "[指南]";
		mes "我无法告诉你各个商店卖什么商品，因为它们都不同。";
		mes "您为什么不经常访问亲自了解一下呢？";
		close;
	case 3:
		mes "[指南]";
		mes "有很多具有商人灵魂的人愿意将自己的物品出售给有需要的人。";
		mes "Eden Market Group为这些人指定了一个买卖场所。";
		next;
		mes "[指南]";
		mes "我们与商业联盟和冒险家合作，为原本属于违禁品的商品提供了以公平和透明的方式买卖的手段。";
		next;
		mes "[指南]";
		mes "有人嘀咕Eden Market Group是黑市……哈哈，真是愚蠢的想法……";
		mes "我们只是一个商人联盟。";
		next;
		mes "[指南]";
		mes "Error 500 (Server Error)!!1500.That’s an error.There was an error. Please try again later.That’s all we know.";
		mes "您可以在这里出售冒险中的战利品，我们赚取的利润将用于提供对您有利的服务。我们确实是一石二鸟。";
		next;
		mes "[指南]";
		mes "请记住，数量有限的商品永远不会打折！";
		close;
	case 4:
		mes "[指南]";
		mes "玩的很开心！";
		close;
	}

OnTimer60000:
	npctalk "欢迎。这是伊甸市场集团。";

OnInit:
	initnpctimer;
	end;
}

// Guards
paramk,103,17,3	script	Market Guard#1	4_M_YOUNGKNIGHT,{
	callfunc("F_CoolDown", 5);
	switch (atoi(strnpcinfo(2))) {
	default:
		npctalk "欢迎。";
		break;
	case 3:
		npctalk "欢迎来到伊甸市场集团。";
		break;
	case 4:
		npctalk "你好吗？这是伊甸市场集团。";
		break;
	case 5:
		npctalk "感谢您在 Eden Market Group 购物。";
		break;
	case 6:
		npctalk "欢迎。伊甸市场集团欢迎您。";
		break;
	}
	end;
}

// Guard Duplicates
paramk,92,17,6	duplicate(Market Guard#1)	Market Guard#2	4_M_YOUNGKNIGHT
paramk,53,47,3	duplicate(Market Guard#1)	Market Guard#3	4_M_YOUNGKNIGHT
paramk,142,48,3	duplicate(Market Guard#1)	Market Guard#4	4_M_YOUNGKNIGHT
paramk,142,67,3	duplicate(Market Guard#1)	Market Guard#5	4_M_YOUNGKNIGHT
paramk,53,67,3	duplicate(Market Guard#1)	Market Guard#6	4_M_YOUNGKNIGHT


// Center-North ConversationNPC
// A wise customer
paramk,78,44,3	script	A wise customer#gst01	4_TOWER_14,6,6,{
OnTouch:
	callfunc("F_CoolDown", 4);
	npctalk "我正在等待周末出售的武器。听说如果我来的时候，红色版和蓝色版都会提供。";
	end;
}

// A dining customer
paramk,119,80,3	script	A dining customer#gst02	4_M_CHN8GUEK,6,6,{
OnTouch:
	callfunc("F_CoolDown", 4);
	npctalk "太辣了！我不管这是不是‘家乡的味道’还是别的什么，太辣了！啊啊啊！";
	end;
}

// Customer
paramk,106,61,3	script	Customer#gst03	4_TOWER_04,6,6,{
OnTouch:
	callfunc("F_CoolDown", 4);
	npctalk "当我需要休息的时候我就来这里。我喜欢这个地方。很舒服，周围有很多有趣的东西。";
	end;
}

// Window-shopper
paramk,77,60,3	script	Window-shopper#gst04	4_TOWER_02,6,6,{
OnTouch:
	callfunc("F_CoolDown", 4);
	npctalk "我想知道我是否真的可以整天逛街而不买任何东西......";
	end;
}

// Cassie
paramk,113,56,3	script	Cassie#gst05	1_F_PUBGIRL,6,6,{
	npctalk "欢迎来到Eden Market集团~想要喝点美酒吗？";
	mes "[卡西]";
	mes "您想买一杯饮料吗？";
	next;
	switch (select("热带索格拉特：海滩上的朱红：不。")) {
	case 1:
		.@item = 12112;
		break;
	case 2:
		.@item = 12113;
		break;
	case 3:
		mes "[卡西]";
		mes "玩的很开心。";
		close;
	}
	if (Zeny < 800) {
		mes "[卡西]";
		mes "一杯饮料要800兹。";
		close;
	}
	mes "[卡西]";
	mes "好好享受";
	Zeny -= 800;
	getitem(.@item, 1);
	close;

OnTouch:
	callfunc("F_CoolDown", 4);
	npctalk "欢迎来到伊甸市场集团。您想喝一杯美味的饮料吗？";
	end;
}

// Leria
paramk,74,15,0	script	Leria#coin	4_M_BIBI,{
	if ($event_paramk > 0) {
		mes "[莱里亚]";
		mes "我出售从异次元收集的硬币。";
		mes "购买任何吸引您眼球的东西。";
		mes "请注意：它们相当昂贵。为了获得它们，我不得不经历巨大的麻烦。";
		next;
		if (select("查看正在出售的商品。：取消。") == 1) {
			mes "[莱里亚]";
			mes "我可能没有任何库存了，因为我只卖少量的商品。";
			close2;
			callshop "para_coin10";
			end;
		}
		mes "[莱里亚]";
		mes "由于我走遍世界寻找我出售的物品，所以价格很高......";
		mes "但无论如何还是有人购买它们。";
		next;
		mes "[莱里亚]";
		mes "好吧，那些需要它们的人必须付出代价。不是这样吗？";
		close;
	} else { // holiday
		mes "[莱里亚]";
		mes "啊……今天是市场假期。";
		mes "如果你需要硬币，明天再来。";
		close;
	}
}

// Heimhunter
paramk,71,15,3	script	Heimhunter#hei	4_GEFFEN_11,{
	if ($event_paramk > 0) {
		mes "[海姆亨特]";
		mes "啊，你就是那个想要古堡‘那个’的顾客吗？";
		mes "我每天只能得到一个，但我会卖掉它。";
		next;
		if (select("查看该项目：取消。") == 1) {
			mes "[海姆亨特]";
			mes "只有一个..";
			mes "它可能已经被别人买了。";
			mes "请理解。";
			close2;
			callshop "para_hei10";
			end;
		}
		mes "[海姆亨特]";
		mes "如你所愿。";
		close;
	} else { // holiday
		mes "[海姆亨特]";
		mes "今天是官方市场假期。";
		mes "我也去休息一下";
		close;
	}
}

// Zephet
paramk,68,15,0	script	Zephet#key	4_M_HUMERCHANT,{
	if ($event_paramk > 3) {
		mes "[泽菲特]";
		mes "你需要钥匙吗？";
		mes "我可能有一些额外的钟楼钥匙吗？";
		next;
		if (select("购买钥匙：取消。") == 1) {
			mes "[泽菲特]";
			mes "我自己收集并出售钟楼的钥匙......";
			mes "尽管我整个星期都在寻找它们，但可用的东西并不多。";
			mes "它们可能已经卖完了。";
			close2;
			callshop "para_key10";
			end;
		}
		mes "[泽菲特]";
		mes "你不需要钥匙吗？";
		mes "请记住，我只在周末在这里出售钥匙。";
		mes "仅供您参考。";
		close;
	} else { // holiday
		mes "[泽菲特]";
		mes "我还没有足够的钥匙。";
		mes "周六下午后回来。";
		close;
	}
}


// Strong Friends - Weapon shop
// Sign
paramk,72,33,0	script	[Strong Friends]#wp00	HIDDEN_NPC,{
	mes "==坚强的朋友==";
	mes "从普通武器到价值连城的镶满宝石的剑！";
	mes "你想要的每一件武器都在这里供你细读。";
	close;
}

// Miffy
paramk,65,30,3	script	Miffy#wp01	4_F_LGTGIRL,{
	if ($event_paramk > 1) {
		mes "[米菲]";
		mes "限量系列，仅在火热的周五、周六和周日发售！";
		mes "我们提供冒险家从地下城中取回的各种古代武器！";
		next;
		if (select("查看项目：取消。") == 1) {
			mes "[米菲]";
			mes "让我们向您展示“Strong Friends”特别收藏。";
			mes "如果你打开箱子却发现里面什么也没有";
			mes "这意味着该武器已经被出售。";
			mes "战利品流向反应敏捷的人。";
			close2;
			callshop "para_wp00";
			end;
		}
		mes "[米菲]";
		mes "周末出售特殊武器！";
		mes "随时来看我们！";
		close;
	} else { // Wednesday Holiday
		mes "[米菲]";
		mes "你好。这里是武器店‘Strong Friends’。";
		mes "我们出售冒险家从地下城和战斗中取回的武器，";
		mes "漫游大陆上空。";
		next;
		mes "[米菲]";
		mes "^000000周五周六周日，连续三天限量武器领取！^000000";
		mes "随时来看我们！";
		close;
	}
}

// Nelsha
paramk,70,31,3	script	Nelsha#wp02	4_M_SITDOWN,{
	mes "[内尔莎]";
	mes "你好。";
	mes "这是“Strong Friends”，其中有一系列基本武器可供您细读。";
	next;
	setarray .@shopname$[0], "para_wp01", "para_wp02", "para_wp03", "para_wp04", "para_wp05";
	setarray .@shopdesc$[0], "Maces, Rods and Knuckles", "One-Handed Swords and Two-Handed Sword", "Bows, Innstruments and Whips", "Daggers and Katars", "Axes and Spears";
	.@s = select("狼牙棒、棍棒、指关节：单手剑、双手剑：弓、乐器、鞭子：匕首、卡塔尔石：斧头、长矛：取消") - 1;
	if (.@s == 5) {
		mes "[内尔莎]";
		mes "感谢您的购物。";
		close;
	}
	mes "[内尔莎]";
	mes "我会告诉你" + .@shopdesc$[.@s];
	close2;
	callshop(.@shopname$[.@s], 1);
	end;
}


// Reliable Friends
// Sign
paramk,72,33,3	script	[Reliable Friends]#arm00	HIDDEN_NPC,{
	mes "==可靠的朋友==";
	mes "你需要盾牌、盔甲和坚固的头盔吗？";
	mes "您可以信赖可靠的朋友！";
	close;
}

// Coco
paramk,78,31,3	script	Coco#arm01	4_F_SITDOWN,{
	mes "[可可]";
	mes "你需要坚固的盔甲来保护你脆弱的身体吗？";
	mes "或者盾牌，来阻挡敌人的攻击？";
	next;
	switch (select("查看盔甲。：查看盾牌。：取消。")) {
	case 1:
		mes "[可可]";
		mes "廉价而坚固的盔甲。";
		close2;
		callshop "para_arm01",1;
		end;
	case 2:
		mes "[可可]";
		mes "可能会，也可能不会，阻止以合理力量进行的打击……";
		close2;
		callshop "para_arm02",1;
		end;
	case 3:
		mes "[可可]";
		mes "再来吧。";
		close;
	}
}


// Handsome Friends
// Sign
paramk,84,33,3	script	[Handsome Friends]#cst00	HIDDEN_NPC,{
	mes "==帅气的朋友==";
	mes "专为爱打扮的人打造的服装店！";
	close;
}

// Minho Doh
paramk,90,31,3	script	Minho Doh#cst01	4W_M_01,{
	if ($event_paramk > 2) {
		mes "[珉豪]";
		mes "限量物品收集，仅在周末提供！";
		mes "只有一件物品。想要的话就赶快购买吧！你知道该怎么做。";
		next;
		if (select("查看收藏：取消。") == 1) {
			mes "[珉豪]";
			mes "这是我们的特别收藏！";
			mes "如果所有箱子都是空的，那就意味着你来得太晚了！";
			close2;
			callshop "para_arm10";
			end;
		}
		mes "[珉豪]";
		mes "再见！";
		mes "哈哈哈哈！";
		close;
	} else { // holiday
		mes "[珉豪]";
		mes "您想仔细阅读我们的特别收藏品吗？";
		mes "但如果你每天都能看到它们，这个收藏就不会很特别，对吧？";
		next;
		mes "[珉豪]";
		mes "如果您想参观我们的特别收藏，请周末前来。";
		mes "我们在^000000周六和周日^000000每天销售一件商品。";
		next;
		mes "[珉豪]";
		mes "哈哈哈哈..是不是很期待即将公开的特别单品呢？哈哈哈哈哈哈！";
		close;
	}
}

// Lapelt
paramk,94,32,3	script	Lapelt#hat01	4_M_LGTMAN,{
	mes "[翻领]";
	mes "欢迎！这就是‘帅哥朋友’，一个让你变得时尚的服装店！";
	mes "即使在节假日我们也出售普通的衣服和帽子。";
	next;
	switch (select("衣服和鞋子。：帽子和眼镜。：取消。")) {
	case 1:
		mes "[翻领]";
		mes "不要因为物品便宜而小看它们。";
		mes "它们是来自世界各地的制作精良的物品。";
		close2;
		callshop "para_hat01",1;
		end;
	case 2:
		mes "[翻领]";
		mes "时尚源自搭配配饰！";
		mes "搭配好单品，配上一顶好帽子，你就能站在时尚巅峰！";
		close2;
		callshop "para_hat02",1;
		end;
	case 3:
		mes "[翻领]";
		mes "再见。";
		close;
	}
}


// Meds for Pharmacists
// Sign
paramk,96,33,3	script	[Meds for Pharma]#ptn00	HIDDEN_NPC,{
	mes "== 药剂师药品 ==";
	mes "我们出售各种药水。";
	mes "";
	mes "[运行天数]";
	mes "特殊药水 - 周一、周二、周四、周五、周六";
	mes "常见药水 - 每天";
	close;
}

// Head Pharmacist
paramk,100,30,3	script	Head Pharmacist#ptn01	4_F_SCIENCE,{
	if ($event_paramk > 0) {
		mes "[首席药剂师]";
		mes "喝一口就能昏倒的剧毒，保护身体和精神的药物，暂时赋予超自然速度的药物。";
		mes "我们都有。";
		mes "“药剂师的药物”，在这里为您提供您需要的药物。";
		next;
		if (select("请吃药！：取消。") == 1) {
			mes "[首席药剂师]";
			mes "这是我们目前所剩下的。";
			mes "如果什么都没有，那就说明我们已经卖完了。";
			mes "我们不会大批量酿造药水。请理解。";
			close2;
			callshop "para_ptn00";
			end;
		}
		mes "[首席药剂师]";
		mes "如果您对药物有疑问，请回来。";
		close;
	} else { // holiday
		mes "[首席药剂师]";
		mes "哦，你需要药水吗？";
		mes "我们不出售普通药物。我们坚持使用特殊药水。";
		mes "今天是假期。我们目前正在酿造新药水。";
		next;
		mes "[首席药剂师]";
		mes "如果你明天回来的话，我会给你看我们收集的高级药剂，好吗？";
		mes "呵呵~";
		close;
	}
}

// Johanna
paramk,106,32,3	script	Johanna#ptn02	4_M_BRZ_JACI,{
	mes "[约翰娜]";
	mes "欢迎来到“药剂师用药”。";
	mes "你需要什么？";
	next;
	switch (select("查看物品。：出售物品。：操作天数。：取消。")) {
	case 1:
		mes "[约翰娜]";
		mes "这里有治疗药水。";
		mes "好的药水以实惠的价格出售。";
		close2;
		callshop "para_ptn01",1;
		end;
	case 2:
		mes "[约翰娜]";
		mes "我会把它们从你手中夺走，并付出高昂的代价。";
		mes "不要告诉别人我付出了那么多。";
		close2;
		callshop "para_ptn01",2;
		end;
	case 3:
		mes "[约翰娜]";
		mes "Eden Market Group 的大多数商店在节假日期间不营业。";
		mes "不过，一些商店在节假日仍照常营业。";
		next;
		mes "[约翰娜]";
		mes "即使在节假日，我们的药房也提供普通治疗药水，但不提供特殊药水。";
		mes "利用我们的服务！";
		close;
	case 4:
		mes "[约翰娜]";
		mes "再见。";
		close;
	}
}

// Wyn, independent contractor || Name is too long
paramk,106,27,3	script	Brewer Wyn#ptn03	4_M_ALCHE_A,{
	if ($event_paramk > 0) {
		mes "<<< BR0 >>> 温 <<< BR1 >>>";
		mes "..你好...";
		mes "这是“致药物药剂师”......";
		mes "你需要蓝色药水吗...？";
		mes "我每天制作 100 个……是的……只有我。";
		next;
		if (select("查看项目：取消。") == 1) {
			mes "<<< BR0 >>> 温 <<< BR1 >>>";
			mes "呃..不确定我们还有没有剩下的。";
			mes "请稍等一下。如果没有的话我们就卖完了..";
			mes "啊啊……我还需要做更多吗？";
			mes "啊啊……药草的臭味";
			mes "感觉想吐……啊啊啊……";
			close2;
			callshop "para_ptn10";
			end;
		}
		mes "<<< BR0 >>> 温 <<< BR1 >>>";
		mes "谢谢。。再来吧。";
		close;
	} else { // holiday
		mes "<<< BR0 >>> 温 <<< BR1 >>>";
		mes "呃……头疼……";
		mes "为了还大学学费我开始打工";
		mes "我想要吐。整个星期都被香草的气味包围着。";
		mes "布莱奇——！";
		close;
	}
}


// We-Have-Everything
// sign
paramk,108,33,3	script	[WeHaveEverything]#itm00	HIDDEN_NPC,{
	mes "==我们拥有一切==";
	mes "元素箭袋、陷阱、毒药 DIY 套件。";
	mes "随时可用。";
	mes "";
	mes "[运行天数]";
	mes "周三 - 假期！";
	mes "周五、周六、周日 - 活动销售！";
	close;
}

// Is-this-your-lucky-day Napeed#itm01
paramk,114,30,3	script	Lucky Day Napeed#itm01	4_F_03,{
	if ($event_paramk > 0) {
		mes "[颈背]";
		mes "感谢您在周末促销期间光临。";
		mes "100 个随机物品包，仅在周末出售！仅剩一百张！";
		mes "你有什么想要的吗？";
		next;
		if (select("查看项目：取消。")) {
			mes "[颈背]";
			mes "最受欢迎的选择是“配件盒”、“礼品包装面具”、“旧相册”、“带回家的礼物”......";
			mes "如果您没有看到任何东西，则表示我们已售完。下周末再来吧~";
			close2;
			callshop "para_itm00";
			end;
		}
		mes "[颈背]";
		mes "感谢您经常光临“我们拥有一切”！";
		close;
	} else { // holiday
		mes "[颈背]";
		mes "欢迎欢迎欢迎~！这家商店是“我们拥有一切”。我们拥有的一切，我们都有，我们没有的一切，我们没有！";
		mes "我们目前没有开放。";
		mes "今天是假期，所以我们正在泡药水。";
		close;
	}
}

// Parayo
paramk,118,31,3	script	Manager Parayo#itm02	4_F_KHELLISIA,{
	if ($event_paramk > 0) {
		mes "[帕拉约]";
		mes "感谢您来到“我们拥有一切”。";
		mes "我们出售各种元素箭袋和陷阱。";
		mes "有什么你想要的吗？";
		next;
		switch (select("查看元素箭袋。：查看陷阱。：取消。")) {
		case 1:
			mes "[帕拉约]";
			mes "我们尽力备货，以便为尽可能多的客户提供服务";
			mes "但不幸的是，有时我们会卖完。";
			close2;
			callshop "para_itm10";
			end;
		case 2:
			mes "[帕拉约]";
			mes "这是陷阱。";
			mes "“我们拥有一切”始终为您服务。";
			close2;
			callshop "para_itm01",1;
			end;
		case 3:
			mes "[帕拉约]";
			mes "感谢您成为“我们拥有一切”的忠实客户。";
			mes "再来吧。";
			close;
		}
	} else { // holiday
		mes "[帕拉约]";
		mes "你好，这是“我们拥有一切”。";
		mes "周三是 Eden Market Group 的法定假日，因此我们在此期间不营业。";
		mes "再见。";
		close;
	}
}


// Malangdonya
// sign
paramk,122,33,3	script	[Malangdonya]#seed00	HIDDEN_NPC,{
	mes "== 马兰多尼亚 ==";
	mes "马兰多的特殊物品。";
	mes "Yggdrasil系列和Doram系列就在这里！";
	mes "";
	mes "[运行天数]";
	mes "特别系列 - 周一、周二、周四、周五、周六、周日";
	mes "一直在卖 - 马斯特拉水果。";
	close;
}

// Whiteberry
paramk,126,34,3	script	Whiteberry#seed01	4_CAT_SAILOR1,{
	if ($event_paramk > 0) {
		mes "[白莓]";
		mes "咕噜~欢迎光临。";
		mes "Catpaw 商人集团很高兴欢迎您。我们卖马兰多的特产，咕噜~";
	} else { // holiday
		mes "[白莓]";
		mes "我们有卖玛斯黛拉水果~";
		mes "节假日也有售卖一些商品~";
	}
	next;
	switch (select("查看物品。：出售物品。：取消。")) {
	case 1:
		mes "[白莓]";
		mes "新鲜的马斯黛拉水果来了，咕噜~";
		mes "请多多购买。";
		mes "虽然是我们自己种的，但说实话，它们太酸了……啊……没关系。";
		close2;
		callshop "para_seed01",1;
		end;
	case 2:
		mes "[白莓]";
		mes "咕噜~会以合理的价格购买。";
		mes "请出售有用的物品。";
		close2;
		callshop "para_seed01",2;
		end;
	case 3:
		mes "[白莓]";
		mes "咕噜~再见~";
		close;
	}
}

// Blackphone
paramk,129,33,3	script	Blackphone#seed02	4_M_MERCAT2,{
	if ($event_paramk > 0) {
		mes "[黑手机]";
		mes "喵～欢迎光临";
		mes "喵～马郎多尼亚猫爪商团欢迎您。";
		mes "喵~我们出售马兰多的各种农作物，还有Doram系列。";
		next;
		if (select("查看项目：取消。") == 1) {
			mes "[黑手机]";
			mes "喵～这里有多拉姆罐头和瓶子、伊格德拉希尔和荆棘浆果。";
			mes "喵~如果你什么也没看到，那就说明我们已经卖完了。明天再来。";
			close2;
			callshop "para_seed10";
			end;
		}
		mes "[黑手机]";
		mes "喵～快点回来吧。我们提供有趣的物品。";
		close;
	} else { // holiday
		mes "[黑手机]";
		mes "喵～节假日不营业。";
		mes "喵～今天是我们从马兰多发货的日子。";
		mes "喵~如果你需要瓶装或罐装的Doram，或者Yggdrasil水果，明天再来吧。";
		close;
	}
}


// These comments are spoken continuously when in a specific region
// Restraurant
// Jeffrey
paramk,144,13,3	script	Jeffrey#food00	1_M_MERCHANT,{
	mes "[杰弗里]";
	mes "每个人都认为我是这里的厨师。我不是！我只是来讨餐馆老板欠我的债的！";
	next;
	mes "[杰弗里]";
	mes "我做了几道菜只是为了让抱怨的顾客闭嘴，但这已经失控了！";
	close;

OnTimer30000:
	npctalk "让我说清楚！我不是厨师！不要等待服务！我不会再做任何菜了！";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Vincent
paramk,140,18,3	script	Vincent#food01	4_TOWER_15,{
	mes "[文森特]";
	mes "...新厨师做的食物很棒，但显然他很懒。";
	close;

OnTimer33000:
	npctalk "接受订单，不要只站在厨房里！请！";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Panel
paramk,138,19,3	script	Panel#food02	4_TOWER_14,{
	mes "[面板]";
	mes "这家餐厅最近才开业，但自从他们聘请了新厨师后，它就变得非常受欢迎。";
	close;

OnTimer35000:
	npctalk "谁在乎我是否迟到了。重要的是食物有多美味。";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Matina
paramk,136,20,3	script	Matina#food03	4_TOWER_05,{
	mes "[马蒂娜]";
	mes "它是什么？你要订购吗？忘了它。看看阵容。";
	close;

OnTimer37000:
	npctalk "你为什么不接受命令？这个地方对消费者来说不太友好......";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Lucy
paramk,134,21,3	script	Lucy#food04	4_TOWER_10,{
	mes "[露西]";
	mes "啊。我打算再等一会儿，但如果仍然没有得到服务，我就会离开。";
	close;

OnTimer40000:
	npctalk "这就是极限！我不能再等了。五分钟，我要回家了。";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Mara
paramk,136,16,3	script	Mara#food05	4_TOWER_02,{
	mes "[玛拉]";
	mes "听说这家餐厅的老板去远方旅行了，新来的厨师正在经营这家餐厅。听说他的厨艺很厉害。";
	close;

OnTimer28000:
	npctalk "新来的厨师脾气似乎很不好。我真的觉得这很有吸引力。";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Lana
paramk,133,16,3	script	Lana#food06	4_TOWER_04,{
	mes "[拉娜]";
	mes "我两天前来过，昨天也来过，今天也来过。他们这里提供的面食很棒。唯一的问题是这里很难订。";
	close;

OnTimer25000:
	npctalk "我的订单成功了吗？我在这里快饿死了！";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Rahoul
paramk,127,16,3	script	Rahoul#food07	4_TOWER_12,{
	mes "[拉胡尔]";
	mes "我已经等了几个小时了，厨师仍然不接受订单。啊，我饿得快要死了。";
	close;

OnTimer23000:
	npctalk "呵呵。我想知道他们会让我等多久......";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Nameless Drunk
paramk,124,20,3	script	Drunk#food08	4_TOWER_06,{
	mes "[喝醉了]";
	mes "嘿嘿，多带点酒来……";
	close;

OnTimer50000:
	npctalk "呃……酒……";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Annoyed Customer
paramk,127,10,3	script	Annoyed Customer#food09	4_TOWER_07,{
	mes "[恼怒的顾客]";
	mes "主人旅行回来后我会和他谈谈。告诉他解雇后面的员工。";
	mes "那么多顾客在闲逛，他们甚至不为任何人服务";
	close;

OnTimer20000:
	npctalk "我希望能为员工做点什么。";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Kalin
paramk,124,11,3	script	Kalin#food10	4_F_01,{
	mes "[卡林]";
	mes "我是索兰的同事，我喜欢在这家商店当清洁工。我可以随心所欲地偷懒，但仍然可以获得报酬。";
	close;
}

// Thrann
paramk,121,11,3	script	Thrann#food11	4_F_02,{
	mes "[字符串]";
	mes "我会在这里安静地喝咖啡几个小时，尽管我实际上是在工作，假装在打扫卫生。他们甚至没有把我赶出去。";
	close;

OnTimer47000:
	npctalk "好吵啊！如果他们因为厨房出了问题而不接受订单，那也是可以理解的。";
	initnpctimer;
	end;

OnInit:
	initnpctimer;
	end;
}

// Sobok the Cook
paramk,127,88,3	script	Sobok the Cook#fd01	4_M_CHNCOOK,{
	mes "[索博克]";
	mes "我叫 Sobok，来自 Eden Market Group 的领先餐厅 Sobokru。";
	mes "您想订购吗？每道菜的价格是 3000 Zeny，周末特价是 5000 Zeny。";
	next;
	switch( select("鹅汤。：通心粉面条。：周末特价。：取消。") ) {
	case 1:
		.@hp = 100;
		.@sp = 0;
		.@zeny = 3000;
		.@talk$ = "Goose Soup shall detoxify and heal you.";
		break;
	case 2:
		.@hp = 0;
		.@sp = 100;
		.@zeny = 3000;
		.@talk$ = "Maccaroni Noodles shall refresh your spirit.";
		break;
	case 3:
		.@hp = 100;
		.@sp = 100;
		.@zeny = 5000;
		.@talk$ = "Ultimate combination for body and mind.";
		break;
	case 4:
		mes "[索博克]";
		mes "你什么时候过来就过来吧。";
		close;
	}
	mes "[索博克]";
	mes .@talk$;
	next;
	if (select("是：否") == 2)
		close;
	if (Zeny < .@zeny) {
		mes "[索博克]";
		mes "你以为你可以免费吃饭吗？";
		close;
	}
	mes "[索博克]";
	mes "很好吃，不是吗？";
	Zeny -= .@zeny;
	percentheal(.@hp, .@sp);
	close;
}


// Cafe Mora
// Pomegranate
paramk,75,86,3	script	Pomegranate#mora01	4_F_RAFLE_PK,{
	mes "[石榴]";
	mes "出来吧，快点！";
	mes "请问你这是哪里？莫拉咖啡馆，永远营业。";
	mes "石榴，这个东西那个东西都卖。";
	next;
	switch (select("检查商店：你为什么这样说话？：还有什么吗？")) {
	case 1:
		mes "[石榴]";
		mes "有！莫拉村的闷棍！";
		mes "卖！牛肉吐司。";
		mes "可口的。牛肉吐司。很多。好~";
		mes "石榴！";
		close2;
		callshop "para_mora01",1;
		end;
	case 2:
		mes "[石榴]";
		mes "你不知道，莫拉舌头。";
		mes "石榴。知道。人类言语。";
		mes "石榴，厉害了";
		mes "学习了，做了。超级难！";
		close;
	case 3:
		mes "[石榴]";
		mes "卖。掌握。周五至周六至周日。";
		mes "可口的。压榨的水果。莫拉最棒！";
		close;
	}
}

// Cocktail Master
paramk,65,86,3	script	Cocktail Master#mora01	4_M_RAFLE_VI,{
	if ($event_paramk > 1) {
		mes "[鸡尾酒大师]";
		mes "欢迎来到莫拉咖啡馆。";
		mes "我们提供莫拉的水果和饮料。";
		mes "它们是^4d4dffRafflechino、Mora Hip Tea、Mora Mandarin 和Mora Berry^000000。我们可能会缺货。";
		next;
		if (select("查看项目：取消。") == 1) {
			mes "[鸡尾酒大师]";
			mes "如果什么都没有，那就意味着我们已经卖完了。明天再来。";
			close2;
			callshop "para_mora10";
			end;
		}
		mes "[鸡尾酒大师]";
		mes "我们很感谢您来拜访我们。";
		close;
	} else { // holiday
		mes "[鸡尾酒大师]";
		mes "您想购买特殊的 Mora 饮料吗？";
		mes "我们很遗憾地通知您，我们只在周末生产饮料。请于^4d4dff周五、周六、周日^000000回来。";
		mes "石榴，卖这个卖那个。";
		close;
	}
}


// Forge
// Hans of the miner coalition
paramk,49,103,3	script	Hans #ref01	1_M_SIGNMCNT,{
	if ($event_paramk > 3) {
		mes "[他的]";
		mes "我们出售我们联盟开采的金属矿石。";
		mes "如果您需要铁、钢、煤炭或铁矿石，请四处看看。";
		next;
		if (select("查看矿石：取消。") == 1) {
			mes "[他的]";
			mes "如果这里什么都没有的话，我们已经卖完了。";
			mes "我们每天都开采铁、钢、煤、铁矿石，所以明天再来。";
			close2;
			callshop "para_ref10";
			end;
		}
		mes "[他的]";
		mes "我们的铁、钢、煤炭等基本金属库存每天都会补充。";
		mes "请记住这一点。";
		close;
	} else { // holiday
		mes "[他的]";
		mes "我们今天不营业。";
		mes "我正要去参观矿井。";
		mes "如果你有什么需要的话明天再来吧。";
		close;
	}
}

// Dus the Smith
paramk,53,104,3	script	Dus the Smith#ref02	2_M_OLDBLSMITH,{
	if ($event_paramk > 3) {
		mes "[铁匠达斯]";
		mes "Oridecon、Elunium 和 Anvils 在这里。";
		mes "数量不多，但都是手工制作的，你会看到的。";
		next;
		if (select("查看项目：取消。")) {
			mes "[铁匠达斯]";
			mes "不确定我们是否还有剩余。";
			mes "如果我们这样做，请购买。";
			close2;
			callshop "para_ref20";
			end;
		}
		mes "[铁匠达斯]";
		mes "稀有矿石来之不易，提炼也困难。";
		mes "这就是为什么我们的库存有限。";
		close;
	} else { // holiday
		mes "[铁匠达斯]";
		mes "啊，顾客？";
		mes "抱歉，我们今天不卖矿石。";
		next;
		mes "[铁匠达斯]";
		mes "你看，它们很难制作。";
		mes "你问我们卖什么？";
		mes "啊啊，我们这里出售 Oridecon、Elunium 和 Anvils。";
		next;
		mes "[铁匠达斯]";
		mes "如果您需要稀有矿石或铁砧，请周末来。";
		mes "周六下午晚些时候会是完美的。";
		close;
	}
}


// Rain-of-Gems
// sign
paramk,63,98,3	script	[Rain-of-Gems]#jw	HIDDEN_NPC,{
	mes "？宝石雨？";
	mes "我们经营黄金和宝石！";
	mes "";
	mes "[运行天数]";
	mes "节假日我们不营业。";
	close;
}

// Aurora
paramk,69,102,3	script	Aurora#jew01	4_F_02,{
	if ($event_paramk > 0) {
		mes "[极光]";
		mes "欢迎光临~";
		mes "这就是《宝石雨》。我们的产品范围从纯金到玫瑰石等未切割的宝石！";
		mes "我们出售黄金、蛋白石、金云母、白云母等。";
		mes "它们非常稀有且难以获得，因此价格昂贵！";
		next;
		switch (select("查看物品。：出售物品。：取消。")) {
		case 1:
			mes "[极光]";
			mes "如果你没有看到任何东西，那就意味着所有东西都卖完了！";
			mes "谢谢您的来访~！";
			close2;
			callshop "para_jew10";
			end;
		case 2:
			mes "[极光]";
			mes "我们将以合理的价格购买。";
			close2;
			callshop "para_fod01",2;
			end;
		case 3:
			mes "[极光]";
			mes "我们经营黄金、金云母、玛瑙和绿松石等产品。";
			mes "但未切割的宝石很少见。";
			next;
			mes "[极光]";
			mes "我们从矿山采购，但是供应有限~";
			mes "我们的库存每周都会补充，所以如果你的时间恰到好处……也许可以？哈哈。";
			close;
		}
	} else { // holiday
		mes "[极光]";
		mes "哦，抱歉你今天过来了。";
		mes "我们的市场组每周三不营业。";
		mes "我们需要时间补货和检查库存水平，当然也需要时间休息。";
		next;
		mes "[极光]";
		mes "如果您明天来，我们很乐意为您提供我们的一系列产品，让您的武器变得更加强大！";
		close;
	}
}


// The Alchemical Cat
// sign
paramk,73,98,3	script	[Alchemical Cat]#alc00	HIDDEN_NPC,{
	mes "==炼金猫==";
	mes "炼金术试剂和";
	mes "药物成分";
	mes "都在这里卖~";
	mes "";
	mes "[运行天数]";
	mes "除节假日外每天";
	close;
}

// Dye the Alchemist
paramk,75,102,3	script	Dye the Alchemist#alc01	4_M_ALCHE_C,{
	if ($event_paramk > 0) {
		mes "[为炼金术士染色]";
		mes "炼金术的试剂！";
		mes "我们出售酒精、Karvodailnirol 和 Detrimindexta！";
		mes "我们每天卖 20 个！";
		next;
		if (select("查看试剂：取消。")) {
			mes "[为炼金术士染色]";
			mes "不知道还有没有剩下的！";
			mes "如果没有的话，所有的东西都卖完了！";
			mes "它们可能有点贵，但很容易消费……好吧，你知道我在说什么。是的？";
			close2;
			callshop "para_alc10";
			end;
		}
		mes "[为炼金术士染色]";
		mes "明天过来，我们每天都在酿造试剂！";
		close;
	} else { // holiday
		mes "[为炼金术士染色]";
		mes "我们每周三休息哦~";
		mes "明天再来吧！";
		close;
	}
}

// Earl the Alchemist
paramk,80,104,3	script	Earl the Alchemist#alc02	4_TOWER_16,{
	if ($event_paramk > 0) {
		mes "[炼金术士伯爵]";
		mes "制作试剂、药剂的原料，";
		mes "还有用于魔法实验的元素石，都在这里出售！";
		next;
		mes "[炼金术士伯爵]";
		mes "不确定我们的库存还有多少，因为我们的供应有限，但是......";
		mes "在找东西吗？";
		next;
		switch (select("看成分。：看元素石。：取消。")) {
		case 1:
			mes "[炼金术士伯爵]";
			mes "不朽之心、茎和来自各个地下城的许多其他成分";
			mes "这里以合理的价格提供";
			next;
			mes "[炼金术士伯爵]";
			mes "如果你看不到任何东西，那就意味着所有东西都卖完了。";
			close2;
			callshop "para_alc20";
			end;
		case 2:
			mes "[炼金术士伯爵]";
			mes "星尘和星屑在这里。";
			mes "不确定我们是否还有剩余。";
			close2;
			callshop "para_alc30";
			end;
		case 3:
			mes "[炼金术士伯爵]";
			mes "再来吧！";
			mes "请记住，我们周三不营业，因为那是我们发货的时间！";
			close;
		}
	} else { // holiday
		mes "[炼金术士伯爵]";
		mes "星期三是假期。";
		mes "如果你有什么想买的，明天再来。";
		close;
	}
}


// Manbok Groceries
// sign
paramk,83,98,3	script	[Manbok Groceries]#fod00	HIDDEN_NPC,{
	mes "== 曼博克杂货 ==";
	mes "我们提供令人惊叹的烹饪原料！";
	mes "狼血和娇小的尾巴！";
	mes "";
	mes "[运行天数]";
	mes "除节假日外每天";
	close;
}

// Manbok
paramk,87,101,3	script	Manbok#fod01	1_M_03,{
	if ($event_paramk > 0) {
		mes "[曼博克]";
		mes "欢迎。";
		mes "如果您需要烹饪原料，那么您来对地方了。";
		next;
		switch (select("高级烹饪原料。：酱汁和间谍。：出售物品。：取消。")) {
		case 1:
			mes "[曼博克]";
			mes "不知道我们还剩下多少。";
			mes "好吧，^4d4dff如果你什么也没看到，那就意味着所有东西都卖完了^000000。";
			mes "请随意阅读。";
			close2;
			callshop "para_fod10";
			end;
		case 2:
			mes "[曼博克]";
			mes "来自世界各地的酱汁和香料..";
			mes "如果你想要烹饪原料，可以找我。";
			close2;
			callshop "para_fod01",1;
			end;
		case 3:
			mes "[曼博克]";
			mes "如果是我经营的产品，我会以合理的价格向你购买。";
			close2;
			callshop "para_fod01",2;
			end;
		case 4:
			mes "[曼博克]";
			mes "你可能已经知道了，但我还是要说。";
			mes "我们的供应有限。如果您犹豫太久，我们的食材经常会卖完。";
			next;
			mes "[曼博克]";
			mes "只是说。";
			close;
		}
	} else { // holiday
		mes "[曼博克]";
		mes "周三不营业。";
		mes "我们今天补货了。明天再来。";
		close;
	}
}


// Magical Store, Ma-Ma-Ma-Ma
// sign
paramk,93,98,3	script	Ma-Ma-Ma-Ma#mag	HIDDEN_NPC,{
	mes "== 在-on-on-in ==";
	mes "== 魔法商店==";
	mes "魔法的原料和宝石，";
	mes "专门研究魔法卷轴";
	mes "";
	mes "[运行天数]";
	mes "部分商品节假日不售卖";
	mes "但每天都营业。";
	close;
}

// Madoha
paramk,95,102,3	script	Madoha#mag01	4_M_SAGE_A,{
	if ($event_paramk > 0) {
		mes "[小圆]";
		mes "欢迎。";
		mes "这是Ma-Ma-Ma-Ma魔法商店，由Ma兄弟经营，帮助非魔法师使用魔法。";
		next;
		mes "[小圆]";
		mes "我们出售魔法卷轴和宝石。";
		mes "您正在寻找什么特别的东西吗？";
		next;
		if (select("查看项目：取消。") == 1) {
			mes "[小圆]";
			mes "蓝色、红色、黄色宝石。流星风暴、暴风雨、朱红之主等魔法卷轴……";
			mes "^4d4dff如果没有任何内容，则表示全部售完^000000，请注意。";
			close2;
			callshop "para_mag10";
			end;
		}
		mes "[小圆]";
		mes "制作魔法卷轴是一项有趣的工作。";
		mes "魔法卷轴还可以帮助许多非法师冒险家，让他们使用他们不知道的魔法。";
		next;
		mes "[小圆]";
		mes "但我每周可以抄写的卷轴数量是有限的，所以有时会用完。";
		close;
	} else { // holiday
		mes "[小圆]";
		mes "哦，顾客？";
		mes "今天是假期。今天我们只出售基本的魔法成分。没有卷轴。可以吗？";
		mes "我们有蓝色宝石、积分和符石。";
		next;
		if (select("查看项目：取消。") == 1) {
			mes "[小圆]";
			mes "我们尝试尽可能多地备货，但可能已经用完了。";
			mes "如果你什么也没看到，那就意味着所有东西都卖完了。";
			close2;
			callshop "para_mag11";
			end;
		}
		mes "[小圆]";
		mes "其他日子出售黄色和红色宝石以及魔法卷轴，";
		mes "所以明天再来吧。";
		close;
	}
}

// Magiha
paramk,100,102,3	script	Magiha#mag02	4_M_SAGE_C,{
	if ($event_paramk > 0) {
		mes "[魔法师]";
		mes "也欢迎哦哦哦。";
		mes "妈妈、妈妈、妈妈、妈妈，神奇商店。";
		mes "我们出售抵抗药水来提高您的元素抵抗力。";
		next;
		if (select("查看项目：取消。") == 1) {
			mes "[魔法师]";
			mes "我们酿造并销售四种抵抗水、风、土或火的抵抗药水。";
			mes "如果你什么也没看到，那就意味着所有药水都卖完了。";
			close2;
			callshop "para_mag20";
			end;
		}
		mes "[魔法师]";
		mes "再来吧！";
		close;
	} else { // holiday
		mes "[魔法师]";
		mes "今天是假期！我今天没有酿造任何药剂...";
		mes "明天再来。";
		close;
	}
}

// Blessings of Yggdrasil
// sign
paramk,103,98,3	script	[Yggdrasil Blessing]#00	HIDDEN_NPC,{
	mes "==世界树的祝福==";
	mes "即将搬入";
	close;
}


// Exotic Plants
// sign
paramk,113,98,3	script	[Exotic Plants]#plt	HIDDEN_NPC,{
	mes "== 外来植物 ==";
	mes "有毒植物出售。";
	mes "运营天数：始终";
	close;
}

// Hwaran
paramk,116,100,3	script	Hwaran#plt01	4_F_RUSGREEN,{
	mes "[花兰]";
	mes "欢迎来到外来植物。";
	mes "我接触的植物在咀嚼时会非常苦。";
	mes "你会四处看看吗？";
	next;
	if (select("查看项目：取消。") == 1) {
		mes "[花兰]";
		mes "尽管它们是有毒植物，但请给予它们关爱。";
		mes "这些婴儿是我亲自从农场挑选的。制成药物后非常有效。";
		close2;
		callshop "para_plt01";
		end;
	}
	mes "[花兰]";
	mes "我店里的这么多常客都是来自刺客公会，这真是太令人惊讶了。我不知道为什么。";
	mes "显然他们内心一定是热爱和平的人，因为他们如此喜爱植物。";
	close;
}

// You and Me
// sign
paramk,133,98,3	script	[You and Me]#unm00	HIDDEN_NPC,{
	mes "==你和我==";
	mes "专注于可爱宠物用品";
	mes "我们向您介绍可以骑乘的野兽以及可以一起狩猎的伙伴";
	mes "[运行天数]";
	mes "总是！";
	close;
}

// Gecko the Trainer
paramk,124,97,3	script	Gecko the Trainer#unm01	4_M_DESERT,{
	mes "[训练师壁虎]";
	switch( Class ) {
	case Job_Rune_Knight:
	case Job_Rune_Knight_T:
	case Job_Baby_Rune_Knight:
		mes "欢迎。你想租一条龙吗？";
		next;
		if (select("是：否") == 2) {
			mes "[训练师壁虎]";
			mes "我懂了。然后祝你有美好的一天。";
			close;
		}
		// if (!getskilllv("RK_DRAGONTRAINING")) {
		if (!getskilllv("KN_RIDING")) {
			mes "[训练师壁虎]";
			mes "请先学习如何骑龙。";
			close;
		} else if (checkriding() || checkdragon()) {
			mes "[训练师壁虎]";
			mes "你已经拥有一条龙了。";
			close;
		} else if (ismounting()) {
			mes "[训练师壁虎]";
			mes "请移除您的现金。";
			close;
		}
		setdragon;
		close;

	case Job_Royal_Guard:
	case Job_Royal_Guard_T:
	case Job_Baby_Royal_Guard:
		mes "欢迎。您想租一辆狮鹫吗？";
		next;
		if (select("是：否") == 2) {
			mes "[训练师壁虎]";
			mes "我懂了。然后祝你有美好的一天。";
			close;
		}
		if (!getskilllv("KN_RIDING")) {
			mes "[训练师壁虎]";
			mes "请先学习如何骑狮鹫。";
			close;
		}
		if (checkriding()) {
			mes "[训练师壁虎]";
			mes "你已经拥有了一只狮鹫。";
			close;
		}
		if (ismounting()) {
			mes "[训练师壁虎]";
			mes "请移除您的现金。";
			close;
		}
		setriding;
		close;

	default:
		mes "我来这里是为了给符文骑士和皇家卫队提供骑乘生物。";
		close;
	}
}

// Tete
paramk,126,100,3	script	Tamer Tete#unm02	4_M_DST_CHILD,{
	mes "[泰特]";
	mes "“你和我”，我们爱动物！";
	mes "我们 24/7 全天候营业，为您提供可爱宠物和伴侣动物所需的所有物品！";
	mes "您需要为您的宠物准备物品吗？";
	next;
	if (select("查看项目：取消。") == 1) {
		mes "[泰特]";
		mes "我们有多种宠物食品可供选择。";
		mes "我们也有孵化器！";
		close2;
		callshop "para_unm01",1;
		end;
	}
	mes "[泰特]";
	mes "谢谢你的光临~！";
	close;
}

// Mist the Hunter
paramk,130,98,3	script	Mist the Hunter#unm03	4_M_JOB_HUNTER,{
	if (Upper == 0) .@price = 2500; // Normal Falcon - default price
	if (Upper == 1) .@price = 2500; // Scarf Falcon
	if (Upper == 2) .@price = 2500; // Baby Falcon
	mes "[猎人迷雾]";
	if (BaseJob == Job_Hunter) {
		if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
			mes "一...";
			mes "你不能租猎鹰";
			mes "直到你先解散你的座狼！";
			close;
		}
		mes "你需要猎鹰吗？";
		mes "你可以租自己的";
		mes "值得信赖的猛禽";
		mes "费用只需" + .@price + " zeny~";
		next;
		switch (select("租用猎鹰：购买猎鹰笛子：取消。")) {
		case 1:
			if (checkwug()) {
				mes "[猎人迷雾]";
				mes "一...";
				mes "你不能租猎鹰";
				mes "直到你先解散你的座狼！";
				close;
			}
			if (Zeny < .@price) {
				mes "[猎人迷雾]";
				mes "这是什么？";
				mes "你没有";
				mes "足够的禅尼吗？！";
				mes "你最好开始";
				mes "猎钱";
				mes "而不是怪物~";
				close;
			}
			if (getskilllv("HT_FALCON") == 0) {
				mes "[猎人迷雾]";
				mes "天哪~";
				mes "去学习如何";
				mes "管理猎鹰";
				mes "第一的！我租不到一个";
				mes "如果你不能的话";
				mes "处理它，你知道的。";
				close;
			}
			if (checkfalcon()) {
				mes "[猎人迷雾]";
				mes "一...";
				mes "你已经有了";
				mes "一只猎鹰。是的";
				mes "在那里，你看不到吗？";
				close;
			}
			Zeny -= .@price;
			setfalcon;
			close;
		case 2:
			mes "[猎人迷雾]";
			mes "有了神奇的长笛，您就可以从任何地方召唤您的猎鹰！";
			next;
			mes "[猎人迷雾]";
			mes "太棒了，不是吗？哈哈哈！";
			next;
			mes "[猎人迷雾]";
			mes "我为什么要卖这些猎鹰笛子？";
			mes "这是因为我不知道它们是如何工作的。";
			mes "哈哈哈！";
			next;
			mes "[猎人迷雾]";
			mes "当然，你需要技能^ff0000猎鹰掌握^000000。";
			mes "我没有技能，所以不知道如何对付猎鹰。";
			mes "就目前而言，我可能永远不会拥有猎鹰。";
			next;
			mes "[猎人迷雾]";
			mes "但我不能免费给你一个！";
			mes "我还要赚钱，因为我也要吃饭！";
			next;
			mes "[猎人迷雾]";
			mes "如果你需要猎鹰笛子，我会以 12,500 泽尼的价格出售。还是那太贵了？";
			next;
			if (eaclass()&EAJL_THIRD) {
				mes "[猎人迷雾]";
				mes "不过，对于流浪者队，我有 10,000 泽尼的特价！";
				.@price = 10000;
				next;
			} else
				.@price = 12500;
			if (select("不要买：买一支猎鹰笛子。") == 1) {
				mes "[猎人迷雾]";
				mes "那太糟糕了。如果你需要的话，就回来吧！";
				close;
			}
			if (Zeny >= .@price) {
				if (countitem(12848) >= 1) { // Falcon_Flute
					mes "[猎人迷雾]";
					mes "正如我所看到的，你已经拥有了猎鹰长笛！";
					mes "别担心，我的年轻朋友，一旦您购买了猎鹰笛，您就不需要再购买了";
					mes "既然隼笛不会因为使用而消失，那不是很好吗？";
					close;
				}
				mes "[猎人迷雾]";
				mes "来，拿着这个笛子。";
				mes "有了它，您可以从任何地方为您的猎鹰吹口哨。";
				Zeny -= .@price;
				getitem(12848, 1); // Falcon_Flute
				next;
				mes "[猎人迷雾]";
				mes "只是一个提醒。";
				mes "您需要技能^ff0000猎鹰掌握^000000才能使用它！";
				close;
			}
			mes "[猎人迷雾]";
			mes "抱歉，您没有足够的钱购买隼笛，请等您有钱后再回来！";
			close;

		case 3:
			mes "[迷雾猎人]";
			mes "查看我们商店中的其他商品。";
			mes "我们可能很快就会推出新的宠物用品~";
			mes "哈哈哈哈！";
			close;
		}
	} else {
		mes "年轻的傻瓜！";
		mes "猎鹰只能";
		mes "被猎人使用";
		mes "还有狙击手，卡皮什？";
		mes "……呵呵，吃醋了？";
		close;
	}
}

// exit
paramk,97,5,0	warp	paramk#ex	1,1,moc_para01,106,35
