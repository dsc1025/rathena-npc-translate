//===== rAthena Script =======================================
//= Cor Operation
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.1 Securing Elyumina
//= Episode 17.1 EL1-A17T Suppression (Cor Memorial)
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//===== Translated By: =======================================
//= dsc
//============================================================

1@cor,1,1,0	script	#171_cor_control	-1,{
	end;

OnInstanceInit:
	'map$ = instance_mapname("1@cor");
	'step = 0;
	setcell 'map$,159,216,159,225,CELL_WALKABLE,0;
	setcell 'map$,159,216,159,225,CELL_SHOOTABLE,0;
	setcell 'map$,98,216,98,225,CELL_WALKABLE,0;
	setcell 'map$,98,216,98,225,CELL_SHOOTABLE,0;
	setcell 'map$,132,240,141,240,CELL_WALKABLE,0;
	setcell 'map$,132,240,141,240,CELL_SHOOTABLE,0;

	//= Story
	disablenpc instance_npcname("Reinforced Energy#171_box_0");
	disablenpc instance_npcname("Biological Battery#171_box_0");
	disablenpc instance_npcname("Chemical Poison#171_box_1");
	disablenpc instance_npcname("Rebellion#171_box_4");
	disablenpc instance_npcname("Elena Volkova#171_cmd_1");
	disablenpc instance_npcname("Elena Volkova#171_cmd_2");
	disablenpc instance_npcname("Elyumina#171_cor_end");
	disablenpc instance_npcname("Rebellion#171_cmd_6");
	disablenpc instance_npcname("Rebellion#171_cmd_7");
	disablenpc instance_npcname("#171_cor_warp_0");
	disablenpc instance_npcname("#171_cor_warp_1");
	for (.@i = 0; .@i < 6; .@i++)
		disablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
	for (.@i = 0; .@i < 4; .@i++) {
		disablenpc instance_npcname("Rebellion#171_box_" + .@i);
		disablenpc instance_npcname("Box#171_box_" + .@i);
	}

	//= Daily
	for (.@i = 0; .@i < 4; .@i++) {
		disablenpc instance_npcname("Elyumina#171_box_d" + .@i);
		disablenpc instance_npcname("Box#171_box_d" + .@i);
	}
	disablenpc instance_npcname("Biological Battery#171_box_trap_0");
	disablenpc instance_npcname("Chemical Poison#171_box_trap_1");
	disablenpc instance_npcname("Biological Battery#171_box_trap_2");
	disablenpc instance_npcname("Chemical Poison#171_box_trap_3");
	disablenpc instance_npcname("Reinforced Energy#171_trap_0");
	disablenpc instance_npcname("Reinforced Energy#171_trap_2");
	disablenpc instance_npcname("Elyumina#171_cmd_0");
	disablenpc instance_npcname("Elyumina#171_cmd_1");

	//= Boss Stage
	for (.@i = 0; .@i < 11; .@i++) {
		disablenpc instance_npcname("Biological Battery#171_cor_" + .@i);
		disablenpc instance_npcname("Chemical Poison#171_cor_" + .@i);
	}
	disablenpc instance_npcname("Reinforced Energy#171_cor_0");
	end;

OnStory01:
	disablenpc instance_npcname("Elena Volkova#171_cmd_0");
	disablenpc instance_npcname("Elyumina#171_cmd_0");
	for (.@i = 0; .@i < 6; .@i++)
		disablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
	for (.@i = 0; .@i < 4; .@i++) {
		enablenpc instance_npcname("Rebellion#171_box_" + .@i);
		enablenpc instance_npcname("Box#171_box_" + .@i);
	}
	enablenpc instance_npcname("Rebellion#171_box_4");
	end;

OnStory02:
	for (.@i = 0; .@i < 4; .@i++) {
		disablenpc instance_npcname("Rebellion#171_box_" + .@i);
		disablenpc instance_npcname("Box#171_box_" + .@i);
	}
	disablenpc instance_npcname("Rebellion#171_box_4");
	enablenpc instance_npcname("Elena Volkova#171_cmd_1");
	mapannounce 'map$,"Elena Volkova : Awesome, she came out! I've sent the signal, come to my location!",bc_map,0xFFFF99;
	viewpointmap 'map$,1,172,223,5,0xFFFFFF;
	viewpointmap 'map$,2,140,79,1,0xFFFFFF;
	viewpointmap 'map$,2,160,119,2,0xFFFFFF;
	viewpointmap 'map$,2,220,170,3,0xFFFFFF;
	viewpointmap 'map$,2,222,236,4,0xFFFFFF;
	end;

OnStory03:
	disablenpc instance_npcname("Elena Volkova#171_cmd_1");
	donpcevent instance_npcname("#171_cor_warp_0") + "::OnActive";
	end;

OnDaily01:
	disablenpc instance_npcname("Elena Volkova#171_cmd_0");
	disablenpc instance_npcname("Elyumina#171_cmd_0");
	for (.@i = 0; .@i < 4; .@i++) {
		enablenpc instance_npcname("Elyumina#171_box_d" + .@i);
		enablenpc instance_npcname("Box#171_box_d" + .@i);
	}
	end;

OnDaily02:
	viewpointmap 'map$,1,172,223,5,0xFFFFFF;
	mapannounce 'map$,"Elena Volkova : Once you've finish all of it, come to the barracks and Elyumina will guide you!",bc_map,0xFFFF99;
	enablenpc instance_npcname("Elyumina#171_cmd_1");
	end;

OnDaily03:
	disablenpc instance_npcname("Elyumina#171_cmd_1");
	donpcevent instance_npcname("#171_cor_warp_1") + "::OnActive";
	end;
}

1@cor,177,169,0	script	#171_cor_ev	HIDDEN_WARP_NPC,4,4,{
	end;

OnTouch:
	if (!is_party_leader())
		end;
	if (isbegin_quest(16360) == 2)
		'mode = 1;
	disablenpc();
	end;
}

1@cor,180,169,3	script	Elena Volkova#171_cmd_0	4_F_ELENA,5,5,{
	if (!'mode) {
		cutin "162elena_01",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "我将开始解释操作。首先...";
		specialeffect EF_TETRA_GROUND;
		next;
		for (.@i = 0; .@i < 6; .@i++)
			enablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
		next;
		mes "[埃琳娜・沃尔科娃]";
		mes "我将开始解释操作。首先...";
		mes "......现在你可以撒野了。";
		next;
		cutin "",255;
		mes "[叛乱]";
		mes "那边好像有什么东西爆炸了。火焰从前方升起！";
		next;
		mes "[叛乱]";
		mes "这些爆炸有 4 个已确定的位置！我已经^FF0000在你的小地图上显示了它们。^000000";
		viewpointmap 'map$,1,140,79,1,0xFFFFFF;
		viewpointmap 'map$,1,160,119,2,0xFFFFFF;
		viewpointmap 'map$,1,220,170,3,0xFFFFFF;
		viewpointmap 'map$,1,222,236,4,0xFFFFFF;
		next;
		mes "[埃琳娜・沃尔科娃]";
		mes "虽然很突然，但是我正在改变操作。";
		mes "不要担心那些已经在前线的人，他们会适应任何情况。";
		next;
		mes "[埃琳娜・沃尔科娃]";
		mes "分成4个小队，每个人在自己的位置，开始压制领先者。如果任何团队找到 Elyumina，请立即通知我们。";
		next;
		mes "[埃琳娜・沃尔科娃]";
		mes "大家各就各位！开始行动吧！";
		npctalk "是的！",instance_npcname("叛乱#171_cmd_0");
		npctalk "……",instance_npcname("叛乱#171_cmd_1");
		npctalk "是的！",instance_npcname("叛乱#171_cmd_2");
		npctalk "是的！",instance_npcname("叛乱#171_cmd_3");
		npctalk "是的！",instance_npcname("叛乱#171_cmd_4");
		npctalk "是的！",instance_npcname("叛乱#171_cmd_5");
		next;
		cutin "162elena_02",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "请冒险者单独行动。请检查您的^0000CD小地图^000000以尽快组织情况。如果他们需要支持，请加入团队！";
		close2;
		cutin "",255;
		if ('step == 0) {
			'step = 1;
			donpcevent instance_npcname("#171_cor_control") + "::OnStory01";
		}
	} else {
		enablenpc instance_npcname("Elyumina#171_cmd_0");
		.@elyumina$ = instance_npcname("Elyumina#171_cmd_0");
		cutin "162elena_01",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "简报...这里的罪犯将是做这件事的人。";
		mes "照顾罪犯是很困难的。";
		npctalk "埃琳娜・沃尔科娃：简报……这里的罪犯将是做这件事的人。照顾罪犯是很困难的。";
		next;
		cutin "ep171_elyumina04",0;
		mes "[艾柳米娜]";
		mes "哈哈！这不是你的工作吗？正确地做。你为什么把简报留给我？";
		npctalk "艾柳米娜：哈哈！这不是你的工作吗？正确地做。你为什么把简报留给我？",.@elyumina$;
		next;
		cutin "162elena_01",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "太吵了。在我们开始之前。您现在是 USU 的一员吗？";
		npctalk "埃琳娜・沃尔科娃：太吵了。在我们开始之前。您现在是 USU 的一员吗？";
		next;
		cutin "ep171_elyumina03",0;
		mes "[艾柳米娜]";
		mes "那、他就是这么说的……你是怕我害怕吗！";
		mes "呵呵，我来解释一下，因为我要照顾我的孩子！";
		npctalk "Elyumina：呵呵，我来解释一下，因为我想照顾我的孩子！",.@elyumina$;
		next;
		cutin "ep171_elyumina01",0;
		mes "[艾柳米娜]";
		mes "……好吧，时间差不多了。";
		mes "我的孩子们将开始在<TIPBOX>[四个地方]<INFO>8086</INFO></TIPBOX>设置陷阱。";
		npctalk "Elyumina：……好吧，时间差不多了。我的孩子们将开始在四个地方设置陷阱。",.@elyumina$;
		next;
		mes "[艾柳米娜]";
		mes "来，拿地图。这些信息也将传递给党员。";
		mes "你并不孤单吧？你知道我的孩子们很强大吗？";
		npctalk "Elyumina：给你，拿着地图。这些信息也将传递给党员。",.@elyumina$;
		viewpointmap 'map$,1,140,79,1,0xFFFFFF;
		viewpointmap 'map$,1,160,119,2,0xFFFFFF;
		viewpointmap 'map$,1,220,170,3,0xFFFFFF;
		viewpointmap 'map$,1,222,236,4,0xFFFFFF;
		next;
		cutin "ep171_elyumina04",0;
		mes "[艾柳米娜]";
		mes "继续前进并触摸陷阱。陷阱将会启动，我可爱的孩子们就会出现。";
		mes "你还记得陷阱是如何运作的，对吧？";
		npctalk "Elyumina：去触碰陷阱吧。陷阱将会启动，我可爱的孩子们就会出现。",.@elyumina$;
		next;
		cutin "ep171_elyumina03",0;
		mes "[艾柳米娜]";
		mes "当你清除了四个陷阱后，EL1_A17T就会出来，这位女士会带你去和上次一样的地方。";
		npctalk "Elyumina：当你清除了四个陷阱后，EL1_A17T就会出来，这位女士会带你去和上次一样的地方。",.@elyumina$;
		next;
		cutin "ep171_elyumina04",0;
		mes "[艾柳米娜]";
		mes "这就是解释的目的。现在，开始工作吧！肌肉白痴！啊哈哈哈！";
		npctalk "Elyumina：解释就是这样。现在，开始工作吧！肌肉白痴！啊哈哈哈！",.@elyumina$;
		emotion ET_SMILE,getnpcid(0,.@elyumina$);
		next;
		cutin "162elena_01",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "...无论如何，我稍后会与这个罪犯交谈一下。我指望你们了，冒险者们！";
		npctalk "Elena Volkova：……我稍后会和这个罪犯谈谈……无论如何。我指望你们了，冒险者们！";
		close2;
		cutin "",255;
		if ('step == 0) {
			'step = 1;
			donpcevent instance_npcname("#171_cor_control") + "::OnDaily01";
		}
	}
	end;

OnTouch:
	npctalk "埃琳娜・沃尔科娃：这边走。等大家都到齐了之后我们再简单介绍一下。";
	end;
}

1@cor,178,172,3	duplicate(dummy_npc)	Elyumina#171_cmd_0	4_EP17_ELYUMINA

1@cor,172,223,3	script	Elyumina#171_cmd_1	4_EP17_ELYUMINA,{
	cutin "ep171_elyumina03",0;
	mes "[艾柳米娜]";
	mes "速度太快了……但预计我的孩子不会获胜。";
	mes "上面还有很多麻烦事~";
	npctalk "Elyumina：那很快……但预计我的孩子不会获胜。上面还有很多麻烦事~";
	next;
	cutin "ep171_elyumina01",0;
	mes "[艾柳米娜]";
	mes "...您要直接购买我的 EL1-A17T 吗？";
	mapannounce 'map$,"Elena Volkova : Yeah, damn criminal! Why don't you open the portal right now?",bc_map,0xFFFF99;
	npctalk "Elyumina：...你直接去我的 EL1_A17T 吗？";
	next;
	if (select("进去：先别进去。") == 2) {
		cutin "ep171_elyumina02",0;
		mes "[艾柳米娜]";
		mes "来吧，我正想打开它。";
		close3;
	}
	cutin "ep171_elyumina04",0;
	mes "[艾柳米娜]";
	mes "哈哈哈！是的。让我们努力吧。为我收集更多的战斗数据！";
	npctalk "艾柳米娜：哈哈哈！是的。让我们努力吧。为我收集更多的战斗数据！";
	close2;
	cutin "",255;
	viewpointmap 'map$,2,172,223,5,0xFFFFFF;
	if ('step == 1) {
		'step = 2;
		donpcevent instance_npcname("#171_cor_control") + "::OnDaily03";
	}
	end;
}

1@cor,177,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_0	4_M_REBELLION3
1@cor,180,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_1	4_M_GONY
1@cor,183,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_2	4_F_REBELLION3
1@cor,177,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_3	4_F_ANYA
1@cor,180,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_4	4_M_ILYA
1@cor,183,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_5	4_F_REBELLION2

1@cor,224,238,3	script	Rebellion#171_box_0	4_M_REBELLION3,{
	if (isbegin_quest(16355) == 0) {
		mes "[叛乱]";
		mes "有一个可疑的盒子！里面传来信号，正在解密！";
		close;
	}
	.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
	if (isbegin_quest(16355) == 1 && mobcount('map$,.@event$)) {
		mes "[叛乱]";
		mes "摧毁生物电池并杀死怪物！";
		close;
	}
	if (isbegin_quest(16355) == 1 && !mobcount('map$,.@event$)) {
OnTalk:
		mes "[叛乱]";
		mes "抱歉分析晚了。它不是召唤怪物的装置。";
		next;
		mes "[叛乱]";
		mes "召唤怪物后，会召唤生物电池来强化怪物并爆炸。";
		next;
		mes "[叛乱]";
		mes "如果你想处理它，就摧毁它吧。这很好，因为它是一个简单的解决方案。";
		next;
		mes "[叛乱]";
		mes "嗯，看来这件事已经结束了......";
		next;
		mes "[叛乱]";
		mes "看来你在这里发现了一些有趣的东西。你应该看看其他球队！";
		close2;
		completequest 16355;
		disablenpc();
		disablenpc instance_npcname("Box#" + strnpcinfo(2));
		viewpointmap 'map$,2,224,236,4,0xFFFFFF;
		if (isbegin_quest(16355) == 2 && isbegin_quest(16356) == 2 && 'step == 1) {
			'step = 2;
			doevent instance_npcname("#171_cor_control") + "::OnStory02";
		}
	}
	end;
}

1@cor,222,236,3	script	Box#171_box_0	4_STEELBOX,{
	if (isbegin_quest(16355) == 0) {
		mes "[叛乱]";
		mes "爆炸原因尚不清楚，但我在现场发现了这个可疑的盒子。这似乎是某种机制。";
		next;
		mes "[叛乱]";
		mes "里面有信号传来……呃，小心点。有事发生了！";
		close2;
		setquest 16355;
		specialeffect EF_BAKU;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSpawn";
		end;
	}
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (isbegin_quest(16355) == 1 && mobcount('map$,.@event$)) {
		mes "[叛乱]";
		mes "摧毁生物电池并杀死怪物！";
		close;
	}
	if (isbegin_quest(16355) == 1 && !mobcount('map$,.@event$))
		doevent instance_npcname("Rebellion#" + strnpcinfo(2)) + "::OnTalk";
	end;

OnSpawn:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	setarray .@xy,229,229,20341,217,235,20341,222,229,20341;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	initnpctimer;
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (!mobcount('map$,.@event$)) {
		stopnpctimer;
		npctalk "感谢您的辛勤工作！走之前，请先听听我要说的话。",instance_npcname("叛乱#" + strnpcinfo(2));
		killmonster 'map$,instance_npcname("Biological Battery#" + strnpcinfo(2)) + "::OnBombKill";
		disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
		stopnpctimer instance_npcname("Reinforced Energy#" + strnpcinfo(2));
		disablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
	}
	end;

OnTimer1000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (mobcount('map$,.@event$)) {
		npctalk "分析结果是，那块电池正在为召唤出来的怪物提供电力。踩到它并立即避免损坏！",instance_npcname("叛乱#" + strnpcinfo(2));
		enablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
	} else {
		stopnpctimer;
		disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
	}
	end;

OnTimer11000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@event2$ = instance_npcname("Biological Battery#" + strnpcinfo(2)) + "::OnBombKill";
	stopnpctimer;
	if (mobcount('map$,.@event$)) {
		disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
		if (unitexists(getnpcid(0,instance_npcname("Biological Battery#" + strnpcinfo(2)))))
			initnpctimer;
	}
	end;
}

1@cor,222,235,3	script	Biological Battery#171_box_0	1738,3,3,{
	end;

OnTouch:
	disablenpc();
	stopnpctimer instance_npcname("Box#171_box_0");
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBombKill";
	if (!mobcount('map$,.@event$)) {
		getmapxy(.@m$,.@x,.@y,BL_NPC);
		monster 'map$,.@x,.@y,"",20345,1,.@event$;
	}
	initnpctimer;
OnBombKill:
	end;

OnTimer7000:
	.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
	if (mobcount('map$,.@event$))
		enablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
	else
		stopnpctimer;
	end;

OnTimer17000:
	.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
	.@event2$ = instance_npcname(strnpcinfo(0)) + "::OnBombKill";
	if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#" + strnpcinfo(2))))) {
		disablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
		if (mobcount('map$,.@event2$))
			killmonster 'map$,.@event2$;
		stopnpctimer;
		if (mobcount('map$,.@event$))
			initnpctimer instance_npcname("Box#171_box_0");
	}
	end;
}

1@cor,225,232,3	script	Reinforced Energy#171_box_0	4_ENERGY_WHITE,{
	if (!getd("'rf_" + strnpcinfo(2))) {
		setd("'rf_" + strnpcinfo(2),1);
		specialeffect2 EF_ENHANCE;
		specialeffect2 EF_LIGHTSPHERE_STAR;
		sc_start SC_GLASTHEIM_STATE,30000,20,10000,SCSTART_NOTICKDEF;
		npctalk "力量，压倒性的力量！";
		unittalk getcharid(3),strcharinfo(0) + " : My whole body is full of power!",bc_self;
		sleep 1500;
		disablenpc();
		setd("'rf_" + strnpcinfo(2),0);
		stopnpctimer instance_npcname("Biological Battery#" + strnpcinfo(2));
		initnpctimer instance_npcname("Box#" + strnpcinfo(2));
	}
	end;
}

1@cor,218,172,5	script	Rebellion#171_box_1	4_F_REBELLION2,{
	if (isbegin_quest(16356) == 0) {
		mes "[叛乱]";
		mes "有一个可疑的盒子！里面传来信号，正在解密！";
		close;
	}
	.@event$ = instance_npcname("Box#" + strnpcinfo(2)) + "::OnMobKill";
	if (isbegin_quest(16356) == 1 && mobcount('map$,.@event$)) {
		mes "[叛乱]";
		mes "避免化学毒药并杀死怪物！";
		close;
	}
	if (isbegin_quest(16356) == 1 && !mobcount('map$,.@event$)) {
OnTalk:
		mes "[叛乱]";
		mes "这是一个可怕但有趣的设备。它不是用来召唤怪物的装置。";
		next;
		mes "[叛乱]";
		mes "召唤怪物后，会不断释放出化学毒液，让清除怪物变得更加困难。";
		next;
		mes "[叛乱]";
		mes "如果你接触到化学毒物，它就会像炸弹一样扩散。你不应该再靠近。";
		next;
		mes "[叛乱]";
		mes "看来这件事已经结束了，我去加入另一个队伍吧！你也应该。";
		close2;
		disablenpc();
		disablenpc instance_npcname("Box#" + strnpcinfo(2));
		viewpointmap 'map$,2,220,170,3,0xFFFFFF;
		completequest 16356;
		if (isbegin_quest(16355) == 2 && isbegin_quest(16356) == 2 && 'step == 1) {
			'step = 2;
			doevent instance_npcname("#171_cor_control") + "::OnStory02";
		}
	}
	end;
}

1@cor,220,170,3	script	Box#171_box_1	4_WOODBOX,{
	if (isbegin_quest(16356) == 0) {
		mes "[叛乱]";
		mes "爆炸原因尚不清楚，但我在现场发现了这个可疑的盒子。这似乎是某种机制。";
		next;
		mes "[叛乱]";
		mes "里面有信号传来……呃，小心点。有事发生了！";
		close2;
		setquest 16356;
		specialeffect EF_BAKU;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSpawn";
		end;
	}
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
	if (isbegin_quest(16356) == 1 && mobcount('map$,.@event$)) {
		mes "[叛乱]";
		mes "避免化学毒药并杀死怪物！";
		close;
	}
	if (isbegin_quest(16356) == 1 && !mobcount('map$,.@event$))
		doevent instance_npcname("Rebellion#" + strnpcinfo(2)) + "::OnTalk";
	end;

OnSpawn:
	setd("'" + strnpcinfo(2),1);
	setarray .@xy,213,175,20342,216,174,20342,219,167,20342;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	initnpctimer;
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
	if (!mobcount('map$,.@event$)) {
		stopnpctimer;
		killmonster 'map$,instance_npcname("Chemical Poison#" + strnpcinfo(2)) + "::OnPoisonKill";
		disablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
		npctalk "感谢您的辛勤工作！走之前，请先听听我要说的话。",instance_npcname("叛乱#" + strnpcinfo(2));
	}
	end;

OnTimer1000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (mobcount('map$,.@event$)) {
		npctalk "它喷出了有毒的化学物质！避免踩到它，继续战斗吧！",instance_npcname("叛乱#" + strnpcinfo(2));
		enablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
	} else
		stopnpctimer;
	end;

OnTimer11000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@event2$ = instance_npcname("Chemical Poison#" + strnpcinfo(2)) + "::OnPoisonKill";
	stopnpctimer;
	if (mobcount('map$,.@event$)) {
		disablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
		if (unitexists(getnpcid(0,instance_npcname("Chemical Poison#171_box_1"))))
			initnpctimer;
	}
	end;
}

1@cor,220,169,3	script	Chemical Poison#171_box_1	2531,3,3,{
	end;

OnTouch:
	disablenpc();
	stopnpctimer instance_npcname(strnpcinfo(0));
	npctalk "当您接触化学毒物时，有毒物质就会扩散。你已经踩到了，赶紧躲开！",instance_npcname("叛乱#" + strnpcinfo(2));
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnPoisonKill";
	if (!mobcount('map$,.@event$)) {
		getmapxy(.@m$,.@x,.@y,BL_NPC);
		monster 'map$,.@x,.@y,"",20344,1,.@event$;
	}
	initnpctimer;
	end;

OnPoisonKill:
	end;

OnTimer10000:
	stopnpctimer;
	initnpctimer instance_npcname("Box#" + strnpcinfo(2));
	end;
}

1@cor,162,117,1	script	Rebellion#171_box_2	4_M_GONY,{
	mes "[叛乱]";
	mes ".....";
	close;
}

1@cor,160,119,3	script	Box#171_box_2	4_STEELBOX,{
	mes "[叛乱]";
	mes ".....";
	specialeffect EF_M03;
	next;
	mes "[叛乱]";
	mes ".....";
	next;
	mes "- 可疑的盒子里冒出浓烟。 -";
	next;
	select("你已经这样做了吗？：你还沉默吗？");
	mes "[叛乱]";
	mes ".....";
	next;
	mes "[叛乱]";
	mes "*点头，点头*";
	next;
	mes "- 我们去支持另一支球队吧。 -";
	viewpointmap 'map$,2,160,119,2,0xFFFFFF;
	close2;
	disablenpc();
	disablenpc instance_npcname("Rebellion#" + strnpcinfo(2));
	end;
}

1@cor,138,82,5	script	Rebellion#171_box_3	4_F_ANYA,{
	mes "[叛乱]";
	mes "我有没有告诉过你我很高兴与秘密之翼合作？感谢他们，我被选中参加这次手术。";
	next;
	mes "[叛乱]";
	mes "让我们向他们展示我们的全部，以及我们是好人。";
	close;
}

1@cor,142,82,3	script	Rebellion#171_box_4	4_M_ILYA,{
	mes "[叛乱]";
	mes "好久没见到你了，冒险者。";
	next;
	mes "[叛乱]";
	mes "我已经认识你这个冒险家了，不过请小心点。";
	close;
}

1@cor,140,79,3	script	Box#171_box_3	4_WOODBOX,{
	mes "[叛乱]";
	mes "你好冒险家。";
	specialeffect EF_M03;
	next;
	mes "[叛乱]";
	mes "很高兴成为这支球队的一员，我们已经处理好了这个问题。";
	next;
	mes "[叛乱]";
	mes "这些机器使用化学毒物，要小心。";
	next;
	mes "[叛乱]";
	mes "我们现在将进行搜索，直到信号到来。冒险家，小心！";
	viewpointmap 'map$,2,140,79,1,0xFFFFFF;
	close2;
	disablenpc();
	disablenpc instance_npcname("Rebellion#171_box_3");
	disablenpc instance_npcname("Rebellion#171_box_4");
	end;
}

1@cor,172,223,3	script	Elena Volkova#171_cmd_1	4_F_ELENA,{
	cutin "162elena_01",2;
	mes "[埃琳娜・沃尔科娃]";
	mes "我一直在等你。我找到了那个疯狂的研究员。";
	mes "其他人都围着这个地方。";
	next;
	mes "[埃琳娜・沃尔科娃]";
	mes "为了如此精细地控制这样的机器，Elyumina 必须在身边。";
	next;
	mes "[埃琳娜・沃尔科娃]";
	mes "我可以操纵这台机器，但那样我就无法抓住艾柳米娜了。";
	next;
	mes "[埃琳娜・沃尔科娃]";
	mes "如果你照顾好机器，其他搜索方就会找到 Elyumina。";
	next;
	cutin "162elena_02",2;
	mes "[埃琳娜・沃尔科娃]";
	mes "叛乱小队协调性很好，这项工作最适合我们。所以把这个任务留给我们吧。";
	next;
	cutin "162elena_01",2;
	mes "[埃琳娜・沃尔科娃]";
	mes "我们要进入的地方是九点钟。我确认没有人躲在那里，我们封锁了出口。";
	next;
	mes "[埃琳娜・沃尔科娃]";
	mes "当你准备好进去时给我一个信号。我会告诉小队让你进去。";
	next;
	if (select("发出信号：还没有。") == 2) {
		mes "[埃琳娜・沃尔科娃]";
		mes "是的，这是一个危险的对手。我很抱歉不得不留下你一个人去战斗。当你准备好时请告诉我。";
		close3;
	}
	cutin "162elena_02",2;
	mes "[埃琳娜・沃尔科娃]";
	mes "好吧，小心！我打开了传送门，以便你可以进去。这是单向入口，所以请确保你做好准备！";
	close2;
	cutin "",255;
	viewpointmap 'map$,2,172,223,5,0xFFFFFF;
	if ('step == 2) {
		'step = 3;
		donpcevent instance_npcname("#171_cor_control") + "::OnStory03";
	}
	end;
}

1@cor,138,221,3	script	Elena Volkova#171_cmd_2	4_F_ELENA,{
	if ('mode) {
		if (isbegin_quest(16363) == 0) {
			cutin "162elena_01",2;
			mes "[埃琳娜・沃尔科娃]";
			mes "哦是的。这次你又辛苦了。这是相当艰难的！我们需要使用那块金属碎片多久才能让它停下来？";
			next;
			cutin "ep171_elyumina02",0;
			mes "[艾柳米娜]";
			mes "我不是说了不要叫废铁吗？！你这个白痴！";
			next;
			cutin "162elena_01",2;
			mes "[埃琳娜・沃尔科娃]";
			mes "是的，是的，一开始它不是废金属。但现在我们抓住了它，它是废金属。无论如何，你已经很努力了。你现在就回去休息吧。";
			next;
			cutin "",255;
			if (select("回去吧：稍等一下。") == 2) {
				mes "[埃琳娜・沃尔科娃]";
				mes "好吧，当你准备好出去时请告诉我。";
				close3;
			}
			cutin "162elena_02",2;
			mes "[埃琳娜・沃尔科娃]";
			mes "好工作。有趣的是我们明天还要再做一次。明天见。";
			setquest 16363;
			getitem 25723,1;
			getitem 25669,5;
			close2;
			warp "sp_cor",111,125;
		}
		end;
	}
	if (isbegin_quest(16354) == 1 && checkquest(16376,HUNTING) == 2) {
		cutin "162elena_02",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "干得好，" +strcharinfo(0)+ "。手术很成功！";
		mes "我也跑了，你把那块大铁片撞倒了，真丢脸。我还捕捉到了一些令人着迷的东西。";
		next;
		cutin "ep171_elyumina02",0;
		mes "[艾柳米娜]";
		mes "废金属！这是一个杰作！我伟大的孩子！";
		mes "你！你……伤害了我的孩子！你毁了我的孩子！";
		next;
		cutin "162elena_02",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "你刚才把那些废品称为孩子吗？";
		mes "嗯，你的孩子可能很好。那我们就去为孩子的错误付出代价吧。";
		next;
		cutin "162elena_01",2;
		mes "[埃琳娜・沃尔科娃]";
		mes "" + strcharinfo(0) + " 现在你打算做什么？";
		next;
		if (select("我马上回去：我会在这儿待一会儿") == 2) {
			mes "[埃琳娜・沃尔科娃]";
			mes "好吧，当你准备好出去时请告诉我。";
			close3;
		}
		mes "[埃琳娜・沃尔科娃]";
		mes "干得好。我们会清理这里的事情，你可以休息一下。我要去审问她。休息一下，然后回到我身边。";
		completequest 16354;
		erasequest 16376;
		setquest 16357;
		setquest 16377;
		close2;
		warp "sp_cor",176,169;
	}
	end;
}

1@cor,140,221,3	script	Elyumina#171_cor_end	4_EP17_ELYUMINA,{
	npctalk "Elyumina：你们……你们杀了它？！你宰了它！刽子手！";
	end;
}
1@cor,141,222,3	script	Rebellion#171_cmd_6	4_M_ILYA,{
	npctalk "幻象研究员艾柳米娜被抓获了！";
	end;
}
1@cor,141,220,3	script	Rebellion#171_cmd_7	4_F_ANYA,{
	npctalk "我们成功活捉了她，行动结束了！";
	end;
}

1@cor,1,1,0	script	#171_cor_boss	HIDDEN_WARP_NPC,{
	end;

OnSummonStory:
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	monster 'map$,137,221,"EL1_A17T",20340,1,.@event$;
	'boss_gid = $@mobid[0];
	getunitdata 'boss_gid,.@boss_data;
	getunitdata $@mobid[0],.@boss_data;
	.@DAMAGE = (.@boss_data[UMOB_MAXHP]/10) * 9;
	.@HP = (.@boss_data[UMOB_MAXHP] - .@DAMAGE)/2;
	setunitdata $@mobid[0],UMOB_HP,.@HP;
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
	initnpctimer;
	end;

OnSummonDaily:
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	monster 'map$,137,221,"EL1_A17T",20340,1,.@event$;
	'boss_gid = $@mobid[0];
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
	initnpctimer;
	end;

OnBossKill:
	stopnpctimer;
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnClear";
	enablenpc instance_npcname("Elena Volkova#171_cmd_2");
	enablenpc instance_npcname("Elyumina#171_cor_end");
	enablenpc instance_npcname("Rebellion#171_cmd_6");
	enablenpc instance_npcname("Rebellion#171_cmd_7");
	end;

OnClear:
	for (.@i = 0; .@i < 11; .@i++) {
		disablenpc instance_npcname("Biological Battery#171_cor_" + .@i);
		disablenpc instance_npcname("Chemical Poison#171_cor_" + .@i);
	}
	killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnPoisonKill";
	killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnBombKill";
	killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	end;

OnTimer30000:
	stopnpctimer;
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
	end;

OnSummonSkill:
	if (unitexists('boss_gid)) {
		mapannounce 'map$,"Elyumina : It's starting to spread something. Prepare yourself!",bc_map,0xFFFF99;
		if (rand(2)) {
			.@npc$ = "Chemical Poison#171_cor_";
			.@type$ = "trap_0_";
		} else {
			.@npc$ = "Biological Battery#171_cor_";
			.@type$ = "trap_1_";
		}
		while(true) {
			.@id = rand(11);
			if (getd("'" + .@type$ + .@id))
				continue;
			if (unitexists('boss_gid)) {
				enablenpc instance_npcname(.@npc$ + .@id);
				setd("'" + .@type$ + .@id,1);
			}
			break;
		}
		initnpctimer;
	}
	end;
}

1@cor,151,221,3	script	Biological Battery#171_cor_0	1738,3,3,{
	end;

OnTouch:
	.@id = atoi(replacestr(strnpcinfo(2),"171_cor_",""));
	disablenpc();
	setd("'trap_1_" + .@id,0);
	getmapxy('map$,.@x,.@y,BL_NPC);
	monster 'map$,.@x,.@y,"",20345,1,instance_npcname("#171_cor_boss") + "::OnBombKill";
	initnpctimer;
	end;

OnTimer7000:
	if (!'rf_171_cor_0)
		enablenpc instance_npcname("Reinforced Energy#171_cor_0");
	end;

OnTimer12000:
	stopnpctimer;
	if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_cor_0"))))
		disablenpc instance_npcname("Reinforced Energy#171_cor_0");
	end;
}

1@cor,151,221,3	script	Chemical Poison#171_cor_0	2531,3,3,{
	end;

OnTouch:
	disablenpc();
	getmapxy('map$,.@x,.@y,BL_NPC);
	.@event$ = instance_npcname("#171_cor_boss") + "::OnPoisonKill";
	if (!mobcount('map$,.@event$))
		monster 'map$,.@x,.@y,"",20344,1,.@event$;
	end;
}

1@cor,137,220,3	script	Reinforced Energy#171_cor_0	4_ENERGY_WHITE,{
	if (!getd("'rf_" + strnpcinfo(2))) {
		setd("'rf_" + strnpcinfo(2),1);
		specialeffect2 EF_ENHANCE;
		specialeffect2 EF_LIGHTSPHERE_STAR;
		sc_start SC_GLASTHEIM_STATE,30000,20,10000,SCSTART_NOTICKDEF;
		npctalk "力量，压倒性的力量！";
		unittalk getcharid(3),strcharinfo(0) + " : My whole body is full of power!",bc_self;
		sleep 1500;
		disablenpc();
		setd("'rf_" + strnpcinfo(2),0);
	}
	end;
}

1@cor,145,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_1	1738,3,3
1@cor,129,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_2	1738,3,3
1@cor,138,206,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_3	1738,3,3
1@cor,136,213,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_4	1738,3,3
1@cor,138,228,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_5	1738,3,3
1@cor,138,235,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_6	1738,3,3
1@cor,102,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_7	1738,3,3
1@cor,108,221,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_8	1738,3,3
1@cor,115,219,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_9	1738,3,3
1@cor,122,221,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_10	1738,3,3

1@cor,145,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_1	2531,3,3
1@cor,129,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_2	2531,3,3
1@cor,138,206,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_3	2531,3,3
1@cor,136,213,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_4	2531,3,3
1@cor,138,228,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_5	2531,3,3
1@cor,138,235,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_6	2531,3,3
1@cor,102,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_7	2531,3,3
1@cor,108,221,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_8	2531,3,3
1@cor,115,219,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_9	2531,3,3
1@cor,122,221,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_10	2531,3,3

1@cor,161,221,0	script	#171_cor_warp_0	WARPNPC,2,2,{
	end;

OnActive:
	enablenpc();
	specialeffect EF_READYPORTAL2;
	end;

OnTouch:
	if (!'boss_summon) {
		'boss_summon = 1;
		donpcevent instance_npcname("#171_cor_boss") + "::OnSummonStory";
	}
	warp 'map$,151,220;
	end;
}

1@cor,161,221,0	script	#171_cor_warp_1	WARPNPC,2,2,{
	end;

OnActive:
	enablenpc();
	specialeffect EF_READYPORTAL2;
	end;

OnTouch:
	if (!'boss_summon) {
		'boss_summon = 1;
		donpcevent instance_npcname("#171_cor_boss") + "::OnSummonDaily";
	}
	warp 'map$,151,220;
	end;
}

1@cor,224,238,3	script	Elyumina#171_box_d0	4_EP17_ELYUMINA,{
	if ('cor == 4) {
		npctalk "Elyumina：你知道军营在哪里吗？我在你的小地图上显示了它。我很快就会前往那里。";
		end;
	}
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	.@var = getd("'box_" + .@id);
	switch (.@var) {
		case 0: .@talk$ = "Here, here! Then please, touch the box and it will activate."; break;
		case 1: .@talk$ = "Chatting during a battle, quite relaxed aren't you? Hm, that's a bit annoying."; break;
		case 2: .@talk$ = "That's a very good combat data... Well, go ahead now!";
	}
	npctalk "埃卢米娜：" + .@talk$;
	end;
}

1@cor,218,172,5	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d1	4_EP17_ELYUMINA
1@cor,162,117,3	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d2	4_EP17_ELYUMINA
1@cor,140,82,3	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d3	4_EP17_ELYUMINA

1@cor,222,236,3	script	Box#171_box_d0	4_STEELBOX,{
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	if (!getd("'box_" + .@id)) {
		setd("'box_" + .@id,1);
		switch (.@id) {
			case 0: setarray .@xy,228,232,20341,216,229,20341,223,242,20341,226,239,20341,227,242,20341,229,230,20341,214,241,20343,224,235,20343,219,244,20343,227,230,20355,215,228,20355,221,231,20355,230,227,20355; break;
			case 1: setarray .@xy,224,164,20342,215,175,20342,220,174,20342,215,170,20342,216,164,20342,217,175,20342,226,164,20343,224,165,20343,215,172,20343,219,170,20356,217,176,20356,213,165,20356; break;
			case 2: setarray .@xy,158,119,20341,165,125,20341,160,119,20341,162,119,20341,158,119,20341,165,113,20341,149,117,20343,162,125,20343,149,125,20343,166,113,20355,167,115,20355,166,119,20355; break;
			case 3: setarray .@xy,143,72,20342,141,77,20342,146,84,20342,139,82,20342,138,75,20342,144,82,20342,140,70,20343,140,73,20343,142,72,20343,133,85,20356,140,82,20356,142,76,20356;
		}
		specialeffect EF_BAKU;
		for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
			monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
		initnpctimer;
	}
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (!mobcount('map$,.@event$)) {
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
		setd("'box_" + .@id,2);
		'cor += 1;
		if (.@id == 0 || .@id == 2) {
			.@npc$ = "Biological Battery#";
			disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
		} else
			.@npc$ = "Chemical Poison#";
		switch (.@id) {
		case 0:
			viewpointmap 'map$,2,222,236,4,0xFFFFFF;
			.@npc$ = "Biological Battery#";
			break;
		case 1:
			viewpointmap 'map$,2,220,170,3,0xFFFFFF;
			.@npc$ = "Chemical Poison#";
			break;
		case 2:
			viewpointmap 'map$,2,160,119,2,0xFFFFFF;
			.@npc$ = "Biological Battery#";
			break;
		case 3:
			viewpointmap 'map$,2,140,79,1,0xFFFFFF;
			.@npc$ = "Chemical Poison#";
		}
		killmonster 'map$,instance_npcname(.@npc$ + "171_box_trap_" + .@id) + "::OnMobKill";
		disablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
		if ('cor == 4)
			donpcevent instance_npcname("#171_cor_control") + "::OnDaily02";
	}
	end;

OnTimer1000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	if (.@id == 0 || .@id == 2)
		.@npc$ = "Biological Battery#";
	else
		.@npc$ = "Chemical Poison#";
	if (mobcount('map$,.@event$))
		enablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
	else
		stopnpctimer;
	end;

OnTimer11000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	stopnpctimer;
	if (mobcount('map$,.@event$)) {
		if (.@id == 0 || .@id == 2)
			.@npc$ = "Biological Battery#";
		else
			.@npc$ = "Chemical Poison#";
		disablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
		if (unitexists(getnpcid(0,instance_npcname(.@npc$ + "171_box_trap_" + .@id))))
			initnpctimer;
	}
	end;
}

1@cor,222,235,3	script	Biological Battery#171_box_trap_0	1738,3,3,{
	end;

OnTouch:
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	if (.@id == 0 || .@id == 2)
		.@mob = 20345;
	else
		.@mob = 20344;
	stopnpctimer instance_npcname("Box#171_box_d" + .@id);
	.@event$ = instance_npcname("Box#171_box_d" + .@id) + "::OnMobKill";
	disablenpc();
	if (mobcount('map$,.@event$)) {
		.@event2$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
		getmapxy(.@m$,.@x,.@y,BL_NPC);
		if (!mobcount('map$,.@event2$))
			monster 'map$,.@x,.@y,"--ja--",.@mob,1,.@event2$;
		initnpctimer;
	}
	end;

OnMobKill:
	end;

OnTimer7000:
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	if (.@id == 0 || .@id == 2)
		.@mob = 20345;
	else
		.@mob = 20344;
	if (.@mob == 20345) {
		if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_trap_" + .@id)))) {
			disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
			enablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
		}
	}
	end;

OnTimer10000:
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	if (.@id == 0 || .@id == 2)
		.@mob = 20345;
	else
		.@mob = 20344;
	if (.@mob == 20344) {
		stopnpctimer;
		initnpctimer instance_npcname("Box#171_box_d" + .@id);
	}
	end;

OnTimer17000:
	stopnpctimer;
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	.@event$ = instance_npcname("Box#171_box_d" + .@id) + "::OnMobKill";
	if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_cor_0"))))
		disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
	if (mobcount('map$,.@event$))
		initnpctimer instance_npcname("Box#171_box_d" + .@id);
	end;
}

1@cor,220,169,3	duplicate(Biological Battery#171_box_trap_0)	Chemical Poison#171_box_trap_1	2531,3,3
1@cor,160,118,3	duplicate(Biological Battery#171_box_trap_0)	Biological Battery#171_box_trap_2	1738,3,3
1@cor,140,78,3	duplicate(Biological Battery#171_box_trap_0)	Chemical Poison#171_box_trap_3	2531,3,3

1@cor,225,232,3	duplicate(Reinforced Energy#171_cor_0)	Reinforced Energy#171_trap_0	4_ENERGY_WHITE
1@cor,163,115,3	duplicate(Reinforced Energy#171_cor_0)	Reinforced Energy#171_trap_2	4_ENERGY_WHITE

1@cor,220,170,3	duplicate(Box#171_box_d0)	Box#171_box_d1	4_WOODBOX
1@cor,160,119,3	duplicate(Box#171_box_d0)	Box#171_box_d2	4_STEELBOX
1@cor,140,79,3	duplicate(Box#171_box_d0)	Box#171_box_d3	4_WOODBOX

1@cor,159,218,0	script	#cor_barricade_0	4_ROPEPILE,{
	end;

OnTouch:
	npctalk "入口被堵住了！";
	end;
}

1@cor,159,220,3	duplicate(#cor_barricade_0)	#cor_barricade_1	4_ROPEPILE
1@cor,159,222,3	duplicate(#cor_barricade_0)	#cor_barricade_2	4_ROPEPILE
1@cor,159,224,3	duplicate(#cor_barricade_0)	#cor_barricade_3	4_ROPEPILE,2,2
1@cor,98,218,3	duplicate(#cor_barricade_0)	#cor_barricade_4	4_ROPEPILE
1@cor,98,220,3	duplicate(#cor_barricade_0)	#cor_barricade_5	4_ROPEPILE
1@cor,98,222,3	duplicate(#cor_barricade_0)	#cor_barricade_6	4_ROPEPILE
1@cor,98,224,3	duplicate(#cor_barricade_0)	#cor_barricade_7	4_ROPEPILE,2,2
1@cor,134,240,3	duplicate(#cor_barricade_0)	#cor_barricade_8	4_ROPEPILE
1@cor,136,240,3	duplicate(#cor_barricade_0)	#cor_barricade_9	4_ROPEPILE
1@cor,138,240,3	duplicate(#cor_barricade_0)	#cor_barricade_10	4_ROPEPILE
1@cor,140,240,3	duplicate(#cor_barricade_0)	#cor_barricade_11	4_ROPEPILE,2,2
