//===== rAthena Script ======================================= 
//= Al De Baran Guides
//===== By: ================================================== 
//= ding
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Al De Baran.
//============================================================ 

aldebaran,139,63,4	script	Guide#01aldebaran::GuideAldearan	105,{
	cutin "prt_soldier",2;
	mes "[阿尔德巴兰指南]";
	mes "欢迎来到^8B4513阿尔德巴兰^000000，";
	mes "边境城市。";
	mes "您需要帮助游览这座城市吗？";
	F_Navi("[阿尔德巴兰指南]");
	next;
	set .@str$,"Would you like to check any other locations?";
	while (1) {
		switch(select("[主要设施]：[商人和助手]：从小地图上删除标记：取消")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[卡夫拉总部]：[炼金术士公会]：[钟楼]：[旅馆和工具店]：[修理和购买装备]：[酒馆]：上一个菜单")) {
				case 1:
					callsub L_Mark, F_Navi("卡夫拉总部","aldebaran,61,229","^B9062F");
					mes .@str$;
					viewpoint 1,61,229,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("炼金术士公会","aldebaran,60,60","^0000FF");
					mes .@str$;
					viewpoint 1,60,60,1,0x0A82FF;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("钟楼","aldebaran,139,133","^FF5400");
					mes .@str$;
					viewpoint 1,139,133,2,0xFFB400;
					next;
					break;
				case 4:
					mes "[阿尔德巴兰指南]";
					mes "让我标记一下位置";
					mes F_Navi("旅馆及工具店","毕宿五,197,70","^006400")+" 大楼";
					mes "在你的小地图上。";
					mes .@str$;
					viewpoint 1,197,70,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[阿尔德巴兰指南]";
					mes "如果你愿意";
					mes F_Navi("购买或维修设备","毕宿五,197.70","^7F3300")+",";
					mes "那么这就是您想要参观的地方。";
					mes .@str$;
					viewpoint 1,72,197,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("酒馆","aldebaran,231,106","^800080");
					viewpoint 1,231,106,5,0xDA70D6;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[伊甸传送官]：[卡夫拉员工]：[克劳斯先生]：[厨师助理]：上一个菜单")) {
				case 1:
					callsub L_Mark, F_Navi("伊甸传送官","aldebaran,133,119","^B9062F");
					mes .@str$;
					viewpoint 1,133,119,6,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, "^0000FFKafra Employee^000000";
					viewpoint 1,143,119,7,0x0A82FF;
					next;
					break;
				case 3:
					mes "[阿尔德巴兰指南]";
					mes "克劳斯先生可以传送";
					mes "您到"+F_Navi("Lutie","毕宿五，168,168","^006400")+".";
					mes "让我标记他的位置";
					mes "在你的小地图上。";
					mes .@str$;
					viewpoint 1,168,168,8,0xAAFF00;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("助理厨师长","aldebaran,165,107","^7F3300");
					mes .@str$;
					viewpoint 1,165,107,9,0xD2691E;
					next;
					break;
				case 5:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[阿尔德巴兰指南]";
			mes "当然，我会从你的小地图上删除所有标记。";
			mes "我还能为您做些什么吗？";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			next;
			break;
		case 4:
			mes "[阿尔德巴兰指南]";
			mes "好吧，那么，";
			mes "祝您住宿愉快！";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[阿尔德巴兰指南]";
	mes "让我标记一下位置";
	mes getarg(0);
	mes "在你的小地图上。";
	return;
}
aldebaran,243,143,2	duplicate(GuideAldearan)	Guide#02aldebaran	105
aldebaran,135,243,4	duplicate(GuideAldearan)	Guide#03aldebaran	105
aldebaran,36,135,6	duplicate(GuideAldearan)	Guide#04aldebaran	105
