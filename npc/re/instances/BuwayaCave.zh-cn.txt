//===== rAthena Script ======================================= 
//= Buwaya Cave
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Defeat Buwaya in his cave.
//= Part of the "Secret in the Woods" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//===== Translated By: =======================================
//= dsc
//============================================================ 

// Instance Creation :: buwaya_dun
//============================================================
ma_fild02,312,317,5	script	Guard#buwaya_cave	570,{
	if (BaseLevel < 130) {
		mes "[守卫]";
		mes "^ff0000130级^000000以下的人";
		mes "无法进入这个地方。";
		mes "这个地方很危险。请回去吧。";
		close;
	}

	set .@party_id,getcharid(1);
	set .@md_name$,"Buwaya Cave";

	if (!.@party_id) {
		mes "[守卫]";
		mes "鳄鱼很危险。";
		mes "请在您组建后回来";
		mes "与其他人合作。";
		close;
	}
	if (is_party_leader() == true) {
		mes "[守卫]";
		mes "这个地方很危险。请回去吧。";
		mes "如果你这里没有什么事，就请回去吧。";
		next;
		while(1) {
			switch(select("为什么这么危险？：我是来追捕布瓦亚的。：结束对话。")) {
			case 1:
				mes "[守卫]";
				mes "最近，有一个案例";
				mes "村民失踪的地方。";
				mes "勘察踪迹后，";
				mes "我们发现了巨型怪物布瓦亚";
				mes "有责任。";
				next;
				mes "[守卫]";
				mes "士兵和雇佣兵已被派往";
				mes "追捕布瓦亚，但从未";
				mes "完全成功。";
				next;
				mes "[守卫]";
				mes "布瓦亚仍在绑架";
				mes "村民们至今。";
				mes "请召集一些人来除掉布瓦亚。";
				next;
				break;
			case 2:
				set .@playtime, checkquest(4229,PLAYTIME);
				if (.@playtime == -1) {
					// fall through
				} else if (.@playtime == 0 || .@playtime == 1) {
					mes "[守卫]";
					mes "布瓦亚仍然躲藏着。";
					mes "就算你现在进去，布瓦亚也不会出来。";
					mes "请稍后再回来。";
					close;
				} else {
					erasequest 4229;
					// fall through
				}
				if (instance_create(.@md_name$) < 0) {
					mes "[守卫]";
					mes "队伍名称是..."+getpartyname(.@party_id)+".";
					mes "党的领导人是..."+strcharinfo(0);
					mes "^0000ff"+.@md_name$+"^000000 现在无法打开。";
					mes "请稍后再试。";
					close;
				}
				mes "[守卫]";
				mes "我将打开通往 ^0000ff"+.@md_name$+"^000000 的隧道。";
				mes "祝你好运。";
				close;
			case 3:
				mes "[守卫]";
				mes "这个地方很危险。你不能进入。";
				close;
			}
		}
	} else {
		mes "[守卫]";
		mes "让我和党的领导人谈谈。";
		mes "请领导喊话。";
		close;
	}
}

ma_fild02,315,323,0	script	Cave Entrance#buwaya	45,2,2,{
	end;
OnTouch:
	if (BaseLevel < 130) {
		mes "[守卫]";
		mes "^ff0000130级^000000以下的人";
		mes "无法进入这个地方。";
		mes "这个地方很危险。请回去吧。";
		close;
	}
	set .@playtime, checkquest(4229,PLAYTIME);
	if (.@playtime == -1) {
		// fall through
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "[守卫]";
		mes "布瓦亚仍然躲藏着。";
		mes "就算你现在进去，布瓦亚也不会出来。";
		mes "请稍后再回来。";
		close;
	} else {
		erasequest 4229;
		// fall through
	}
	switch(select("输入：返回。")) {
	case 1:
		switch(instance_enter("Buwaya Cave")) {
		case IE_OTHER:
			mes "[守卫]";
			mes "唉，现在可不是好时机。";
			mes "请稍后重试。";
			close;
		case IE_NOINSTANCE:
		case IE_NOMEMBER:
			mes "[守卫]";
			mes "这个地方很危险。请不要进入。";
			close;
		case IE_OK:
			mapannounce "ma_fild02",getpartyname(getcharid(1))+" party's "+strcharinfo(0)+" member began hunting Buwaya in Buwaya Cave.",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
			setquest 4229;
			//warp "1@ma_c",35,57;
			end;
		default:
			mes "- 由于不明力量而无法进入。 -";
			close;
		}
	case 2:
		mes "[守卫]";
		mes "你必须珍惜你的生命。";
		mes "行动之前最好仔细考虑一下。";
		close;
	}
}

// Instance Scripts :: buwaya_in
//============================================================
1@ma_c,33,112,0	script	#damage	139,7,7,{
	end;
OnInstanceInit:
	initnpctimer;
	disablenpc instance_npcname("#damage");
	end;
OnTimer1000:
	enablenpc instance_npcname("#damage");
	specialeffect EF_POISONHIT;
	end;
OnTimer2000:
	stopnpctimer;
	initnpctimer;
	disablenpc instance_npcname("#damage");
	end;
OnTouch:
	percentheal -10,-10;
	sc_start SC_BLEEDING,60000,0;
	sc_start SC_POISON,60000,0;
	end;
}

1@ma_c,29,110,5	script	Kidnapped People#1	575,{
	mes "[被绑架的居民]";
	mes "布瓦亚四处奔走，";
	mes "新的人被抓了。";
	mes "我们的余生都会被困在这里。";
	next;
	switch(select("带我离开这里！！：Ca…逃不掉？")) {
	case 1:
		mes "[被绑架的居民]";
		mes "我们没有足够的力量摧毁它，但布瓦亚有一个弱点。";
		mes "看看那边的墙。";
		mes "如果你敲那里，Buywaya 会感到疼痛并将你吐出来。";
		donpcevent instance_npcname("#box_mob_call")+"::OnEnable";
		close2;
		disablenpc instance_npcname("Kidnapped People#1");
		end;
	case 2:
		mes "[被绑架的人]";
		mes "这里出了点问题...";
		mes "您以前经历过这种情况吗？";
		close;
	}
	end;
OnInstanceInit:
	disablenpc instance_npcname("Kidnapped People#1");
	end;
OnEnable:
	enablenpc instance_npcname("Kidnapped People#1");
	donpcevent instance_npcname("#box_mob_call")+"::OnDisable";
	end;
}

1@ma_c,36,110,5	script	Kidnapped People#2	574,{
	mes "[被绑架的居民]";
	mes "啊哈！我希望我能拥有更多一点的力量...";
	mes "我知道它的弱点...";
	next;
	switch(select("告诉我：你真的知道吗？")) {
	case 1:
		mes "[被绑架的居民]";
		mes "你确定你能做到吗...？";
		mes "好吧，仔细听，";
		mes "它的弱点是...";
		next;
		mes "[被绑架的居民]";
		mes "...在我们后面的墙上。";
		sc_start SC_ATKPOTION,60000,45;
		sc_start SC_MATKPOTION,60000,45;
		disablenpc instance_npcname("Kidnapped People#2");
		close;
	case 2:
		mes "[被绑架的居民]";
		mes "什么？！";
		mes "你不理我吗";
		mes "因为我被抓到这里了？";
		close;
	}
	end;
OnInstanceInit:
	disablenpc instance_npcname("Kidnapped People#2");
	end;
OnEnable:
	enablenpc instance_npcname("Kidnapped People#2");
	end;
}

1@ma_c,3,3,0	script	#box_mob_call	139,1,1,{
	end;
OnInstanceInit:
	setcell instance_mapname("1@ma_c"),30,118,35,118,cell_shootable,1; //custom
	disablenpc instance_npcname("#box_mob_call");
	end;
OnEnable:
	enablenpc instance_npcname("#box_mob_call");
	set .@label$, instance_npcname("#box_mob_call")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_c");
	monster .@map$,30,118,"Buwaya's Weakness",2333,1,.@label$;
	monster .@map$,35,118,"Buwaya's Weakness",2333,1,.@label$;
	end;
OnDisable:
	killmonster instance_mapname("1@ma_c"),instance_npcname("#box_mob_call")+"::OnMyMobDead";
	disablenpc instance_npcname("#box_mob_call");
	end;
OnMyMobDead:
	if (mobcount(instance_mapname("1@ma_c"),instance_npcname("#box_mob_call")+"::OnMyMobDead") < 1)
		donpcevent instance_npcname("#box_out")+"::OnEnable";
	end;
}

1@ma_c,33,118,0	script	#box_out	45,2,2,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#box_out");
	end;
OnEnable:
	enablenpc instance_npcname("#box_out");
	end;
OnTouch:
	set .@x, rand(1,20) + 97;
	set .@y, rand(1,20) + 74;
	warp instance_mapname("1@ma_c"),.@x,.@y;
	end;
}

1@ma_c,97,74,0	script	#box_call	139,50,50,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#box_call");
	initnpctimer;
	end;
OnTimer30000:
	mapannounce instance_mapname("1@ma_c"),"Buwaya : I will put you in my treasure box!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	// Should execute OnTimer33000, but client doesn't render the effect fast enough.
	for(set .@i,1; .@i<=9; set .@i,.@i+1)
		donpcevent instance_npcname("#yunobi"+.@i)+"::OnEnable";
	end;
OnTimer33000:
	donpcevent instance_npcname("#box_out")+"::OnDisable";
	donpcevent instance_npcname("#box_mob_call")+"::OnDisable";
	donpcevent instance_npcname("Kidnapped People#1")+"::OnEnable";
	donpcevent instance_npcname("Kidnapped People#2")+"::OnEnable";
	end;
OnTimer34000:
	enablenpc instance_npcname("#box_call");
	end;
OnTimer35000:
	stopnpctimer;
	initnpctimer;
	disablenpc instance_npcname("#box_call");
	end;
OnTouch_:
	specialeffect2 EF_GUIDEDATTACK;
	warp instance_mapname("1@ma_c"),33,112;
	end;
OnDisable:
	stopnpctimer;
	disablenpc instance_npcname("#box_call");
	end;
}

1@ma_c,97,74,0	script	#yunobi1	139,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	specialeffect EF_MAPPILLAR2;
	end;
}
1@ma_c,97,94,0	duplicate(#yunobi1)	#yunobi2	139
1@ma_c,117,94,0	duplicate(#yunobi1)	#yunobi3	139
1@ma_c,117,74,0	duplicate(#yunobi1)	#yunobi4	139
1@ma_c,117,54,0	duplicate(#yunobi1)	#yunobi5	139
1@ma_c,97,54,0	duplicate(#yunobi1)	#yunobi6	139
1@ma_c,77,54,0	duplicate(#yunobi1)	#yunobi7	139
1@ma_c,77,74,0	duplicate(#yunobi1)	#yunobi8	139
1@ma_c,77,94,0	duplicate(#yunobi1)	#yunobi9	139

1@ma_c,1,1,0	script	#bunshin	139,{
	end;
OnInstanceInit:
	initnpctimer;
	end;
OnTimer58000:
	mapannounce instance_mapname("1@ma_c"),"Buwaya : I had enough with this, let's go till the end!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer61000:
	mapannounce instance_mapname("1@ma_c"),"Buwaya : This is!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer62000:
	mapannounce instance_mapname("1@ma_c"),"Buwaya : This is...MY!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer63000:
	mapannounce instance_mapname("1@ma_c"),"Buwaya : This is...MY...Deadly!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer64000:
	mapannounce instance_mapname("1@ma_c"),"Buwaya : This is...MY...Deadly... ATTACK!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer65000:
	set .@label$, instance_npcname("#bunshin")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_c");
	areamonster .@map$,112,89,122,99,"Buwaya",2332,1,.@label$;
	areamonster .@map$,112,49,122,59,"Buwaya",2332,1,.@label$;
	areamonster .@map$,72,49,82,59,"Buwaya",2332,1,.@label$;
	areamonster .@map$,72,89,82,99,"Buwaya",2332,1,.@label$;
	end;
OnTimer66000:
	mapannounce instance_mapname("1@ma_c"),"Buwaya : Are you scared?",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer105000:
	killmonster instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead";
	stopnpctimer;
	initnpctimer;
	end;
OnMyMobDead:
	if (mobcount(instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead") < 1) {
		stopnpctimer;
		initnpctimer;
	}
	end;
OnDisable:
	stopnpctimer;
	killmonster instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead";
	disablenpc instance_npcname("#bunshin");
	end;
}

1@ma_c,2,2,0	script	#buwaya_con	139,{
	end;
OnInstanceInit:
	areamonster instance_mapname("1@ma_c"),90,67,104,81,"Buwaya",2319,1,instance_npcname("#buwaya_con")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_c");
	if (mobcount(.@map$,instance_npcname("#buwaya_con")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#box_call")+"::OnDisable";
		donpcevent instance_npcname("#bunshin")+"::OnDisable";
		donpcevent instance_npcname("#exit_mob")+"::OnDisable";
		donpcevent instance_npcname("#cave_out")+"::OnEnable";
		mapannounce .@map$,"Guard : You did great work. Please hurry up and escape to the way you came in!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	}
	end;
}

1@ma_c,3,3,0	script	#exit_mob	139,{
	end;
OnInstanceInit:
	initnpctimer;
	end;
OnTimer60000:
	set .@label$, instance_npcname("#exit_mob")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_c");
	if (mobcount(.@map$,.@label$) < 30)
		set .@amount,10;
	else
		set .@amount,1;
	areamonster .@map$,43,58,47,60,"Water Plant",2331,.@amount,.@label$;
	areamonster .@map$,43,58,47,60,"Egg",2329,.@amount,.@label$;
	stopnpctimer;
	initnpctimer;
	end;
OnDisable:
	stopnpctimer;
	killmonster instance_mapname("1@ma_c"),instance_npcname("#exit_mob")+"::OnMyMobDead";
	disablenpc instance_npcname("#exit_mob");
	end;
OnMyMobDead:
	end;
}

1@ma_c,28,57,0	script	#cave_out	45,2,2,{
OnInstanceInit:
	disablenpc instance_npcname("#cave_out");
	end;
OnEnable:
	enablenpc instance_npcname("#cave_out");
	end;
OnTouch:
	mes "想出去吗？";
	next;
	if(select("是的！：不，我会留下来。") == 1)
		warp "ma_fild02",315,315;
	close;
}

1@ma_c,1,1,0	script	#buwaya_spawn_mobs	-1,{
OnInstanceInit:
	set .@map$, instance_mapname("1@ma_c");
	areamonster .@map$,73,81,93,101,"Seaweed",2331,18;
	areamonster .@map$,110,97,116,103,"Seaweed",2331,8;
	areamonster .@map$,59,63,63,67,"Seaweed",2331,8;
	areamonster .@map$,73,55,77,59,"Seaweed",2331,4;
	areamonster .@map$,103,69,107,73,"Seaweed",2331,4;
	areamonster .@map$,108,45,122,63,"Seaweed",2331,15;
	areamonster .@map$,73,81,93,101,"Buwaya's Egg",2329,10;
	areamonster .@map$,110,97,116,103,"Buwaya's Egg",2329,8;
	areamonster .@map$,59,63,63,67,"Buwaya's Egg",2329,4;
	areamonster .@map$,73,55,77,59,"Buwaya's Egg",2329,3;
	areamonster .@map$,103,69,107,73,"Buwaya's Egg",2329,3;
	areamonster .@map$,108,45,122,63,"Buwaya's Egg",2329,15;
	monster .@map$,0,0,"Seaweed",2331,5;
	monster .@map$,0,0,"Buwaya's Slave",2330,5;
	end;
}
