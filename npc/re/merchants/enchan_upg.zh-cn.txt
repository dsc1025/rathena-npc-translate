//===== rAthena Script =======================================
//= Upgrade Weapon Enchants
//===== By: ==================================================
//= Skorm
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds enchantments to Upgrade weapons.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Standardizing, grammar and bug fixes. [Euphy]
//= 1.2 Moved to Cash Mall [Lemongrass]
//===== Translated By: =======================================
//= dsc
//============================================================

// Main NPC :: 201105_luk_enc
//============================================================
itemmall,29,71,3	script	Devil Enchant Master#prq	63,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您携带的物品过多，请减少携带，然后再回来！";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您超出了体重限制，请减轻重量，然后再回来！";
		close;
	}
	mes "[恶魔附魔大师]";
	mes "是的？";
	mes "你在找我吗？";
	next;
	switch(select("这是第一次见到你！：听说你是最棒的！：请初始化附魔。")) {
	case 1:
		mes "[恶魔附魔大师]";
		mes "哈哈哈~当然，我不是那种单纯与人打交道的人。";
		next;
		mes "[恶魔附魔大师]";
		mes "包括你！即使你给我很多钱，我也不会简单地为你着迷！！";
		next;
		mes "[恶魔附魔大师]";
		mes "你看到我的标题了吗？很少有人有这个头衔是有原因的！";
		next;
		mes "[恶魔附魔大师]";
		mes "只有当你带上^0000ff附魔书^000000时我才会附魔！";
		next;
		mes "[恶魔附魔大师]";
		mes "不然我也不会为你着迷......";
		close;
	case 2:
		if (!countitem(6484)) {
			mes "[恶魔附魔大师]";
			mes "你在听我说话吗？只有你带了附魔书我才会为你做事！";
			close;
		}
		set .@select,1;
		break;
	case 3:
		mes "[恶魔附魔大师]";
		if (Zeny < 100000) {
			mes "初始化将花费 100,000 zeny。看来你的zeny还不够……";
			close;
		}
		mes "初始化需要花费100,000 zeny并确认武器是否附魔！";
		next;
		if (select("让我考虑一下：现在就初始化吧！") == 1) {
			mes "[恶魔附魔大师]";
			mes "确认后再回来！";
			close;
		}
		set .@select,2;
		break;
	}
	set .@part, EQI_HAND_R;

	mes "[恶魔附魔大师]";
	if (!getequipisequiped(.@part)) {
		mes "你想移除附魔装备吗？";
		close;
	}
	setarray .@equip_card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	// if (!getequipisequiped(.@part)) {
	// 	mes "It is dangerous to remove equipment during enchant process!";
	// 	close;
	// }
	set .@equip_id, getequipid(.@part);
	.@equip_refine = getequiprefinerycnt(.@part);
	set .@item$, "|1292|1394|1491|1585|2015|13071|13115|16019|18112|21000|";
	if (!compare(.@item$,"|"+.@equip_id+"|")) {
		mes "我现在不想碰你的装备！";
		close;
	}

	if (.@select == 1) {
		if (!countitem(6484)) {
			mes "你在听我说话吗？只有你带了附魔书我才会为你做事！";
			close;
		}
		mes "您想要附魔哪种类型的效果？";
		next;
		switch(select("让我想想：物理系列：魔法系列")) {
		case 1:
			mes "[恶魔附魔大师]";
			mes "改变主意后再回来吧！";
			close;
		case 2:
			set .@enc_type,1;
			break;
		case 3:
			set .@enc_type,2;
			break;
		}
		mes "[恶魔附魔大师]";
		if (.@equip_card[3]) {
			mes "这件装备已经被附魔了！您需要先进行初始化，然后才能再次对其进行附魔。";
			close;
		}
		mes "附魔过程可能会失败，^ff0000会降低部分精炼度^000000，但插槽卡和武器不会损坏！您确定要继续吗？";
		next;
		if (select("下次！：现在开始！") == 1) {
			mes "[恶魔附魔大师]";
			mes "决定后再回来吧！";
			close;
		}
		if (.@equip_card[3]) {
			mes "[恶魔附魔大师]";
			mes "好像有问题，我看看。";
			close;
		}

		if (.@enc_type == 1) { // Physical Series
			set .@i, rand(1,1300);
			     if (.@i < 51)    set .@enchant,4734; //Agility5
			else if (.@i < 76)    set .@enchant,4735; //Agility6
			else if (.@i < 88)    set .@enchant,4736; //Agility7
			else if (.@i < 93)    set .@enchant,4737; //Agility8
			else if (.@i < 95)    set .@enchant,4738; //Agility9
			else if (.@i < 96)    set .@enchant,4739; //Agility10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4704; //Strength5
			else if (.@i < 341)   set .@enchant,4705; //Strength6
			else if (.@i < 366)   set .@enchant,4706; //Strength7
			else if (.@i < 378)   set .@enchant,4707; //Strength8
			else if (.@i < 383)   set .@enchant,4708; //Strength9
			else if (.@i < 384)   set .@enchant,4709; //Strength10
			else if (.@i < 434)   set .@enchant,4754; //Luck5
			else if (.@i < 459)   set .@enchant,4755; //Luck6
			else if (.@i < 471)   set .@enchant,4756; //Luck7
			else if (.@i < 476)   set .@enchant,4757; //Luck8
			else if (.@i < 478)   set .@enchant,4758; //Luck9
			else if (.@i < 479)   set .@enchant,4759; //Luck10
			else if (.@i < 679)   set .@enchant,4744; //Vitality5
			else if (.@i < 779)   set .@enchant,4745; //Vitality6
			else if (.@i < 829)   set .@enchant,4746; //Vitality7
			else if (.@i < 854)   set .@enchant,4747; //Vitality8
			else if (.@i < 866)   set .@enchant,4748; //Vitality9
			else if (.@i < 867)   set .@enchant,4749; //Vitality10
			else if (.@i < 967)   set .@enchant,4808; //Fighting_Spirit4
			else if (.@i < 992)   set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 1092)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1117)  set .@enchant,4836; //Expert_Archer5
			else if (.@i < 1217)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1242)  set .@enchant,4836; //Expert_Archer5
			else set .@enchant,0;
		} else if (.@enc_type == 2) { // Magical Series
			set .@i, rand(1,1200);
			     if (.@i < 51)    set .@enchant,4714; //Inteligence5
			else if (.@i < 76)    set .@enchant,4715; //Inteligence6
			else if (.@i < 88)    set .@enchant,4716; //Inteligence7
			else if (.@i < 93)    set .@enchant,4717; //Inteligence8
			else if (.@i < 95)    set .@enchant,4718; //Inteligence9
			else if (.@i < 96)    set .@enchant,4719; //Inteligence10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4734; //Agility5
			else if (.@i < 341)   set .@enchant,4735; //Agility6
			else if (.@i < 366)   set .@enchant,4736; //Agility7
			else if (.@i < 378)   set .@enchant,4737; //Agility8
			else if (.@i < 383)   set .@enchant,4738; //Agility9
			else if (.@i < 384)   set .@enchant,4739; //Agility10
			else if (.@i < 484)   set .@enchant,4754; //Luck5
			else if (.@i < 534)   set .@enchant,4755; //Luck6
			else if (.@i < 559)   set .@enchant,4756; //Luck7
			else if (.@i < 571)   set .@enchant,4757; //Luck8
			else if (.@i < 576)   set .@enchant,4758; //Luck9
			else if (.@i < 577)   set .@enchant,4759; //Luck10
			else if (.@i < 777)   set .@enchant,4744; //Vitality5
			else if (.@i < 877)   set .@enchant,4745; //Vitality6
			else if (.@i < 927)   set .@enchant,4746; //Vitality7
			else if (.@i < 952)   set .@enchant,4747; //Vitality8
			else if (.@i < 964)   set .@enchant,4748; //Vitality9
			else if (.@i < 969)   set .@enchant,4749; //Vitality10
			else if (.@i < 1069)  set .@enchant,4812; //Spell4
			else if (.@i < 1094)  set .@enchant,4826; //Spell5
			else if (.@i < 1119)  set .@enchant,4761; //Matk2
			else if (.@i < 1124)  set .@enchant,4806; //Matk3
			else set .@enchant,0;
		} else {
			mes "[恶魔附魔大师]";
			mes "唔！该商品有问题，请重新检查！";
			close;
		}
		mes "[恶魔附魔大师]";
		if (.@equip_card[3]) {
			mes "这件物品已经被施了魔法！";
			close;
		}
		if (!countitem(6484)) {
			mes "你在听我说话吗？只有你带了附魔书我才会为你做事！";
			close;
		}
		if (.@enchant == 0) {
			specialeffect EF_SHIELDCHARGE;
			mes "哦！难以置信！！失败了！！请再来吧！";
			set .@lost_refine, rand(0,.@equip_refine);
			set .@equip_refine, .@equip_refine - .@lost_refine;
		} else {
			specialeffect EF_REPAIRWEAPON;
			mes "插槽^9900004^000000已被施魔法！";
		}
		delitem 6484,1; //Enchant_Book

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@enchant
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@enchant;

		if (.@lost_refine) {
			next;
			mes "[恶魔附魔大师]";
			mes "该故障已将精炼度降低了"+callfunc("F_InsertPlural",.@lost_refine,"级别")+"！不要沮丧！";
		}
		close;
	} else if (.@select == 2) {
		if (Zeny < 100000) {
			mes "你需要带一些钱来初始化！";
			close;
		}
		if (getiteminfo(.@equip_card[3], ITEMINFO_SUBTYPE) != CARD_ENCHANT) {
			mes "此物品未附魔！";
			close;
		}
		if (!getequipisequiped(.@part)) {
			mes "现在还脱装备吗？";
			close;
		}
		specialeffect EF_REPAIRWEAPON;
		mes "我初始化了附魔效果。";
		set Zeny, Zeny - 100000;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] 0
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],0;

		close;
	} else {
		mes "看来你选错工作了？？";
		close;
	}
}
