//===== rAthena Script =======================================
//= Eden iRO NPCs
//===== By: ==================================================
//= -SkittleNugget-
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Eden scripts custom to iRO (disabled by default).
//= You may need iRO's data.grf to view some of the items.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Edited and added more NPCs, lots of placeholders. [Euphy]
//= 1.2 Added VIP NPCs and features. [Euphy]
//= 1.3 Added Mail Annex Station scripts, commented out. [Euphy]
//= 1.4 Added Merry Badger. [Euphy/-SkittleNugget-]
//===== Translated By: =======================================
//= dsc
//============================================================

// Acolyte Warper (part of other/acolyte_warp.txt)
//============================================================
moc_para01,14,32,5	script	Aperture#acolytewarp	90,{
	mes "[光圈]";
	mes "你好，冒险家。";
	mes "我一直在研究米德加德各地的魔法，以升级我认为是侍僧职业最伟大的技能之一。";
	next;

	if (is_function("VIP_iRO_Acolyte"))
		callfunc "VIP_iRO_Acolyte","[Aperture]","Prontera";

	mes "[光圈]";
	mes "我是唯一一个获得 10 级传送门技能的侍僧！";
	emotion ET_THINK, playerattached();
	next;
	mes "[光圈]";
	mes "这是正确的！和...";
	mes "我向你保证，我不会忘记我已经记住的地点。";
	mes "总有一天我会提升自己的技能，随心所欲地扭曲~";
	next;
	mes "[光圈]";
	mes "我愿意带你去我已经记住的许多地点，只需支付少量费用。";
	mes "您想使用这项服务吗？";
	next;
	switch(select("是：否")) {
	case 1:
		mes "[光圈]";
		mes "您想去哪里？";
		mes "祝您旅途愉快。";
		next;

		setarray .@towns$[0], "Prontera", "Izlude", "Geffen", "Payon", "Morocc", "Alberta", "Al De Baran", "Comodo", "Umbala", "Juno", "Einbroch", "Lighthalzen", "Hugel", "Rachel";
		setarray .@cost[0],          600,      600,     1200,    1200,     1200,      1800,          2200,     2200,     2200,   1800,       2200,          2200,    2200,     2200;

		set .@size, getarraysize(.@towns$);
		for(set .@i,0; .@i<.@size; set .@i,.@i+1)
			set .@menu$, .@menu$+.@towns$[.@i]+" -> "+.@cost[.@i]+"z:";
		set .@i, select(.@menu$+"取消")-1;
		if (.@i == .@size) {
			mes "[光圈]";
			mes "当你需要扭曲时回来。";
			close;
		}
		if (Zeny < .@cost[.@i]) {
			mes "[光圈]";
			mes "抱歉，但你没有";
			mes "足够的 zeny 进行传送";
			mes "服务。传送费用";
			mes "到"+.@towns$[.@i]+"是"+.@cost[.@i]+" zeny。";
			close;
		}
		specialeffect2 EF_READYPORTAL;
		specialeffect2 EF_TELEPORTATION;
		specialeffect2 EF_PORTAL;
		next;
		set Zeny, Zeny - .@cost[.@i];
		switch(.@i) {
			case 0: warp "prontera",116,72; break;
			case 1: warp "izlude",128,98; break;
			case 2: warp "geffen",120,39; break;
			case 3: warp "payon",161,58; break;
			case 4: warp "morocc",156,46; break;
			case 5: warp "alberta",117,56; break;
			case 6: warp "aldebaran",168,112; break;
			case 7: warp "comodo",209,143; break;
			case 8: warp "umbala",100,154; break;
			case 9: warp "yuno",158,125; break;
			case 10: warp "einbroch",67,195; break;
			case 11: warp "lighthalzen",159,90; break;
			case 12: warp "hugel",98,150; break;
			case 13: warp "rachel",119,135; break;
		}
		close;
	case 2:
		mes "[光圈]";
		mes "当你需要扭曲时回来。";
		close;
	}
}

function	script	VIP_iRO_Acolyte	{
	if (!vip_status(VIP_STATUS_ACTIVE)) {
		mes getarg(0);
		mes "但是，我的全部权力只有那些拥有 VIP 的人才能使用。";
		mes "不过我仍然可以为您提供前往"+getarg(1)+"的传送。";
		next;
		switch(select(getarg(0)+"-> 1800z：取消")) {
		case 1:
			if (Zeny < 1800) {
				mes getarg(0);
				mes "抱歉，但你没有";
				mes "足够的 zeny 进行传送";
				mes "服务。传送费用";
				mes "到 "+getarg(1)+" 是 1,800 zeny。";
				close;
			}
			specialeffect2 EF_READYPORTAL;
			specialeffect2 EF_TELEPORTATION;
			specialeffect2 EF_PORTAL;
			next;
			set Zeny, Zeny - 1800;
			if (getarg(1) == "Juno")
				warp "yuno",158,125;
			else
				warp "prontera",116,72;
			close;
		case 2:
			close;
		}
	}
	return;
}


// Primo d'Buffer, VIP stat bonuses
//============================================================
moc_para01,26,15,3	script	Primo d'Buffer	790,{
	mes "[Primo d'Buffer]";
	mes "你好冒险家，你看起来很想出去拯救世界！";
	if (!vip_status(VIP_STATUS_ACTIVE)) {
		mes "我还可以看出，您还没有获得“^6666ffVIP 状态^000000”的祝福。";
		next;
		mes "[Primo d'Buffer]";
		mes "我的命令是帮助任何被标记为 VIP 的冒险家提供特殊增益！";
		mes "目前，如果您是 VIP，我将为您的所有统计数据提供 ^ff0000+7，持续 30 分钟^000000。";
		next;
		mes "[Primo d'Buffer]";
		mes "如果您想成为 VIP，您可以使用 WarpPortal ^FF8C42 为您的游戏帐户提供 VIP 状态。当然，成为VIP还有很多其他的好处，比如更高的经验值和掉率、更多的存储空间，以及像我这样的特殊buff NPC！";
		mes "旅途愉快！";
		close;
	}
	mes "我还可以告诉您，您有幸获得了“^007FFFVIP 状态^000000”。";
	next;
	mes "[Primo d'Buffer]";
	mes "太棒了！";
	mes "这里为您 30 分钟内的所有统计数据+7，以帮助您顺利完成旅程。当您需要补充统计数据时就回来吧！";
	close2;
	sc_start SC_FOOD_STR_CASH,1200000,7;
	sc_start SC_FOOD_AGI_CASH,1200000,7;
	sc_start SC_FOOD_VIT_CASH,1200000,7;
	sc_start SC_FOOD_DEX_CASH,1200000,7;
	sc_start SC_FOOD_INT_CASH,1200000,7;
	sc_start SC_FOOD_LUK_CASH,1200000,7;
	end;
}

// Clearchus, VIP mercenary rentals
//============================================================
moc_para01,53,30,3	script	Clearchus	470,{
	if (checkquest(62238,PLAYTIME) > -1 && checkquest(62238,PLAYTIME) < 2) {
		mes "您必须等待计时器结束才能雇用新的雇佣兵。";
		close;	
	}
	mes "[克利阿库斯]";
	mes "问候年轻的冒险家！我代表中庭雇佣兵团。";
	next;
	mes "[克利阿库斯]";
	mes "我们为有抱负的雇佣兵提供培训，将他们与像您这样的冒险家配对。";
	next;
	mes "[克利阿库斯]";
	mes "我们的组织有一万多人！现在让我看看您是否注册了这项服务。";
	next;
	if (!vip_status(VIP_STATUS_ACTIVE)) {
		mes "[克利阿库斯]";
		mes "不幸的是，您似乎不是 VIP，请获得 VIP 才能使用此服务。您可以通过与伊甸集团总部的爷爷交谈来获得临时 VIP 访问权限。";
		next;
		mes "[克利阿库斯]";
		mes "Vip 对于冒险家来说是一个非常有用的工具，可以访问额外的 Kafra 服务、折扣扭曲、独家内容以及使用 Primo d'Buffer。如果冒险家拥有完整的 VIP，他们甚至可以获得经验值、掉落物和存储空间提升！";
		close;
	}
	mes "[克利阿库斯]";
	mes "你的名字在名单上。";
	next;
	mes "[克利阿库斯]";
	mes "您希望雇佣兵具备哪些技能？";
	next;

	switch(select("剑士：弓箭手：Lancer")) {
	case 1:
		set .@base_id,12163; //SwordMercenary_Scroll
		break;
	case 2:
		set .@base_id,12153; //Bow_Mercenary_Scroll
		break;
	case 3:
		set .@base_id,12173; //SpearMercenary_Scroll
		break;
	}
	     if (BaseLevel < 11) set .@i,0;
	else if (BaseLevel < 21) set .@i,1;
	else if (BaseLevel < 31) set .@i,2;
	else if (BaseLevel < 41) set .@i,3;
	else if (BaseLevel < 51) set .@i,4;
	else if (BaseLevel < 61) set .@i,5;
	else if (BaseLevel < 71) set .@i,6;
	else if (BaseLevel < 81) set .@i,7;
	else if (BaseLevel < 91) set .@i,8;
	else set .@i,9;

	mes "[克利阿库斯]";
	mes "我心里只有佣兵！";
	next;
	mes "[克利阿库斯]";
	mes "现在玩得开心，听到了吗？";
	getitem .@base_id + .@i,1;
	if (isbegin_quest(62238) > 0)
		erasequest 62238;
	setquest 62238;
	close;
}

// Carrot & Red Potion Traders
//============================================================
moc_para01,166,51,3	script	Phelix#edco	85,{
	mes "[菲利克斯]";
	mes "哦...我从那个表情看出你想让我工作并给你肉和胡萝卜...";
	mes "好吧，我的朋友伊扎克带着一些红色药剂，但我只有一些胡萝卜。";
	mes "所以我可以用 ^0000CC1 根胡萝卜换 3 个果冻^000000。";
	next;
	mes "[菲利克斯]";
	mes "如果您对我的报价感兴趣，请给我买我提到的果冻。";
	next;
	select("请胡萝卜。");
	mes "[菲利克斯]";
	mes "好吧，让我们看看你得到了什么......";
	next;
	if (countitem(909) < 3) {
		mes "[菲利克斯]";
		mes "嗯，听着老兄，我说 1 胡萝卜 3 果冻……明白了吗？";
		close;
	}
	mes "[菲利克斯]";
	mes "还不错...";
	mes "你想要几个？";
	next;
	switch(select("尽可能多的，请。：我想选择。：没关系，我喜欢我的果冻。")) {
	case 1:
		set .@amount, countitem(909) / 3;
		break;
	case 2:
		set .@available, countitem(909) / 3;
		mes "[菲利克斯]";
		mes "你想要几个？";
		mes "^ff0000您最多有足够的" + .@available + "胡萝卜。^000000";
		input .@input;
		next;
		if (.@input < 1 || .@input > 10000) {
			mes "[菲利克斯]";
			mes "不想交易？很公平...";
			close;
		}
		if (.@input > .@available) {
			mes "[菲利克斯]";
			mes "伙计，我说过每 3 个果冻我就给你 1 个胡萝卜。";
			close;
		}
		set .@amount, .@input;
		break;
	case 3:
		mes "[菲利克斯]";
		mes "如果您不想交易，请不要打扰我。";
		close;
	}
	if (checkweight(515,.@amount) == 0) {
		mes "[菲利克斯]";
		mes "你超重了。";
		close;
	}
	mes "[菲利克斯]";
	mes "就这样啦~！正如我所承诺的。";
	mes "尽量不要把脸塞满。";
	delitem 909,.@amount*3; //Jellopy
	getitem 515,.@amount; //Carrot
	end;
}

moc_para01,163,51,7	script	Izaac#edco	86,{
	mes "[艾萨克]";
	mes "再会！";
	mes "你从怪物那里收集过什么奇怪的东西吗？";
	mes "我想要果冻、绒毛、贝壳、鸟毛、树根、虫皮和蛹之类的东西。";
	mes "我需要那些东西。";
	next;
	mes "[艾萨克]";
	mes "当然，我不会要求你免费给我那些东西。如果我用以下物品换取 ^CC00001 红色药水^000000：";
	mes "^00CC005 外壳^000000";
	mes "^0000CC10 绒毛^000000";
	mes "^00CC0010果冻^000000";
	mes "^0000CC6 树根^000000";
	mes "^00CC001 蠕虫剥皮^000000";
	mes "^0000CC7 鸟的羽毛^000000";
	mes "^00CC006 蛹^000000";
	next;
	switch(select("当然！：我认为这是诈骗！：不，谢谢。")) {
	case 1:
		mes "[艾萨克]";
		mes "那么哪些项目可以";
		mes "你想带我来吗？";
		next;

		setarray .@items[0], 935, 914, 909, 902, 955, 916, 915;
		setarray .@count[0],   5,  10,  10,   6,   1,   7,   6;

		set .@size, getarraysize(.@items);
		for(set .@i,0; .@i<.@size; set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@items[.@i])+":";
		set .@select, select(.@menu$+"取消")-1;
		if (.@select == .@size) {
			mes "[艾萨克]";
			mes "当然啦~";
			mes "没问题。";
			close;
		}
		set .@item, .@items[.@select];
		set .@item$, getitemname(.@item);
		set .@price, .@count[.@select];

		if (countitem(.@item) < .@price) {
			mes "[艾萨克]";
			mes "嘿，都在哪里";
			mes "那"+.@item$+"那";
			mes "你答应过我吗？";
			mes "给我"+.@item$+"！";
			close;
		}
		mes "[艾萨克]";
		mes "好的，让我检查一下";
		mes "多少"+.@item$;
		mes "你有你的。";
		mes "唔...";
		next;
		set .@amount, countitem(.@item) / .@price;
		set .@trade_amount, .@amount * .@price;
		mes "[艾萨克]";
		mes "你有";
		mes "总共"+countitem(.@item)+" "+.@item$+"...";
		mes "对于所有这些，我可以给你";
		mes "总共"+.@amount+"红色药水。";
		next;
		mes "[艾萨克]";
		mes "你怎么说？";
		mes "我们有协议吗？";
		next;
		if(select("交易：没有交易。") == 2) {
			mes "[艾萨克]";
			mes "呵呵~";
			mes "好吧。";
			mes "虽然是";
			mes .@item$ + "更有用";
			mes "像你这样的冒险家？";
			close;
		}
		if (checkweight(501,.@amount) == 0) { //custom check
			mes "[艾萨克]";
			mes "你超重了。";
			close;
		}
		delitem .@item,.@trade_amount;
		getitem 501,.@amount; //Red_Potion
		mes "[艾萨克]";
		mes "就这样吧！";
		mes "检查有多少"+.@item$;
		mes "我给你了，应该不错。";
		mes "谢谢，真是划算啊~";
		close;
	case 2:
		mes "[艾萨克]";
		mes "敲诈……？";
		mes "如果你检查一下市场价值";
		mes "在被交易的物品中，我实际上是得到的那个";
		mes "在这里被扯掉了。";
		close;
	case 3:
		mes "[艾萨克]";
		mes "好的，没问题。";
		mes "但如果你改变主意，请回到我身边。";
		close;
	}
}

// Safe to 7 Certificate Exchanger
//============================================================
moc_para01,50,39,4	script	Eve Natalia	882,{
	mes "[伊芙娜塔莉亚]";
	mes "所以我在城里呆了一段时间，看看是否可以与 MVP 猎人进行一些交易！";
	mes "我愿意拿一些 MVP 装备来交换我的这些升级证书；";
	mes "如果一切顺利的话我会开拓更多的商机！";
	next;
	mes "[伊芙娜塔莉亚]";
	mes "那么，你们有我正在寻找的可以随机获得 Safe to 7 证书的装备吗？";
	next;
	switch(select("什么装备？：是的，我是！：没关系。")) {
	case 1:
		mes "[伊芙娜塔莉亚]";
		mes "尼德霍古尔的暗影装束 4 随机安全到 7 证书";
		mes "Valkyrja's Shield 4 随机安全到 7 证书";
		mes "瓦尔基里安铠甲 5 随机安全到 7 证书";
		mes "恶魔长袍 5 随机安全到 7 证书";
		mes "Diabolus Armor 2随机安全到7个证书";
		mes "Diabolus Boots 2 随机安全到 7 个证书";
		mes "Diabolus Manteau 4 随机安全至 7 个证书";
		mes "Twin Edge of Naght Sieger Blue 2 随机安全至 7 个证书";
		mes "Twin Edge of Naght Sieger Red 2 随机安全至 7 个证书";
		close;
	case 2:
		mes "[伊芙娜塔莉亚]";
		mes "我将询问您想要交易哪一件装备，请确认您库存中的第一个装备是您想要放弃的装备。";
		mes "^CC0000因此，如果它被卡片、升级、附魔等，您应该在我们继续之前将其放入存储中。^000000";
		next;
		set .@i, select("等一下：因德霍格暗影装 4：女武神的盾牌 4：女武神铠甲 5：迪博洛斯长袍 5：迪博洛斯铠甲 2：迪博洛斯靴子 2：迪博洛斯披风 攻城红 2")-2;
		if (.@i == -1)
			break;

		setarray .@items[0],   2554, 2115, 2357, 2374, 2375, 2433, 2537, 13412, 13413;
		setarray .@tickets[0],    4,    4,    5,    5,    2,    2,    4,     2,     2;

		set .@item, .@items[.@i];
		set .@amount, .@tickets[.@i];

		mes "[伊芙娜塔莉亚]";
		mes "我看到你有"+countitem(.@item)+" "+getitemname(.@item)+".";
		mes "那么您想用库存中的第一个证书来交换^00CC00"+.@amount+"随机安全到7个证书^000000吗？";
		mes "如果需要，请告诉我“1”；如果取消，请告诉我“0”。";
		next;
		input .@input;
		if (.@input < 0 || .@input > 1) {
			mes "[伊芙娜塔莉亚]";
			mes "是0还是1，没那么难。";
			close;
		} else if (.@input == 0) {
			mes "[伊芙娜塔莉亚]";
			mes "最好在交易前确保祝您有美好的一天。";
			close;
		}
		if (countitem(.@item) == 0) {
			mes "[伊芙娜塔莉亚]";
			mes "你连1个都没有……别再浪费我的时间了。";
			close;
		}
		mes "[伊芙娜塔莉亚]";
		mes "哦，完美，您将获得 ^CC0000" + .@amount + "^000000 安全到 7 个证书！";
		delitem .@item,1;
		for(set .@i,0; .@i<.@amount; set .@i,.@i+1) {
			// Note: iRO lists item 6235 as "Safe to 7 Headgear Certificate", but it's Guarantee_Armor_6Up in our database.
			if (rand(2))
				getitem 6230,1; //Guarantee_Weapon_7Up
			else
				getitem 6234,1; //Guarantee_Armor_7Up
		}
		close;
	case 3:
		break;
	}
	mes "[伊芙娜塔莉亚]";
	mes "公平地说，如果您将来与我有生意，请不要害羞！";
	close;
}

// Bubble Gum & Battle Manual Exchanger
//============================================================
moc_para01,48,184,2	script	Gum & Manual Exchanger	562,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "你现在携带的物品太多了。";
		close;	
	}
	mes "[交易机]";
	mes "插入泡泡糖和/或战斗手册以换取更高效率的口香糖和手册。";
	next;
	mes "[交易机]";
	mes "汇率：";
	mes "2 泡泡糖 -> 1 HE 泡泡糖";
	mes "2 战斗手册 -> 1 HE 战斗手册";
	mes "4 HE 战斗手册 -> 1 战斗手册 X3";
	mes "3厚战斗手册->2战斗手册X3";
	mes "你想用什么来交换？";
	next;
	switch(select("HE泡泡糖:HE 战斗手册:1 战斗手册 X3:2 战斗手册 X3:1 服装 海盗:无")) {
	case 1:
		callsub L_Exchange,12210,2,12412,1;
		break;
	case 2:
		callsub L_Exchange,12208,2,12411,1;
		break;
	case 3:
		callsub L_Exchange,12411,4,14545,1;
		break;
	case 4:
		callsub L_Exchange,12312,3,14545,2;
		break;
	case 5:
		mes "[交易机]";
		mes "我可以给你 1 战斗手册 X3 换 1 服装海盗。";
		next;
		if (countitem(19619) == 0) {
			mes "[交易机]";
			mes "你没有海盗服装帽子来进行这项交易。";
			close;
		}
		next;
		if(select("是的，给我吧！：不，先别等。") == 2)
			close;
		// Unofficial dialogue.
		mes "[交易机]";
		mes "您将所需的物品放入机器中...";
		next;
		mes "[交易机]";
		mes "机器给了你一件物品作为回报！";
		delitem 19619,1; //C_Corsair
		getitem 14545,1; //Battle_Manual_X3
		close;
	case 6:
		close;
	}

//callsub L_Exchange,<Insert Item>,<Amount>,<Return Item>,<Amount>
L_Exchange:
	mes "[交易机]";
	mes "您想要多少个"+getitemname(getarg(2))+"？";
	mes "我最多可以给你60。";
	mes "输入 0 取消。";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[交易机]";
		mes "取消。";
		close;
	} else if (.@amount < 0 || .@amount > 60) {
		mes "[交易机]";
		mes "我说最多只能60个。";
		close;
	}
	mes "[交易机]";
	mes "总共是 "+.@amount+" "+getitemname(getarg(2))+".";
	mes "这是正确的吗？";
	next;
	if(select("是的，把它们给我！：不，先别等。") == 2)
		close;
	set .@cost, getarg(1) * .@amount;
	set .@total_amount, getarg(3) * .@amount;
	if (countitem(getarg(0)) < .@cost) {
		mes "[交易机]";
		mes "您没有足够的 "+getitemname(getarg(0))+" 来进行该交易。";
		close;
	}
	delitem getarg(0),.@cost;
	getitem getarg(2),.@total_amount;
	close;
}

// Merry Badger
//============================================================
moc_para01,13,22,4	script	Merry Badger#xch	882,{
	disable_items;
	mes "[风流獾]";
	mes "你好呀！  我是风流獾，我被雇佣来交换^00CC00伊甸园功绩徽章^000000！";
	next;
	mes "[风流獾]";
	mes "我可以拿走你的功绩徽章，给你一些有用的东西，或者我可以拿走你的一些东西，给你一些伊甸园功绩徽章。";
	mes "当我获得更多授权时，我可以提供更多东西与您交易，所以在访问伊甸集团时一定要记住我。";
	next;
	// switch( select( "Redeem Badges", "Trade in FOR badges", "Exchange Rates", "15 Job Manuals - 1 JM Box", "Sky Fortress Tickets", "Weekly Special" ) ) {// Sky Fortress Tickets meaningless without the instance
	switch( select( "兑换徽章", "FOR徽章交易", "汇率", "15本工作手册 - 1 JM Box", "", "每周特价" ) ) {
	case 1:
		setarray .@menu_list$[0],
			"4 EMB for 10 KVM Badge",
			"4 EMB for 10 Bravery Medals",
			"4 EMB for 10 Valor Medals", 
			"8 EMB for 1 Reset Stone",
			"5 EMB for Battle Manual Buff",
			"5 EMB for Job Battle Manual Buff",
			"10 EMB for HE Battle Manual Buff",
			"20 EMB for 3X Battle Manual Buff",
			"5 EMB for Bubblegum Buff", 
			"10 EMB for HE Bubblegum Buff",
			"",	// "5 EMB for Miracle Buff" (item doesn't exist in db)
			"1 EMB for Yggdrasil Leaf",
			"",	// "5 EMB for Rental Wing of Beelzebub" (item doesn't exist in db)
			"5 EMB for Rental Halter Lead",
			"5 EMB for 20 Blue Herbs",
			"Token of Siegfried",
			"Nothing Now";
		.@menu$ = implode( .@menu_list$, ":" );
		while(1) {
			mes "[风流獾]";
			mes "当然，我目前有多种徽章可供选择！";
			next;
			switch( select(.@menu$) ) {
			case 1:// "4 EMB for 10 KVM Badge"
				callsub L_Exchange,4,6376,10;// KVM_Badge
				break;
			case 2:// "4 EMB for 10 Bravery Medals"
				callsub L_Exchange,4,7828,10;// BF_Badge1
				break;
			case 3:// "4 EMB for 10 Valor Medals"
				callsub L_Exchange,4,7829,10;// BF_Badge2
				break;
			case 4:// "8 EMB for 1 Reset Stone"
				callsub L_Exchange,8,6320,1;// Premium_Reset_Stone
				break;
			case 5:// "5 EMB for Battle Manual Buff"
				callsub L_Exchange,5,12208,1;// Battle_Manual
				break;
			case 6:// "5 EMB for Job Battle Manual Buff"
				callsub L_Exchange,5,14592,1;// Job_Manual50
				break;
			case 7:// "10 EMB for HE Battle Manual Buff"
				callsub L_Exchange,10,12411,1;// HE_Battle_Manual
				break;
			case 8:// "20 EMB for 3X Battle Manual Buff"
				callsub L_Exchange,20,14545,1;// Battle_Manual_X3
				break;
			case 9:// "5 EMB for Bubblegum Buff"
				callsub L_Exchange,5,12210,1;// Bubble_Gum
				break;
			case 10:// "10 EMB for HE Bubblegum Buff"
				callsub L_Exchange,10,12412,1;// HE_Bubble_Gum
				break;
			case 11:// "5 EMB for Miracle Buff"
				.@item_id = 22819;
				if (countitem(6495) < 5) {
					mes "[风流獾]";
					mes "您没有足够的^00CC00伊甸功绩徽章^000000。让我们重新开始吧！";
					break;
				}
				mes "[风流獾]";
				mes "好的，我可以给您 ^ff0000" + getitemname(.@item_id) + "^000000 换取 ^0066CC 5 个伊甸园功绩徽章^000000。";
				mes "你想要_____吗？";
				next;
				if (select("是的", "不") == 2) {
					mes "[风流獾]";
					mes "我明白了，让我们看看你是否想要一些不同的东西！";
					break;
				}
				mes "[风流獾]";
				mes "就这样吧！你还想要什么？";
				delitem 6495, 5;// Para_Logro_Badge
				consumeitem .@item_id;
				break;
			case 12:// "1 EMB for Yggdrasil Leaf"
				callsub L_Exchange,1,610,1;// Leaf_Of_Yggdrasil
				break;
			case 13:// "5 EMB for Rental Wing of Beelzebub"
				.@item_id = 14645;
				if (countitem(6495) < 5) {
					mes "[风流獾]";
					mes "您没有足够的^00CC00伊甸功绩徽章^000000。让我们重新开始吧！";
					break;
				}
				mes "[风流獾]";
				mes "好的，我可以给您 ^ff0000" + getitemname(.@item_id) + "^000000 换取 ^0066CC 5 个伊甸园功绩徽章^000000。";
				mes "你想要_____吗？";
				next;
				if (select("是的", "不") == 2) {
					mes "[风流獾]";
					mes "我明白了，让我们看看你是否想要一些不同的东西！";
					break;
				}
				mes "[风流獾]";
				mes "就这样吧！你还想要什么？";
				delitem 6495, 5;// Para_Logro_Badge
				rentitem .@item_id, 604800;	// 7 days
				break;
			case 14:// "5 EMB for Rental Halter Lead"
				callsub L_Exchange,5,17162,1;// Boarding_Halter_Box7
				break;
			case 15:// "5 EMB for 20 Blue Herbs"
				callsub L_Exchange,5,510,20;// Blue_Herb
				break;
			case 16:// Token of Siegfried
				if (vip_status(VIP_STATUS_ACTIVE) == false) {
					mes "[风流獾]";
					mes "VIP 服务为任何 VIP 用户提供这些服务！";
					close;
				}
				// custom from here
				.@date = gettime(DT_YYYYMMDD);
				if (.@date <= eden_iro_token_date) {
					mes "[风流獾]";
					mes "您今天已经获得了令牌！";
					close;
				}
				mes "[风流獾]";
				mes "就这样吧！你还想要什么？";
				eden_iro_token_date = .@date;
				getitem 7621,10;// Token_Of_Siegfried
				close;
			case 17:// Nothing Now
				mes "[风流獾]";
				mes "精彩的！";
				mes "如果你想兑换伊甸功绩徽章，我会在这里！";
				close;
			}
			next;
		}
	case 2:
		mes "[风流獾]";
		mes "目前我的选择有限，但我可以提供以下内容来获得 1 个伊甸园功绩徽章！";
		next;
		switch( select( "等物品", "设备", "取消" ) ) {
		case 1:
			while(1) {
				if (select( "5x 旧紫色盒子", "什么都没有" ) == 2) {
					mes "[风流獾]";
					mes "精彩的！";
					mes "如果你想兑换伊甸功绩徽章，我会在这里！";
					close;
				}
				while(1) {
					if (countitem(617) < 5) {
						mes "[风流獾]";
						mes "您没有足够的钱来获得 1 个伊甸园功绩徽章。让我们重新开始吧！";
						break;
					}
					mes "[风流獾]";
					mes "好的，我可以给你 ^CC00001 个伊甸功绩徽章^000000 来换取 ^0000CC 5 个旧紫色盒子^000000。你想要_____吗？";
					next;
					if (select( "是的", "不" ) == 2) {
						mes "[风流獾]";
						mes "我明白了，让我们看看你是否想要一些不同的东西！";
						break;
					}
					else {
						mes "[风流獾]";
						mes "就这样吧！  你还想要什么？";
						delitem 617,5;// Old_Violet_Box
						getitem 6495,1;// Para_Logro_Badge
						next;
					}
				}
				next;
			}
		case 2:
			while(1) {
				mes "[风流獾]";
				mes "我会拿走你库存中最上面的物品！确保您没有任何不想交换的物品。";
				next;
				switch( select( "巨型面虫蛇皮", "巨型面虫蛇皮[1]", "无" ) ) {
				case 1:
					.@item_req = 20717;// FaceWorm_Skin
					.@item_name$ = "Giant Faceworm Snake Skin";
					.@amount_reward = 5;
					break;
				case 2:
					.@item_req = 20718;// FaceWorm_Skin_
					.@item_name$ = "Giant Faceworm Snake Skin [1]";
					.@amount_reward = 7;
					break;
				case 3:
					mes "[风流獾]";
					mes "精彩的！";
					mes "如果你想兑换伊甸功绩徽章，我会在这里！";
					next;
					mes "[风流獾]";
					mes "好吧，没关系，如果你想看看我提供什么，就问吧！";
					close;
				}
				while(1) {
					if (countitem(.@item_req) < 1) {
						mes "[风流獾]";
						mes "您没有足够的钱来获得 1 个伊甸园功绩徽章。让我们重新开始吧！";
						break;
					}
					mes "[风流獾]";
					mes "好的，我可以给您 ^ff0000" + .@amount_reward + " 伊甸园功绩徽章 ^000000 的 ^00ff00" + .@item_name$ + "^000000。你想要_____吗？";
					next;
					if (select( "是的", "不" ) == 2) {
						mes "[风流獾]";
						mes "我明白了，让我们看看你是否想要一些不同的东西！";
						break;
					}
					delitem .@item_req, 1;
					getitem 6495, .@amount_reward;// Para_Logro_Badge
					mes "[风流獾]";
					mes "就这样吧！你还喜欢什么？";
					next;
				}
				next;
			}
		case 3:
			mes "[风流獾]";
			mes "好吧，没关系，如果你想看看我提供什么，就问吧！";
			close;
		}
	case 3:
		mes "[风流獾]";
		mes "这就是我给你的^00CC00伊甸功绩徽章^000000！";
		mes "好的，我有一个特别的 3 功绩徽章。这周我会给你^00CC0040个轻颗粒^000000，^00CC0040个火药^000000，^0000CC3个伊兹多尔^000000，^00CC0010个多刺果^000000，^0000CC1个曼陀罗花盆^000000和^00CC003根老树枝^000000。";
		mes "^00CC002 个 Eden 功绩徽章^000000 = ^0000CC10x KVM 徽章^000000";
		mes "^00CC002 伊甸功绩徽章^000000 = ^0000CC10x 勇敢勋章^000000";
		mes "^00CC002 伊甸功绩徽章^000000 = ^0000CC10x 勇气勋章^000000";
		mes "^00CC005 伊甸功绩徽章^000000 = ^0000CC5x 中等生命药水^000000";
		mes "^00CC005 伊甸园功绩徽章^000000 = ^0000CC5x 人寿保险^000000";
		mes "^00CC005 伊甸功绩徽章^000000 = ^0000CC5x 齐格弗里德令牌^000000";
		mes "^00CC007 伊甸功绩徽章^000000 = ^0000CC1x 工作战斗手册^000000";
		mes "^00CC008 伊甸功绩徽章^000000 = ^0000CC1x 重置石^000000";
		next;
		mes "[风流獾]";
		mes "这就是你给我的东西，我可以得到^00CC001个伊甸功绩徽章^000000！";
		mes "^0000CC5x 旧紫色盒子^000000";
		close;
	case 4:
		mes "[风流獾]";
		mes "我很坚强地给出了这个，但你是值得的！";
		mes "您想用 15 份工作手册换取 1 盒可交易的 10 份工作手册吗？";
		mes "我知道 15 不等于 10，但是盒子很贵，你知道的。";
		next;
		if (countitem(14592) < 15) {
			mes "[风流獾]";
			mes "您没有 15 份或更多工作手册即可获得 1 盒！";
			close;
		}
		// custom dialogue below
		if (select("是的", "不") == 2) {
			mes "[风流獾]";
			mes "精彩的！";
			mes "如果你想兑换伊甸功绩徽章，我会在这里！";
			close;
		}
		mes "[风流獾]";
		mes "太棒了，你还想要什么？";
		delitem 14592,15; //Job_Manual50
		getitem 13990,1; //Job_Manual50_Box
		close;
	case 5:
		mes "[风流獾]";
		mes "我不太明白为什么，但我有一叠优惠券要卖。";
		mes "您想用 25 个伊甸功绩徽章兑换 1 张地下城门票吗？如果您是 VIP，您的门票将持续更长时间！";
		next;
		if (select( "是的", "不" ) == 2) {
			mes "[风流獾]";
			mes "我明白了，让我们看看你是否想要一些不同的东西！";
			close;
		}
		if (countitem(6495) < 25) {
			mes "[风流獾]";
			mes "你没有足够的徽章！";
			close;
		}
		delitem 6495,25;
		if (vip_status(VIP_STATUS_ACTIVE))
			getitem 17570,1;// Sky Fortress Ticket 3 Hour Box
		else
			getitem 17569,1;// Sky Fortress Ticket 1 Hour Box
		mes "[风流獾]";
		mes "就这样吧！你还想要什么？";
		close;
	case 6:
		while(1) {
			mes "[风流獾]";
			mes "每周特别节目是......";
			next;
			mes "[风流獾]";
			mes "好的，我有 3 个功勋徽章或 1,000,000 zeny 的特价。  这周我会给你^00CC0040个轻颗粒^000000，40个火药^000000，^0000CC3个伊兹多尔^000000";
			mes "^00CC0010个多刺果^000000、^0000CC1个曼陀罗花盆^000000和^00CC003个接骨木枝^000000。";
			next;
			switch( select( "徽章", "Zeny", "否" ) ) {
			case 1:
				if (countitem(6495) < 3) {
					mes "[风流獾]";
					mes "您没有足够的^00CC00伊甸功绩徽章^000000。让我们重新开始吧！";
					next;
					continue;
				}
				delitem 6495,3; //Para_Logro_Badge
				break;
			case 2:
				if (Zeny < 1000000) {
					mes "[风流獾]";
					mes "您没有足够的 ^00CC00Zeny^000000。让我们重新开始吧！";
					next;
					continue;
				}
				Zeny -= 1000000;
				break;
			case 3:
				mes "[风流獾]";
				mes "我明白了，让我们看看你是否想要一些不同的东西！";
				close;
			}
			getitem 7938,40;// Light_Granule
			getitem 6244,40;// Gun_Power
			getitem 709,3;	// Izidor
			getitem 576,10;	// Prickly_Fruit
			getitem 6217,1;	// Mandragora_Flowerpot
			getitem 7939,3;	// Elder_Branch
			mes "[风流獾]";
			mes "就这样吧！你还想要什么？";
			next;
		}
	}
	end;

//callsub L_Exchange,<badge cost>,<item id>,<amount>;
L_Exchange:
	if (countitem(6495) < getarg(0)) {
		mes "[风流獾]";
		mes "您没有足够的^00CC00伊甸功绩徽章^000000。让我们重新开始吧！";
		return;
	}
	mes "[风流獾]";
	mes sprintf("好的，我可以为您提供 ^ff0000%s^000000 来换取 ^0066CC%d 伊甸园功绩徽章^000000。",
				callfunc("F_InsertPlural",getarg(2),getitemname(getarg(1))), getarg(0));
	mes "你想要_____吗？";
	next;
	if (select("是的", "不") == 2) {
		mes "[风流獾]";
		mes "我明白了，让我们看看你是否想要一些不同的东西！";
		return;
	}
	mes "[风流獾]";
	mes "就这样吧！你还想要什么？";
	delitem 6495, getarg(0); //Para_Logro_Badge
	getitem getarg(1),getarg(2);
	return;
}

// Kafra Stacker
//============================================================
moc_para01,41,169,2	script	Kafra Stacker#eden	749,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 稍等！ -";
		mes "- 目前你携带的是 -";
		mes "- 物品重量过重。 -";
		mes "- 请稍后再回来 -";
		mes "- 将一些放入 Kafra 存储后。 -";
		close;
	}
	mes "[托玛]";
	mes "我可以拿走你的非堆叠 Kafra 消耗品";
	mes "并使它们整齐地叠放！";
	next;
	mes "[托玛]";
	mes "请理解我可以";
	mes "一次只能堆叠10组";
	mes "所以继续跟我说话直到";
	mes "每个都有 1 叠！";
	next;
	while(1) {
		if(select("做吧！：没关系。") == 2)
			break;
		// Something strange goes on here, haven't quite figured it out...
		mes "[托玛]";
		mes "十叠下来！";
		mes "还想再做一次吗？";
		next;
	}
	mes "[托玛]";
	mes "好吧，稍后。";
	close;
}

// Code Redeemer
//============================================================
moc_para01,38,162,2	script	Code the Redeemer#Give1	405,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "^0000ff稳住！";
		mes "您携带的物品过多，并且没有足够的库存空间来接收任何奖励。请腾出更多可用库存空间并回来接受挑战。^000000";
		close;
	}
	mes "[救赎者代码]";
	mes "欢迎，"+strcharinfo(0)+"！";
	mes "我一直在等你。";
	next;
	mes "[救赎者代码]";
	mes "我是所有的经销商";
	mes "冒险家喜欢的物品";
	mes "自己在网站上购买";
	mes "或作为促销活动获得。";
	next;
	mes "[救赎者代码]";
	mes "^0000FF如果列表为空，";
	mes "这意味着你有";
	mes "未购买或输入";
	mes "您在购买网站上获得的序列号。^000000";
	next;

	// If you have a code redemption system, write your SQL queries here.
	select("");

	close;
}

// Cash Shops
//============================================================
/* Due to the irregularity of iRO's databases, some of these sprites may not display properly. */

moc_para01,32,171,6	cashshop	Kafra Headgears#eden1	115,14024:500,14098:150,17081:300,14097:500,14094:1000,14017:400,14099:300
moc_para01,35,174,2	cashshop	Kafra Dye Shop#dyes2	114,6287:150
moc_para01,37,170,2	cashshop	Kafra Consumables#eden3	112,13810:5,14167:400,16556:100,14111:75,14075:25,16419:30,14107:70,14104:50,14112:25,14082:30,14091:60,14088:30,14085:60,14169:40,14170:40,14172:40,14173:40,14171:40,14174:40,14159:100,14160:100,14161:100,14162:100,14163:100:14164:100,16420:1500
moc_para01,32,174,2	cashshop	Kafra Rental Items#eden	721,16683:150,16682:50,14126:250,14125:250,16430:250,14137:250,14127:250,14118:250,14119:250,14132:250,16425:250,14124:250,14133:250,14130:250,16424:250,14136:250,14122:250,16426:250,14135:250,14134:250,14138:250,16428:250,16431:250,14139:250,14131:250,16429:250,16427:250
moc_para01,37,172,2	cashshop	Kafra Costume#eden5	860,16974:300,16972:600,16973:150,16975:150,16977:150,17014:150,17015:150
moc_para01,35,168,1	cashshop	Kafra Utility Shop#eden	113,16677:25,13990:400,16396:100,16394:100,17106:500,6241:10,6240:10,6225:15,6226:15,14002:5,6294:500,7776:50,7619:30,7620:30,12208:40,14156:400,12210:40,14158:400,14042:50,14044:50,14046:50,14048:50,14114:50,14166:40,16418:500,16381:400,14165:100,14157:100,14178:100

// Duplicate NPCs
//============================================================
/* Technically these NPC names are different, but it's not worth editing the original scripts. */

// Weapon/Armor Refiner "Normalson" (merchants/refine.txt)
moc_para01,14,185,4	script	Normalson#ed	826,{
	callfunc "refinemain","Normalson",0;
	end;
}

// Advanced Refiner "Suhnmi" (merchants/advanced_refiner.txt)
moc_para01,26,185,4	duplicate(Suhnbi#cash)	Suhnmi#eden	85

// HD Refiner "Blacksmith Mighty Hammer" (re/merchants/hd_refiner.txt)
// A combination of the +7~9 and +10 and up refiner, currently a placeholder.
moc_para01,38,185,4	duplicate(MightyHammer)	Mighty Hammer#ed	826

// Refiner Effect
-	script	#eden_refine_effect	-1,{
OnInit:
	initnpctimer;
	end;
OnTimer5000:
	specialeffect EF_REFINEOK,AREA,"Suhnmi#eden";
	specialeffect EF_REFINEOK,AREA,"Mighty Hammer#ed";
	initnpctimer;
	end;
}

// Gym Pass Trainer "Ripped Cabus" (other/gympass)
moc_para01,33,162,6	duplicate(Ripped Cabus#GymPass)	Ripped Callus#ed	899

// RWC 2012 Enchanter "Goldenthiefberg" (events/RWC_2012.txt)
// A combination of the slotter and enchanter, currently a placeholder.
moc_para01,27,179,4	script	RWC Enchanter#new10	87,{ end; }

// Cash Headgear Dyer "Alora" (merchants/cashheadgear_dye.txt)
// Also converts some +8 and up headgears into costumes, currently a placeholder.
moc_para01,21,185,4	duplicate(Alora)	Alora#headgear_dye2	862

// Stylist "Dinorah Lacostt" (?)
// Dyes clothing for an Omni Clothing Dye, currently a placeholder.
moc_para01,33,185,4	script	Dinorah Lacostt#ed	862,{ end; }

// Commonly Updated NPCs
//============================================================
/* These scripts change on a regular basis, so they're mostly just placeholders for now. */

moc_para01,29,35,4	script	Gramps#huntquests	866,{
	mes "[老爷爷]";
	mes "当你成为我的";
	mes "年纪大了，你就变得苦涩。";
	mes "我太久没有对四处游荡的怪物采取任何行动了";
	mes "米德加德。";
	next;
	mes "[老爷爷]";
	mes "这就是为什么我请你们帮忙";
	mes "我狩猎一些怪物。";
	mes "你愿意帮助我吗，年轻的冒险家？";
	close;
	//next;
	//switch(select("Yes, I'll help.:Reward Me!:^ff0000I want to quit hunting^000000:^0000ffCan you warp me?^000000:Can you erase the timer?:What missions are you giving?:Give me Limited 3 day VIP:Tell me more about the VIP Access:No thanks, pops.")) {
}

moc_para01,174,33,3	script	Bathory#2012spirits	1102,{
	mes "[浴室]";
	mes "诶嘿嘿嘿！本周的焦点是 Glast Heim，所以我将人们运送到那里，费用仅为 1,000 兹！";
	next;
	mes "[浴室]";
	mes "怎么样？想骑上美丽的巴托里扫帚吗？";
	next;
	switch(select("准备起飞！：不用了，谢谢。")) {
	case 1:
		if (Zeny < 1000) {
			mes "[浴室]";
			mes "来吧亲爱的...";
			mes "你连1000泽尼都没有吗？";
			close;
		}
		mes "[浴室]";
		mes "好啦，坚持住！";
		close2;
		set Zeny, Zeny-1000;
		warp "niflheim",194,185;
		end;
	case 2:
		mes "["+strcharinfo(0)+"]";
		mes "不用了，谢谢，也许以后吧。";
		close;
	}
}

moc_para01,26,174,4	script	Grandma Boxter#boxx	840,{ end; }

// Mail Annex Station
//============================================================
/* Map "auction_03" is a duplicate of "auction_02".

moc_para01,30,187,4	script	Mail Annex Station	888,{
	mes "[邮件附件站]";
	mes "您想去卡夫拉邮政附属站吗？";
	next;
	switch(select("是的，现在发给我！：不，谢谢。")) {
	case 1:
		mes "[邮件附件站]";
		mes "带您前往邮件附件室。";
		close2;
		warp "auction_03",151,23;
		end;
	case 2:
		mes "[邮件附件站]";
		mes "那么好吧！";
		close;
	}
}
auction_03,152,16,0	warp	#mail_annex_warp	1,1,moc_para01,30,184

auction_03,152,42,4	script	Kafra Employee#annex	117,{
	mes "[卡夫拉员工]";
	mes "欢迎来到卡夫拉的邮件附属站。这是现在发送和检索邮件的唯一地方。";
	next;
	mes "[卡夫拉员工]";
	mes "你想做什么？";
	next;
	switch(select("访问邮件：存储：取消")) {
	case 1:
		if (Zeny < 130) {
			mes "[卡夫拉员工]";
			mes "对不起，你没有足够的钱。";
			mes "要使用邮箱服务，";
			mes "您需要支付 130 zeny。";
			close;
		}
		mes "[卡夫拉员工]";
		mes "感谢您使用 Kafra 服务。";
		set Zeny, Zeny - 130;
		close2;
		openmail;
		end;
	case 2:
		callfunc "F_KafStor",0,1200,0;
		end;
	case 3:
		mes "[卡夫拉员工]";
		mes "感谢您使用 Kafra 服务。";
		close;
	}
}

auction_03,145,47,2	shop	Tool Dealer#annex	83,611:-1,501:-1,502:-1,503:-1,506:-1,504:-1,645:-1,656:-1,657:-1,601:-1,602:-1,1065:-1,911:-1,910:-1,717:-1,1092:-1,1093:-1,713:400

*/

// Fallen_Angel_Wing Enchant
//============================================================
moc_para01,179,27,3	script	Dark Luhir#new10	4_F_VALKYRIE2,{
	disable_items;
	mes "[黑暗路希尔]";
	mes "你好~我正在向那些拥有堕落天使之翼物品的人赠送额外的统计数据。";
	next;
	if (getequipid(EQI_GARMENT) != 2589) {// Fallen_Angel_Wing
		mes "[黑暗路希尔]";
		mes "请^0000ff先穿上你的堕落天使之翼物品^000000，然后我会给你额外的属性。";
		close;
	}
	switch( select( "取消", "附魔堕落天使之翼", "重置堕落天使之翼统计数据" ) ) {
	case 1:
		mes "[黑暗路希尔]";
		mes "后来……";
		close;
	case 2:
		.@refine = getequiprefinerycnt(EQI_GARMENT);
		setarray .@card[0],
			getequipcardid(EQI_GARMENT,0),
			getequipcardid(EQI_GARMENT,1),
			getequipcardid(EQI_GARMENT,2),
			getequipcardid(EQI_GARMENT,3);
		mes "[黑暗路希尔]";
		mes "当精炼等级在1~6之间时，只有1个插槽可以获得额外的属性。";
		mes "如果精炼度为7~8，可以接收2个插槽的属性。";
		mes "+9 可以接收所有 3 个插槽的统计数据。";
		next;
		mes "[黑暗路希尔]";
		if (.@refine < 7)
			mes "该物品的精炼等级低于7。只有^ff0000插槽4^000000可以接收额外的属性。";
		else if (.@refine < 9)
			mes "该物品的精炼度在7~8之间。 ^ff0000插槽插槽 3 和 4^000000 可以接收额外的统计信息。";
		else
			mes "该物品的精炼度超过9。^ff0000插槽2、3、4^000000的所有3个都可以获得额外属性。";
		mes "您想要什么样的额外统计数据？";
		next;
		switch( select( "让我穿上装备", "1.斗志", "2.魔法攻击", "3.射箭", "4.临界", "5.MaxHP", "6.MaxSP", "7.Atk速度", "8.STR", "9.AGI", "10.VIT", "11.INT", "12.DEX", "13.LUK" ) ) {
		case 1:
			mes "[黑暗路希尔]";
			mes "请装备好物品后回来。";
			close;
		case 2:
			setarray .@enchants[0],4809,4808,4820,4821;		// Fighting Spirit 3~5 / Fighting Spirit 6
			.@string$ = "Fighting Spirit stats will be given randomly by 3~5 (ATK+HIT) range.";
			break;
		case 3:
			setarray .@enchants[0],4812,4826,4827,4828;		// Spell 4~6 / Spell 7
			.@string$ = "Magic spell stats will be given randomly by 4~6 (MATK + Reduce change casting time).";
			break;
		case 4:
			setarray .@enchants[0],4832,4833,4834,4835;		// Expert Archer 1~3 / Expert Archer 4
			.@string$ = "Archery stats will be given randomly by 1~3 (Range atack rate).";
			break;
		case 5:
			setarray .@enchants[0],4863,4864,4865,4866;		// Fatal 1~3 / Fatal 4
			.@string$ = "Critical stats will be given randomly by 1~4 (Critical damage + CRI).";
			break;
		case 6:
			setarray .@enchants[0],4861,4862,4867,4868;		// MaxHP +1~3% / MaxHP +4%
			.@string$ = "MHP stats will be given randomly by 1~3 (Increase MHP %).";
			break;
		case 7:
			setarray .@enchants[0],4870,4800,4871,4801;		// +25,50 or 75 sp / +100 sp
			.@string$ = "SP stats will be given randomly by 25~75.";
			break;
		case 8:
			setarray .@enchants[0],4869,4872,4873,4807;		// Delayafterattack 1~3 / +1 ASPD
			.@string$ = "Atack delay stats will be given randomly by 1~3 (Reduce attack delay %).";
			break;
		case 9:
			setarray .@enchants[0],4702,4703,4704,4853;		// Str +3~5 / Special Str
			.@string$ = "STR stats will be given randomly by 3~5.";
			break;
		case 10:
			setarray .@enchants[0],4731,4732,4733,4854;		// Agi +2~4 / Special Agi
			.@string$ = "AGI stats will be given randomly by 2~4.";
			break;
		case 11:
			setarray .@enchants[0],4742,4743,4744,4855;		// Vit +3~5 / Special Vit
			.@string$ = "VIT stats will be given randomly by 3~5.";
			break;
		case 12:
			setarray .@enchants[0],4712,4713,4714,4856;		// Int +3~5 / Special Int
			.@string$ = "INT stats will be given randomly by 3~5.";
			break;
		case 13:
			setarray .@enchants[0],4722,4723,4724,4857;		// Dex +3~5 / Special Dex
			.@string$ = "DEX stats will be given randomly by 3~5.";
			break;
		case 14:
			setarray .@enchants[0],4752,4753,4754,4858;		// Luk +3~5 / Special Luk
			.@string$ = "LUK stats will be given randomly by 3~5.";
			break;
		}
		mes "[黑暗路希尔]";
		mes .@string$ + "^ff0000给定的统计数据无法恢复。^000000 您想继续吗？";
		next;
		if (select( "1.退出", "2.是的，我想尝试一下。" ) == 1) {
			mes "[黑暗路希尔]";
			mes "后来……";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_GARMENT, 2589) || callfunc("F_IsEquipCardHack", EQI_GARMENT, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", EQI_GARMENT, .@refine))
			close;

		if (.@card[3] == 0) {
			.@card[3] = .@enchants[ rand(3) ];
			.@slot = 4;
		}
		else if (.@card[2] == 0 && .@refine >= 7) {
			.@card[2] = .@enchants[ rand(3) ];
			.@slot = 3;
		}
		else if (.@card[1] == 0 && .@refine >= 9) {
			.@card[1] = .@enchants[ rand(4) ];
			.@slot = 2;
		}
		else {
			mes "[黑暗路希尔]";
			mes "看来这件物品已经拥有最大附魔了。请再带一份以获得额外的统计数据。";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[黑暗路希尔]";
		mes "为项目的^990000" + .@slot + "套接字^000000提供能力。";
		delequip EQI_GARMENT;
		getitem2 2589,1,1, .@refine,0, .@card[0], .@card[1], .@card[2], .@card[3];
		close;
	case 3:
		mes "[黑暗路希尔]";
		mes "堕落天使之翼的属性重置需要花费 1,000,000 zeny？";
		mes "您想继续吗？";
		next;
		if (select( "是的", "不，谢谢！" ) == 2)
			close;
		if (Zeny < 1000000) {
			mes "[黑暗路希尔]";
			mes "你这个骗子，你的智商还不够！";
			close;
		}
		if (getequipid(EQI_GARMENT) == 2589) {
			.@refine = getequiprefinerycnt(EQI_GARMENT);
			.@card[0] = getequipcardid(EQI_GARMENT,0);

			mes "[黑暗路希尔]";
			specialeffect2 EF_REPAIRWEAPON;
			mes "重置项目统计数据。";
			delequip EQI_GARMENT;
			Zeny -= 1000000;
			getitem2 2589,1,1, .@refine,0, .@card[0],0,0,0;// Fallen_Angel_Wing
		}
		close;
	}
}
