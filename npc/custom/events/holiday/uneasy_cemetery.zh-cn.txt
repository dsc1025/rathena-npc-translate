//===== rAthena Script ======================================= 
//= Uneasy Prontera Cemetery Quest
//===== By: ================================================== 
//= Lupus
//===== Current Version: ===================================== 
//= 1.2a (Tested and fully working!)
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= A periodical quest of the Uneasy Cemetery (Kill undead / Prevent their appearance)
//= Every day, at the midnight Prontera receive a wave of Undeads.
//= They come from Uneasy Cemetery of Prontera. To protect the players
//= from the undeads terror you may either kill the enemy. Or supply Mother Mathana
//= with needed amount of Holy Water. Every citizen can take his part in the 
//= saving of Prontera city. After some days of quiet life... the Cemetery strikes back.
//===== Additional Comments: ================================= 
//= 1.1 More advanced ver. Added some bonus for the one who'd kill the last walking undead
//= 1.2 Added coords to the script to make label OmMobDead working
//= 1.2a Changed item names to item IDs. [Samuray22]
//===== Translated By: =======================================
//= dsc
//============================================================ 

prontera,3,3,3	script	Uneasy_Check	-1,{
	end;

OnHour00:
	set $UNEASY_DL,$UNEASY_DL-1;
	set $UNEASY_BL,$UNEASY_BL+30; //add need of HW for 30 bottles per day
	if ($UNEASY_BL>666) set $UNEASY_BL,666; //keep needed bottles not <=666
	if ($UNEASY_DL < 0) goto L_Start_Undead;
//The Cemetery is OK yet.
	disablenpc "Mother Mathana";
	end;
OnInit:
	if ($UNEASY_DL >= 0) disablenpc "Mother Mathana";
	end;

OnHour06:
	killmonsterall "prontera";	//The Sun kills undead in the morning
	end;

OnHour01:
	if ($@UNEASY_MOB > 0) mapannounce "prontera","[Mother Mathana]: In the name of Odin, please finish these roaming undead leftovers!",0;
	end;

OnZombieDead:
	set $@UNEASY_MOB,$@UNEASY_MOB-1;
	if ($@UNEASY_MOB>0) end;
	set $UNEASY_DL,0;
	set $UNEASY_H$,strcharinfo(0);
	if (Sex==SEX_MALE) mapannounce "prontera","[Mother Mathana]: Brave "+$UNEASY_H$+" has just killed the last undead in Prontera!",0;
	else mapannounce "prontera","[Mother Mathana]: Lady "+$UNEASY_H$+" has just killed the last undead in Prontera!",0;
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	end;

L_Start_Undead:
	killmonsterall "prontera";	//kills any left monsters
	enablenpc "Mother Mathana";
//call some monsters in the city
	set $@UNEASY_MOB, 65;
	areamonster "prontera",0,0,0,0,"Zombie",1015,30,"Uneasy_Check::OnZombieDead";
//in the Cemetery
	monster "prontera",268,349,"Zombie",1015,30,"Uneasy_Check::OnZombieDead";
	monster "prontera",269,350,"Ghoul",1036,5,"Uneasy_Check::OnZombieDead";
//announce
	mapannounce "prontera","[Mother Mathana]: The cemetery has become restless!  In the name of Odin, hurry to the Sanctuary! Save the city of Prontera!",0;
}

prontera,257,313,5	script	Mother Mathana	79,{
	mes "[玛莎娜妈妈]";
	if ($UNEASY_DL <= 0) goto L_Undead_Walk;
	mes "恐怕我们的旧墓地出了问题……";
	if ($UNEASY_H$==strcharinfo(0)) mes "但多亏了你，"+$UNEASY_H$+"，我们才能在 " + $UNEASY_DL + " 的夜晚安然入睡！";
	if ($UNEASY_H$!=strcharinfo(0)) mes "但感谢"+$UNEASY_H$+"的支持，我们度过了" + $UNEASY_DL + "轻松的夜晚！";
	emotion ET_SURPRISE;
	close;

L_Undead_Walk:
	if ($UNEASY_DL == 0) mes "他们明天晚上可能会再次返回！";
	if ($UNEASY_DL == 0 && $UNEASY_H$==strcharinfo(0)) mes "谢谢你，"+$UNEASY_H$+"！现在我们可以休息到下一个午夜了！";
	if ($UNEASY_DL == 0 && $UNEASY_H$!=strcharinfo(0)) mes "但由于"+$UNEASY_H$+"有您的帮助，我们就能休息到第二天午夜了！";
	mes "为了让躁动的墓地平静下来，我们应该用圣水浇灌所有这些坟墓。但我们的兄弟姐妹已经用完了。";
	mes "你能为我们提供圣水吗？";
	next;
	menu "Yes, have all my Holy Water!",-, "Nope, I need it.",M_NO, "I don't have any.",M_DONT_HAVE;

	if ( countitem(523)<1 ) goto M_DONT_HAVE;
	set $UNEASY_BL,$UNEASY_BL-countitem(523);
	delitem 523,countitem(523);

	if ( $UNEASY_BL > 0 ) goto L_NEED_MORE;
//set quiet days!!! no more undead for this period!
	set $UNEASY_DL,5+((0-$UNEASY_BL)/30);
	set $UNEASY_H$,strcharinfo(0);
	mes "[玛莎娜妈妈]";
	mes "谢谢你，"+$UNEASY_H$+"！现在我们已经有足够的圣水了！";
	next;
	mes "[玛莎娜妈妈]";
	mes "用这些水浇灌墓地后，我们将获得" + $UNEASY_DL + "安全的夜晚！";
	next;
	killmonsterall "prontera";	//kills any left monsters
	mes "[玛莎娜妈妈]";
	mes "看，"+ $UNEASY_H$ +"？他们现在都走了！";
	next;
	mes "[玛莎娜妈妈]";
	mes "我们的教会将亲自感谢您......";
	next;
	if (Sex==SEX_MALE) mapannounce "prontera","[Mother Mathana]: In the name of Odin we declare handsome "+$UNEASY_H$+" as a Prontera savior!",0;
	else mapannounce "prontera","[Mother Mathana]: In the name of Odin we declare beautiful "+$UNEASY_H$+" as a Prontera savior!",0;
	mes "[玛莎娜妈妈]";
	mes "我们以奥丁之名祝福您，并体面地赠送来自 Mareusis 酒窖的一份朴素礼物。";
	getitem 505,1; //Blue_Potion
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	close;

L_NEED_MORE:
	mes "[玛莎娜妈妈]";
	mes "谢谢你，很好的"+strcharinfo(0)+"，但我们还需要" + $UNEASY_BL + "更多的圣水瓶。";
	close;

M_NO:
	mes "[玛莎娜妈妈]";
	mes "恐怕旧墓地很快就要失控了……拜托，把你能弄到的圣水都给我们拿来吧。";
	close;

M_DONT_HAVE:
	mes "[玛莎娜妈妈]";
	mes "唉!我们还需要" + $UNEASY_BL + "更多瓶圣水...你为什么不去向其他人要一些额外的圣水呢？";
	mes "旧墓地即将失控……";
	mes "请以奥丁之名，帮助普隆德拉城。";
	close;
}
