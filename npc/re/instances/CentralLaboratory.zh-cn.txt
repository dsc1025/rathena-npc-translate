//===== rAthena Script =======================================
//= Central Laboratory
//===== Description: =========================================
//= [Official Conversion]
//= Central Laboratory Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//= 1.1 Updated to match official script. [Capuche]
//============================================================

verus01,153,155,5	script	Temporary Dimension-mov#pa0829_01	PORTAL,{
	.@party_id = getcharid(1);
	if (.@party_id == 0) {
		mes "[临时维度移动器]";
		mes "你没有同伴陪你吗？即使独自旅行也可以组织您的聚会。";
		close;
	}
	switch( checkquest(12347,PLAYTIME) ) {
	case -1:
		switch( instance_enter("Central Laboratory") ) {
		case IE_NOMEMBER:
		case IE_NOINSTANCE:
			mes "[临时维度移动器]";
			mes "次元移动的通道还没有被激活。";
			close;
		case IE_OTHER:
			mes "[临时维度-mov]";
			mes "未知错误。";
			close;
		case IE_OK:
			mapannounce "verus01", "" + strcharinfo(0) + " of the party, " + getpartyname(.@party_id) + ", is entering Central Laboratory.",bc_map,"0x00FF99";
			setquest 12347;// Trace of Laboratory Access
			end;
		}
		end;
	case 0:
	case 1:
		mes "[临时维度移动器]";
		mes "还残留着上次事件的痕迹。拒绝访问。";
		close;
	case 2:
		mes "^0000ff使用维度移动器似乎是可能的。^000000";
		erasequest 12347;// Trace of Laboratory Access
		close;
	}
}

verus01,149,155,5	script	Civilization Explorer#pa0829_01	4_LGTSCIENCE,{
	if (BaseLevel < 135) {
		mes "[文明探索者]";
		mes "嗯……不知道能不能派上用场，看来还是有差距啊！";
		next;
		mes "[文明探索者]";
		mes "等你变强了，就回到我身边吧。你应该快到135级了吧！";
		close;
	}
	mes "^ff0000驯服怪物会阻碍这个地下城的进展。请注意这一点。^000000";
	next;
	mes "[文明探索者]";
	switch( isbegin_quest(12346) ) {
	case 0:
		mes "这个地区的古代文明有很多有趣的事情。";
		next;
		mes "[文明探索者]";
		mes "观察土层，估计有500到600年的历史了！";
		mes "但这里的许多出土文物已经远远超过了我们现在的技术。";
		next;
		mes "[文明探索者]";
		mes "在恢复的记录中还发现了许多未定义的术语。";
		next;
		select("什么样的未知术语？");
		mes "[文明探索者]";
		mes "^0000ff主机计算机^000000或，^0000ff LSI芯片^000000或者，还有什么。 ^0000ff鸡^000000？";
		next;
		mes "[文明探索者]";
		mes "嗯，没错。肯定有“鸡”这个词。我不知道这可能是一种什么样的技术组合......";
		next;
		mes "[文明探索者]";
		mes "它在人们的记录中被引用，这些人似乎在许多技术痕迹中“扎根”。";
		next;
		if (select( "这不是我感兴趣的话题。", "有趣。有结果了吗？" ) == 1) {
			mes "[文明探索者]";
			mes "不幸。";
			close;
		}
		mes "[文明探索者]";
		mes "我的目的是研究与相关时代相关的技术的使用。尽管到目前为止我发现的东西微不足道。";
		next;
		mes "[文明探索者]";
		mes "如果我按照自己的方式进行的话，我会使用维度移动器来亲自查看，但工作量太大了。更何况我不是冒险家……";
		next;
		mes "[文明探索者]";
		mes "无论如何，你有兴趣回到过去吗？";
		next;
		mes "[文明探索者]";
		mes "如果你发现一个有趣的物体，带我四处看看就更好了。";
		next;
		if (select( "不感兴趣。", "好的。" ) == 1) {
			mes "[文明探索者]";
			mes "不幸。";
			close;
		}
		mes "[文明探索者]";
		mes "你是一个冒险家。告诉我你决定做什么。";
		if (isbegin_quest(12346) == 0)
			setquest 12346;// Strange Ancient Science
		break;
	case 1:
		mes "我该如何帮助您？";
		break;
	case 2:
		mes "这次你想做什么？";
		break;
	}
	next;
	if (isbegin_quest(12346) == 1)
		completequest 12346;
	.@party_id = getcharid(1);
	if (.@party_id == 0) {
		mes "[文明探索者]";
		mes "请在1人以上组成队伍或加入队伍后与我交谈。";
		close;
	}
	switch( checkquest(12347,PLAYTIME) ) {
	case -1:
		if (is_party_leader() == false) {
			mes "[文明探索者]";
			mes "啊。你不是党的领袖。带我去见你们的领导！或者带你的领导来见我。";
			close;
		}
		if (select( "产生时间裂缝", "取消" ) == 2)
			close;
		if (instance_create("Central Laboratory") < 1) {
			mes "派对：" + getpartyname( getcharid(1) ) + "";
			mes "领导者：" + getpartyleader( getcharid(1) ) + "";
			mes "^0000ff中央实验室^000000 - 未知错误";
			close;
		}
		mes "[文明探索者]";
		mes "当时间的裂缝打开时，深吸一口气，然后再次说话。";
		close;
	case 0:
	case 1:
		mes "[文明探索者]";
		mes "哦，哦不。";
		mes "维度旅行还剩下一些后遗症。在这种情况下我们不能再旅行了。";
		close;
	case 2:
		mes "^0000ff所有进入中心实验室的痕迹都已被抹去。与文明探索者对话似乎是可能的。^000000";
		erasequest 12347;// Trace of Laboratory Access
		close;
	}
}

// Entrance
1@lab,104,34,4	script	Probationary researcher#pa0829	4_LGTSCIENCE,{
	if ('switch_enabled == 1) {
		mes "[见习研究员]";
		mes "入口的门已经打开了。";
		close;
	}
	if (is_party_leader() == false) {
		mes "[见习研究员]";
		mes "请稍等。我正在与导游讨论您是否已获得授权。";
		close;
	}
	mes "[见习研究员]";
	mes "您作为32001研究项目的实验助理参与吗？";
	next;
	if (select( "是的。", "不。" ) == 2) {
		mes "[见习研究员]";
		mes "看来你进错实验室了。请退出并寻求帮助。";
		close;
	}
	mes "[见习研究员]";
	mes "我们需要大约 5 秒的时间。确认访问代码。";
	close2;
	sleep 3000;
	npctalk "试用研究员：访问代码匹配。我会打开门。";
	sleep 6000;
	npctalk "试用研究员：现在可以进入了。请注意，您无法从另一侧打开门。";
	if ('switch_enabled == 0) {
		'switch_enabled = 1;
		enablenpc instance_npcname("lab_warp#1");
		enablenpc instance_npcname("lab_warp#2");
		enablenpc instance_npcname("lab_warp#3");
		enablenpc instance_npcname("Main Switch#");
		enablenpc instance_npcname("Senior researcher#pa082");
		enablenpc instance_npcname("#eff_nc");
		for ( .@i = 1; .@i <= 8; ++.@i )
			enablenpc instance_npcname( "Switch" + .@i + "#" );
	}
	end;

OnInstanceInit:
	'map_name$ = instance_mapname("1@lab");
	'code_found = 'bool_event = 'switch_enabled = 0;
	for ( .@i = 1; .@i <= 8; .@i++ )
		'switch_position[.@i] = 0;

	// Warps
	disablenpc instance_npcname("lab_warp#1");
	disablenpc instance_npcname("lab_warp#2");
	disablenpc instance_npcname("lab_warp#3");
	disablenpc instance_npcname("lab_warp#4");
	disablenpc instance_npcname("lab_warp#5");

	// Switchs
	disablenpc instance_npcname("Main Switch#");
	disablenpc instance_npcname("Senior researcher#pa082");
	disablenpc instance_npcname("#eff_nc");
	for ( .@i = 1; .@i <= 8; ++.@i )
		disablenpc instance_npcname( "Switch" + .@i + "#" );

	// Experiments
	disablenpc instance_npcname("DR Silvia#pa0829");
	disablenpc instance_npcname("DR Federico#pa0829");
	disablenpc instance_npcname("DR Artemia#pa0829");
	disablenpc instance_npcname("DR Vincent#pa0829");
	disablenpc instance_npcname("#wp01");
	disablenpc instance_npcname("#npc_con01");
	disablenpc instance_npcname("#npc_con03");
	disablenpc instance_npcname("#npc_con05");
	disablenpc instance_npcname("#npc_con06");
	disablenpc instance_npcname("#npc_con07");
	disablenpc instance_npcname("#npc_con08");
	disablenpc instance_npcname("#npc_con10");
	disablenpc instance_npcname("#npc_con11");
	for ( .@i = 1; .@i <= 14; ++.@i )
		hideonnpc instance_npcname("#gen_" + (.@i < 10 ? "0" : "") + .@i);
	disablenpc instance_npcname("lab_warp#end");
	end;
}
// 1@lab,104,34,4	script	Probationary researcher#pa0829_2	4_LGTSCIENCE

// Switchs
1@lab,37,37,3	script	Switch1#	CLEAR_NPC,{
	.@num = charat( strnpcinfo(1),6 );
	setarray .@position$[0], "OFF", "ON";
	mes "[开关" + .@num + "]";
	mes "该开关目前处于" + .@position$[ 'switch_position[.@num] ] + "位置。你会怎么做？";
	next;
	'switch_position[.@num] = !( select( "在","离开" ) - 1 );
	close;
}
1@lab,39,37,3	duplicate(Switch1#)	Switch2#	CLEAR_NPC
1@lab,41,37,3	duplicate(Switch1#)	Switch3#	CLEAR_NPC
1@lab,43,37,3	duplicate(Switch1#)	Switch4#	CLEAR_NPC
1@lab,46,37,3	duplicate(Switch1#)	Switch5#	CLEAR_NPC
1@lab,48,37,3	duplicate(Switch1#)	Switch6#	CLEAR_NPC
1@lab,50,37,3	duplicate(Switch1#)	Switch7#	CLEAR_NPC
1@lab,52,37,3	duplicate(Switch1#)	Switch8#	CLEAR_NPC

1@lab,34,37,3	script	Main Switch#	CLEAR_NPC,{
	.@string$ = 'switch_position[1] + "" + 'switch_position[2] + "" + 'switch_position[3] + "" + 'switch_position[4] + "" +
		'switch_position[5] + "" + 'switch_position[6] + "" + 'switch_position[7] + "" + 'switch_position[8];
	.@current_code = strtol( .@string$,2 );
	.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
	npctalk "今天的访问代码" + .@code + ", 当前输入的代码" + .@current_code + ".";
	sleep 6000;
	if ('code_found > 0)
		npctalk "门已经打开了。";
	else if (.@current_code != .@code)
		npctalk "代码不正确。请再试一次。";
	else {
		'code_found = 1;
		npctalk "代码是正确的。开门。";
		enablenpc instance_npcname("lab_warp#4");
		enablenpc instance_npcname("lab_warp#5");
		enablenpc instance_npcname("DR Silvia#pa0829");
		enablenpc instance_npcname("DR Federico#pa0829");
		enablenpc instance_npcname("DR Artemia#pa0829");
		enablenpc instance_npcname("DR Vincent#pa0829");
		enablenpc instance_npcname("#wp01");
		donpcevent instance_npcname("#eff_nc") + "::OnStop";
		for ( .@i = 1; .@i <= 8; ++.@i )
			disablenpc instance_npcname( "Switch" + .@i + "#" );
	}
	end;
}

1@lab,45,32,3	script	Senior researcher#pa082	4_LGTSCIENCE,{
	if ('code_found > 0) {
		mes "[高级研究员]";
		mes "门是开着的，所以你现在可以进去了。请注意，您无法从另一侧打开门。";
		close;
	}
	.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
	mes "[高级研究员]";
	mes "看来你是来参加粒子加速实验的。";
	next;
	mes "[高级研究员]";
	mes "今天的访问代码是^0000ff" + .@code + "^000000。";
	mes "代码需要以二进制数形式输入。使用我身后的开关进入它。";
	next;
	mes "[高级研究员]";
	mes "输入代码后，按入口附近的主按钮。";
	for ( .@i = 1; .@i <= 8; ++.@i )
		specialeffect EF_STEAL, AREA, instance_npcname("Switch" + .@i + "#");
	close2;
	donpcevent instance_npcname("#eff_nc") + "::OnStart";
	end;
}

1@lab,1,1,0	script	#eff_nc	-1,{
	end;
OnStart:
OnTimer2000:
	specialeffect 203,AREA, instance_npcname("Main Switch#");
	for ( .@i = 1; .@i <= 8; ++.@i )
		specialeffect (203+.@i),AREA, instance_npcname("Switch" + .@i + "#");
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}


// Experiments
1@lab,81,84,7	script	DR Silvia#pa0829	4_F_SCIENCE,{
	mes "[西尔维娅博士]";
	mes "实验结束后我想回家享受一杯冰啤酒和一些炸鸡。";
	next;
	mes "[西尔维娅博士]";
	mes "我已经快两年没回家了。";
	next;
	mes "^0000ff西尔维娅一脸疲惫地看着粒子加速器。^000000";
	close;
}

1@lab,80,87,7	script	DR Federico#pa0829	4_LGTSCIENCE,{
	mes "[费德里科博士]";
	mes "这个装置我越看越觉得神奇。从未有过如此规模的粒子加速器。";
	next;
	mes "[费德里科博士]";
	mes "有了这个尺寸，我想知道是否有可能创建一个小规模的黑洞......";
	next;
	mes "[费德里科博士]";
	mes "啊，我只是说说而已。这样的事件与其说是实验，不如说更接近于灾难哈哈。";
	close;
}

1@lab,80,90,6	script	DR Artemia#pa0829	4_F_SCIENCE,{
	mes "[卤虫博士]";
	mes "看，现实不喜欢你抓住一根绳子并用力拉它。";
	next;
	mes "[卤虫博士]";
	mes "如此强大的粒子加速可能会发生尺寸扭曲。";
	next;
	mes "[卤虫博士]";
	mes "我对此有一种不好的预感，因为……大自然会找到办法。";
	close;
}

1@lab,81,93,6	script	DR Vincent#pa0829	4_LGTSCIENCE,{
	mes "[文森特博士]";
	mes "机器很完美。一切都不会出错！";
	next;
	mes "[文森特博士]";
	mes "卤虫担心太多了。无论如何，实验结束后我要去旅行。";
	close;
}

1@lab,80,89,0	script	#wp01	HIDDEN_WARP_NPC,9,9,{
	end;
OnTouch:
	donpcevent instance_npcname("#npc_con01") + "::OnStart";
	disablenpc instance_npcname("#wp01");
	end;
}

// Round 1
1@lab,4,1,3	script	#npc_con01	CLEAR_NPC,{
	end;
OnStart:
	'vincent$ = instance_npcname("DR Vincent#pa0829");
	'federico$ = instance_npcname("DR Federico#pa0829");
	'silvia$ = instance_npcname("DR Silvia#pa0829");
	'artemia$ = instance_npcname("DR Artemia#pa0829");
	'gen_01$ = instance_npcname("#gen_01");
	'gen_02$ = instance_npcname("#gen_02");
	'gen_03$ = instance_npcname("#gen_03");
	'gen_04$ = instance_npcname("#gen_04");
	'gen_05$ = instance_npcname("#gen_05");
	'gen_06$ = instance_npcname("#gen_06");
	'gen_07$ = instance_npcname("#gen_07");
	'gen_08$ = instance_npcname("#gen_08");
	'gen_09$ = instance_npcname("#gen_09");
	'gen_10$ = instance_npcname("#gen_10");
	'gen_11$ = instance_npcname("#gen_11");
	'gen_12$ = instance_npcname("#gen_12");
	'gen_13$ = instance_npcname("#gen_13");
	'gen_14$ = instance_npcname("#gen_14");

	npctalk "费德里科：让我们开始吧。嗯...弗雷萨博士，你准备好了吗？", 'federico$;
	sleep 5000;
	npctalk "Artemia：可能会进行最后的确认检查。这种规模的实验需要如此谨慎。", 'artemia$;
	sleep 6000;
	npctalk "Silvia：第一次实验的成品需要小心操作。", 'silvia$;
	sleep 4000;
	npctalk "文森特：啊，我希望我们能快点结束，这样我就可以去吃鸡肉了。", 'vincent$;
	sleep 3000;
	npctalk "西尔维娅：啊！我也喜欢这样！你走的时候给我打电话。", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "?????: Ah, ah, one two one two, mic test.", bc_map,0x00ff77;
	sleep 2000;
	npctalk "费德里科：她在这里。", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "?????: Chief director Lona Fresa of project particle accelerator.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa : Current time " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day 25 minutes after 10.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa : Testing the effects of particle acceleration on its surroundings in an open space.", bc_map,0x00ff77;
	sleep 5000;
	npctalk "Artemia：啊……我真的不喜欢这个实验。", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : Starting the experiment. Chief researcher Dr. Federico, Artemia, Silvia, Vincent and assistant researchers.", bc_map,0x00ff77;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	npctalk "卤虫：我们开始吧~", 'artemia$;
	sleep 3000;
	npctalk "文森特：发电机开始旋转。", 'vincent$;
	sleep 3000;
	npctalk "Federico：发电机转速固定为 10000 RPM。", 'federico$;
	sleep 2500;
	npctalk "西尔维娅：电压仍在容许范围内。", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : This experiment is for the creation of new material that hasn't existed before.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa : Recorders make sure to make detailed records of the catalyst bed's reaction when it is installed to the accelerator.", bc_map,0x00ff77;
	sleep 6000;
	npctalk "卤虫：接触催化剂床数 1。", 'artemia$;
	sleep 9000;
	npctalk "西尔维娅：发电机电压不稳定。仍在公差范围内。", 'silvia$;
	sleep 3000;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	sleep 2000;
	npctalk "Silvia：检测到接近催化剂床和加速器之间的公差带的热量！", 'silvia$;
	sleep 5000;
	npctalk "文森特：加速器正在加热。", 'vincent$;
	sleep 5000;
	npctalk "文森特：320c。", 'vincent$;
	sleep 5000;
	npctalk "Vincent：670c 升温太快了！", 'vincent$;
	sleep 3000;
	npctalk "Vincent：突破1000c！！！弗雷萨博士！我们必须暂时停止设备！", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : It is fine.", bc_map,0x00ff77;
	sleep 5000;
	npctalk "西尔维娅：这不是一个简单的问题。气温已经远远超出了预计的数字！", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : This problem was predicted. Maintain current state.", bc_map,0x00ff77;
	sleep 3000;
	npctalk "Artemia：但至少……我稍后会吃鸡肉。", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Receiving changed circumstance through main server. Detecting dimensional distortion between particle accelerator and catalyst.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "卤虫：弗雷萨博士！这是主机计算机的危险情况。关闭主电源！", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Alert. New unidentified creature approaching from demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "文森特：发生什么事了。有人关掉那台电脑吗！", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 1 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	donpcevent instance_npcname("#npc_con05") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con05	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con05");
	killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
	.@boss_call = rand(1,105);
	if (.@boss_call <= 7)			.@mob_id = 1087;	// ORK_HERO
	else if (.@boss_call <= 14)		.@mob_id = 1147;	// MAYA
	else if (.@boss_call <= 21)		.@mob_id = 1190;	// ORC_LORD
	else if (.@boss_call <= 28)		.@mob_id = 1115;	// EDDGA
	else if (.@boss_call <= 35)		.@mob_id = 1086;	// GOLDEN_BUG
	else if (.@boss_call <= 42)		.@mob_id = 1038;	// OSIRIS
	else if (.@boss_call <= 49)		.@mob_id = 1159;	// PHREEONI
	else if (.@boss_call <= 56)		.@mob_id = 1389;	// DRACULA
	else if (.@boss_call <= 63)		.@mob_id = 1046;	// DOPPELGANGER
	else if (.@boss_call <= 70)		.@mob_id = 1059;	// MISTRESS
	else if (.@boss_call <= 77)		.@mob_id = 1150;	// MOONLIGHT
	else if (.@boss_call <= 84)		.@mob_id = 1688;	// LADY_TANEE
	else if (.@boss_call <= 91)		.@mob_id = 1039;	// BAPHOMET
	else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con05") + "::OnMyMobDead";
	sleep 3000;
	npctalk "Artemia：那……那是什么？！", 'artemia$;
	sleep 3000;
	npctalk "Silvia：它没有对我们做出反应。也许它看不到我们？", 'silvia$;
	sleep 5000;
	npctalk "Vincent：但是它对项目助理充满敌意！那是什么生物！？", 'vincent$;
	sleep 5000;
	npctalk "文森特：我的身体动不了！我认为空间被冻结了！", 'vincent$;
	end;

OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Restarting device.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con06") + "::OnStart";
	disablenpc instance_npcname("#npc_con05");
	stopnpctimer;
	end;
}

// Round 2
1@lab,4,1,3	script	#npc_con06	CLEAR_NPC,{
	end;
OnStart:
	sleep 9000;
	npctalk "卤虫：MA-4885！！你按照谁的命令重启！停止！！", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: Unrecognized command.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 6000;
	npctalk "Artemia：弗雷萨博士！！我们必须停下来！这太危险了！", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
	sleep 3000;
	npctalk "文森特：弗雷萨！我们没有时间！关掉电脑！！", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: Restarting particle acceleration procedure.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	npctalk "西尔维娅：弗雷萨！做出决定吧！", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa : Current time 20 minutes past 11. Momentarily postpone experiment.", bc_map,0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: Access denied. Cannot understand command.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	sleep 2000;
	mapannounce 'map_name$, "Lona Fresa : Mother Comp MA-4885. I am your Chief director Dr. Fresa. Cease current progress.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Access denied. Cannot understand command.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	npctalk "费德里科：可恶！还有别的东西来了！！", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "MA-4885: Alert. New unidentified creature approaching from demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "文森特：噢……该死。这就是为什么您不应该安装最新的 CPU。", 'vincent$;
	sleep 3000;
	npctalk "Federico：啊……我的身体……动不了。该死", 'federico$;
	sleep 4000;
	mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 1 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	donpcevent instance_npcname("#npc_con07") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con07	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con07");
	killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
	.@boss_call = rand(1,98);
	if (.@boss_call <= 7)		.@mob_id = 1980;	// KUBLIN
	else if (.@boss_call <= 14)	.@mob_id = 1157;	// PHARAOH
	else if (.@boss_call <= 21)	.@mob_id = 1112;	// DRAKE
	else if (.@boss_call <= 28)	.@mob_id = 1251;	// KNIGHT_OF_WINDSTORM
	else if (.@boss_call <= 35)	.@mob_id = 2068;	// BOITATA
	else if (.@boss_call <= 42)	.@mob_id = 1373;	// LORD_OF_DEATH
	else if (.@boss_call <= 49)	.@mob_id = 2156;	// LEAK
	else if (.@boss_call <= 56)	.@mob_id = 1272;	// DARK_LORD
	else if (.@boss_call <= 63)	.@mob_id = 1630;	// BACSOJIN_
	else if (.@boss_call <= 70)	.@mob_id = 1252;	// GARM
	else if (.@boss_call <= 77)	.@mob_id = 1779;	// KTULLANUX
	else if (.@boss_call <= 84)	.@mob_id = 1708;	// THANATOS
	else if (.@boss_call <= 86)	.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 88)	.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 90)	.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 92)	.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 94)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 96)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 98)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con07") + "::OnMyMobDead";
	sleep 6000;
	npctalk "西尔维娅：和上次一样。它似乎对我们没有兴趣。", 'silvia$;
	sleep 4000;
	npctalk "文森特：让我们在主电源暂时关闭的情况下，彻底关闭母机吧！", 'vincent$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Answering question. This space is currently stuck in a distorted phase.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Direct interaction between the demi-plane creature and this world's organisms will be possible after the phase has been balanced.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Conducting calculations to restore balance to distorted phase. Please wait a moment.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	npctalk "Artemia：那团疯狂的金属在说什么？难道说我们和那些怪物之类的东西不是同一个阶段？", 'artemia$;
	sleep 4000;
	npctalk "Federico：啊，我想我们应该希望主机计算机没有做出正确的计算。", 'federico$;
	end;

OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Phase adjustment failed. Restarting relevant procedure.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con08") + "::OnStart";
	disablenpc instance_npcname("#npc_con07");
	stopnpctimer;
	end;
}

// Round 3
1@lab,4,1,3	script	#npc_con08	CLEAR_NPC,{
	end;
OnStart:
	sleep 13000;
	mapannounce 'map_name$, "Lona Fresa: Mother Computer MA-4885. Looks like you are trying to synchronize the other dimension and this world.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: That is correct.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 6000;
	npctalk "Artemia：弗雷萨博士！！这个荒唐的事件与我们原来的实验完全无关！", 'artemia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: I have fully realized that.", bc_map, 0x00ff77;
	sleep 6000;
	npctalk "西尔维娅：我们不能完全关闭主电源吗？", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: Restarting particle acceleration procedure.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	mapannounce 'map_name$, "Lona Fresa: " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day. Current time 38 minutes past 11 temporarily terminating particle accelerator project.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: Ending record with reasons of Mother Computer malfunction and overheating of accelerator.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "西尔维娅：好的！现在弗雷萨博士只需关闭电源即可。", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: Cutting off power to Mother Computer MA-4885.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: Suspending operation of all equipment as per Administrator request.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: .....command... ----!!!  Zizip... ---!! -- following command ----- !! Zizip.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	'bool_event = false;
	npctalk "Artemia：呼……结束了吗？", 'artemia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: Completed shut down of Mother computer MA-4885. Confirm equipment suspension.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "西尔维娅：看来一切都结束了。", 'silvia$;
	sleep 4000;
	npctalk "Federico：工厂初始化是最好的。", 'federico$;
	sleep 4000;
	mapannounce 'map_name$, "Lona Fresa: Recording all experiment progress and terminating at current point.", bc_map, 0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: The final experiment failure is unfortunate. But I will promise a more perfect experiment next time.", bc_map, 0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: Good work everybody.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "Artemia：看来已经结束了。", 'artemia$;
	sleep 5000;
	npctalk "Federico：我以为这是 Fresa 的教条式实验。但这是母机的问题。", 'federico$;
	sleep 5500;
	npctalk "西尔维娅：我们冷静一下吧。离开这个可怕的实验室。", 'silvia$;
	sleep 5000;
	npctalk "文森特：顺便说一下...我还是动不了，这是怎么回事？", 'vincent$;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	donpcevent instance_npcname("#npc_con03") + "::OnStart3";
	sleep 2000;
	mapannounce 'map_name$, "MA-4885: CODE NAME - DESCENT", bc_map, 0xff0000,FW_NORMAL,16,0;
	sleep 5000;
	npctalk "费德里科：什么？这不是初始化阶段吗？", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "MA-4885: Starting final phase transformation.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 3000;
	npctalk "西尔维娅：一切都结束了。", 'silvia$;
	sleep 5500;
	mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 1 second remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	donpcevent instance_npcname("#npc_con10") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Temporarily shutting down main power for syntonization with transformed phase.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con10	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con10");
	killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
	.@boss_call = rand(1,105);
	if (.@boss_call <= 7)			.@mob_id = 1623;	// RSX_0806
	else if (.@boss_call <= 14)		.@mob_id = 1418;	// DARK_SNAKE_LORD
	else if (.@boss_call <= 21)		.@mob_id = 1312;	// TURTLE_GENERAL
	else if (.@boss_call <= 28)		.@mob_id = 1785;	// ATROCE
	else if (.@boss_call <= 35)		.@mob_id = 1734;	// KIEL_
	else if (.@boss_call <= 42)		.@mob_id = 1719;	// DETALE
	else if (.@boss_call <= 49)		.@mob_id = 1768;	// GLOOMUNDERNIGHT
	else if (.@boss_call <= 56)		.@mob_id = 2087;	// QUEEN_SCARABA
	else if (.@boss_call <= 63)		.@mob_id = 1751;	// RANDGRIS
	else if (.@boss_call <= 70)		.@mob_id = 2253;	// DAEHYON
	else if (.@boss_call <= 77)		.@mob_id = 2255;	// KADES
	else if (.@boss_call <= 84)		.@mob_id = 1832;	// IFRIT
	else if (.@boss_call <= 91)		.@mob_id = 1874;	// BEELZEBUB_
	else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con10") + "::OnMyMobDead";
	mapannounce 'map_name$, "MA-4885: Final phase transformation process.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: This world will be summoned to the demi-plane after the phase interference phenomenon with creature fades.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	end;
OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Phase synchronization completed before creature is eliminated. Starting dimensional travel.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con11") + "::OnStart";
	disablenpc instance_npcname("#npc_con10");
	stopnpctimer;
	end;
}

1@lab,4,1,3	script	#npc_con11	CLEAR_NPC,{
	end;
OnStart:
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	sleep 12000;
	mapannounce 'map_name$, "MA-4885: Conducting calculations to restore balance to distorted phase. Please wait a moment.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	npctalk "Federico：Mother comp 正在进行一项超出我们想象的巨大实验。", 'federico$;
	sleep 2500;
	npctalk "文森特：看起来比我们宽宏大量。弗雷萨！！！你在听吗？拉响警报！", 'vincent$;
	sleep 5000;
	npctalk "文森特：我们至少应该疏散其他实验室的人。", 'vincent$;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: But ... you.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "西尔维娅：哎呀，我不知道会发生什么，但我对改变维度很好奇。别管我！", 'silvia$;
	sleep 5000;
	npctalk "Artemia：在尝试新材料生产时进行维度移动。陷入了不同级别的实验中。", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "Alarm System : Alert! Alert! Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "Alarm System : I repeat. Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
	npctalk "Federico：这就是移动维度的感觉吗？多么奇怪啊...", 'federico$;
	sleep 4000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'federico$;
	disablenpc 'federico$;
	mapannounce 'map_name$, "MA-4885: Dimension move at 20% progress. There could be dimensional interference with certain creatures. Please be aware.", bc_map, 0xffff00,FW_NORMAL,13,0;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	npctalk "文森特：不知道其他人撤离了吗？", 'vincent$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'vincent$;
	disablenpc 'vincent$;
	npctalk "西尔维娅：啊……我应该一边吃鸡肉和啤酒一边看这个……", 'silvia$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'silvia$;
	disablenpc 'silvia$;
	mapannounce 'map_name$, "MA-4885: Dimension move at 78% progress. Most of the dimension encroached by demi-plane.", bc_map, 0xffff00,FW_NORMAL,13,0;
	npctalk "Artemia：哈...现在谁去吃鸡？", 'artemia$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'artemia$;
	disablenpc 'artemia$;
	donpcevent instance_npcname("#npc_con03") + "::OnStart3";
	sleep 10000;
	'bool_event = false;
	mapannounce 'map_name$, "MA-4885: Dimension move 100% completed. Welcome to the demi-plane dimension.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: A gateway possible for contact with the demi-plane open at 3 o'clock of the laboratory.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Cannot guarantee what kind of dimension will be met due to the instability of the gateway...", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: If there are any surviving organisms I wish you luck. I hope to see you again.", bc_map, 0xffff00,FW_NORMAL,13,0;
	enablenpc instance_npcname("lab_warp#end");
	end;
}


// Display effects
1@lab,4,1,5	script	#npc_con03	CLEAR_NPC,{
	end;

function effect_gen {
	.@effect = getarg(0);
	.@count = getargcount();
	for ( .@i = 1; .@i < .@count; .@i++ )
		specialeffect .@effect, AREA, getd( "'gen_" + ( getarg(.@i) < 10 ? "0" : "" ) + getarg(.@i) + "$" );
	return;
}

function effect_1_10_gen { effect_gen( getarg(0), 1,2,3,4,5,6,7,8,9,10 ); }

OnStart:
	effect_gen( EF_MAGICALATTHIT, 1 );
	sleep 2000;
	effect_gen( EF_MAGICALATTHIT, 1,3 );
	sleep 1900;
	effect_gen( EF_MAGICALATTHIT, 1,3,8 );
	sleep 1700;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4 );
	sleep 1600;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6 );
	sleep 1500;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2 );
	sleep 1400;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7 );
	sleep 1300;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9 );
	sleep 1200;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9,5 );
	sleep 1100;
	while( 'bool_event ) {
		effect_1_10_gen( EF_MAGICALATTHIT );
		sleep 1000;
	}
	end;
OnStart2:
	effect_gen( EF_BLASTMINEBOMB, 1 );
	sleep 2000;
	effect_gen( EF_BLASTMINEBOMB, 1,3 );
	sleep 1900;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8 );
	sleep 1800;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4 );
	sleep 1700;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6 );
	sleep 1600;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2 );
	sleep 1500;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7 );
	sleep 1400;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9 );
	sleep 1300;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9,5 );
	sleep 1200;
	while( 'bool_event ) {
		effect_1_10_gen( EF_BLASTMINEBOMB );
		sleep 1100;
	}
	end;

OnStart3:
	while( 'bool_event ) {
		specialeffect EF_LORD, AREA, 'gen_11$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_12$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_13$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_14$;
		sleep 2000;
	}
	end;
}

1@lab,91,80,5	script	#gen_01	CLEAR_NPC,{ end; }
1@lab,96,81,5	duplicate(#gen_01)	#gen_02	CLEAR_NPC
1@lab,99,85,5	duplicate(#gen_01)	#gen_03	CLEAR_NPC
1@lab,99,91,5	duplicate(#gen_01)	#gen_04	CLEAR_NPC
1@lab,96,95,5	duplicate(#gen_01)	#gen_05	CLEAR_NPC
1@lab,91,97,5	duplicate(#gen_01)	#gen_06	CLEAR_NPC
1@lab,85,96,5	duplicate(#gen_01)	#gen_07	CLEAR_NPC
1@lab,82,91,5	duplicate(#gen_01)	#gen_08	CLEAR_NPC
1@lab,82,86,5	duplicate(#gen_01)	#gen_09	CLEAR_NPC
1@lab,85,81,5	duplicate(#gen_01)	#gen_10	CLEAR_NPC
1@lab,90,97,5	duplicate(#gen_01)	#gen_11	CLEAR_NPC
1@lab,82,89,5	duplicate(#gen_01)	#gen_12	CLEAR_NPC
1@lab,91,80,5	duplicate(#gen_01)	#gen_13	CLEAR_NPC
1@lab,99,88,5	duplicate(#gen_01)	#gen_14	CLEAR_NPC


// Warps
1@lab,102,28,0	warp2	lab_warp#1	2,2,1@lab,97,28
1@lab,80,28,0	warp2	lab_warp#2	2,2,1@lab,75,28
1@lab,58,28,0	warp2	lab_warp#3	2,2,1@lab,53,28
1@lab,29,38,0	warp2	lab_warp#4	2,2,1@lab,29,43
1@lab,48,82,0	warp2	lab_warp#5	2,2,1@lab,58,82
1@lab,123,88,0	warp2	lab_warp#end	2,2,verus01,152,150


//-- GM NPCs
1@lab,1,1,3	script	#adsw_	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0,0,2000) < 1)
		end;
	mes "[时间管理员]";
	mes "你想什么时间去？";
	next;
	switch( select( "取消", "1 开始", "2 开始", "3 开始", "4 开始" ) ) {
	case 1:
		close;
	case 2:
		donpcevent instance_npcname("#npc_con01") + "::OnStart";
		break;
	case 3:
		donpcevent instance_npcname("#npc_con06") + "::OnStart";
		break;
	case 4:
		donpcevent instance_npcname("#npc_con08") + "::OnStart";
		break;
	case 5:
		end;
	}
	warp instance_mapname("1@lab"), 84,90;
	end;
}

verus01,1,5,5	script	Laboratory Administrator#pa0829_01	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0,0,2000) < 1) {
		warp "prontera",100,100;
		end;
	}
	mes "[实验室管理员]";
	mes "一切都处理得很好！";
	close2;
	if (isbegin_quest(12347) > 0)
		erasequest 12347;
	end;
}
