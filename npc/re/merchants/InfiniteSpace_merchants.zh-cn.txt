//===== rAthena Script =======================================
//= Infinite Space
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Infinite Space related merchants and enchanter
//===== Changelogs: ==========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Removed unecessary use of functions [Everade]
//= 1.2 Added warp scroll merchant [Everade]
//============================================================

// Food Merchant
-	shop	inf_ration	-1,512:-1,513:-1,515:-1,516:-1

cmd_fild07,63,268,1	script	Emergency Food Merchant#pa0829_01	4_M_BIBI,{
	mes "[紧急食品商人]";
	mes "我这里有很多东西，当然，不是免费的。";
	close2;
	callshop "inf_ration",1;
	end;
}

// Warp Scroll Seller
cmd_fild07,375,167,1	script	Ruins Black Trader#pa0829_01	4_F_JOB_HUNTER,{
	mes "[黑色商人废墟]";
	mes "哎，每次进出这里不是很辛苦吗？只需要^0000ff20,000^000000 Zeny，就可以拥有一张直接通往遗迹入口的卷轴，你觉得怎么样？";
Purchase:
	next;
	switch( select( "给我一个", "我不这么认为" )) {
		case 1:
			if (!checkweight(22980,1) || (MaxWeight - Weight) < 1000) {
				mes "您无法继续对话，因为您有大量物品。";
				mes "请整理您的物品并重试。";
				close;
			}
			else if (Zeny < 20000) {
				mes "[黑色商人废墟]";
				mes "你似乎没有更多的钱了。一个卷轴花费 20,000 Zeny。";
				close;
			}
			Zeny -= 20000;
			getitem 22980,1;
			mes "[黑色商人废墟]";
			mes "这是一笔很好的交易。需要更多吗？";
			goto Purchase;
		case 2:
			mes "[黑色商人废墟]";
			mes "随时回来";
			close;
	}
}

// Equipment Shop
cmd_fild07,57,275,5	script	Artifact Appraiser#pa0829_01	1_F_02,{
	if (!checkweight(1201,1) || (MaxWeight - Weight) < 1000) {
		mes "您无法继续对话，因为您有大量物品。";
		mes "请整理您的物品并重试。";
		close;
	}
	.@stone_id = 6905;
	mes "[ 神器鉴定师 ]";
	mes "选择您要购买的设备类型。只要您有" + getitemname(.@stone_id) + ".";
	next;
	switch (select("取消：武器：盔甲")) {
		case 1:
			mes "[ 神器鉴定师 ]";
			mes "你想什么时候回来就什么时候回来吧~";
			close;

		case 2:
			setarray .@equip_id,1994,1938,13323,13126,28703,2024,16038,21014,28105,18128;
			.@price = 50;
			break;

		case 3:
			setarray .@equip_id,15141,22075,20779,19033;
			.@price = 50;
			break;
	}
	.@menu$ = "Cancel:";
	for (.@i = 0; .@i < getarraysize(.@equip_id); .@i++)
		.@menu$ += getitemname(.@equip_id[.@i]) + ":";
	.@s = select(.@menu$) - 1;
	switch (.@s) {
		case 0:
			mes "[ 神器鉴定师 ]";
			mes "你想什么时候回来就什么时候回来吧~";
			close;

		default:
			.@s--;
			mes "[ 神器鉴定师 ]";
			mes "您需要^0000FF" + .@price + "^000000 " + getitemname(.@stone_id) + "来购买" + getitemname(.@equip_id[.@s]) + ">~";
			next;
			if (select("取消：购买") == 1) {
				mes "[ 神器鉴定师 ]";
				mes "你想什么时候回来就什么时候回来吧~";
				close;
			}
			if (countitem(.@stone_id) < .@price) {
				mes "[ 神器鉴定师 ]";
				mes "您没有足够的 " + getitemname(.@stone_id) + " 来购买该商品。";
				close;
			}
			mes "[ 神器鉴定师 ]";
			mes "感谢您的信任。请下次再回来。";
			delitem .@stone_id,.@price;
			getitem .@equip_id[.@s],1;
			close;
	}
}

// Equipment Enchanter
cmd_fild07,60,275,3	script	Artifact Enhancer#pa0829_01	4_F_JOB_BLACKSMITH,{
	if (!checkweight(1201,1) || (MaxWeight - Weight) < 1000) {
		mes "您无法继续对话，因为您有大量物品。";
		mes "请整理您的物品并重试。";
		close;
	}
	disable_items;
	.@stone_id = 6905;
	function equip_check;
	mes "[神器增强器]";
	mes "如果您想强化在无限空间中获得的装备，那么您来对地方了。";
	mes "你有" + getitemname(.@stone_id) + "吗？";
	next;
	switch (select("如何为我的装备附魔？：为装备附魔。：初始化装备的附魔。")) {
		case 1:
			mes "[神器增强器]";
			mes "如果您探索帕罗斯灯塔下方的空间，您将能够获得" + getitemname(.@stone_id) + "。";
			next;
			mes "[神器增强器]";
			mes "那个材料，我可以强化你从无边空间得到的装备。";
			next;
			mes "[神器增强器]";
			mes "我只能对第三个和第四个槽位进行附魔。";
			next;
			mes "[神器增强器]";
			mes "装备在附魔过程中不会被破坏，但在初始化附魔时有很大概率会被破坏。";
			next;
			mes "[神器增强器]";
			mes "想要给装备附魔的话就来找我吧。";
			break;

		case 2:
			.@stone_id = 6905;
			.@fee = 20;
			mes "[神器增强器]";
			mes "请选择您想要附魔的装备。";
			next;
			switch (select("取消:武器:盔甲:鞋子:服装:头盔")) {
				case 1:
					mes "[神器增强器]";
					mes "想要给装备附魔的话就来找我吧。";
					close;

				case 2:
					.@part = EQI_HAND_R;
					break;

				case 3:
					.@part = EQI_ARMOR;
					break;

				case 4:
					.@part = EQI_SHOES;
					break;

				case 5:
					.@part = EQI_GARMENT;
					break;

				case 6:
					.@part = EQI_HEAD_TOP;
					break;
			}
			.@equip_id = getequipid(.@part);
			.@refine = getequiprefinerycnt(.@part);
			equip_check(.@equip_id);
			for (.@i = 0;.@i < 4;.@i++) {
				.@card[.@i] = getequipcardid(.@part,.@i);
				.@check[.@i] = getequipcardid(.@part,.@i);
			}
			if (.@card[2] > 0) {
				mes "[神器增强器]";
				mes "我只能附魔到第三个位置。你的装备无法进一步附魔。";
				close;
			}
			switch (.@part) {
				case EQI_HAND_R:
					setarray .@enchant$,
					"4700,4701:4710,4711:4720,4721",
					"4811,4810,4809,4808,4820,4821,4822,4823:4815,4814,4813,4812,4826,4827,4828,4829:4832,4833,4834,4835,4836,4837,4838,4839";
					break;

				case EQI_ARMOR:
				case EQI_SHOES:
					setarray .@enchant$,
					"4700,4701,4702,4703:4710,4711,4712,4713:4720,4721,4722,4723",
					"4795,4796,4797:4870,4871,4800:4870,4871,4800";
					break;

				case EQI_GARMENT:
				case EQI_HEAD_TOP:
					setarray .@enchant$,
					"4700,4701,4702,4703:4710,4711,4712,4713:4720,4721,4722,4723",
					"4861,4862,4867,4868,4900:4861,4862,4867,4868,4900:4861,4862,4867,4868,4900";
					break;
			}
			.@slot = (.@card[3] > 0 ? 2 : 3);
			.@index = (.@slot == 3 ? 0 : 1);
			mes "[神器增强器]";
			mes "您可以选择3种附魔，附魔费用为" + .@fee + " " + getitemname(.@stone_id) + ".";
			mes "我会确保它毫无问题地被施魔法。";
			next;
			.@type = select("退出：物理：魔法：范围") - 2;
			mes "[神器增强器]";
			if (.@slot == 3)
				mes "好吧，让我们开始第一个附魔吧。";
			else
				mes "好吧，我们继续第二个结界。";
			next;
			if (select("我稍后回来：请继续。") == 1) {
				mes "[神器增强器]";
				mes "如果你改变主意，就回到我身边吧。";
				close;
			}
			if (countitem(.@stone_id) < .@fee) {
				mes "[神器增强器]";
				mes "嗯。顺便说一句，我认为你没有在听，你没有足够的" + getitemname(.@stone_id) + ".";
				close;
			}
			explode(.@T$,.@enchant$[.@index],":");
			explode(.@TT$,.@T$[.@type],",");
			.@enchant = atoi(.@TT$[rand(getarraysize(.@TT$))]);
			.@card[.@slot] = .@enchant;
			delitem .@stone_id,.@fee;
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@check[0], .@check[1], .@check[2], .@check[3]))
				close;
			delequip .@part;
			getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
			specialeffect2 EF_REPAIRWEAPON;
			mes "[神器增强器]";
			mes "嗯。做得很好。立即检查您的设备。";
			break;
		
		case 3:
			.@stone_id = 6905;
			.@fee = 30;
			.@break_chance = 30;
			mes "[神器增强器]";
			mes "请选择您想要附魔的装备。";
			next;
			switch (select("取消:武器:盔甲:鞋子:服装:头盔")) {
				case 1:
					mes "[神器增强器]";
					mes "想要给装备附魔的话就来找我吧。";
					close;

				case 2:
					.@part = EQI_HAND_R;
					break;

				case 3:
					.@part = EQI_ARMOR;
					break;

				case 4:
					.@part = EQI_SHOES;
					break;

				case 5:
					.@part = EQI_GARMENT;
					break;

				case 6:
					.@part = EQI_HEAD_TOP;
					break;
			}
			.@equip_id = getequipid(.@part);
			.@refine = getequiprefinerycnt(.@part);
			equip_check(.@equip_id);
			for (.@i = 0; .@i < 4; .@i++) {
				.@card[.@i] = getequipcardid(.@part,.@i);
				.@check[.@i] = .@card[.@i];
			}
			if (.@card[3] == 0) {
				mes "[神器增强器]";
				mes "你的装备没有任何附魔。";
				close;
			}
			mes "[神器增强器]";
			mes "你的无限空间装备有可能会在初始化过程中被破坏。你还愿意继续吗？";
			next;
			if (select("退出：继续") == 1) {
				mes "[神器增强器]";
				mes "如果你改变主意，就回到我身边吧。";
				close;
			}
			if (countitem(.@stone_id) < .@fee) {
				mes "[神器增强器]";
				mes "嗯。顺便说一句，我认为你没有在听，你没有足够的" + getitemname(.@stone_id) + ".";
				close;
			}
			delitem .@stone_id,.@fee;
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@check[0], .@check[1], .@check[2], .@check[3]))
				close;
			delequip .@part;
			if (rand(1,100) > .@chance) {
				getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],0,0;
				specialeffect2 EF_REPAIRWEAPON;
				mes "[神器增强器]";
				mes "嗯。做得很好。立即检查您的设备。";
			} else {
				specialeffect2 EF_REFINEFAIL;
				mes "[神器增强器]";
				mes "嗯，我确实警告过你。你并不幸运，是吗？";
			}
			break;
	}
	close;

	function	equip_check	{
		setarray .@equip_id,1994,1938,13323,13126,28703,2024,16038,21014,28105,18128,15141,22075,20779,19033;
		if (!getarg(0)) {
			mes "[神器增强器]";
			mes "你把装备脱下来了吗？";
			close;
		}
		if (inarray(.@equip_id,getarg(0)) == -1) {
			mes "[神器增强器]";
			mes "该装备不适合附魔。别忘了，只有无限装备系列才能附魔。";
			close;
		}
		return;
	}
}
