//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= Temporal Boots enchants NPCs.
//===== Changelogs: ==========================================
//= 1.0 First Version merged from idathena.
//=     Credit exneval. [Capuche]
//===== Translated By: =======================================
//= dsc
//============================================================

// Merchant
//============================================================
glast_01,210,273,5	script	Hugin's Butler#pa0829	1_F_04,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "你的包太重了。减轻一些体重然后回来。";
		close;
	}
	mes "[休金的管家]";
	mes "为各种时空靴子物品带来时空水晶物品。选择你想要的东西。";
	next;
	switch( select( "取消", "购买时空靴子（需要1个时空水晶）", "升级时空靴子（需要5个时空水晶）", "升级改装靴子（需要5个时空水晶）" ) ) {
	case 1:
		mes "[休金的管家]";
		mes "你想什么时候回来就什么时候回来。";
		close;
	case 2:
		if (countitem(6607) > 0) {
			mes "[休金的管家]";
			mes "带上这双颞靴。我们可以用 5 个时空水晶升级你的时空靴子。";
			delitem 6607,1;// Temporal_Crystal
			getitem 2499,1;// Temporal_Boots
			close;
		}
		mes "[休金的管家]";
		mes "您需要更多时空水晶。当您在旧格拉斯海姆旅行期间获得更多时空水晶时，请回来。";
		close;
	case 3:
		setarray .@equip_type[0],
			22000,	// Temporal_STR_Boots
			22001,	// Temporal_INT_Boots
			22002,	// Temporal_AGI_Boots
			22003,	// Temporal_VIT_Boots
			22004,	// Temporal_DEX_Boots
			22005;	// Temporal_LUK_Boots
		break;
	case 4:
		setarray .@equip_type[0],
			22107,	// Modified_STR_Boots
			22108,	// Modified_INT_Boots
			22109,	// Modified_AGI_Boots
			22110,	// Modified_VIT_Boots
			22111,	// Modified_DEX_Boots
			22112;	// Modified_LUK_Boots
		break;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[休金的管家]";
		mes "只有您佩戴的物品才能升级。";
		mes "穿上想要升级的^0000FF颞靴^000000，然后再跟我说话。";
		close;
	}
	if (getequipid(EQI_SHOES) != 2499) {
		mes "[休金的管家]";
		mes "你没有穿着颞靴。穿上后跟我说话。";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[休金的管家]";
		mes "时空水晶不够。您需要 5 个时空水晶来升级您的物品。";
		close;
	}
	mes "[休金的管家]";
	mes "您可以用 ^0000FF1 个时空靴子和 5 个时空水晶^000000 换取以下物品之一。 ^FF0000新的精炼率将不会保留^000000。";
	next;
	.@s = select( "取消", "力量靴子", "智力靴子", "敏捷靴子", "维特靴子", "敏捷靴子", "陆克靴子" ) - 2;
	if (.@s == -1) {
		mes "[休金的管家]";
		mes "你想什么时候回来就什么时候回来。";
		close;
	}
	setarray .@type$[0], "Strength", "Intelligence", "Agility", "Vitality", "Dexterity", "Lucky";
	mes "[休金的管家]";
	mes "您确定要购买 Temporal ^FF0000" + .@type$[.@s] + "^000000 靴子吗？没有退款。";
	next;
	if (select( "再想想", "是的，我确定。" ) == 1) {
		mes "[休金的管家]";
		mes "好吧，等你确定了再回来。";
		close;
	}
	mes "[休金的管家]";
	mes "靴子已根据您的需要进行了升级。";
	if (getequipid(EQI_SHOES) == 2499) {
		delequip EQI_SHOES;
		delitem 6607,5;// Temporal_Crystal
		getitem .@equip_type[.@s],1;
	}
	close;
}

// Enchant and Socket NPC
//============================================================
glast_01,212,273,4	script	Hugin's Magician#pa0829	1_F_01,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "你的包太重了。减轻一些体重然后回来。";
		close;
	}
	mes "[休金的魔术师]";
	mes "所以你是来给时空之靴附魔的。请注意，带有插槽的靴子无法被附魔，并且此升级仅适用于您所穿着的靴子。";
	next;
	if (select( "附魔如何发挥作用？", "对我的时空靴子产生效果" ) == 1) {
		mes "[休金的魔术师]";
		mes "你可以在旅途中不断获得^0000FF凝固法术^000000。";
		next;
		mes "[休金的魔术师]";
		mes "我可以为你的时空之靴物品附魔，具体取决于该物品的数量。 ^0000FF不是普通型靴子^000000。仅适用于那 6 个扩展版靴子。";
		next;
		mes "[休金的魔术师]";
		mes "但是，一旦将插槽放入该物品中，就无法对其进行附魔。请务必记住这一点。";
		next;
		mes "[休金的魔术师]";
		mes "我不会随机发出效果。您可以为该项目选择您想要的效果。";
		next;
		mes "[休金的魔术师]";
		mes "当然，它仅适用于第四个插槽。一开始可以产生很小的效果，然后效果就会变得更强。";
		next;
		mes "[休金的魔术师]";
		mes "为了获得最终的选项升级，它将需要更多的凝固法术物品。别忘了这一点。";
		next;
		mes "[休金的魔术师]";
		mes "一旦第四个插槽完成最佳选项，您将获得第三个插槽的随机奖励效果。加上..一定的付款~";
		next;
		mes "[休金的魔术师]";
		mes "另外，即使附魔失败你也不会丢失物品，我们还保证保留卡牌和精炼率。但我们无法初始化附魔。";
		next;
		mes "[休金的魔术师]";
		mes "呼……";
		mes "无论如何，这就是基本的附魔信息。现在你应该亲自看看。";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[休金的魔术师]";
		mes "你穿着该物品吗？";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);

	setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
	setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
	setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
	setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6
	setarray .@enchant_cost[0],1,4,15,30,10;

	switch(.@equip_id) {
	case 22000:	// Temporal_STR_Boots
	case 22001:	// Temporal_INT_Boots
	case 22002:	// Temporal_AGI_Boots
	case 22003:	// Temporal_VIT_Boots
	case 22004:	// Temporal_DEX_Boots
	case 22005:	// Temporal_LUK_Boots

	case 22107:	// Modified_STR_Boots
	case 22108:	// Modified_INT_Boots
	case 22109:	// Modified_AGI_Boots
	case 22110:	// Modified_VIT_Boots
	case 22111:	// Modified_DEX_Boots
	case 22112:	// Modified_LUK_Boots
		.@equip_name$ = getequipname(EQI_SHOES);
		setarray .@card[0],
			getequipcardid(EQI_SHOES,0),
			getequipcardid(EQI_SHOES,1),
			getequipcardid(EQI_SHOES,2),
			getequipcardid(EQI_SHOES,3);
		copyarray .@equip_card[0], .@card[0], 4;	// for final check
		.@equip_refine = getequiprefinerycnt(EQI_SHOES);

		if (.@card[2] > 0) {
			mes "[休金的魔术师]";
			mes "这双靴子已经突破了附魔的极限。我们不能再迷惑他们了。";
			close;
		}
		if (.@card[3] == 0) {// 4th slot 1st try enchanting
			.@cost = .@enchant_cost[0];
			mes "[休金的魔术师]";
			mes "想要附魔^0000FF" + .@equip_name$ + "^000000吗？";
			mes "对于第一个附魔，您需要^0000FF" + .@cost + "^000000凝固法术物品。";
			next;
			.@s = select( "退出", "斗志", "射箭", "法术", "活力", "攻击速度", "幸运" ) - 2;
			if (.@s == -1) {
				mes "[休金的魔术师]";
				mes "好吧，等你准备好了就回来吧。";
				close;
			}
			.@card[3] = .@enchant_1[.@s];
			.@string$ = "enchant number ^6300001^000000.";
		}
		else {
			for ( .@enchant_num = 1; .@enchant_num < 5; .@enchant_num++ ) {
				for ( .@enchant_type = 0; .@enchant_type < 6 && .@card[3] != getd( ".@enchant_" + .@enchant_num + "[" + .@enchant_type + "]" ); .@enchant_type++ )
					continue;
				if (.@enchant_type < 6)
					break;
			}
			if (.@enchant_num == 5) {
				mes "[休金的魔术师]";
				mes "发生了一些错误。";
				close;
			}
			.@cost = .@enchant_cost[.@enchant_num];

			mes "[休金的魔术师]";
			if (.@enchant_num == 4) {
				.@card[2] = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
				.@string$ = "^990000Bonus effect ^000000 upgrade.";
				mes "第四个插槽已经升级为最终选项。您想要第三个插槽的随机奖励效果吗？你需要^0000ff" + .@cost + "^000000凝固法术物品。";
			}
			else {
				.@number = .@enchant_num + 1;
				.@card[3] = getd( ".@enchant_" + (.@enchant_num+1) + "[" + .@enchant_type + "]" );
				.@string$ = "enchant number ^630000" + .@number + "^000000.";
				mes "附魔^0000FF" + .@equip_name$ + "^000000的第四个插槽为^0000FF" + .@number + "^000000级别效果。需要^0000FF" + .@cost + "^000000凝固法术物品。";
			}
			next;
			if (select("退出","效果升级！") == 1) {
				mes "[休金的魔术师]";
				mes "好吧，等你准备好了就回来吧。";
				close;
			}
		}
		if (countitem(6608) < .@cost) {
			mes "[休金的魔术师]";
			mes "嗯，您缺少 " + (.@cost - countitem(6608)) + " 凝固法术物品。去获得更多，然后我们可以谈论更多的附魔。";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		delitem 6608,.@cost;// Coagulated_Spell

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_SHOES, .@equip_id) || callfunc("F_IsEquipCardHack", EQI_SHOES, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", EQI_SHOES, .@equip_refine))
			close;

		delequip EQI_SHOES;
		mes "[休金的魔术师]";
		mes "尝试着" + .@string$;
		getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@card[2],.@card[3];
		close;
	default:
		mes "[休金的魔术师]";
		mes "这不是适合该附魔的物品。";
		mes "请记住，只有这 6 种时空之靴可用于附魔。";
		close;
	}
}

glast_01,210,270,0	script	Hugin's Craftsman#pa0829	4_F_JOB_BLACKSMITH,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "你的包太重了。减轻一些体重然后回来。";
		close;
	}
	mes "[胡金的工匠]";
	mes "那个魔法师总是说，附魔时空之靴是最好的。但是，我认为完全不同。";
	next;
	mes "[胡金的工匠]";
	mes "我认为在该项目中添加一个插槽是最好的。即使存在一定的失败风险。";
	next;
	mes "[胡金的工匠]";
	mes "给我带来^0000FF5个时空水晶^000000，我会尝试在你的高级时空靴子上放一个插槽。但是，有可能^FF0000失败并破坏项目^000000。可以吗？";
	next;
	if (select( "取消", "冒险尝试添加插槽" ) == 1) {
		mes "[胡金的工匠]";
		mes "好的，你想什么时候回来就什么时候回来。";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[胡金的工匠]";
		mes "您确定您穿着该商品吗？";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);
	switch(.@equip_id) {
	case 22000:	callsub( S_Slot,22006 );	// Temporal_STR_Boots
	case 22001:	callsub( S_Slot,22009 );	// Temporal_INT_Boots
	case 22002:	callsub( S_Slot,22010 );	// Temporal_AGI_Boots
	case 22003:	callsub( S_Slot,22007 );	// Temporal_VIT_Boots
	case 22004:	callsub( S_Slot,22008 );	// Temporal_DEX_Boots
	case 22005:	callsub( S_Slot,22011 );	// Temporal_LUK_Boots

	case 22107:	callsub( S_Slot,22113 );	// Modified_STR_Boots
	case 22108:	callsub( S_Slot,22114 );	// Modified_INT_Boots
	case 22109:	callsub( S_Slot,22115 );	// Modified_AGI_Boots
	case 22110:	callsub( S_Slot,22116 );	// Modified_VIT_Boots
	case 22111:	callsub( S_Slot,22117 );	// Modified_DEX_Boots
	case 22112:	callsub( S_Slot,22118 );	// Modified_LUK_Boots
	default:
		mes "[胡金的工匠]";
		mes "不是这个！我们需要^0000FF高级颞靴^000000物品！物品^0000FF具有统计效果但没有插槽^000000！";
		close;
	}

S_Slot:
	.@equip_id = getequipid(EQI_SHOES);
	mes "[胡金的工匠]";
	mes "并且^FF0000如果失败，所有附魔或精炼率都将丢失。^000000";
	mes "还想冒险吗？";
	next;
	if (select( "取消", "我同意！" ) == 1) {
		mes "[胡金的工匠]";
		mes "好的，你想什么时候回来就什么时候回来。";
		close;
	}
	mes "[胡金的工匠]";
	mes "再说一次，^FF0000那个魔术师不会为任何带有插槽的物品附魔。^000000";
	mes "还想继续吗？";
	next;
	if (select( "取消", "我明白。尝试将其插入插槽。" ) == 1) {
		mes "[胡金的工匠]";
		mes "好的，你想什么时候回来就什么时候回来。";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[胡金的工匠]";
		mes "您必须携带^0000FF5个时空水晶物品^000000。没有什么是免费的，你知道...";
		close;
	}
	delitem 6607,5;// Temporal_Crystal
	if (getequipid(EQI_SHOES) == .@equip_id) {
		delequip EQI_SHOES;
		if (rand(1,100) < 50) {
			mes "[胡金的工匠]";
			mes "Arrggg，我们失败了。下次好运。";
			specialeffect2 EF_PHARMACY_FAIL;
			close;
		}
		mes "[胡金的工匠]";
		mes "成功的！干得好。";
		specialeffect2 EF_MAXPOWER;
		getitem getarg(0),1;
	}
	close;
}

// Temporal shoes slotted - npc enchant
glast_01,184,283,4	script	Dark magic master#pa082	1_F_01,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "你的包太重了。减轻一些体重然后回来。";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "看来你带的东西太多了。将一些物品存放起来，然后再回来。";
		close;
	}
	// requirements
	.@coagulated_id = 6608;// Coagulated_Spell
	.@polluted_id = 6755;// Polluted_Spell
	.@cost_zeny = 100000;

	mes "[黑魔法大师]";
	mes "我只处理带插座的颞靴。";
	next;
	if (select( "附魔如何发挥作用？", "对我的时空靴子产生效果" ) == 1) {
		mes "[黑魔法大师]";
		mes "你应该向胡金的魔法大师询问所有基本的事情。我想更深入地讨论迷人的工作。";
		next;
		mes "[黑魔法大师]";
		mes "我需要 4 件物品。 ^0000ff" + callfunc("F_InsertComma",.@cost_zeny) + "、" + getitemname(.@polluted_id) + "、" + getitemname(.@coagulated_id) + "、颞靴（插座）系列^000000。如果你没有这些，我无法帮助你。";
		next;
		mes "[黑魔法大师]";
		mes "这是我的小爱好.. 使用污染和凝结法术对带孔时空靴子进行附魔。";
		next;
		mes "[黑魔法大师]";
		mes "当然，每当你想一次又一次地附魔时，你就必须应对更大的破坏风险。";
		next;
		mes "[黑魔法大师]";
		mes "附魔顺序与胡金的魔法师的附魔顺序相同。请参考他的解释。";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);

	switch(.@equip_id) {
	case 22006:	// Temporal_Str_Boots_
	case 22007:	// Temporal_Vit_Boots_
	case 22008:	// Temporal_Dex_Boots_
	case 22009:	// Temporal_Int_Boots_
	case 22010:	// Temporal_Agi_Boots_
	case 22011:	// Temporal_Luk_Boots_
	case 22113:	// Modified_Str_Boots_
	case 22114:	// Modified_Int_Boots_
	case 22115:	// Modified_Agi_Boots_
	case 22116:	// Modified_Vit_Boots_
	case 22117:	// Modified_Dex_Boots_
	case 22118:	// Modified_Luk_Boots_
		break;
	case -1:
		mes "[黑魔法大师]";
		mes "你穿着该物品吗？";
		close;
	default:
		mes "[黑魔法大师]";
		mes "这不是适合该附魔的物品。请记住，只有这 6 种带有插槽的时空靴子才可用于隐藏附魔。";
		close;
	}
	// enchants ID
	setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
	setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
	setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
	setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6

	// requirements amount
	setarray .@en_amount_coag[0],3,10,20,40,50;
	setarray .@en_amount_polluted[0],1,2,4,7,10;

	// (custom) chances of success (official value unknown)
	setarray .@chances[0],100,70,70,70,70;

	// data of item equipped
	.@equip_name$ = getequipname(EQI_SHOES);
	setarray .@card[0],
		getequipcardid(EQI_SHOES,0),
		getequipcardid(EQI_SHOES,1),
		getequipcardid(EQI_SHOES,2),
		getequipcardid(EQI_SHOES,3);
	copyarray .@equip_card[0], .@card[0], 4;
	.@equip_refine = getequiprefinerycnt(EQI_SHOES);

	if (.@card[2] > 0) {
		mes "[黑魔法大师]";
		mes "这双靴子已经突破了附魔的极限。我们不能再迷惑他们了。";
		close;
	}
	if (.@card[3] == 0) {// 4th slot 1st try enchanting
		.@amount_coag = .@en_amount_coag[0];
		.@amount_poll = .@en_amount_polluted[0];

		mes "[黑魔法大师]";
		mes "想要附魔^0000ff" + .@equip_name$ + "（套接字）^000000？对于第一个附魔，您需要 ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + "、" + .@amount_coag + " " + getitemname(.@coagulated_id) + " 和 " + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000。";
		mes "有一定的失败风险..";
		next;
		.@s = select( "退出", "斗志", "射箭", "法术", "活力", "攻击速度", "幸运" ) - 2;
		if (.@s == -1) {
			mes "[黑魔法大师]";
			mes "好吧，等你准备好了就回来吧。";
			close;
		}
		.@card[3] = .@enchant_1[.@s];
		.@string$ = "enchant number ^6300001^000000.";
		.@num = 0;
	}
	else {
		for ( .@num = 1; .@num < 5; .@num++ ) {
			for ( .@type = 0; .@type < 6 && .@card[3] != getd( ".@enchant_" + .@num + "[" + .@type + "]" ); .@type++ )
				continue;
			if (.@type < 6)
				break;
		}
		if (.@num == 5) {
			mes "[黑魔法大师]";
			mes "发生了一些错误。";
			close;
		}
		.@amount_coag = .@en_amount_coag[.@num];
		.@amount_poll = .@en_amount_polluted[.@num];

		mes "[黑魔法大师]";
		if (.@num == 4) {
			.@card[2] = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
			.@string$ = "^990000 Bonus effect ^000000 upgrade.";
			mes "您想要第三个插槽的随机奖励效果吗？您需要 ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + "、" + .@amount_coag + " " + getitemname(.@coagulated_id) + " 和 " + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000。但失败的风险很大..";
		}
		else {
			.@level = .@num + 1;
			.@card[3] = getd( ".@enchant_" + (.@num+1) + "[" + .@type + "]" );
			.@string$ = "enchant number ^990000" + .@level + "^000000.";
			mes "附魔^0000ff" + .@equip_name$ + "（插槽）^000000的第4个插槽作为^0000ff" + .@level + "^000000级别效果。需要 ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + "、" + .@amount_coag + " " + getitemname(.@coagulated_id) + " 和 " + callfunc("F_InsertComma",.@cost_zeny) + " z^000000。有一定的失败风险..";
		}
		next;
		if (select( "退出", "效果升级！" ) == 1) {
			mes "[黑魔法大师]";
			mes "好吧，等你准备好了就回来吧。";
			close;
		}
	}
	if (countitem(.@coagulated_id) < .@amount_coag) {
		mes "[黑魔法大师]";
		mes "嗯，您缺少 " + (.@amount_coag - countitem(.@coagulated_id)) + " " + getitemname(.@coagulated_id) + " 项目。去获得更多，然后我们可以谈论更多的附魔。";
		close;
	}
	if (countitem(.@polluted_id) < .@amount_poll) {
		mes "[黑魔法大师]";
		mes "嗯，您缺少 " + (.@amount_poll - countitem(.@polluted_id)) + " " + getitemname(.@polluted_id) + " 项目。去获得更多，然后我们可以谈论更多的附魔。";
		close;
	}
	if (Zeny < .@cost_zeny) {
		mes "[黑魔法大师]";
		mes "您必须携带^0000ff" + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000。没有什么是免费的，你知道...";
		close;
	}
	delitem .@coagulated_id, .@amount_coag;
	delitem .@polluted_id, .@amount_poll;
	Zeny -= .@cost_zeny;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", EQI_SHOES, .@equip_id) || callfunc("F_IsEquipCardHack", EQI_SHOES, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", EQI_SHOES, .@equip_refine))
		close;

	delequip EQI_SHOES;
	if (.@chances[.@num] < rand(1,100)) {
		specialeffect2 EF_LORD;
		mes "[黑魔法大师]";
		mes "Arrggg，我们失败了。下次好运。";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	mes "[休金的魔术师]";
	mes "尝试着" + .@string$;
	getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@card[2],.@card[3];
	close;
}

// Items exchange for card
glast_01,216,292,5	script	Portrait collector#0002	4_M_OILMAN,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "你的包太重了。减轻一些体重然后回来。";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "看来你带的东西太多了。将一些物品存放起来，然后再回来。";
		close;
	}
	.@coagulated_id = 6608;		// Coagulated_Spell
	.@polluted_id = 6755;		// Polluted_Spell
	.@white_card = 4608;		// White_Knightage_Card
	.@khalitzburg_card = 4609;	// Khaliz_Knightage_Card

	mes "[肖像收藏家]";
	mes "我收集肖像已经一年了。现在是时候向人们展示我的收藏了。";
	next;
	mes "[肖像收藏家]";
	mes "嘿，你有很多" + getitemname(.@polluted_id) + "或" + getitemname(.@coagulated_id) + "吗？如果是这样，你为什么不交换我拥有的一幅漂亮的肖像呢？";
	next;
	switch( select( "取消", "兑换" + getitemname(.@white_card), "兑换" + getitemname(.@khalitzburg_card) ) ) {
	case 1:
		mes "[肖像收藏家]";
		mes "好吧，好吧。准备好后就回来吧。";
		close;
	case 2:
		.@reward_id = .@white_card;
		.@amount_coag = 3000;
		.@amount_polluted = 70;
		break;
	case 3:
		.@reward_id = .@khalitzburg_card;
		.@amount_coag = 5000;
		.@amount_polluted = 100;
		break;
	}
	mes "[肖像收藏家]";
	mes "您想要用什么来换取" + getitemname(.@reward_id) + "？";
	next;
	if (countitem(.@coagulated_id) < .@amount_coag)
		.@color$[0] = "^666666";
	if (countitem(.@polluted_id) < .@amount_polluted)
		.@color$[1] = "^666666";
	switch( select( "取消", sprintf("%s%s %s^000000", .@color$[0], callfunc("F_InsertComma",.@amount_coag), getitemname(.@coagulated_id)), sprintf("%s%s %s^000000", .@color$[1], callfunc("F_InsertComma",.@amount_polluted), getitemname(.@polluted_id)) ) ) {
	case 1:
		mes "[肖像收藏家]";
		mes "好吧，好吧。准备好后就回来吧。";
		close;
	case 2:
		.@id = .@coagulated_id;
		.@amount = .@amount_coag;
		break;
	case 3:
		.@id = .@polluted_id;
		.@amount = .@amount_polluted;
		break;
	}
	if (countitem(.@id) < .@amount) {
		mes "[肖像收藏家]";
		mes "嗯，你现在没有足够的法术。去获取更多。";
		close;
	}
	mes "[肖像收藏家]";
	mes "你确定你不会后悔吗？";
	next;
	if (select( "取消", "当然" ) == 1) {
		mes "[肖像收藏家]";
		mes "好吧，好吧。准备好后就回来吧。";
		close;
	}
	mes "[肖像收藏家]";
	mes "酷，我们来做个交易吧。";
	delitem .@id, .@amount;
	getitem .@reward_id,1;
	close;
}
