//===== rAthena Script =======================================
//= Karachun Event (Xmas Rings Quest)
//===== By: ==================================================
//= Lupus (1.0)
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project; SVN r4880+
//===== Description: =========================================
//= Custom event: Karachun (from 8 Dec till 8 Jan), X-Mas, HNY
//===== Additional Comments: =================================
//= 1.0 Tested, fully working. 1.0a a few typos fix
//= 1.1 Readded Mdef +1 and  Luk +1 to Santa's Hat [Lupus]
//= 1.2 Fixed double quotes not being escaped [brianluau]
//===== Translated By: =======================================
//= dsc
//============================================================

prontera,156,174,4	script	Snower	713,{
	mes "[雪人]";
	if (Q_XMSRINGS == 2) {
		mes "哦……这是真的吗？你帮助了我姐姐史尼威？谢谢。";
	} else if (Q_XMSRINGS == 3) {
		mes "斯诺威给我发了一只鸟并留言。她告诉我你的帮助...";
		mes "我会告诉你魔法戒指的秘密。";
		emotion ET_BEST;
		next;
		mes "[雪人]";
		mes "他们仅在12月8日至1月8日期间拥有魔力。";
		next;
		mes "[雪人]";
		mes "顺便说一句，只要戴上圣诞老人的帽子就能发现他们真正的力量。";
		next;
		mes "[雪人]";
		mes "如果你把两个戒指都戴上，他们的力量就会加倍。";
		mes "祝你今天过得愉快！";
		close;
	} else {
		mes "你好！我和我的两个姐妹正在为整个三个假期准备礼物！";
	}
	emotion ET_CRY;
	next;
	menu "Where are your sisters?",-, "What holidays?",M_PR,"What gifts?",M_WRAP,"May I help you?",M_QUEST;

	mes "[雪人]";
	mes "我的妹妹斯尼威去了某个遥远的地方，为他们可怜的孩子们准备礼物……我不知道在哪里。你应该问问我姐姐和她的小鸟。";
	next;
	mes "[雪人]";
	mes "我姐姐？她的名字叫斯诺伊。她航行到某个绿色岛屿...她正在谈论一些给海豚的礼物什么的...";
	close;

M_PR:
	mes "[雪人]";
	mes "整个三个假期？嗯，他们陆续来了！";
	mes "^000080圣诞节^000000、^FF0000卡拉春^000000和^008000新年前夜^000000！";
	next;
	mes "[雪人]";
	mes "^FF0000卡拉春^000000？呵呵……这是一个古老的异教徒节日。你知道，他们过去常常在新年和圣诞节出现之前庆祝它。";
	next;
	goto M_QUEST;

M_WRAP:
	mes "[雪人]";
	mes "我的姐妹们说所有的礼物都应该看起来一样。否则有些孩子可能会为了漂亮的东西而打架！";
	next;
	mes "[雪人]";
	mes "您需要我用灰色的纸包装您精美的礼品盒吗？";
	next;
	menu "Wrap them, please!",-, "No, thanks.",M_QUEST;

	set @prizes, countitem(664)+countitem(665)+countitem(666)+countitem(667);
	if (@prizes == 0) {
		mes "[雪人]";
		mes "我没看到你身上有什么精美盒子里的礼物……";
		if(countitem(644))mes "你只有灰色的盒子。";
		close;
	}
	delitem 664,countitem(664);
	delitem 665,countitem(665);
	delitem 666,countitem(666);
	delitem 667,countitem(667);
	getitem 644,@prizes;

	mes "[雪人]";
	mes "*随机播放*给你。";
	next;

M_QUEST:
	mes "[雪人]";
	mes "我希望你尽快找到我的姐妹们……她们需要帮助。";
	if (Q_XMSRINGS == 0) set Q_XMSRINGS,1;
	close;

OnInit:
//Santa's Hat
	setitemscript 2236,"{ bonus bMdef,1; bonus bLuk,1; if(isequipped(2636,2637)){if(@xmr == gettime(DT_MINUTE))end; set @xmr,gettime(DT_MINUTE); misceffect EF_RAINBOW; end;} if(isequipped(2636)){if(@xmr == gettime(DT_MINUTE))end; set @xmr,gettime(DT_MINUTE); misceffect EF_SPHERE;} if(isequipped(2637)){if(@xmr == gettime(DT_MINUTE))end; set @xmr,gettime(DT_MINUTE); misceffect EF_ANGEL2;}}";
//Gold Xmas Ring
	setitemscript 2636,"{ bonus bLoseSPWhenUnequip,30; if(isequipped(2236)==0)end; if(getskilllv(\"AL_HEAL\")){skill \"TF_HIDING\",4+isequipped(2637);}else{skill \"AL_HEAL\",1+4*isequipped(2637);} }";
//Silver Xmas Ring
	setitemscript 2637,"{ bonus bDamageWhenUnequip,40; if(isequipped(2236)==0)end; if(getskilllv(\"AL_TELEPORT\")){skill \"MC_IDENTIFY\",1;}else{skill \"AL_TELEPORT\",1+isequipped(2636);} }";
	end;
}

louyang,224,249,4	script	Sneewy	717,{
	mes "[打喷嚏]";
	if (Q_XMSRINGS == 0) {
		mes "你好！我在等姐姐的小鸟邮件……";
		emotion ET_CRY;
		close;
	}
	if (Q_XMSRINGS == 1) {
		mes "我希望我能再为本地孩子准备 4 个灰色礼品盒……";
		emotion ET_CRY;
		next;
		mes "[打喷嚏]";
		mes "哦，你好。我哥哥斯诺尔把一切都告诉你了吗？";
		next;
		menu "Here are your 4 Gift Boxes",-, "I don't know any Snowhatevers...",M_END;

		mes "[打喷嚏]";
		if (countitem(644) < 4) {
			mes "给我 4 件礼物给贫穷的孩子...等等，你还没有 4 件！";
			emotion ET_SORRY;
			close;
		}
		set Q_XMSRINGS,2;
		delitem 644,4;
		getnameditem 2637,strcharinfo(0);
		emotion ET_CHUPCHUP;
		mes "谢谢你！这是我给你的东西！";
		next;
		mes "[打喷嚏]";
		mes "顺便说一句，如果你戴上 2 个不同的戒指，那么你的假期升级会更容易！";
		close;
	}
	emotion ET_THANKS;
	mes "再次感谢！没有礼物，孩子们就无法迎接节日！";
	close;

M_END:
	mes "[打喷嚏]";
	mes "可惜...我有一个神奇的圣诞老人戒指...我愿意用它换4个礼盒...";
	emotion ET_SWEAT;
	close;
}

izlu2dun,133,160,2	script	Snoowy	714,{
	mes "[雪白]";
	if (Q_XMSRINGS == 0) {
		mes "你好哦！你认识我的哥哥和姐姐吗？为何如此？我的兄弟斯诺尔在普隆德拉很有名！！问问那里的孩子就知道了。";
		emotion ET_SCRATCH;
		close;
	}
	if (Q_XMSRINGS == 1) {
		mes "该死...可怜的孩子怎么可能比海军陆战队员更重要..我错了吗？";
		next;
		mes "[雪白]";
		mes "呃。你好。我姐姐史尼威给我发了一条小鸟信息。这封信湿漉漉的，除了她地址的一些结尾字母之外，我什么也看不懂……";
		next;
		mes "[雪白]";
		mes "看，最后好像是“yang”……嗯……她一定是拼错了“Al Doo Boryang”……";
		close;
	}
	if (Q_XMSRINGS == 2) {
		mes "你帮过我妹妹吗？太棒了！";
		next;
		mes "[雪白]";
		mes "但我还需要 3 件礼物送给我的海洋民间朋友......";
		next;
		menu "Have these gifts!",-, "I need a li'l something in return",M_END;

		mes "[雪白]";
		if (countitem(644) < 3) {
			mes "哎呀！！你身上没有 3 个灰色礼品盒。";
			emotion ET_SORRY;
			close;
		}
		set Q_XMSRINGS,3;
		delitem 644,3;
		getnameditem 2636,strcharinfo(0);
		emotion ET_CHUP;
		mes "谢谢你！拥有这枚魔法戒指！";
		close;
	}
	emotion ET_THANKS;
	mes "哦...全体海军人员向您表示感谢！他们非常喜欢礼物！";
	if (rand(2))
		mes "您难道不知道圣诞老人的帽子揭示了戒指的真正力量吗？";
	close;

M_END:
	mes "[雪白]";
	mes "我愿意用 GOLD 魔法戒指换 3 个礼盒......";
	emotion ET_SWEAT;
	close;
}
