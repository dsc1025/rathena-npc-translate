//===== rAthena Script =======================================
//= Guild Warehouse Manager
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Gives players access to their guild storage and log.
//===== Changelogs: ==========================================
//= 1.0 First Version. [Lemongrass]
//============================================================

-	script	GuildWarehouse	-1,{
	mes "[仓库经理]";
	mes "你好吗？我们专门从事公会仓库。这是我们的^0000cd故事^000000：";
	mes "为什么公会成员不能共享存储空间？我们从这个简单的问题开始。";
	next;
	mes "[仓库经理]";
	mes "你可以用1000 Zeny打开仓库或者查看公会的使用历史。";
	mes "我怎么帮你？";
	next;
	if( select( "开放公会仓库：查看仓库使用历史" ) == 1 ){
		.@guildid = getcharid( 2 );

		if( .@guildid == 0 ){
			mes "[仓库经理]";
			mes "公会仓库仅供公会成员使用。";
			close;
		}

		if( getgdskilllv( .@guildid, "GD_GUILD_STORAGE" ) == 0 || !guild_has_permission( GUILD_PERM_STORAGE ) ){
			mes "[仓库经理]";
			mes "看来你的公会还不能使用公会仓库或者你没有仓库的访问权限。";
			mes "请检查公会技能和仓库访问权限后再回来。";
			close;
		}

		if( Zeny < 1000 ){
			mes "[仓库经理]";
			mes "恐怕你没有足够的钱来支付费用。";
			mes "费用为1000 Zeny。";
			close;
		}

		mes "[仓库经理]";
		mes "那我就给你打开公会仓库。度过一段难忘的时光！";
		close2;

		if( Zeny < 1000 ){
			// Cheat prevention
			end;
		}

		if( guildopenstorage() == GSTORAGE_OPEN ){
			Zeny -= 1000;
			end;
		}else{
			mes "[仓库经理]";
			mes "恐怕现在有其他人正在使用仓库。";
			mes "请稍后再试。";
			close;
		}
	}else{
		.@guildid = getcharid( 2 );

		if( .@guildid == 0 ){
			mes "[仓库经理]";
			mes "公会仓库仅供公会成员使用。";
			close;
		}

		if( getgdskilllv( .@guildid, "GD_GUILD_STORAGE" ) == 0 || !guild_has_permission( GUILD_PERM_STORAGE ) ){
			mes "[仓库经理]";
			mes "看来你的公会还不能使用公会仓库或者你没有仓库的访问权限。";
			mes "请检查公会技能和仓库访问权限后再回来。";
			close;
		}

		mes "[仓库经理]";
		mes "我将向您展示仓库的使用历史。使用历史记录最多将保留 3 个月。";
		mes "度过一段难忘的时光！";
		close2;
		guildopenstorage_log();
		end;
	}
}

alberta,114,65,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#alberta	896
aldebaran,146,122,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#aldebaran	896
amatsu,100,156,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#amatsu	896
ayothaya,203,173,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#ayothaya	896
brasilis,204,227,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#brasilis	896
comodo,204,153,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#comodo	896
dewata,196,193,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#dewata	896
einbech,182,124,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#einbech	896
einbroch,238,203,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#einbroch	896
geffen,128,68,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#geffen	896
gonryun,164,127,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#gonryun	896
harboro1,288,212,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#harboro1	896
hugel,91,158,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#hugel	896
izlude,133,149,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#izlude	896
lighthalzen,162,102,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#lighthalzen	896
louyang,210,111,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#louyang	896
malaya,238,206,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#malaya	896
morocc,168,107,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#morocc	896
moscovia,211,200,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#moscovia	896
niflheim,200,184,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#niflheim	896
payon,180,106,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#payon	896
prontera,150,191,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#prontera	896
rachel,123,145,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#rachel	896
umbala,106,160,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#umbala	896
veins,205,130,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#veins	896
xmas,155,140,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#xmas	896
yuno,176,187,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#yuno	896
