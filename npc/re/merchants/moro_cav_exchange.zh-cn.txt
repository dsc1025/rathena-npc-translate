//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= [Official Conversion]
//= Hero Ring enchants NPCs
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//= 1.1 Slight change to match official script. [Capuche]
//============================================================

moro_cav,32,73,5	script	Hero Token Exchanger#1	4_F_SHADOWCHASER,{
	disable_items;
	mes "[英雄代币兑换器]";
	mes "如果你与摩洛哥作战";
	mes "并收集了";
	mes "英雄代币，";
	mes "我可以用一枚特殊的戒指给你交换。";
	mes "你想看看我的戒指吗？";
	while(true) {
		next;
		if (checkweight(2163,2) == 0) {
			mes "[英雄代币兑换器]";
			mes "你的包里有很多东西，";
			mes "请安排好后再来！";
			close;
		}
		switch( select( "响铃描述", "与响铃交换。", "结束通话。" ) ) {
		case 1:
			mes "[英雄代币兑换器]";
			mes "这个英雄戒指";
			mes "不得";
			mes "看";
			mes "感人的，";
			next;
			mes "[英雄代币兑换器]";
			mes "但把它带到";
			mes "专家附魔师伯恩斯，";
			mes "他可以附魔";
			mes "里面的所有 4 个插座";
			mes "并带出";
			mes "它的潜在能力。";
			next;
			mes "[英雄代币兑换器]";
			mes "英雄戒指";
			mes "可以帮助你";
			mes "在征服";
			mes "摩洛哥。";
			mes "还有其他问题吗？";
			continue;
		case 2:
			mes "- 英雄戒指 -";
			mes "^006400为战士打造的戒指^000000";
			mes "^006400致力于击败摩洛哥..^000000";
			mes "^006400魔法时，^000000";
			mes "^006400它借给它的所有者^000000";
			mes "^006400对抗摩洛哥的力量。^000000";
			mes "^006400类别：配件/DEF：0^000000";
			mes "^006400权重：20/所需等级：160^000000";
			mes "^006400槽位：0/装备至：所有工作^000000";
			next;
			mes "[英雄代币兑换器]";
			mes "英雄戒指需要";
			mes "30 个英雄代币";
			mes "用于交换。";
			mes "你想交换吗？";
			next;
			if (select( "上一菜单。", "兑换 30 个英雄代币。" ) == 1) {
				mes "[英雄代币兑换器]";
				mes "思考";
				mes "再次。";
				continue;
			}
			if (countitem(6684) < 30) {
				mes "[英雄代币兑换器]";
				mes "^006400英雄令牌^000000";
				mes "被给予";
				mes "那些勇敢地与摩洛哥作战的人。";
				mes "你没有";
				mes "30 个^006400英雄代币^000000，";
				mes "因此我无法帮助你。";
				continue;
			}
			mes "[英雄代币兑换器]";
			mes "我来交换";
			mes "30 个^006400英雄代币^000000";
			mes "带有 1 个^006400英雄戒指^000000。";
			mes "还要别的吗？";
			delitem 6684,30;	// TokenOfHero
			getitem 2981,1;		// RingOfHero
			continue;
		case 3:
			mes "[英雄代币兑换器]";
			mes "一天";
			mes "你会发现自己";
			mes "需要英雄戒指。";
			close;
		}
	}
}

moro_cav,34,65,5	script	Expert Enchanter Byrnes	1_M_03,{
	disable_items;
	mes "[伯恩斯]";
	mes "你听说过英雄戒指吗？只有真正的英雄才配拥有这枚华丽的戒指，而我一直在等待一枚戒指。";
	mes "我能为你做什么？";
	next;
	switch( select( "附魔英雄戒指。", "重置英雄戒指上的附魔。", "取消。" ) ) {
	case 1:
		mes "[伯恩斯]";
		mes "我只能对装备好的戒指进行附魔。您想要为哪一枚已装备的戒指附魔？";
		next;
		setarray .@eqi_slot[0], EQI_ACC_L, EQI_ACC_R;
		.@s = select( "左边", "正确的" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[伯恩斯]";
			mes "请装备您想要附魔的英雄戒指。";
			close;
		case 2981:
			break;
		default:
			mes "[伯恩斯]";
			mes "请装备你的英雄戒指。我无法对其他任何东西进行附魔。";
			close;
		}
		// custom at this point
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[0] > 0) {
			mes "[伯恩斯]";
			mes "完全附魔的英雄戒指无法再进行附魔。";
			close;
		}
		if (.@card[3] == 0) {
			setarray .@enchant_list$[0], "DEF", "MDEF";
			.@slot = 4;
		}
		else if (.@card[2] == 0) {
			setarray .@enchant_list$[0], "Max HP", "Max SP";
			.@slot = 3;
		}
		else if (.@card[1] == 0) {
			setarray .@enchant_list$[0], "Atk", "MAtk";
			.@slot = 2;
		}
		else {
			setarray .@enchant_list$[0], "STR", "INT", "AGI", "VIT", "DEX", "LUK";
			.@slot = 1;
		}
		mes "[伯恩斯]";
		mes "您想要哪种能力作为插槽 " + .@slot + " 上的附魔？";
		next;
		.@enchant_list_size = getarraysize(.@enchant_list$);
		for ( .@i = 0; .@i < .@enchant_list_size; ++.@i )
			.@menu$ += .@enchant_list$[.@i] + ":";
		.@type = select(.@menu$) - 1;

		switch(.@slot) {
		case 4:
			switch(.@type) {// enchant list index
			case 0:	// DEF
				setarray .@bonus[0], 4791, 4792, 4793, 4794, 4893;
				break;
			default:	// MDEF
				setarray .@bonus[0], 4890, 4786, 4891, 4787, 4892;
				break;
			}
			break;
		case 3:
			switch(.@type) {
			case 0:	// Max HP
				setarray .@bonus[0], 4861, 4862, 4867, 4868, 4900;
				break;
			default:	// Max SP
				setarray .@bonus[0], 4800, 4870, 4871, 4801, 4802;
				break;
			}
			break;
		case 2:
			switch(.@type) {
			case 0:	// Atk
				setarray .@bonus[0], 4882, 4766, 4767, 4894, 4895;
				break;
			default:	// Matk
				setarray .@bonus[0], 4883, 4896, 4897, 4898, 4899;
				break;
			}
			break;
		case 1:
			switch(.@type) {
			case 0:	// STR
				setarray .@bonus[0], 4700, 4701, 4702, 4703, 4704;
				break;
			case 1:	// INT
				setarray .@bonus[0], 4710, 4711, 4712, 4713, 4714; 
				break;
			case 2:	// AGI
				setarray .@bonus[0], 4730, 4731, 4732, 4733, 4734;
				break;
			case 3:	// VIT
				setarray .@bonus[0], 4740, 4741, 4742, 4743, 4744;
				break;
			case 4:	// DEX
				setarray .@bonus[0], 4720, 4721, 4722, 4723, 4724;
				break;
			default:	// LUK
				setarray .@bonus[0], 4750, 4751, 4752, 4753, 4754;
				break;
			}
			break;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 2981) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		.@bonus_size = getarraysize(.@bonus);
		.@card[.@slot-1] = .@bonus[ rand(.@bonus_size) ];	// unknown rates
		specialeffect2 EF_ANGEL2;
		mes "[伯恩斯]";
		mes "附魔完成！";
		delequip .@eqi_slot[.@s];
		getitem2 2981,1,1,0,0,.@card[0],.@card[1],.@card[2],.@card[3];
		close;
	case 2:
		mes "[伯恩斯]";
		mes "要重置英雄戒指上的结界，我需要 3 个英雄令牌。";
		next;
		if (countitem(6684) < 3) {
			mes "[伯恩斯]";
			mes "您没有足够的英雄令牌。我无法重置你的英雄戒指。";
			close;
		}
		mes "[伯恩斯]";
		mes "我只能重置已装备的戒指。您想要重置哪一枚已装备的戒指？";
		next;
		setarray .@eqi_slot[0], EQI_ACC_L, EQI_ACC_R;
		.@s = select( "左边", "正确的" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[伯恩斯]";
			mes "您首先必须装备该物品以重置其附魔状态。";
			close;
		case 2981:
			break;
		default:
			mes "[伯恩斯]";
			mes "请装备你的英雄戒指。我无法重置其他任何东西。";
			close;
		}
		// custom at this point
		if (countitem(6684) < 3) {
			mes "[伯恩斯]";
			mes "您没有足够的英雄令牌。我无法重置你的英雄戒指。";
			close;
		}
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[3] == 0) {
			mes "[伯恩斯]";
			mes "你的英雄戒指目前处于没有结界的状态。";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 2981) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		specialeffect2 EF_ANGEL2;
		mes "[伯恩斯]";
		mes "初始化完成。";
		delitem 6684,3;
		delequip .@eqi_slot[.@s];
		getitem 2981,1;
		close;
	case 3:
		close;
	}
}
